
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Форма          = ПараметрыВыполненияКоманды.Источник;
	ЭтоФормаСписка = Найти(Форма.ИмяФормы, "ФормаСписка") > 0;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	
	Если ЭтоФормаСписка Тогда
		
		ВыделенныеСтроки = Форма.ТекущийЭлемент.ВыделенныеСтроки;
		Для Каждого Ссылка Из ВыделенныеСтроки Цикл
			
			ОткрытьДокументВСБИС(Ссылка, ДополнительныеПараметры);
			
		КонецЦикла;
		
	Иначе
		
		Если Форма.Объект.Ссылка.Пустая() Тогда
			Форма.Модифицированность = Истина;
		КонецЕсли;
		
		ОткрытьДокументВСБИС = Новый ОписаниеОповещения(
			"ОткрытьДокументВСБИС",
			ЭтотОбъект, ДополнительныеПараметры);
		
		Saby_ТНОбщегоНазначенияКлиент.ПроверитьМодифицированностьИПродолжитьВыполнение(
			ОткрытьДокументВСБИС);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОповещений

&НаКлиенте
Процедура ОткрытьДокументВСБИС(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат)  = Тип("КодВозвратаДиалога") Тогда
		
		СсылкаНаДокумент    = ДополнительныеПараметры.Форма.Объект.Ссылка;
		
	Иначе
		
		СсылкаНаДокумент    = Результат;
		
	КонецЕсли;
	
	Saby_ТНОбщегоНазначенияКлиент.ОткрытьДокументВСБИС(СсылкаНаДокумент);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
