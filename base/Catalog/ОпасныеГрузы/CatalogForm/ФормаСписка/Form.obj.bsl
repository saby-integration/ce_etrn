
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьВесьСправочник(Команда)
	
	РезультатЗагрузки = РезультатЗагрузкиСправочника();
	
	ПоказатьРезультатЗагрузки(РезультатЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПоНаименованию(Команда)
	
	ПроцедураПослеВводаНаименования = Новый ОписаниеОповещения("ПослеВводаНаименования", ЭтаФорма);
	
	ПоказатьВводСтроки(
		ПроцедураПослеВводаНаименования,
		НаименованиеСправочникаДляПоиска,
		"Введите наименование справочника, который нужно загрузить.");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерекодироватьСправочник(Команда)
	
	ПерекодироватьСправочникНаСервере();
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВводаНаименования(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	МассивНаименований = Новый Массив;
	МассивНаименований.Добавить(ВРег(СокрЛП(Результат)));
	
	РезультатЗагрузки = РезультатЗагрузкиСправочника(МассивНаименований);
	
	ПоказатьРезультатЗагрузки(РезультатЗагрузки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РезультатЗагрузкиСправочника(Знач МассивНаименований = Неопределено)
	
	Результат = Новый Структура;
	
	Справочники.Saby_ОпасныеГрузы.ЗагрузитьОпасныеГрузыСОнлайна(МассивНаименований, Результат);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьРезультатЗагрузки(РезультатЗагрузки)
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = РезультатЗагрузки.Текст;
	Сообщение.Сообщить();
	
	Если ЗначениеЗаполнено(РезультатЗагрузки.ОписаниеОшибок) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = РезультатЗагрузки.ОписаниеОшибок;
		Сообщение.Сообщить();
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПерекодироватьСправочникНаСервере()
	
	Справочники.Saby_ОпасныеГрузы.ПерекодироватьСправочник();
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
