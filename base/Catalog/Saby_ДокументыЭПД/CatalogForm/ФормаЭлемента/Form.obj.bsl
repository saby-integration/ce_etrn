
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьОграничениеТипов();
	
	УстановитьПодсказкуТипа();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
			
	Если Не Объект.ДанныеЮрЛиц.Количество() Тогда 
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнены стороны документа!";
		Сообщение.Поле  = "Объект.ДанныеЮрЛиц";
		Сообщение.Сообщить();					
		
		Отказ = Истина;
		
	КонецЕсли;      
					
КонецПроцедуры

&НаКлиенте
Процедура ВыборЗначения(СтандартнаяОбработка)
	
	ЭтаФорма.ВыборДоступен = Истина;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	
	УстановитьПодсказкуТипа();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеЮрЛиц

&НаКлиенте
Процедура ДанныеЮрЛицВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыРеквизитаФормы = Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыРеквизитаФормы(
		"НаименованиеОрганизации",
		"ДанныеЮрЛицНаименованиеОрганизации",
		ВыбраннаяСтрока);
		
	ПараметрыРеквизитаФормы.ИмяИсходнойТаблицы          = "ДанныеЮрЛиц";
	ПараметрыРеквизитаФормы.ИдентификаторИсходнойСтроки = ВыбраннаяСтрока;
	
	Saby_ТНОбщегоНазначенияКлиент.ЮрЛицоНажатие(ЭтаФорма, "СторонаДокумента", ПараметрыРеквизитаФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеЮрЛицПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Роль = ПредопределенноеЗначение("Перечисление.Saby_РолиКонтрагентов.СторонаДокумента");
		Элемент.ТекущиеДанные.КлючСтроки = Saby_ТНОбщегоНазначенияКлиентСервер.НовыйКлючОсновнойСтроки(
			ЭтаФорма, "ДанныеЮрЛиц");
	КонецЕсли;
	
	ПараметрыРеквизитаФормы = Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыРеквизитаФормы(
		"НаименованиеОрганизации",
		"ДанныеЮрЛицНаименованиеОрганизации",
		Элементы.ДанныеЮрЛиц.ТекущаяСтрока);
			
	ПараметрыРеквизитаФормы.ИмяИсходнойТаблицы          = "ДанныеЮрЛиц";
	ПараметрыРеквизитаФормы.ИдентификаторИсходнойСтроки = Элементы.ДанныеЮрЛиц.ТекущаяСтрока;
			
	Saby_ТНОбщегоНазначенияКлиент.ЮрЛицоНажатие(ЭтаФорма, "СторонаДокумента", ПараметрыРеквизитаФормы);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыДанныеЮрЛиц

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОграничениеТипов()
	
	Если Параметры.Свойство("ОтборТипов") Тогда
		ЗначениеОтбора = Параметры.ОтборТипов;
	ИначеЕсли Параметры.Свойство("ПараметрыВыбора") И Параметры.ПараметрыВыбора.Свойство("Тип") Тогда
		ЗначениеОтбора = Параметры.ПараметрыВыбора.Тип;
	Иначе
		ЗначениеОтбора = Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеОтбора) = Тип("СписокЗначений") Тогда
		Для Каждого ЗначениеСписка Из ЗначениеОтбора Цикл
			Элементы.Тип.СписокВыбора.Добавить(ЗначениеСписка.Значение);
		КонецЦикла;
	ИначеЕсли ТипЗнч(ЗначениеОтбора) = Тип("Массив") Или ТипЗнч(ЗначениеОтбора) = Тип("ФиксированныйМассив") Тогда
		Для Каждого Значение Из ЗначениеОтбора Цикл
			Элементы.Тип.СписокВыбора.Добавить(Значение);
		КонецЦикла;
	ИначеЕсли ТипЗнч(ЗначениеОтбора) = Тип("ПеречислениеСсылка.Saby_ТипыДокумента") Тогда
		Элементы.Тип.СписокВыбора.Добавить(ЗначениеОтбора);
	Иначе
		Элементы.Тип.СписокВыбора.Очистить();
	КонецЕсли;
		
	ОбновитьОтображениеВыбораТипа();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеВыбораТипа()
	
	Если Элементы.Тип.СписокВыбора.Количество() = 1 Тогда
		
		// установка значения по умолчанию 
		Объект.Тип = Элементы.Тип.СписокВыбора[0].Значение;		
		Элементы.Тип.ТолькоПросмотр = Истина;
		
	ИначеЕсли Элементы.Тип.СписокВыбора.Количество() > 1 Тогда
		Элементы.Тип.РежимВыбораИзСписка = Истина;
		Элементы.Тип.ВыбиратьТип         = Ложь;
	Иначе
		Элементы.Тип.РежимВыбораИзСписка = Ложь;
		Элементы.Тип.ВыбиратьТип         = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПодсказкуТипа()
	
	ШаблонПодсказки = "- %1 - %2";
	
	МассивТипов = Новый Массив;
	Если Не Объект.Тип.Пустая() Тогда
		МассивТипов.Добавить(Объект.Тип);
	ИначеЕсли Элементы.Тип.СписокВыбора.Количество() <> 0 Тогда
		Для Каждого ЗначениеСписка Из Элементы.Тип.СписокВыбора Цикл
			МассивТипов.Добавить(ЗначениеСписка.Значение);
		КонецЦикла;
	Иначе
		Перечисления.Saby_ТипыДокумента.ЗаполнитьВсеТипыПоПорядку(МассивТипов);
	КонецЕсли;
	
	МассивПодсказок = Новый Массив;
	Для Каждого Тип Из МассивТипов Цикл
		Подсказка = Перечисления.Saby_ТипыДокумента.Подсказка(Тип);
		Если ЗначениеЗаполнено(Подсказка) Тогда
			ИмяТипа = Строка(Тип);
			МассивПодсказок.Добавить(СтрШаблон(ШаблонПодсказки, ИмяТипа, Подсказка));
		КонецЕсли;
	КонецЦикла;
	
	Элементы.Тип.Подсказка = СтрСоединить(МассивПодсказок, Символы.ПС);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
