
#Область ПрограммныйИнтерфейс

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ОбщиеСтруктуры
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ОбщиеСтруктуры

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСЮрЛицами
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСЮрЛицами

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ПредставлениеДанных
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ПредставлениеДанных

#Область ТранспортныеСредстваИнтерфейс

//DynamicDirective
// Возвращает строки транспортных средств по виду транспортного средства
//
// Параметры:
//  ТранспортныеСредства - ТабличнаяЧасть - табличная часть транспортных средств
//  ВидТС - ПеречислениеСсылка.Saby_ВидыТС - вид транспортного средства для получения строк
//
// Возвращаемое значение:
//   Массив - массив строк табличной части
//
Функция СтрокиТС(ТранспортныеСредства, ВидТС) Экспорт
	
	РезультатФункции = Новый Массив;
	
	Для Каждого СтрокаТС Из ТранспортныеСредства Цикл
		Если СтрокаТС.Вид = ВидТС Тогда
			РезультатФункции.Добавить(СтрокаТС);
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

//DynamicDirective
// Обновляет реквизиты формы связанные с транспортными средствами.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма для обновления реквизитов
//
Процедура ОбновитьТранспортныеСредстваНаФорме(Форма) Экспорт
	
	ОтборПрицепов = Новый Структура;
	ОтборПрицепов.Вставить("Вид", ЗначениеМетаданных("Saby_ВидыТС.Прицеп"));
	ОтборПрицепов = Новый ФиксированнаяСтруктура(ОтборПрицепов);
	
	Форма.Элементы.Прицепы.ОтборСтрок = ОтборПрицепов;
	
	СформироватьНадписьПрицепыСтрокой(Форма);
	
	СформироватьНадписьТранспортноеСредствоСтрокой(Форма);
	
КонецПроцедуры

#КонецОбласти // ТранспортныеСредстваИнтерфейс

//DynamicDirective
// Добавить или заменить существующий элемент отбора динамического списка.
//
// Параметры:
//   Список - ДинамическийСписок - список, в котором требуется установить отбор.
//   РеквизитФормы - Произвольный - значение отбора.
//   ИмяПоляДинамическогоСписка - Строка - поле, по которому необходимо установить отбор.
//
Процедура УстановитьЭлементОтбораДинамическогоСписка(Список, РеквизитФормы, ИмяПоляДинамическогоСписка) Экспорт
	
	ПараметрыУстановки = ПараметрыУстановкиЭлементаДинамическогоСписка(Список, ИмяПоляДинамическогоСписка, РеквизитФормы);
	ПараметрыУстановки.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ПараметрыУстановки.Использование = ЗначениеЗаполнено(РеквизитФормы);
	
	УстановитьЭлементОтбораДинамическогоСпискаБСП(ПараметрыУстановки);
	
КонецПроцедуры

//DynamicDirective
// Обновляет представление водителей на форме
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма для обновления данных
//
Процедура ОбновитьВодителейНаФорме(Форма) Экспорт
	
	ФормаОбъект = ФормаОбъект(Форма);
	
	РольВодитель = ЗначениеМетаданных("Saby_РолиОтветственных.Водитель");
	
	МассивСтрокПредставления = Новый Массив;
	
	Для Каждого СтрокаОтветственного Из ФормаОбъект.ОтветственныеЛица Цикл
		Если СтрокаОтветственного.Роль <> РольВодитель Тогда
			Продолжить;
		КонецЕсли;
		СтрокаОтветственного.ФИО = ПредставлениеФИО(СтрокаОтветственного);
		СтрокаОтветственного.ВУ  = ПредставлениеВУ(СтрокаОтветственного);
		МассивСтрокПредставления.Добавить(СокрЛП(СтрокаОтветственного.ФИО));
	КонецЦикла;
	
	Форма.ВодителиСтрокой = СтрСоединитьЭПД(МассивСтрокПредставления, ", ");
	Если Не ЗначениеЗаполнено(Форма.ВодителиСтрокой) Тогда
		Форма.ВодителиСтрокой = "Заполнить";
	КонецЕсли;
	
КонецПроцедуры

//DynamicDirective
// Ищет документ ЭПД по ключу строки
//
// Параметры:
//  ДокументыЭПД - ТаблицаЗначений - таблица документов для поиска
//  КлючСтроки - Число - значение для поиска по ключу
//
// Возвращаемое значение:
//   Строка - найденная строка по ключу
//
Функция ПоискДокументаЭПДПоКлючу(ДокументыЭПД, КлючСтроки) Экспорт
	
	// найдем строку документа ЭПД
	ОтборЭПД = Новый Структура;
	ОтборЭПД.Вставить("КлючСтроки", КлючСтроки);
	
	НайденныеСтрокиЭПД = НайтиСтрокиУниверсально(ДокументыЭПД, ОтборЭПД);
	Если НайденныеСтрокиЭПД.Количество() Тогда
		Результат =  НайденныеСтрокиЭПД[0];
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_Общие
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_Общие

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РолиКонтрагентов
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РолиКонтрагентов

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ФормаОбъект
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ФормаОбъект

//DynamicDirective
// Возвращает имя дополнительного свойства Код филиала
//
// Возвращаемое значение:
//   Строка - наименование свойства
//
Функция ИмяСвойстваКодФилиала() Экспорт
	
	Возврат "СБИС_КодФилиала";
	
КонецФункции

//DynamicDirective
// Возвращает имя дополнительного свойства идентификатор
//
// Возвращаемое значение:
//   Строка - наименование свойства
//
Функция ИмяСвойстваИдентификатор() Экспорт
	
	Возврат "СБИС_Идентификатор";
	
КонецФункции

//DynamicDirective
// Возвращает параметры необходимые для выгрузки очередного титула документа
//
// Параметры:
//  СсылкаНаДокумент - ДокументСсылка - ссылка на выгружаемый документ
//
// Возвращаемое значение:
//   Структура - параметры необходимые для выгрузки очередного титула документа
//
Функция ПараметрыВыгрузки(СсылкаНаДокумент) Экспорт
	
	РезультатФункции = Новый Структура;
	
	РезультатФункции.Вставить("Этап",                 "");
	РезультатФункции.Вставить("ТипТитула",            Неопределено);
	РезультатФункции.Вставить("КодТитула",            "");
	РезультатФункции.Вставить("ИдентификаторСБИС",    "");
	
	РезультатФункции.Вставить("Ссылка",               СсылкаНаДокумент);
	РезультатФункции.Вставить("ИмяМетаданных",        "");
	РезультатФункции.Вставить("ПараметрыДляАПИ",      Неопределено);
	РезультатФункции.Вставить("СтруктураДокумента",   Неопределено);
	РезультатФункции.Вставить("ДанныеДокумента",      Неопределено);
	
	РезультатФункции.Вставить("ДинамическийТитул",             Ложь);
	РезультатФункции.Вставить("ОтрицательныйПереход",          Ложь);
	РезультатФункции.Вставить("ПричинаОтрицательногоПерехода", "");
	
	// ФЛК
	РезультатФункции.Вставить("ВыполнитьФЛК",            Истина);
	РезультатФункции.Вставить("ПроверятьПоляСОХ",        Ложь);
	РезультатФункции.Вставить("ПроверятьПоляОтгрузчика", Ложь);
	
	РезультатФункции.Вставить("ВыгрузкаНаКлиенте",       Ложь);
	
	Возврат РезультатФункции;
	
КонецФункции

//DynamicDirective
// Возвращает структуру параметров загрузки для дальнейшего вызова функции ЗагрузкаДанныхДокумента
//
// Параметры:
//  СсылкаНаДокумент - ДокументСсылка - ссылка на выгружаемый документ
//
// Возвращаемое значение:
//   Структура - структура параметров с фиксированными колонками:
//     * ИдСбис - Строка - идентификатор СБИС
//     * ДанныеДокумента - Соответствие - полученные данные с онлайна
//     * Ссылка - ДокументСсылка.Saby_ТранспортнаяНакладная - ссылка на документ в базе,
//       в который будут загружены данные.
//     * ИзДокумента - Булево - признак запуска загрузки из документа. Если ложь, то запуск из списка документов.
//     * ЭтоЗаполнениеСПомощьюINI - Булево - признак загрузки данных из другого документа, а не с онлайна.
//     * ДокументОснование - ДокументСсылка - ссылка на документ, на основании которого загружаются данные.
//     * ТекущийТитул - ПеречислениеСсылка.Saby_ТипТитулаЭтрН - тип титула, который необходимо загрузить.
//       Если Неопределено, то загружаются титулы полученные функцией ТитулыДляЗагрузки()
//
Функция ПараметрыЗагрузки(СсылкаНаДокумент = Неопределено) Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ИдСбис",                    "");
	РезультатФункции.Вставить("ДанныеДокумента",           Новый Соответствие);
	РезультатФункции.Вставить("Ссылка",                    СсылкаНаДокумент);
	РезультатФункции.Вставить("ИзДокумента",               Ложь);
	РезультатФункции.Вставить("ИмяМетаданных",             "");
	
	РезультатФункции.Вставить("ЭтоЗаполнениеСПомощьюINI",  Ложь);
	РезультатФункции.Вставить("ДокументОснование",         Неопределено);
	РезультатФункции.Вставить("ПараметрыОснования",        Новый Структура);
	РезультатФункции.Вставить("ЭтоПервоеОснование",        Истина);
	РезультатФункции.Вставить("НоменклатураГруза",         Новый Соответствие);
	РезультатФункции.Вставить("КлючСтроки",                Новый Структура);
	РезультатФункции.Вставить("СопроводительныеДокументы", Новый Массив);
	
	РезультатФункции.Вставить("ТекущийТитул",              Неопределено);
	РезультатФункции.Вставить("ТолькоАктивныйЭтап",        Ложь);
	
	РезультатФункции.Вставить("ЗагрузкаНаКлиенте",        Ложь);
	
	Возврат РезультатФункции;
	
КонецФункции

//DynamicDirective
// Возвращает параметры документа для использования в методах АПИ
//
// Параметры:
//  ИмяМетаданных - Строка - имя метаданных документа
//  СторонняяОрганизация - Булево - признак того, что титул отправляет сторонняя организация.
//
// Возвращаемое значение:
// 	Структура - параметры документа
//
Функция ПараметрыДокументаДляАПИ(ИмяМетаданных, СторонняяОрганизация = Ложь) Экспорт
		
	Если ИмяМетаданных = "Saby_ТранспортнаяНакладная" Тогда
		
		ТипДляАПИ             = "ConsignmentNote";
		РегламентДляАПИ       = "Транспортная накладная";
		ИдентификаторДляАПИ   = "ad40d873-9d1e-47d7-b7e9-f39636b36301";
		ТипДляВыгрузкиДляАПИ  = "ЭТрН";		
		ИмяПеречисленияДляАПИ = "Saby_ТипТитулаЭтрН";
				
	ИначеЕсли ИмяМетаданных = "Saby_ПутевойЛист" Тогда
		
		ТипДляАПИ             = "Waybill";
		ТипДляВыгрузкиДляАПИ  = "Waybill";
		ИмяПеречисленияДляАПИ = "Saby_ТипТитулаЭПЛ";
		
		Если СторонняяОрганизация Тогда
			РегламентДляАПИ      = "ЭПЛ (Входящий)";
			ИдентификаторДляАПИ  = "48e4c1ba-a877-4f16-8a25-da0cf3209bf8";
		Иначе
			РегламентДляАПИ      = "ЭПЛ";
			ИдентификаторДляАПИ  = "7b589621-deec-488c-ada2-5df9db6c329e";
		КонецЕсли;
		
	ИначеЕсли ИмяМетаданных = "Saby_ЗаказНаПеревозку" Тогда
		
		ТипДляАПИ             = "TransportOrder";
		ТипДляВыгрузкиДляАПИ  = "TransportOrder";
		ИмяПеречисленияДляАПИ = "Saby_ТипТитулаЗнП";
		
		Если СторонняяОрганизация Тогда
			РегламентДляАПИ      = "Заказ на перевозку (Входящий)";
			ИдентификаторДляАПИ  = "e000d24d-35a7-4a31-a692-22405b34dfbe";
		Иначе
			РегламентДляАПИ      = "Заказ на перевозку";
			ИдентификаторДляАПИ  = "6c052893-6b54-4f55-baf5-83e515b5aff4";
		КонецЕсли;
		
	Иначе
		
		ТипДляАПИ             = "";
		РегламентДляАПИ       = "";
		ИдентификаторДляАПИ   = "";
		ТипДляВыгрузкиДляАПИ  = "";
		ИмяПеречисленияДляАПИ = "";
		
	КонецЕсли;
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Тип",                     ТипДляАПИ);
	РезультатФункции.Вставить("Регламент",               РегламентДляАПИ);
	РезультатФункции.Вставить("ИдентификаторРегламента", ИдентификаторДляАПИ);
	РезультатФункции.Вставить("ТипДляВыгрузки",          ТипДляВыгрузкиДляАПИ);
	РезультатФункции.Вставить("ИмяПеречисления",         ИмяПеречисленияДляАПИ); 
	
	Возврат РезультатФункции;
	
КонецФункции

//DynamicDirective
// Формирует структуру для накопления результата выгрузки или загрузки данных в/из СБИС
//
// ВАЖНО!!! Данная структура ожидается в модуле Saby_ТНОбщегоНазначенияКлиент
//   при обработке результата длительной операции в функции ОбработатьРезультатКоманды.
//   Поэтому при изменении структуры обязательно нужно проверить модуль Saby_ТНОбщегоНазначенияКлиент.
//
// Параметры:
//  Всего - Число - общее количество обрабатываемых документов.
//
// Возвращаемое значение:
//   Структура - данные для накопления результатов выгрузки или загрузки
//
Функция РезультатВзаимодействияСоСБИС(Всего) Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Итог", Новый Структура);
	
	РезультатФункции.Итог.Вставить("Успех",      Ложь);
	РезультатФункции.Итог.Вставить("Количество", 0);
	РезультатФункции.Итог.Вставить("Всего",      Всего);
	
	РезультатФункции.Вставить("Ошибки",   Новый Соответствие);
	РезультатФункции.Вставить("Успешные", Новый Массив);
	
	РезультатФункции.Вставить("НовыеОпасныеГрузы",    Новый Массив);
	РезультатФункции.Вставить("НовыеВидыУпаковки",    Новый Массив);
	РезультатФункции.Вставить("ОпасныеГрузыСОнлайна", Неопределено);
	РезультатФункции.Вставить("ВидыУпаковкиСОнлайна", Неопределено);
	
	РезультатФункции.Вставить("Выполнение", Новый Структура);
	РезультатФункции.Выполнение.Вставить("НаКлиенте", Ложь);
	РезультатФункции.Выполнение.Вставить("Параметры",  Новый Массив);
	
	Возврат РезультатФункции;
	
КонецФункции

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_СвойстваЭлементовФормы
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_СвойстваЭлементовФормы

//DynamicDirective
// Возвращает признак того, что документооборот завершен.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма на которой необходимо показать данные
//
// Возвращаемое значение:
//   Булево - Истина, если завершен
//
Функция ДокументооборотЗавершен(Форма) Экспорт
	
	ДОЗавершенОтрицательно = 5;
	ДОЗавершенПоложительно = 6;
	
	Возврат Форма.ИндексКартинки = ДОЗавершенОтрицательно Или Форма.ИндексКартинки = ДОЗавершенПоложительно;
	
КонецФункции

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСоСвязаннымиТаблицами
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСоСвязаннымиТаблицами

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ДинамическиеСписки
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ДинамическиеСписки

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ПолучениеДанныхИзКоллекций
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ПолучениеДанныхИзКоллекций

//DynamicDirective
// Сравнить две версии.
//
// Параметры:
//  Версия1 - Строка,РежимСовместимости - номер версии в формате РР.{П|ПП}.ЗЗ.СС.
//  Версия2 - Строка,РежимСовместимости - второй сравниваемый номер версии.
//
// Возвращаемое значение:
//   Число - больше 0, если СтрокаВерсии1 > СтрокаВерсии2; 0, если версии равны.
//
Функция СравнитьВерсии(Знач Версия1, Знач Версия2) Экспорт
	
	СтрокаВерсии1 = ПреобразоватьВерсиюВСтроку(Версия1);
	СтрокаВерсии2 = ПреобразоватьВерсиюВСтроку(Версия2);
	
	ВерсияМассив1 = МодульКода("Saby_Core").СтрРазделить82(СтрокаВерсии1, ".");
	ВерсияМассив2 = МодульКода("Saby_Core").СтрРазделить82(СтрокаВерсии2, ".");
	
	Результат = 0;
	Для Разряд = 0 По 3 Цикл
		Результат = Число(ВерсияМассив1[Разряд]) - Число(ВерсияМассив2[Разряд]);
		Если Результат <> 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСоСтроками
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСоСтроками

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ЗаполнениеНаОснованииИНИ
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ЗаполнениеНаОснованииИНИ

//DynamicDirective

Процедура ОбновитьЭлементыИныхПолучателей(Форма, КоличествоИныхПолучателей = Неопределено) Экспорт
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	КоличествоПолучателей = 0;
	Если КоличествоИныхПолучателей <> Неопределено Тогда
		КоличествоПолучателей = КоличествоИныхПолучателей;
	Иначе
		ФормаОбъект = МодульТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
		
		ОтборИныхПолучателей = Новый Структура("Роль", ЗначениеМетаданных("Saby_РолиКонтрагентов.ИнойПолучатель"));
		ИныеПолучатели = МодульТНОбщегоНазначенияКлиентСервер.НайтиСтрокиУниверсально(ФормаОбъект.ДанныеЮрЛиц, ОтборИныхПолучателей);
		КоличествоПолучателей = ИныеПолучатели.Количество();
	КонецЕсли;
	
	ТекстКоличествоИныхПолучателей = " (%1)";
	Если КоличествоПолучателей > 0 Тогда
		ТекстКоличествоИныхПолучателей = МодульТНОбщегоНазначенияКлиентСервер.СтрШаблонЭПД(
			ТекстКоличествоИныхПолучателей, Формат(КоличествоПолучателей, "ЧДЦ=0; ЧГ="));
	Иначе
		ТекстКоличествоИныхПолучателей = "";
	КонецЕсли;
	Форма.Элементы.ФормаИныеПолучателиДокумента.Заголовок = "Иные получатели документа" + ТекстКоличествоИныхПолучателей;
	
КонецПроцедуры

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

//DynamicDirective
// Формирование текстового представления прицепов из данных формы 
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - источник данных для формирования надписи
//
Процедура СформироватьНадписьПрицепыСтрокой(Форма)
	
	Если Не ЕстьРеквизитИлиСвойство(Форма, "ПрицепыСтрокой") Тогда
		Возврат;
	КонецЕсли;
	
	ФормаОбъект = ФормаОбъект(Форма);
	
	СтрокиПрицепов = СтрокиТС(
		ФормаОбъект.ТранспортныеСредства,
		ЗначениеМетаданных("Saby_ВидыТС.Прицеп"));
	
	МассивНаименований = Новый Массив;
	Для Каждого СтрокаПрицепа Из СтрокиПрицепов Цикл
		МассивНаименований.Добавить(СтрокаПрицепа.РегистрационныйНомер);
	КонецЦикла;
	
	ПрицепыСтрокой = СтрСоединитьЭПД(МассивНаименований, ", ");
	Если Не ЗначениеЗаполнено(ПрицепыСтрокой) Тогда
		ПрицепыСтрокой = "Заполнить";
	КонецЕсли;
	
	Форма.ПрицепыСтрокой = ПрицепыСтрокой;
	
КонецПроцедуры

//DynamicDirective
// Формирование текстового представления транспортного средства на форме 
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - источник данных для формирования надписи
//
Процедура СформироватьНадписьТранспортноеСредствоСтрокой(Форма)
	
	ФормаОбъект = ФормаОбъект(Форма);
	
	СтрокиАвтомобилей = СтрокиТС(
		ФормаОбъект.ТранспортныеСредства,
		ЗначениеМетаданных("Saby_ВидыТС.Автомобиль"));
	
	Если СтрокиАвтомобилей.Количество() > 0 Тогда
		Форма.ТранспортноеСредствоСтрокой = СтрокиАвтомобилей[0].РегистрационныйНомер;
	Иначе
		Форма.ТранспортноеСредствоСтрокой = "Заполнить";
	КонецЕсли;
	
КонецПроцедуры

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_КонтактныеДанные
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_КонтактныеДанные

//DynamicDirective

Функция ПреобразоватьВерсиюВСтроку(Версия)
	
	Если ПустаяСтрока(Строка(Версия)) Тогда
		СтрокаВерсии = "0.0.0.0";
	Иначе
		СтрокаВерсии = Версия;
	КонецЕсли;

	Если ТипЗнч(СтрокаВерсии) = Тип("Строка") Тогда
		Возврат СтрокаВерсии;
	КонецЕсли;
	
	РежимСовместимостиСтрокой = Строка(СтрокаВерсии);
	РежимСовместимостиСтрокой = СтрЗаменить(РежимСовместимостиСтрокой, "Версия", "");
	МассивСоставляющих = МодульКода("Saby_Core").СтрРазделить82(РежимСовместимостиСтрокой, "_");
	
	КоличествоСоставляющихВерсии = 4;

	Пока МассивСоставляющих.Количество() < КоличествоСоставляющихВерсии Цикл
		МассивСоставляющих.Добавить("0");
	КонецЦикла;
	
	Возврат МодульКода("Saby_Core").СтрСоединить82(МассивСоставляющих, ".");
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

#Область include_etrn_base_CommonModule_ОпределениеМодуляКодаКлиентСервер
#КонецОбласти // include_etrn_base_CommonModule_ОпределениеМодуляКодаКлиентСервер

#Область include_etrn_base_CommonModule_ОпределениеМодуляКода
#КонецОбласти // include_etrn_base_CommonModule_ОпределениеМодуляКода

#Область include_etrn_base_CommonModule_ВыполнениеФункцииНаСервере
#КонецОбласти // include_etrn_base_CommonModule_ВыполнениеФункцииНаСервере

#Область include_etrn_base_CommonModule_ЗначениеМетаданных
#КонецОбласти // include_etrn_base_CommonModule_ЗначениеМетаданных

