
//DynamicDirective

// Возвращает структуру параметров для групповой установки свойст элементам формы
//
// Возвращаемое значение:
//   Структура - состоящая из массивов для установки свойств элементам формы
//
Функция ПараметрыСвойствЭлементовФормы() Экспорт 
	
	СвойстваЭлементовФормы = Новый Структура;
	СвойстваЭлементовФормы.Вставить("Видимость_Снять",           Новый Массив);
	СвойстваЭлементовФормы.Вставить("Видимость_Установить",      Новый Массив);
	СвойстваЭлементовФормы.Вставить("Доступность_Снять",         Новый Массив);
	СвойстваЭлементовФормы.Вставить("Доступность_Установить",    Новый Массив);
	СвойстваЭлементовФормы.Вставить("ТолькоПросмотр_Снять",      Новый Массив);
	СвойстваЭлементовФормы.Вставить("ТолькоПросмотр_Установить", Новый Массив);
	
	Возврат СвойстваЭлементовФормы;
	
КонецФункции

//DynamicDirective

// Делает универсальную установку всех свойств формы согласно параметрам
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма источник вызова
//  СвойстваЭлементовФормы - Структура - см. описание функции "ПараметрыСвойствЭлементовФормы()"
//
Процедура ГрупповаяУстановкаСвойствЭлементовФормы(Форма, СвойстваЭлементовФормы) Экспорт
	
	Для Каждого МассивСвойств Из СвойстваЭлементовФормы Цикл
		
		Если Не МассивСвойств.Значение.Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		МассивИмен = СтрРазделить(МассивСвойств.Ключ, "_");
		Свойство   = МассивИмен[0];
		Действие   = МассивИмен[1];
		
		Если Действие = "Установить" Тогда
			Значение = Истина;
		Иначе
			Значение = Ложь;
		КонецЕсли;
		
		ИзменитьСвойствоЭлементовФормы(Форма, Свойство, Значение, МассивСвойств.Значение);
		
	КонецЦикла;
		
КонецПроцедуры

//DynamicDirective

// Производит групповую смену значений свойств элементов формы
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма источник вызова
//	Свойство - Строка - изменяемое свойство
//	Значение - Произвольный - устанавливаемое значение свойста
//	МассивЭлементов - Массив - элементы для изменения свойств 
//
Процедура ИзменитьСвойствоЭлементовФормы(Форма, Свойство, Значение, МассивЭлементов) Экспорт
	
	Для Каждого Элемент Из МассивЭлементов Цикл
		Форма.Элементы[Элемент][Свойство] = Значение;
	КонецЦикла;
	
КонецПроцедуры

