
//DynamicDirective
// Получает значение из соответствия по указанному пути, учитывая вложенность.
// Например: ДанныеСоответствия(Вложение, "1110339.Файл.Документ.ДатИнфГО") получит данные по ключу ДатИнфГО,
//           учитывая вложенность. Если какого-либо ключа не существует, вернется значение Неопределено.
//
// Параметры:
//  Соответствие - Соответствие - соответствие данные которого нужно получить
//  Путь - Строка - имена ключей через точку
//  ЗначениеПоУмолчанию - Произвольный - значение, которое нужно присвоить в случае незаполненного значения.
//
// Возвращаемое значение:
//   Произвольный - значение полученное из соответствия с учетом вложенности.
//
Функция ДанныеСоответствия(Соответствие, Путь, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	РезультатФункции = ПоискРезультатаВКоллекции(Соответствие, Путь);
	
	Если РезультатФункции = Неопределено Тогда
		РезультатФункции = ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

//DynamicDirective
// Получает значение из структуры по указанному пути, учитывая вложенность.
// Например: ДанныеСоответствия(Вложение, "1110339.Файл.Документ.ДатИнфГО") получит данные по ключу ДатИнфГО,
//           учитывая вложенность. Если какого-либо ключа не существует, вернется значение Неопределено.
//
// Параметры:
//  Структура - Структура - структура данные которой нужно получить
//  Путь - Строка - имена ключей через точку
//  ЗначениеПоУмолчанию - Произвольный - значение, которое нужно присвоить в случае незаполненного значения.
//
// Возвращаемое значение:
//   Произвольный - значение полученное из соответствия с учетом вложенности.
//
Функция ДанныеСтруктуры(Структура, Путь, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	РезультатФункции = ПоискРезультатаВКоллекции(Структура, Путь);
	
	Если РезультатФункции = Неопределено Тогда
		РезультатФункции = ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

//DynamicDirective

Функция ПоискРезультатаВКоллекции(Коллекция, Путь)
	
    РезультатФункции = Неопределено;
	Если Не ЗначениеЗаполнено(Коллекция)
		Или Не ЗначениеЗаполнено(Путь) Тогда
		Возврат РезультатФункции;
	Иначе
		РезультатФункции = Коллекция;
	КонецЕсли;
	
	Ошибка = Ложь;
	
	МассивРеквизитов = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(Путь, ".");
	Для Каждого ТекущийРеквизит Из МассивРеквизитов Цикл
		
		ЭтоСтруктура = ТипЗнч(РезультатФункции) = Тип("Структура");

		// Поиск в структуре имеет отличия
		Если ЭтоСтруктура Тогда
			Если РезультатФункции.Свойство(ТекущийРеквизит) Тогда
				РезультатФункции = РезультатФункции[ТекущийРеквизит];
			Иначе
				Ошибка = Истина;
			КонецЕсли;
			
		Иначе 
			// Соответствие
			РезультатФункции = РезультатФункции[ТекущийРеквизит];
			Ошибка           = (РезультатФункции = Неопределено); 
		КонецЕсли;
		
		Если Ошибка Тогда
			РезультатФункции = Неопределено;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

