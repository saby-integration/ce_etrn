
#Область ПрограммныйИнтерфейс

#Область Универсальные_методы

// Возвращает синоним метаданных
//
// Возвращаемое значение:
//   Строка - синоним метаданных
//
Функция ИмяКонфигурации() Экспорт
	Возврат Метаданные.Синоним;
КонецФункции

// Запускает выполнение функции в фоновом задании, если это возможно.
//
// Параметры:
//  СтруктураПараметров - Структура - все параметры, необходимые для запуска функции через длительную операцию.
//  УникальныйИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы, 
//    во временное хранилище которой надо поместить результат выполнения процедуры.
//
// Возвращаемое значение:
//   Структура - см. ДлительныеОперации.ВыполнитьФункцию
//
Функция ДлительнаяОперация(СтруктураПараметров, УникальныйИдентификаторФормы) Экспорт
	
	ПараметрыВыполнения = МодульКода("ДлительныеОперации").ПараметрыВыполненияФункции(УникальныйИдентификаторФормы);
	
	ИмяКоманды = СтруктураПараметров.ИмяКоманды;
	
	Наименование = НаименованиеДлительнойОперации(ИмяКоманды);
	
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = Наименование;
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	
	Если ИмяКоманды = "ВыгрузитьВСбис" Или ИмяКоманды = "ЗагрузитьВСбисДинамическийТитул" Тогда
		
		ИмяФункции = ИмяФункцииФоновогоЗадания(
			"Saby_ТНВыгрузкаСервер.РезультатВыгрузкиДокументов",
			"ВнешняяОбработка.СБИС.МодульОбъекта.РезультатВыгрузкиДокументов",
			СтруктураПараметров,
			ПараметрыВыполнения);
			
		ПараметрыВыполнения.ВнешнийОтчетОбработка = СтруктураПараметров.ДопПараметры.ДвоичныеДанныеОбработки;	
		
		Возврат МодульКода("ДлительныеОперации").ВыполнитьФункцию(
			ПараметрыВыполнения,
			ИмяФункции, 
			СтруктураПараметров.МассивДокументов,
			СтруктураПараметров.context_params,
			СтруктураПараметров.ДопПараметры);
			
	ИначеЕсли ИмяКоманды = "ОбновитьДокументИзСбис" Тогда
		
		ИмяФункции = "Saby_ТНЗагрузкаСервер.ЗагрузкаДокументовПоИзменениям";
		
		Возврат МодульКода("ДлительныеОперации").ВыполнитьФункцию(
			ПараметрыВыполнения,
			ИмяФункции, 
			СтруктураПараметров);
			
	ИначеЕсли ИмяКоманды = "ПрочитатьДокумент" Или ИмяКоманды = "ОбновитьАктивныйЭтап" Тогда
		
		ИмяФункции = ИмяФункцииФоновогоЗадания(
			"Saby_ТНЗагрузкаСервер.РезультатЗагрузкиДокументов",
			"ВнешняяОбработка.СБИС.МодульОбъекта.РезультатЗагрузкиДокументов",
			СтруктураПараметров,
			ПараметрыВыполнения);
			
		ПараметрыВыполнения.ВнешнийОтчетОбработка = СтруктураПараметров.ДопПараметры.ДвоичныеДанныеОбработки;
			
		Возврат МодульКода("ДлительныеОперации").ВыполнитьФункцию(
			ПараметрыВыполнения,
			ИмяФункции,  
			СтруктураПараметров.МассивДокументов,
			СтруктураПараметров.context_params,
			СтруктураПараметров.ДопПараметры);
			
	ИначеЕсли ИмяКоманды = "АктуальныйСтатусВерсии" Тогда 		
					
		ИмяФункции = "Saby_ТНОбщегоНазначенияСервер.СтатусВерсииРасширения";
		
		Возврат МодульКода("ДлительныеОперации").ВыполнитьФункцию(
			ПараметрыВыполнения, 
			ИмяФункции);
			
	ИначеЕсли ИмяКоманды = "РаспознаваниеВУ"
		Или ИмяКоманды = "ЗагрузитьДанныеТСПоРегНомеру"
		Или ИмяКоманды = "ЗагрузитьДанныеТСПоВИН" Тогда 		
		
		ИмяФункции = ИмяФункцииФоновогоЗадания(
			"Saby_ТНЗагрузкаСервер." + ИмяКоманды,
			"ВнешняяОбработка.СБИС.МодульОбъекта." + ИмяКоманды,
			СтруктураПараметров,
			ПараметрыВыполнения);
			
		ПараметрыВыполнения.ВнешнийОтчетОбработка = СтруктураПараметров.ДопПараметры.ДвоичныеДанныеОбработки;
		
		Возврат МодульКода("ДлительныеОперации").ВыполнитьФункцию(
			ПараметрыВыполнения, 
			ИмяФункции,
			СтруктураПараметров.ДопПараметры);		
			
	Иначе 
		
		Возврат Неопределено;
			
	КонецЕсли;
		
КонецФункции

// Преобразует исходную строку в число без вызова исключений.
//
// Параметры:
//   Значение - Строка - строка, которую необходимо привести к числу.
//                       Например, "10", "+10", "010", вернет 10;
//                                 "(10)", "-10",вернет -10;
//                                 "10,2", "10.2",вернет 10.2;
//                                 "000", " ", "",вернет 0;
//                                 "10текст", вернет Неопределено.
//
// Возвращаемое значение:
//   Число, Неопределено - полученное число, либо Неопределено, если строка не является числом.
//
Функция ЧислоИзСтроки(Значение) Экспорт
	
	Значение  = СтрЗаменить(Значение, " ", "");
	Если МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрНачинаетсяСЭПД(Значение, "(") Тогда
		Значение = СтрЗаменить(Значение, "(", "-");
		Значение = СтрЗаменить(Значение, ")", "");
	КонецЕсли;
	
	СтрокаБезНулей = СтрЗаменить(Значение, "0", "");
	Если ПустаяСтрока(СтрокаБезНулей) Или СтрокаБезНулей = "-" Тогда
		Возврат 0;
	КонецЕсли;
	
	ТипЧисло  = Новый ОписаниеТипов("Число");
	Результат = ТипЧисло.ПривестиЗначение(Значение);
	
	Возврат ?(Результат <> 0 И Не ПустаяСтрока(СтрокаБезНулей), Результат, Неопределено);
	
КонецФункции

// Получает значение перечисления ЮрФизЛицо универсально для всех конфигураций
//
// Параметры:
//  ИмяЗначения - Строка - имя значения в перечислении
//
// Возвращаемое значение:
//   ПеречислениеСсылка - значение перечисления
//
Функция ЗначениеЮрФизЛицаКонфигурации(ИмяЗначения) Экспорт

	НетЮрФизЛица = Метаданные.Перечисления.Найти("ЮрФизЛицо") = Неопределено;
	Если НетЮрФизЛица Тогда
		
		// "Бухгалтерия предприятия, редакция 3.0"
		ИмяПеречисления = "ЮридическоеФизическоеЛицо";
		Если ИмяЗначения = "ЮрЛицо" Тогда 
			ИмяЗначения = "ЮридическоеЛицо";
		Иначе
			ИмяЗначения = "ФизическоеЛицо";
		КонецЕсли;
			
	Иначе
		ИмяПеречисления = "ЮрФизЛицо";
	КонецЕсли;
	
	Значение = Перечисления[ИмяПеречисления][ИмяЗначения];
	
	Возврат Значение;

КонецФункции

#Область Метаданные

// Получает имя метаданных документа в конфигурации
//
// Параметры:
//  ДокументСсылка  - ДокументСсылка - ссылка на любой документ конфигурации
//
// Возвращаемое значение:
//   Строка - имя метаданных документа в конфигурации
//
Функция ИмяМетаданныхДокумента(ДокументСсылка) Экспорт
	
	ТипЗначения = ТипЗнч(ДокументСсылка);
	Если ТипЗначения = Тип("Структура") Тогда
		ИмяДокумента = ДокументСсылка.ИмяМетаданных;
	Иначе
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначения);
		Если ОбъектМетаданных <> Неопределено И Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
			ИмяДокумента = ДокументСсылка.Метаданные().Имя;
		Иначе
			ИмяДокумента = "";
		КонецЕсли;
	КонецЕсли;

	Возврат ИмяДокумента;
	
КонецФункции

// Поиск метаданных доступных в конфигурации
// Параметры:
//  МассивПроверяемыхТипов - Массив - проверяемые типы
// Возвращаемое значение:
//  ОписаниеТипов - только доступные типы метаднных в текущей конфигурации
//
Функция ДоступныеТипыМетаданных(МассивПроверяемыхТипов) Экспорт
	
	МассивТипов = Новый Массив; 
	
	Для Каждого ТипМетаданных Из МассивПроверяемыхТипов Цикл 
		
		Если ТипМетаданных = "Строка" Тогда
			МассивТипов.Добавить(Тип("Строка"));
			Продолжить;
		КонецЕсли;	
		
		МассивИмен = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(ТипМетаданных, ".");
		
		Тип = МассивИмен[0];
		Имя = МассивИмен[1];
		
		ЕстьВКонфигурации = МетаданныеЕстьВКонфигурации(Тип, Имя);
		Если ЕстьВКонфигурации Тогда
			
			ИмяТипаМетаданных = СформироватьИмяМетаданных(Тип, Имя);			
			МассивТипов.Добавить(Тип(ИмяТипаМетаданных));
			
		КонецЕсли;	
		
	КонецЦикла;
	
	Типы = Новый ОписаниеТипов(МассивТипов);
	
	Возврат Типы;	
		
КонецФункции

// Проверка наличия метаданных в конфигурации
// Параметры:
//  Тип - Строка - тип метаданных (Документ, Справочник...)
//  Имя - Строка - имя метаданных
// Возвращаемое значение:
//  Булево - если метаданные есть то вернется истина 
//
Функция МетаданныеЕстьВКонфигурации(Тип, Имя) Экспорт
	
	МетаданныеЕсть = Метаданные[Тип].Найти(Имя) <> Неопределено;
	
	Возврат МетаданныеЕсть;
	
КонецФункции

#КонецОбласти // 

// Преобразует строку таблицы значений в структуру.
// Свойства структуры и их значения совпадают с колонками переданной строки.
//
// Параметры:
//  СтрокаТаблицыЗначений - СтрокаТаблицыЗначений - строка для преобразования
//  МассивКолонокИсключений - Массив - колонки для исключения
//
// Возвращаемое значение:
//  Структура - преобразованная строка таблицы значений.
//
Функция СтрокаТаблицыЗначенийВСтруктуруЭПД(СтрокаТаблицыЗначений, МассивКолонокИсключений = Неопределено) Экспорт
	
	Структура = Новый Структура;
	Для Каждого Колонка Из СтрокаТаблицыЗначений.Владелец().Колонки Цикл 
		
		Если МассивКолонокИсключений <> Неопределено Тогда
			
			Если МассивКолонокИсключений.Найти(Колонка.Имя) = Неопределено Тогда 
				Структура.Вставить(Колонка.Имя, СтрокаТаблицыЗначений[Колонка.Имя]);
			КонецЕсли;
				
		Иначе 	
			Структура.Вставить(Колонка.Имя, СтрокаТаблицыЗначений[Колонка.Имя]);
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции

// Записывает информацию в журнал регистрации с уровнем Ошибка
//
// Параметры:
//  Ссылка - ЛюбаяСсылка - ссылка к которой будет привязано сообщение
//  Описание - Строка - имя события журнала регистрации
//  Текст - Строка - комментарий журнала регистрации
//
Процедура ЗаписатьОшибкуВЖурналРегистрации(Ссылка, Описание, Текст) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьЖурналаРегистрации(
		Описание,
		УровеньЖурналаРегистрации.Ошибка,
		, // ОбъектМетаданных - ссылка на элемент справичника или документ
		Ссылка,
		Текст,
    	РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
	
КонецПроцедуры

// Десериализация значений из строки в значение
// Параметры:
//	Строка - Строка - сереиализованное значение
//
// Возвращаемое значение:
//	Значение - значение любого типа, полученное после десериализации
//
Функция ДесериализоватьЗначение(Знач Строка) Экспорт
	
	Если Не ЗначениеЗаполнено(Строка) Тогда 
		Значение = Новый Структура;
	Иначе 	
	    Значение = ЗначениеИзСтрокиВнутр(Строка);
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Сериализация произвольных значений
// Параметры:
//	Значение - ЛюбойТип - значение для сериализации
//
// Возвращаемое значение:
//	Строка - сериализованное значение
// 
Функция СериализоватьЗначение(Знач Значение) Экспорт
	
	Если Не ЗначениеЗаполнено(Значение) Тогда 
		СтрокаЗначения = "";
	Иначе 
		СтрокаЗначения = ЗначениеВСтрокуВнутр(Значение);
	КонецЕсли;
			
	Возврат СтрокаЗначения;
		
КонецФункции

// Заполнение реквизитов формы из источника
// Параметры: 
//	Форма - ФормаКлиентскогоПриложения - форма для заполнения реквизитов
//  ДанныеЗаполнения - Структура - данные для заполнения реквизитов
//  Исключения - Массив - реквизиты исключения
//
Процедура ЗаполнитьРеквизитыФормы(Форма, ДанныеЗаполнения, Исключения = Неопределено) Экспорт	
	
	Если Исключения = Неопределено Тогда
		Исключения = Новый Массив;
	КонецЕсли;	
	
	РеквизитыФормы = Форма.ПолучитьРеквизиты();
	ТипТаблицаЗначений = Новый ОписаниеТипов("ТаблицаЗначений");	
		
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл 		
			
		Если ДанныеЗаполнения.Свойство(РеквизитФормы.Имя) Тогда
			
			Если Исключения.Найти(РеквизитФормы.Имя) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначениеИсточник = ДанныеЗаполнения[РеквизитФормы.Имя];	
			Если РеквизитФормы.ТипЗначения = ТипТаблицаЗначений Тогда 
				ЗаполнитьТаблицуЗначений(Форма, ДанныеЗаполнения, РеквизитФормы, ЗначениеИсточник);								
			Иначе
				Форма[РеквизитФормы.Имя] = ЗначениеИсточник;
			КонецЕсли;
			
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

// Чтение реквизитов формы в структуру
// Параметры: 
//	Форма - ФормаКлиентскогоПриложения - форма источник для чтения данных
//  Исключения - Массив - реквизиты исключения
//
// Возвращаемое значение:
//   Структура - реквизиты формы в структуре
//
Функция РеквизитыФормыВСтруктуру(Форма, Исключения = Неопределено) Экспорт	
	
	Если Исключения = Неопределено Тогда
		РеквизитыИсключения = Новый Массив;
	Иначе 
		РеквизитыИсключения = Исключения; 
	КонецЕсли;		
	
	Результат = Новый Структура;
	
	РеквизитыФормы = Форма.ПолучитьРеквизиты();
	ТипТаблицаЗначений = Новый ОписаниеТипов("ТаблицаЗначений");	
		
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл 		
				
		Если РеквизитыИсключения.Найти(РеквизитФормы.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если РеквизитФормы.ТипЗначения = ТипТаблицаЗначений Тогда 
			// нет обработки таблиц значений
			Продолжить;
		Иначе
			
			Результат.Вставить(РеквизитФормы.Имя, Форма[РеквизитФормы.Имя]);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Очистка значений всех реквизитов формы
// Параметры: 
//	Форма - ФормаКлиентскогоПриложения - форма для очистки реквизитов
//  РеквизитыИсключения - Массив - реквизиты для исключения очистки
//
Процедура ОчиститьРеквизитыФормы(Форма, РеквизитыИсключения = Неопределено) Экспорт
	
	Если РеквизитыИсключения = Неопределено Тогда 
		РеквизитыИсключения = Новый Массив;
	КонецЕсли;
	
	Для Каждого РеквизитФормы Из Форма.ПолучитьРеквизиты() Цикл
		Если РеквизитыИсключения.Найти(РеквизитФормы.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(Форма[РеквизитФормы.Имя]) = Тип("ДанныеФормыКоллекция") Тогда
			Форма[РеквизитФормы.Имя].Очистить();
		Иначе
			Форма[РеквизитФормы.Имя] = "";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Очистка указанных реквизитов объекта
// Параметры:
// 	ОбъектОчистки - Объект - объект для очистки, может быть документ или справочник  
//	РеквизитыДляОчистки - Массив - реквизиты объекта для очистки 
//
Процедура ОчисткаРеквизитовОбъекта(ОбъектОчистки, РеквизитыДляОчистки) Экспорт
		
	Если Не ЗначениеЗаполнено(РеквизитыДляОчистки) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ИмяРеквизита Из РеквизитыДляОчистки Цикл
		
		ЭтоТаблица = Ложь;
		
		ТипЗначенияСтрокой = НРег(ТипЗнч(ОбъектОчистки[ИмяРеквизита]));
		Фрагменты = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(ТипЗначенияСтрокой, ":", Ложь);
		
		Если Фрагменты.Количество() > 1 Тогда
			
			ОсновнаяЧасть = Фрагменты[0]; 			
			Если Найти(ОсновнаяЧасть, "табличная часть") 
				Или Найти(ОсновнаяЧасть, "tabular section") Тогда
				ЭтоТаблица = Истина;
			КонецЕсли;
			
		КонецЕсли;
	
		Если ЭтоТаблица Тогда
			ОбъектОчистки[ИмяРеквизита].Очистить();
		Иначе
			ОбъектОчистки[ИмяРеквизита] = "";
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Создает новую структуру, где присутствуют только заполненные значения
//
// Параметры:
//  ИсходнаяСтруктура - Структура - структура для исключения пустых значений
//  Исключения - Массив - исключения для корректной итоговой структуры
//
// Возвращаемое значение:
//   Структура - новая структура без пустых вложений
//
Функция СтруктураБезПустыхЗначений(ИсходнаяСтруктура, Исключения = Неопределено) Экспорт
	
	РезультатФункции = Новый Структура;
	Если Исключения = Неопределено Тогда
		СписокИсключений = Новый Массив;
	Иначе
	    СписокИсключений = Исключения;
	КонецЕсли;
		
	Для Каждого КлючЗначение Из ИсходнаяСтруктура Цикл		
		
		НужноДобавить = СписокИсключений.Найти(КлючЗначение.Ключ) <> Неопределено
			           		Или ЗначениеЗаполнено(КлючЗначение.Значение)
							Или ТипЗнч(КлючЗначение.Значение) = Тип("Массив");
		
		Если НужноДобавить Тогда
			РезультатФункции.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

// Создает новую структуру, полностью копирующую входящую
//
// Параметры:
//  ИсходнаяСтруктура - Структура - структура для копирования
//
// Возвращаемое значение:
//   Структура - новая структура, копирующая входящую 
//
Функция СкопироватьСтруктуру(ИсходнаяСтруктура) Экспорт
	
	НоваяСтруктура = ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(ИсходнаяСтруктура));
	Возврат НоваяСтруктура;
	
КонецФункции

// Удаление свойств из структуры
// 
// Параметры:
//  Структура - Структура - структура из которой требуется удалить свойства 
//  Элементы - Массив - элементы для удаления
//
Процедура УдалитьСвойстваИзСтруктуры(Структура, Элементы) Экспорт
	
	Для Каждого ЭлементМассива Из Элементы Цикл
		Если Структура.Свойство(ЭлементМассива) Тогда
			Структура.Удалить(ЭлементМассива);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

// Устанавливает указанные типы элемента формы, если они есть в конфигурации
//
// Параметры:
//  ЭлементФормы - ПолеФормы - поле, в которое устанавливается ограничение типа
//  МассивПроверяемыхТипов - Массив - массив строк типов
//  ТипПоУмолчанию - ОписаниеТипов - тип для подстановки, если типы не доступны в конфигурации
//
Процедура УстановитьОграниченияТипов(ЭлементФормы, МассивПроверяемыхТипов, ТипПоУмолчанию = Неопределено) Экспорт
	
	ТипыОграничений = ДоступныеТипыМетаданных(МассивПроверяемыхТипов);
	
	Если ЗначениеЗаполнено(ТипыОграничений) Тогда 
		ЭлементФормы.ОграничениеТипа = ТипыОграничений;
	ИначеЕсли ТипПоУмолчанию <> Неопределено Тогда
		ЭлементФормы.ОграничениеТипа = ТипПоУмолчанию;
	Иначе 
		ЭлементФормы.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает указанные параметры выбора для поля формы
//
// Параметры:
//  ЭлементФормы - ПолеФормы - поле, в которое устанавливается ограничение типа
//  МассивПараметровВыбора - Массив - массив параметров выбора для установки
//
Процедура УстановитьПараметрыВыбора(ЭлементФормы, МассивПараметровВыбора) Экспорт
	
	Если ЭлементФормы.ОграничениеТипа.Типы().Количество() = 0
		Или ЭлементФормы.ОграничениеТипа.СодержитТип(Тип("Строка")) Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементФормы.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	
КонецПроцедуры

#КонецОбласти // Универсальные_методы

#Область КонтактнаяИнформация

// Переопределяемая процедура под свою конфигурацию
// 
// Параметры:
// 	СсылкаИлиОбъект - ДокументОбъект - ссылка или объект-владелец контактной информации
// 	ВидИлиТипКИ - СправочникСсылка.ВидыКонтактнойИнформации - отбор по виду контактной информации.
//              - ПеречислениеСсылка.ТипыКонтактнойИнформации - отбор по типу контактной информации.
//  Дата - Дата - дата среза КИ
//  ТолькоПредставление - Булево - получение только представления, без значения полей
//
// Возвращаемое значение:
//  Структура - данные КИ объекта, в зависимости от типа/вида
//
Функция КонтактнаяИнформацияОбъекта(СсылкаИлиОбъект, ВидИлиТипКИ = Неопределено,
		Дата = Неопределено, ТолькоПредставление = Ложь) Экспорт
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	КонтрактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
		СсылкаИлиОбъект, 
		ВидИлиТипКИ, 
		Дата, 
		ТолькоПредставление);
	
	// Обработка возвращаем одиночное значение
	СтруктураАдреса = Новый Структура;
	СтруктураАдреса.Вставить("Значение",  "");
	СтруктураАдреса.Вставить("Структура", "");
	
	Если НЕ ТолькоПредставление И АдресКонтрагентаИлиОрганизации(ВидИлиТипКИ) Тогда
		
		Если КонтрактнаяИнформация.Количество() Тогда
			
			СтрокаКИ = КонтрактнаяИнформация[0];
			СтруктураАдреса.Значение  = СтрокаКИ.Представление;
			СтруктураАдреса.Структура = СтрокаКИ.Значение;
			
		КонецЕсли;
		
		Возврат СтруктураАдреса;
		
	Иначе
		Возврат КонтрактнаяИнформация;
	КонецЕсли;
	
КонецФункции

#Область ДанныеЮрЛиц

// Возвращает выборку данных юрлиц
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, на которой выводятся данные юр. лиц
//  Роли - Строка - роли юр. лиц, разделенных запятыми, по которым осуществляется обновление.
//    Каждая рольв строке должна соответсвовать имени из перечисления Saby_РолиКонтрагентов.
//  ТаблицаЮрЛиц - ТаблицаЗначений - данные по юрлицам
//
// Возвращаемое значение:
//   ВыборкаИзРезультатаЗапроса - выборка данных юрлиц
//
Функция ВыборкаДанныхЮрЛиц(Форма, Роли, ТаблицаЮрЛиц = Неопределено) Экспорт
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	Если ТаблицаЮрЛиц = Неопределено Тогда
		ФормаОбъект = МодульТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
		ТаблицаЮрЛиц = МодульТНОбщегоНазначенияКлиентСервер.ТаблицаЮрЛиц(Форма, Роли, ФормаОбъект);
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	МассивЮрЛиц = МодульТНОбщегоНазначенияКлиентСервер.ВыгрузитьКолонкуЮрЛиц(ТаблицаЮрЛиц, "Ссылка");
	
	МассивТиповСсылок = Новый Массив;
	МассивТиповСсылок.Добавить(Тип("СправочникСсылка.Организации"));
	МассивТиповСсылок.Добавить(Тип("СправочникСсылка.Контрагенты"));
	
	ОписаниеТипаРоли = Новый ОписаниеТипов("ПеречислениеСсылка.Saby_РолиКонтрагентов");
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(МассивТиповСсылок));
	ТаблицаДанных.Колонки.Добавить("Роль",   ОписаниеТипаРоли);
	
	Для Каждого СтруктураДанных Из ТаблицаЮрЛиц Цикл
		СтрокаДанных = ТаблицаДанных.Добавить();
		СтрокаДанных.Ссылка = СтруктураДанных.Ссылка;
		СтрокаДанных.Роль   = СтруктураДанных.Роль;
	КонецЦикла;
	
	ПолучитьДанныеЮрЛицИзБазы(МассивЮрЛиц, МенеджерВременныхТаблиц);
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЮрЛиц.Ссылка КАК Ссылка,
	|	ТаблицаЮрЛиц.Роль КАК Роль
	|ПОМЕСТИТЬ ДанныеТаблицыЮрЛиц
	|ИЗ
	|	&ТаблицаЮрЛиц КАК ТаблицаЮрЛиц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеТаблицыЮрЛиц.Ссылка КАК Ссылка,
	|	ДанныеТаблицыЮрЛиц.Роль КАК Роль,
	|	ДанныеЮрЛицИзБазы.ИНН КАК ИНН,
	|	ДанныеЮрЛицИзБазы.КПП КАК КПП,
	|	ДанныеЮрЛицИзБазы.СтранаРегистрации КАК СтранаРегистрации,
	|	ДанныеЮрЛицИзБазы.ОГРН КАК ОГРН,
	|	ДанныеЮрЛицИзБазы.ЮрФизЛицо КАК ЮрФизЛицо,
	|	ДанныеЮрЛицИзБазы.Наименование КАК Наименование,
	|	ДанныеЮрЛицИзБазы.Наименование КАК НаименованиеОрганизации,
	|	ДанныеЮрЛицИзБазы.КодФилиала КАК КодФилиала
	|ИЗ
	|	ДанныеТаблицыЮрЛиц КАК ДанныеТаблицыЮрЛиц
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеЮрЛицИзБазы КАК ДанныеЮрЛицИзБазы
	|		ПО ДанныеТаблицыЮрЛиц.Ссылка = ДанныеЮрЛицИзБазы.Ссылка";

	ЗапросДанных.УстановитьПараметр("ТаблицаЮрЛиц", ТаблицаДанных);

	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	
	МенеджерВременныхТаблиц.Закрыть();
	
	Возврат ВыборкаДанных;
	
КонецФункции

// Читает строковое значение JSON в структуру на сервере, так как чтение JSON не доступно в вэб клиенте.
//
// Параметры:
//  СтрокаJSON - Строка - строка с валидным JSON для чтения
//
// Возвращаемое значение:
//   Структура - прочитанные данные JSON
//
Функция ПрочитатьJSONДляВебКлиента(СтрокаJSON) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	РезультатФункции = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	Возврат РезультатФункции;
	
КонецФункции

// Возвращает признак наличия организации в справочнике организаций по ИНН и КПП.
//
// Параметры:
//  ИНН - Строка - ИНН организации
//  КПП - Строка - КПП организации
//
// Возвращаемое значение:
//   Булево - признак наличия организации в базе
//
Функция ОрганизацияПрисутствуетВБазе(ИНН, КПП) Экспорт
	
	Результат = ОрганизацияКонтрагент(ИНН, КПП);
	
	Возврат Результат.Ссылка <> Неопределено И Результат.ЭтоОрганизация;
	
КонецФункции

// Возвращает ссылку на организацию или контрагента из базы
//
// Параметры:
//  ИНН - Строка - ИНН строкой
//  КПП - Строка - КПП строкой
//  Наименование - Строка - наименование организации
//
// Возвращаемое значение:
//   Структура - структура найденного юр лица
//
Функция ОрганизацияКонтрагент(ИНН, КПП, Наименование = Неопределено) Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Ссылка",         Неопределено);
	РезультатФункции.Вставить("ЭтоОрганизация", Ложь);
	
	МассивЗапросов = Новый Массив;
	
	МассивЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Ссылка,
		|	1 КАК Порядок,
		|	ИСТИНА КАК ЭтоОрганизация
		|ПОМЕСТИТЬ НайденныеЮрЛица
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ИНН = &ИНН
		|	И Организации.КПП = &КПП");
	
	МассивЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Ссылка,
		|	2 КАК Порядок,
		|	ЛОЖЬ КАК ЭтоОрганизация
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И Контрагенты.КПП = &КПП");
	
	Если ЗначениеЗаполнено(Наименование) Тогда
		МассивЗапросов.Добавить(
			"ВЫБРАТЬ
			|	Организации.Ссылка КАК Ссылка,
			|	3 КАК Порядок,
			|	ИСТИНА КАК ЭтоОрганизация
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.Наименование = &Наименование");
		
		МассивЗапросов.Добавить(
			"ВЫБРАТЬ
			|	Контрагенты.Ссылка КАК Ссылка,
			|	4 КАК Порядок,
			|	ЛОЖЬ КАК ЭтоОрганизация
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.Наименование = &Наименование");
	КонецЕсли;
	
	РазделительОбъединение = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросДанных.УстановитьПараметр("ИНН",          ИНН);
	ЗапросДанных.УстановитьПараметр("КПП",          КПП);
	ЗапросДанных.УстановитьПараметр("Наименование", Наименование);
	ЗапросДанных.Текст = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(МассивЗапросов, РазделительОбъединение);
	Если Не ЗначениеЗаполнено(КПП) Тогда
		ЗапросДанных.Текст = СтрЗаменить(ЗапросДанных.Текст, "Организации.КПП = &КПП", "ИСТИНА");
		ЗапросДанных.Текст = СтрЗаменить(ЗапросДанных.Текст, "Контрагенты.КПП = &КПП", "ИСТИНА");
	КонецЕсли;
	ЗапросДанных.Выполнить();
	
	ЗапросДанных.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НайденныеЮрЛица.Ссылка КАК Ссылка,
	|	НайденныеЮрЛица.ЭтоОрганизация КАК ЭтоОрганизация
	|ИЗ
	|	НайденныеЮрЛица КАК НайденныеЮрЛица
	|
	|УПОРЯДОЧИТЬ ПО
	|	НайденныеЮрЛица.Порядок";
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(РезультатФункции, ВыборкаДанных);
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // ДанныеЮрЛиц

// см. УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации
//
// Параметры:
//    ТипКонтактнойИнформации - ПеречислениеСсылка.ТипыКонтактнойИнформации
//                            - Строка - тип контактной информации,
//                            - СправочникСсылка.ВидыКонтактнойИнформации - вид контактной информации для
//                              заполнения свойства НастройкиПроверки.
//
// Возвращаемое значение:
//   Структура - см. УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации
//
Функция ПараметрыВидаКонтактнойИнформации(Знач ТипКонтактнойИнформации) Экспорт
	Возврат УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(ТипКонтактнойИнформации);
КонецФункции

// Преобразует представление контактной информации во внутренний формат JSON.
//
// Для адресов, введенных в свободной форме, корректное преобразование не гарантируется.
//
//  Параметры:
//      Значение - Строка  - контактная информация в формате JSON 
//      ТипКонтактнойИнформации - ПеречислениеСсылка.ТипыКонтактнойИнформации
//
// Возвращаемое значение:
//      Строка - контактная информация в виде JSON.
//
Функция КонтактнаяИнформацияПоПредставлению(Значение, ТипКонтактнойИнформации) Экспорт
	
	// ToDo: Убрать в Универсальном расширении 
	// в ВО для типовых модулей нужно перечисление... 
	Если ТипЗнч(ТипКонтактнойИнформации) = Тип("Строка") Тогда
		Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации." + ТипКонтактнойИнформации);
	Иначе
		Тип = ТипКонтактнойИнформации;
	КонецЕсли;	
	
	Возврат УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Значение, Тип);
	
КонецФункции

// Установим значения в списках выбора типов КИ в колонке таблицы формы
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - источник вызова 
//  МассивЭлементов - Массив - элементы для заполнения списка выбора 
//
Процедура УстановкаСпискаДоступныхТиповКИ(Форма, МассивЭлементов) Экспорт
	
	// Типы КИ
	СписокЗначенийКИ = Новый СписокЗначений;
	СписокЗначенийКИ.Добавить(
		ЗначениеМетаданных("ТипыКонтактнойИнформации.Телефон"), "Телефон");
	СписокЗначенийКИ.Добавить(
		ЗначениеМетаданных("ТипыКонтактнойИнформации.АдресЭлектроннойПочты"), "Адрес электронной почты");
			
	Для Каждого ЭлементФормы Из МассивЭлементов Цикл
		
		Форма.Элементы[ЭлементФормы].РежимВыбораИзСписка = Истина;
		
		Форма.Элементы[ЭлементФормы].СписокВыбора.Очистить();
		
		Для Каждого ЭлементСписка Из СписокЗначенийКИ Цикл  
		   Форма.Элементы[ЭлементФормы].СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	   КонецЦикла;		
	КонецЦикла;
	   
КонецПроцедуры

#КонецОбласти // КонтактнаяИнформация

#Область ОбщиеСбис

// Получает параметры СБИС объекта документа из базы.
//
// Параметры:
//  ОбъектДок - ДокументОбъект - объект документа для получения параметров.
//    Например в качестве параметра может передаваться ДокументОбъект.Saby_ТранспортнаяНакладная.
//
// Возвращаемое значение:
//   Структура - имеет всегда фиксированны набор ключей
//       * ИД - Строка - новый текст запроса. 
//       * АктивныйЭтап - Строка - новый текст запроса.
//       * Состояние - Строка - новый текст запроса.
//
Функция ТекущийТитулИИДДокумента(ОбъектДок) Экспорт
	
	// признаки для расчета состояния
	ДОЗавершенУспешно      = 7;
	ДОЗавершенОтрицательно = 9;
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ИД",                     "");
	РезультатФункции.Вставить("АктивныйЭтап",           Неопределено);
	РезультатФункции.Вставить("Состояние",              Неопределено);
	РезультатФункции.Вставить("ДОЗавершенПоложительно", Ложь);
	РезультатФункции.Вставить("ДОЗавершенОтрицательно", Ложь);
	
	Если Не ЗначениеЗаполнено(ОбъектДок.Ссылка) Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	Данные = ДанныеСостоянияОбъекта(ОбъектДок.Ссылка);
	Если Данные <> Неопределено Тогда 
		
		РезультатФункции.ИД           = Данные.UID;
		РезультатФункции.АктивныйЭтап = Данные.АктивныйЭтап;
		РезультатФункции.Состояние    = Данные.Состояние;
				
		Если ЗначениеЗаполнено(Данные.Состояние) Тогда
			
			КодСостояния = МодульКода("Saby_ТНОбщегоНазначенияСервер").ЗначениеРеквизитаОбъекта(Данные.Состояние, "ИдентификаторСБИС");
			
			РезультатФункции.ДОЗавершенПоложительно = (КодСостояния = ДОЗавершенУспешно);
			РезультатФункции.ДОЗавершенОтрицательно = (КодСостояния = ДОЗавершенОтрицательно);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

// Получает идентификатор загруженного документа в СБИС
//
// Параметры:
//  Ссылка - ДокументСсылка.Saby_ТранспортнаяНакладная - ссылка на документ для получения идентификатора.
//
// Возвращаемое значение:
//   Строка - идентификатор документа.
//
Функция ИдентификаторВСбис(Ссылка) Экспорт
	
	Если ТипЗнч(Ссылка) = Тип("Структура") Тогда
		УИД = Ссылка.Идентификатор;
	Иначе
		// ToDo: Вынести в общий модуль. Получение уид и активного этапа. Использовать еще при создани дока 
		Выборка = ДанныеСостоянияОбъекта(Ссылка);
		Если Выборка <> Неопределено Тогда
			УИД = Выборка.UID;
		Иначе 
			УИД = "";
		КонецЕсли;
	КонецЕсли;
	
	Возврат УИД;
	
КонецФункции

// Возвращает ссылку на документ ЭТрН по идентификатору СБИС
//
// Параметры:
//  ИдСбис - Строка - идентификатор документа в СБИС
//
// Возвращаемое значение:
//   ДокументСсылка.Saby_ТранспортнаяНакладная - Ссылка на документ. Если не найден, то Неопределено
//
Функция ДокументПоИдентификаторуСбис(ИдСбис) Экспорт
	
	СсылкаДокВБазе = Неопределено;
	
    Аккаунт = АвторизованныйАккаунтСтрокой();
	
	ВыборкаСостояния = МодульКода("РегистрыСведений.Saby_Состояние").ПрочитатьПоUID(ИДСбис, , Аккаунт);
	Если ВыборкаСостояния.Следующий() Тогда
		СсылкаДокВБазе = ВыборкаСостояния.Объект;
	КонецЕсли;

	Возврат СсылкаДокВБазе;
	
КонецФункции

// Очищает отметки и связанные с ними таблицы документа.
//
// Параметры:
//  ОбъектДок - ДокументОбъект.Saby_ТранспортнаяНакладная - объект документа для зачистки отметок.
//  Титул - ПеречислениеСсылка.Saby_ТипТитулаЭтрН - тип титула для зачистки отметок.
//  ЗначениеРоли - ПеречислениеСсылка.Saby_РолиКонтрагентов - роль контрагента для зачистки отметок.
//
Процедура ЗачисткаОтметокШтрафовАктов(ОбъектДок, Титул, ЗначениеРоли) Экспорт
	
	Если ТипЗнч(ОбъектДок) <> Тип("Структура")
		И МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ЕстьРеквизитИлиСвойство(ОбъектДок, "Ссылка")
		И Не ЗначениеЗаполнено(ОбъектДок.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	КлючиОтметокДляУдаления = Новый Соответствие;
	
	ИндексСтроки = 0;
	Пока ИндексСтроки < ОбъектДок.Отметки.Количество() Цикл
		СтрокаОтметки = ОбъектДок.Отметки[ИндексСтроки];
		
		Если СтрокаОтметки.Этап = Титул И СтрокаОтметки.Роль = ЗначениеРоли Тогда
			КлючиОтметокДляУдаления.Вставить(СтрокаОтметки.КлючСтроки, Истина);
			ОбъектДок.Отметки.Удалить(ИндексСтроки);
		Иначе
			ИндексСтроки = ИндексСтроки + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	ЗачиститьШтрафы(ОбъектДок, КлючиОтметокДляУдаления);
	
	ЗачиститьАкты(ОбъектДок, КлючиОтметокДляУдаления);
	
КонецПроцедуры

#Область ДатаВремя

// Преобразовывает дату, полученную с онлайна, в тип дата.
//
// Параметры:
//  ДатаВходящая - Строка - дата в виде строки, полученная с онлайна.
//
// Возвращаемое значение:
//   Дата - значение даты
//
Функция ПреобразоватьСтрокуВДатуЭПД(ДатаВходящая) Экспорт
	
	Если ТипЗнч(ДатаВходящая) = Тип("Дата") Тогда
		Возврат ДатаВходящая;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаВходящая) Тогда
		Возврат Дата(1, 1, 1);
	КонецЕсли;
	
	// ранее 24.01.2023 12.00.00
	// 15.03.2022T00:00:00+01:00
	// 15.03.2022T00:00:00-01:00
	// 2023-02-02 07:33:30
	// 2023-02-02
	// 2023.01.03
	ФорматБезСмещения = Ложь;
		
	ЕстьВремя = (Найти(ДатаВходящая, "T") > 0);
	
	ЧислоДвоеточийВДате = 2;
	
	// часть дат может приходить в старом формате
	Если Не ЕстьВремя Тогда
		
		ЧислоТочекВДате = 4;
		
		Если СтрЧислоВхождений(ДатаВходящая, ".") = ЧислоТочекВДате
			Или СтрЧислоВхождений(ДатаВходящая, ":") = ЧислоДвоеточийВДате Тогда
			ЕстьВремя = Истина;
			ФорматБезСмещения = Истина;
		КонецЕсли;
	КонецЕсли;	
	
	Если ЕстьВремя Тогда
		
		Если ФорматБезСмещения Тогда 
			Разделитель = " ";
		Иначе 
			Разделитель = "T";			
		КонецЕсли;
		
		МассивЧастейДаты = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(ДатаВходящая, Разделитель);
		ЧастьДата  = МассивЧастейДаты[0];
		ЧастьВремя = МассивЧастейДаты[1];
		
	Иначе
		
		ЧастьДата = ДатаБезВремениВСтандартномФормате(ДатаВходящая);		
		
	КонецЕсли;
				
	МассивДат = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(ЧастьДата, ".", Ложь);
	
	МассивИтог = Новый Массив;
	МассивИтог.Добавить(МассивДат[2]); 
	МассивИтог.Добавить(МассивДат[1]);
	МассивИтог.Добавить(МассивДат[0]); 		
	
	СмещениеВСекундах = Неопределено;
	
	ДобавитьВремяВМассивДаты(МассивИтог, ЧастьВремя, ЕстьВремя, ФорматБезСмещения, СмещениеВСекундах);
	
	ДатаСтрокой = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(МассивИтог, "");
	
	Возврат ?(ЗначениеЗаполнено(СмещениеВСекундах), Дата(ДатаСтрокой) - СмещениеВСекундах, Дата(ДатаСтрокой)); 
	
КонецФункции	

// Преобразовывает дату, полученную с онлайна, в тип дата.
//
// Параметры:
//  ВремяВходящее - Строка - дата в виде строки, полученная с онлайна.
//
// Возвращаемое значение:
//   Время - значение времени
//
Функция ПреобразоватьСтрокуВоВремя(ВремяВходящее) Экспорт
	
	Если ТипЗнч(ВремяВходящее) = Тип("Дата") Тогда
		Возврат ВремяВходящее;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВремяВходящее) Тогда
		Возврат Дата(1, 1, 1);
	КонецЕсли;
	
	// 00:00:00+01:00
	// 00:00:00-01:00
	// может быть +/-
	Если Найти(ВремяВходящее, "+") > 0 Тогда
		РазделительСмещение = "+";
	Иначе
		РазделительСмещение = "-";
	КонецЕсли;
	
	МассивВремени = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(ВремяВходящее, РазделительСмещение);
	Время         = МассивВремени[0];
	
	ПервыйМесяц = "01";
	ПервоеЧисло = "01";
	
	МассивИтог = Новый Массив;
	МассивИтог.Добавить("0001");
	МассивИтог.Добавить(ПервыйМесяц);
	МассивИтог.Добавить(ПервоеЧисло);
		
	ДобавитьВремяВМассивДаты(МассивИтог, Время, Истина, Истина, 0);
	
	ДатаСтрокой = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(МассивИтог, "");
	
	Возврат Дата(ДатаСтрокой); 

КонецФункции
	
#КонецОбласти // ДатаВремя

// Возвращает структуру адреса в формате обмена со СБИС
// Параметры:
//  Выгрузка - Белево - признак того что адрес будет использован для выгрузки в онлайн
//
// Возвращаемое значение:
//   Структура - структура адреса в формате обмена
//
Функция ОбщийШаблонАдреса(Выгрузка = Ложь) Экспорт
	
	Структура = Новый Структура;
	
	// из-за различий формата
	Структура.Вставить("Текст",      "");
	Структура.Вставить("АдресТекст", "");	
	Структура.Вставить("GLN",        "");
	Структура.Вставить("КодГАР",     "");
	Структура.Вставить("КодСтраны",  "643");
	
	Координаты = Новый Структура;
	Координаты.Вставить("Широта",  "");
	Координаты.Вставить("Долгота", "");
	
	Структура.Вставить("Координаты", Координаты);
	
	Если Не Выгрузка Тогда 
		
		СтруктураАдрес = Новый Структура;
		
		СтруктураАдрес.Вставить("Индекс",          "");
		СтруктураАдрес.Вставить("КодРегиона",      "");
		СтруктураАдрес.Вставить("Район",           "");
		СтруктураАдрес.Вставить("Город",           "");
		СтруктураАдрес.Вставить("НаселенныйПункт", "");
		СтруктураАдрес.Вставить("Улица",           "");
		СтруктураАдрес.Вставить("Дом",             "");
		СтруктураАдрес.Вставить("Корпус",          "");
		СтруктураАдрес.Вставить("Квартира",        "");
		
		Структура.Вставить("Структура", СтруктураАдрес);
		
		Структура.Вставить("Типовой",   "");
		
	КонецЕсли;
	 
	Возврат Структура;
		
КонецФункции

// Получает ставку НДС либо в виде представления, либо в виде кода.
//
// Параметры:
//  Значение - Строка - или код с онлайна или представление ставки НДС
//  Загрузка - Булево - назначение использования функции. Истина - для загрузки, ложь - для выгрузки.
//
// Возвращаемое значение:
//   Строка - если Загрузка = Истина, то возвращает представление ставки НДС
//            если Загрузка = Ложь, то возвращает код ставки НДС, который используется на онлайне.
//
Функция ЗначениеСтавкиНДС(Значение, Загрузка = Ложь) Экспорт
	
	Параметры = ПараметрыСоответствия();
	Параметры.ИмяПеречисления = "";
	Параметры.ПоискПоЗначению = Не Загрузка;
	
	ДополнитьСоответствие(Параметры, "1", "без НДС");
	ДополнитьСоответствие(Параметры, "2", "0%");
	ДополнитьСоответствие(Параметры, "3", "10%");
	ДополнитьСоответствие(Параметры, "4", "20%");
	
	Ставка = Параметры.Соответствие.Получить(Значение);
	
	Если Загрузка
		И Ставка = Неопределено Тогда
		
		Ставка = Параметры.Соответствие.Получить("без НДС");
	КонецЕсли;
	
	Возврат Ставка;
	
КонецФункции

// Обновляем надпись строки ответственного
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма для обновления реквизитов
//	Реквизит - Строка - имя реквизита обновления
//	Значение - Строка - сериализорванное значение, для получения структуры
//	         - Структура - готовая структура
//
Процедура ОбновитьОтветственногоНаФорме(Форма, Реквизит, Значение) Экспорт
	
	Форма[Реквизит] = "";
	Если ЗначениеЗаполнено(Значение) Тогда
		
		Если ТипЗнч(Значение) <> Тип("Структура") Тогда
			Структура = ДесериализоватьЗначение(Значение);
		Иначе 
			Структура = Значение;
		КонецЕсли;
			
		ФИО = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ПредставлениеФИО(Структура);
		Форма[Реквизит] = ФИО;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Форма[Реквизит]) Тогда
		Форма[Реквизит] = "Заполнить";
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает отбор по роли в таблице ответственных лиц для визуального отображения необходимых ответственных
//
// Параметры:
//  ТаблицаОтветственных - ТаблицаФормы - таблица формы для установки отбора по роли
//  Роль - Строка,Перечисление.Saby_РолиОтветственных - роль, по которой необходимо установить отбор
//
Процедура УстановитьОтборТаблицыОтветственных(ТаблицаОтветственных, Роль) Экспорт
	
	Если ТипЗнч(Роль) = Тип("Строка") Тогда
		РольСсылка = ЗначениеМетаданных("Saby_РолиОтветственных." + Роль);
	Иначе
		РольСсылка = Роль;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Роль", РольСсылка);
	
	ОтборСтрок = Новый ФиксированнаяСтруктура(Отбор);
	
	ТаблицаОтветственных.ОтборСтрок = ОтборСтрок;
	
КонецПроцедуры

// Дозаполняет дополнительные параметры выгрузки на основании уже заполненных данных в параметрах выгрузки
//
// Параметры:
//  ПараметрыВыгрузки - Структура - см. Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыВыгрузки
//
Процедура ЗаполнитьПараметрыВыгрузки(ПараметрыВыгрузки) Экспорт
	
	ИдентификаторСБИС = "";
	Этап = ПараметрыВыгрузки.Этап;
	ДанныеСостояния = ДанныеСостоянияОбъекта(ПараметрыВыгрузки.Ссылка);
	Если ДанныеСостояния <> Неопределено Тогда
		ИдентификаторСБИС = ДанныеСостояния.UID;
		Если Не ЗначениеЗаполнено(Этап) Тогда
			Этап = ДанныеСостояния.АктивныйЭтап;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыВыгрузки.Ссылка) = Тип("Структура") Тогда
		ПараметрыВыгрузки.ИмяМетаданных = ПараметрыВыгрузки.Ссылка.ИмяМетаданных;
	Иначе
		ПараметрыВыгрузки.ИмяМетаданных = ПараметрыВыгрузки.Ссылка.Метаданные().Имя;
	КонецЕсли;
	
	ТипТитула = РезультатФункцииТитулов(ПараметрыВыгрузки.ИмяМетаданных, "ЗначениеПоПредставлению", Этап);
	Если Не ЗначениеЗаполнено(ТипТитула) Тогда
		ТипТитула = РезультатФункцииТитулов(ПараметрыВыгрузки.ИмяМетаданных, "ПервыйТитул");
	КонецЕсли;
	
	ПредставленияТитула = РезультатФункцииТитулов(ПараметрыВыгрузки.ИмяМетаданных, "ПредставленияТитула", ТипТитула);
	
	ПараметрыВыгрузки.Этап              = ПредставленияТитула.Представление;
	ПараметрыВыгрузки.ТипТитула         = ТипТитула;
	ПараметрыВыгрузки.КодТитула         = ПредставленияТитула.КодТитула;
	ПараметрыВыгрузки.ИдентификаторСБИС = ИдентификаторСБИС;
	ПараметрыВыгрузки.ДинамическийТитул = РезультатФункцииТитулов(
		ПараметрыВыгрузки.ИмяМетаданных, "ЭтоДинамическийТитул", ТипТитула);
		
	ПараметрыВыгрузки.ПараметрыДляАПИ = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыДокументаДляАПИ(
		ПараметрыВыгрузки.ИмяМетаданных);
	
КонецПроцедуры

// Сброс статуса "не просмотрен" у документа
// Параметры:
//  СписокДокументов - Массив - документы для обработки
//
Процедура СбросСтатусаНеПросмотрено(Знач СписокДокументов) Экспорт
	
	Для Каждого Документ Из СписокДокументов Цикл
		ДанныеСостояния = ДанныеСостоянияОбъекта(Документ);
		Если ДанныеСостояния = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыСостояния = ПараметрыЗаписиРегистраСостояний();
		ПараметрыСостояния.ИД           = ДанныеСостояния.UID;
		ПараметрыСостояния.АктивныйЭтап = СокрЛП(ДанныеСостояния.АктивныйЭтап);
		ПараметрыСостояния.Ссылка       = Документ;
		ПараметрыСостояния.Состояние    = ДанныеСостояния.Состояние;
		
		ОбновитьДанныеСостояния(ПараметрыСостояния);
		
	КонецЦикла;
	
	МодульКода("РегистрыСведений.Saby_ДанныеТитулов").Просмотреть(СписокДокументов);
	
КонецПроцедуры

// Возвращает дату последней успешной загрузки.
//
// Параметры:
//  ИмяМетаданных - Строка - имя метаданных документа
//
// Возвращаемое значение:
//   Дата - дата последней успешной загрузки.
//
Функция ПоследнееУспешноЗагруженноеИзменение(ИмяМетаданных) Экспорт
	
	ДатаЗагрузки = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(Saby_ТранспортнаяНакладная.ДатаИзменения) КАК ДатаИзменения
	|ИЗ
	|	Документ.Saby_ТранспортнаяНакладная КАК Saby_ТранспортнаяНакладная";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Saby_ТранспортнаяНакладная", ИмяМетаданных);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.ДатаИзменения) Тогда
			ДатаЗагрузки = Выборка.ДатаИзменения;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДатаЗагрузки;
	
КонецФункции

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияСервер_ЗаполнениеНаОснованииИНИ
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияСервер_ЗаполнениеНаОснованииИНИ

// Возвращат структуру основания для заполнения из INI
//
// Параметры:
//  Основание - Ссылка - ссылка на основание заполнения
//  ИмяИНИ - Строка - наименование ини файла
//  РольКонтрагента - ПеречислениСсылка.Saby_РолиКонтрагентов - роль контрагента для заполнения
//
// Возвращаемое значение:
//   Структура - структура с данными основания
//
Функция СтруктураОснования(Основание, ИмяИНИ, РольКонтрагента = "") Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Основание",       Основание);
	РезультатФункции.Вставить("ini_name",        ИмяИНИ);
	РезультатФункции.Вставить("РольКонтрагента", РольКонтрагента);
	
	Возврат РезультатФункции;
	
КонецФункции

// Возвращает соответстивие данных юр лица из ИНИ
//
// Параметры:
//  ДанныеИзИНИ - Соответствие - данные загруженные из документа основания через ИНИ
//
// Возвращаемое значение:
//   Соответствие - структура данных юрлица
//
Функция ДанныеЮрЛицаИзИНИ(ДанныеИзИНИ) Экспорт
	
	РезультатФункции = Новый Соответствие;
	РезультатФункции.Вставить("Контакты",  ДанныеИзИНИ["Контакты"]);
	РезультатФункции.Вставить("Адрес",     ДанныеИзИНИ["Адрес"]);
	РезультатФункции.Вставить("Реквизиты", ДанныеИзИНИ["Реквизиты"]);
	РезультатФункции.Вставить("Название",  ДанныеИзИНИ["Название"]);
	
	Возврат РезультатФункции;
	
КонецФункции

// Получает актуальный статус версии расширения
//
// Возвращаемое значение:
//   Структура - данные о актуальности установленной версии расширения
//
Функция СтатусВерсииРасширения() Экспорт
	
	СтатусВерсии = Неопределено;
		
	context_params = МодульКода("Saby_Core").ПроверитьНаличиеПараметровПодключения();
	Если Не ЗначениеЗаполнено(context_params) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// context_params.service - может быть стоит проверять только боевой сервер
	
	ОбъектОбработки = ОбъектОбработки();
	СтатусВерсии    = ОбъектОбработки.ПолучитьСтатусВерсии(Ложь, СтатусВерсии);
	
	Возврат СтатусВерсии;
	
КонецФункции

// Формируем структуру параметров для обновления статусов
//
// Возвращаемое значение:
//   Структура - шаблон для записи в регистр
//
Функция ПараметрыЗаписиРегистраСостояний() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИД",           "");
	Параметры.Вставить("АктивныйЭтап", "");
	Параметры.Вставить("Ссылка",       "");
	Параметры.Вставить("Состояние",    ЗначениеМетаданных("Saby_СостоянияОбъектов.ПустаяСсылка", "Справочник"));
	Параметры.Вставить("СбисИД",       "");

	Возврат Параметры;
	
КонецФункции

// Определяет доступность титула по данным текщих этапов документа
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма для которой определяется доступность
//  ТекущиеЭтапы - ДанныеФормыКоллекция - таблица с данными о текущих этапах
//  ТипТитула - ПеречислениеСсылка.Saby_ТипТитула - тип титула документа
//  ДОЗавершен - Булево - Истина - если документооборот завершен.
//
// Возвращаемое значение:
//   Булево - этап доступен
//
Функция ТипТитулаДоступенУчастнику(Форма, ТекущиеЭтапы, ТипТитула, ДОЗавершен) Экспорт
	
	Если ТекущиеЭтапы.Количество() = 0 Тогда
		ИмяМетаданных = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ИмяМетаданныхПоФорме(Форма);
		Возврат РезультатФункцииТитулов(ИмяМетаданных, "ЭтоПервыйТитул", ТипТитула) И Не ДОЗавершен;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ТипТитула", ТипТитула);
	Отбор.Вставить("Доступен",  Истина);
	
	НайденныеСтроки = ТекущиеЭтапы.НайтиСтроки(Отбор);
	
	Возврат НайденныеСтроки.Количество() > 0;
	
КонецФункции

#КонецОбласти // ОбщиеСбис

#Область ОбработкаОшибок

// Обновляет данные по ошибкам ФЛК в форме.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма на которой необходимо обновить реквизиты и элементы ошибок.
//  ДополнительныеПараметры - Структура - дополнительные параметры необходимы
//    для указания доп параметров получения ошибок ФЛК.
//  Ошибки - Структура - состав структуры см. Saby_ТранспортнаяНакладная.ДобавитьОшибкуПользователю
//
Процедура ОбновитьОшибки(Форма, ДополнительныеПараметры, Ошибки = Неопределено) Экспорт
	
	Если Ошибки = Неопределено Тогда
		
		ПараметрыВыгрузки = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыВыгрузки(ДополнительныеПараметры.Ссылка);
		ЗаполнитьЗначенияСвойств(ПараметрыВыгрузки, ДополнительныеПараметры);
		ЗаполнитьПараметрыВыгрузки(ПараметрыВыгрузки);
		
		Если ТипЗнч(ПараметрыВыгрузки.Ссылка) = Тип("Структура") Тогда
			ПараметрыВыгрузки.ДанныеДокумента = ПараметрыВыгрузки.Ссылка;
		КонецЕсли;
		
		Результат = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").РезультатВзаимодействияСоСБИС(0);
		МодульКода("Saby_ТНВыгрузкаСервер").ЗаполнитьСтруктуруДокументаСПроверкойФЛК(ПараметрыВыгрузки, Результат, Истина);
		
		Ошибки = Результат.Ошибки[ПараметрыВыгрузки.Ссылка];
	КонецЕсли;
	
	Если Ошибки = Неопределено Тогда
		Форма.КоличествоОшибок = 0;
		Форма.ТаблицаОшибок.Очистить();
	Иначе
		Форма.КоличествоОшибок = Ошибки.СписокОшибок.Количество();
		
		Форма.ТаблицаОшибок.Очистить();
		Для Каждого СтруктураОшибки Из Ошибки.СписокОшибок Цикл
			СтрокаТаблицыОшибок = Форма.ТаблицаОшибок.Добавить();
			СтрокаТаблицыОшибок.Ошибка          = СтруктураОшибки.ТекстДляОднойОшибки;
			Если СтруктураОшибки.Свойство("РеквизитОбъекта") Тогда
				СтрокаТаблицыОшибок.РеквизитОбъекта = СтруктураОшибки.РеквизитОбъекта;
				СтрокаТаблицыОшибок.ЭлементФормы    = СтруктураОшибки.ЭлементФормы;
				СтрокаТаблицыОшибок.ИндексСтроки    = СтруктураОшибки.ИндексСтроки;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(ДополнительныеПараметры.Ссылка) = Тип("Структура") Тогда
		ДополнительныеПараметры.Ссылка.КоличествоОшибок = Форма.КоличествоОшибок;
		МассивТаблицыОшибок = ТаблицаЗначенийВМассивЭПД(Форма.ТаблицаОшибок.Выгрузить());
		ДополнительныеПараметры.Ссылка.ТаблицаОшибок = МассивТаблицыОшибок;
	КонецЕсли;
	
	ОбновитьВнешнийВидЭлементовОшибок(Форма);
	
КонецПроцедуры

// Обновляет реквизиты формы, связанные с отображением ошибок.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, реквизиты которой необходимо обновить.
//
Процедура ОбновитьВнешнийВидЭлементовОшибок(Форма) Экспорт
	
	ЕстьОшибки = Форма.КоличествоОшибок <> 0;
	
	Форма.Элементы.ФормаПоказатьСкрытьОшибки.Видимость = ЕстьОшибки;
	Форма.Элементы.ГруппаОшибки.Видимость              = ЕстьОшибки И Форма.Элементы.ФормаПоказатьСкрытьОшибки.Пометка;
	
	КоличествоОшибокСтрокой = Формат(Форма.КоличествоОшибок, "ЧДЦ=0; ЧГ=");
	
	Если ЕстьОшибки Тогда
		ШаблонЗаголовкаКнопкиПоказа = "Ошибки (%1)";
		Форма.Элементы.ФормаПоказатьСкрытьОшибки.Заголовок = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(
			ШаблонЗаголовкаКнопкиПоказа,
			КоличествоОшибокСтрокой);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработкаОшибок

#Область ПроверкаФЛК

// Структура описания реквизитов и ошибки
// 
// Параметры:
//  РеквизитОбъекта - Строка - имя проверяемого реквизита.
//  ЭлементФормы - Строка - имя элемента формы для перехода.
//  Ошибка - Строка - описание ошибки для вывода пользователю.
// 
// Возвращаемое значение:
//   Структура - см. в функции
//
Функция СтруктураФЛК(РеквизитОбъекта, ЭлементФормы, Ошибка) Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("РеквизитОбъекта", РеквизитОбъекта);
	РезультатФункции.Вставить("ЭлементФормы",    ЭлементФормы);
	РезультатФункции.Вставить("Ошибка",          Ошибка);
	
	Возврат РезультатФункции;
	
КонецФункции

// Возвращает результат выполнения форматно-логического контроля
//
// Параметры:
//  ДанныеДокумента - Структура - структура данных документа
//  ПараметрыВыгрузки - Структура - см. Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыВыгрузки
//
// Возвращаемое значение:
//   Структура,Неопределено - структура ошибок
//
Функция РезультатФЛК(ДанныеДокумента, ПараметрыВыгрузки) Экспорт
	
	РезультатФункции = Неопределено;
	
	Если Не ПараметрыВыгрузки.ВыполнитьФЛК Или ДанныеДокумента = Неопределено Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	ДокументМенеджер = МодульКода("Документы." + ПараметрыВыгрузки.ИмяМетаданных);
	
	ПараметрыПроверкиРеквизитов = ДокументМенеджер.ПараметрыПроверкиРеквизитов(ДанныеДокумента, ПараметрыВыгрузки);
	ПараметрыПроверкиРеквизитов.Вставить("ДокументМенеджер", ДокументМенеджер);
	
	ПроверитьЗаполненНеЗаполнен(ДанныеДокумента, ПараметрыПроверкиРеквизитов, РезультатФункции);
	
	ДокументМенеджер.ВыполнитьДополнительныеПроверкиФЛК(ДанныеДокумента, ПараметрыВыгрузки, РезультатФункции);
	
	Возврат РезультатФункции;
	
КонецФункции

// Добавляет новую ошибку в структуру ошибок
//
// Параметры:
//  Ошибки - Структура,Неопределено - структура для добаления ошибки
//  ПолеОшибки - Строка - полное наименование поля реквизита на форме
//  ТекстДляОднойОшибки - Строка - описание ошибки
//  РеквизитОбъекта - Строка - наименование реквизита объект
//  ЭлементФормы - Строка - наименование реквизита формы для его выделения
//  ИндексСтроки - Число - индекс строки таблицы, в которой найдена ошибка.
//
// Возвращаемое значение:
//   Структура - структура ошибок
//
Функция ДобавитьОшибкуПользователю(Ошибки, ПолеОшибки, ТекстДляОднойОшибки, РеквизитОбъекта = "",
		ЭлементФормы = "", ИндексСтроки = 0) Экспорт
	
	Если Ошибки = Неопределено Тогда
		Ошибки = Новый Структура;
		Ошибки.Вставить("СписокОшибок", Новый Массив);
		Ошибки.Вставить("ГруппыОшибок", Новый Соответствие);
	КонецЕсли;
	
	Ошибка = Новый Структура;
	Ошибка.Вставить("ПолеОшибки",          ПолеОшибки);
	Ошибка.Вставить("ТекстДляОднойОшибки", ТекстДляОднойОшибки);
	Ошибка.Вставить("РеквизитОбъекта",     РеквизитОбъекта);
	Ошибка.Вставить("ЭлементФормы",        ЭлементФормы);
	Ошибка.Вставить("ИндексСтроки",        ИндексСтроки);
	
	Ошибки.СписокОшибок.Добавить(Ошибка);
	
	Возврат Ошибки;
	
КонецФункции

// Проверяет заполнение реквизита автомобиля
//
// Параметры:
//  ДанныеДокумента - Структура - структура данных документа
//  Ошибки - Массив - массив для накопления ошибок
//  СтруктураДанныхОшибки - Структура - соответствие реквизитов формы и объекта.
//
Процедура ПроверитьЗаполнениеТранспортногоСредства(ДанныеДокумента, Ошибки, СтруктураДанныхОшибки) Экспорт
	
	ОтборАвтомобилей = Новый Структура;
	ОтборАвтомобилей.Вставить("Вид", ЗначениеМетаданных("Saby_ВидыТС.Автомобиль"));
	
	НайденныеСтроки = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").НайтиСтрокиУниверсально(
		ДанныеДокумента.ТранспортныеСредства, ОтборАвтомобилей);
	Если НайденныеСтроки.Количество() = 0 Тогда
		
		ДобавитьОшибкуПользователю(
			Ошибки,
			"ТранспортноеСредствоСтрокой",
			СтруктураДанныхОшибки.Ошибка,
			СтруктураДанныхОшибки.РеквизитОбъекта,
			"ТранспортноеСредствоСтрокой");
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет заполнение документов подтверждающих владение транспортным средством
//
// Параметры:
//  ДанныеДокумента - Структура - структура данных документа
//  Ошибки - Массив - массив для накопления ошибок
//  СоответствиеРеквизитовОшибок - Соответствие - соответствие реквизитов формы и объекта.
//
Процедура ПроверитьЗаполнениеТипаВладения(ДанныеДокумента, Ошибки, СоответствиеРеквизитовОшибок) Экспорт
	
	Для Каждого СтрокаТС Из ДанныеДокумента.ТранспортныеСредства Цикл
		
		Если Не МодульКода("Перечисления.Saby_ТипыВладенияТС").ТребуетПодтвержденияВладения(СтрокаТС.ТипВладения) Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьОшибки = Ложь;
		
		Если Не ЗначениеЗаполнено(СтрокаТС.ОснованияВладения) Тогда
			ЕстьОшибки = Истина;			
		КонецЕсли;
		
		Если Не ЕстьОшибки Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавитьОшибкуТипаВладения(СтрокаТС, Ошибки, СоответствиеРеквизитовОшибок);
		
	КонецЦикла;
	
КонецПроцедуры

// Проверяет наличие контактных данных по указанным параметрам
//
// Параметры:
//  ДанныеДокумента - Структура - структура данных документа
//  Роль - Строка - роль контрагента строкой
//  Тип - Строка - тип контактной информации строкой
//  Ошибки - Массив - массив ошибок
//  СтруктураДанныхОшибки - Структура - соответствие реквизитов формы и объекта.
//
Процедура ПроверитьКонтактныеДанные(ДанныеДокумента, Роль, Тип, Ошибки, СтруктураДанныхОшибки) Экспорт
	
	Если Не ЗначениеЗаполнено(Роль) Тогда
		РольСсылка = ЗначениеМетаданных("Saby_РолиКонтрагентов.ПустаяСсылка");
	Иначе
		РольСсылка = ЗначениеМетаданных("Saby_РолиКонтрагентов." + Роль);
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Роль", РольСсылка);
	Отбор.Вставить("Тип",  ЗначениеМетаданных("ТипыКонтактнойИнформации." + Тип));
	
	ИмяРеквизита = Роль + "_" + Тип;
	
	НайденныеСтроки = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").НайтиСтрокиУниверсально(
		ДанныеДокумента.КонтактныеДанные, Отбор);
	Если НайденныеСтроки.Количество() = 0 Тогда
		МодульКода("Saby_ТНОбщегоНазначенияСервер").ДобавитьОшибкуПользователю(
			Ошибки, 
			ИмяРеквизита, 
			СтруктураДанныхОшибки.Ошибка,
			СтруктураДанныхОшибки.РеквизитОбъекта,
			СтруктураДанныхОшибки.ЭлементФормы);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак необходимости проверки реквизитов водителя
//
// Параметры:
//  СтрокаТЧ - СтрокаТаблицыЗначений - строка таблицы, в которой проверяются реквизиты
//  ИмяРеквизита - Строка - имя текущего проверяемого реквизита
//
// Возвращаемое значение:
//   Булево - Истина, если реквизит необходимо проверять
//
Функция ПроверятьРеквизитВодителя(СтрокаТЧ, ИмяРеквизита) Экспорт
	
	Если ИмяРеквизита = "Серия" Или ИмяРеквизита = "Номер" Или ИмяРеквизита = "ДатаВыдачи" Тогда
		Возврат Не ЗначениеЗаполнено(СтрокаТЧ.ИНН);
	ИначеЕсли ИмяРеквизита = "ИНН" Тогда
		Возврат Не ЗначениеЗаполнено(СтрокаТЧ.Серия)
			Или Не ЗначениеЗаполнено(СтрокаТЧ.Номер)
			Или Не ЗначениеЗаполнено(СтрокаТЧ.ДатаВыдачи);
	Иначе
		Возврат ИмяРеквизита = "Фамилия" Или ИмяРеквизита = "Имя";
	КонецЕсли;
	
КонецФункции

#КонецОбласти // ПроверкаФЛК

#Область ЛентаИнтерфейс

// Перерисовывает визуализацию ленты сверху документа
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма для отрисовки ленты
//  ОбновитьДанные - Булево - признак необходимости перечитать данные из регистра ДанныеТитулов
//
Процедура ОбновитьЛенту(Форма, ОбновитьДанные = Истина) Экспорт
	
	Если ОбновитьДанные Тогда
		ОбновитьДанныеЛенты(Форма);
	КонецЕсли;
	
	ОчиститьЛенту(Форма);
	
	ДобавитьКнопку(Форма, "ЛентаВлево");
	
	ВыведеноТитулов = 0;
	Для Каждого СтрокаЛенты Из Форма.Лента Цикл
		
		Если ВыведеноТитулов = РазмерЛентыВТитулах() Тогда
			Прервать;
		КонецЕсли;
		
		Если Не СтрокаЛенты.Видимость Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВыведеноТитулов = 0 И Форма.Лента[0] <> СтрокаЛенты Тогда
			ДобавитьДекорациюЛинию(Форма, СтрокаЛенты, 2, "НачальнаяЛиния");
		Иначе
			Если ВыведеноТитулов > 0 Тогда
				ДобавитьДекорациюЛинию(Форма, СтрокаЛенты);
			КонецЕсли;
		КонецЕсли;
		ДобавитьДекорациюТитул(Форма, СтрокаЛенты);
		ВыведеноТитулов = ВыведеноТитулов + 1;
		
	КонецЦикла;
	
	Если Не Форма.Лента[Форма.Лента.Количество() - 1].Видимость Тогда
		ДобавитьДекорациюЛинию(Форма, СтрокаЛенты, 2, "КонечнаяЛиния");
	КонецЕсли;
	
	ДобавитьКнопку(Форма, "ЛентаВправо");
	
	ДобавитьПодсказку(Форма);
	
КонецПроцедуры

// Сдвигает отображаемые титулы, когда их больше чем РазмерЛентыВТитулах()
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с лентой
//  Вправо - Булево - признак направления сдвига
//
Процедура СдвинутьЛенту(Форма, Вправо = Истина) Экспорт
	
	Если Вправо Тогда
		СдвинутьЛентуВправо(Форма.Лента);
	Иначе
		СдвинутьЛентуВлево(Форма.Лента);
	КонецЕсли;
	
	ОбновитьЛенту(Форма, Ложь);
	
КонецПроцедуры

#КонецОбласти // ЛентаИнтерфейс

// Ограничение документов оснований в зависимости от типа заполняемого документа
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма на которой необходимо ограничение
//
Процедура ОграничитьТипыДокументовОснований(Форма) Экспорт
	
	ФормаОбъект = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ФормаОбъект(Форма);

	ИмяДокумента = ИмяМетаданныхДокумента(ФормаОбъект.Ссылка);
	
	МассивТипов = МодульКода("Документы." + ИмяДокумента).ТипыДляЗаполненияНаОсновании();
	
	ТипДокументаОснования = ТипЗнч(Форма.ДокументОснование);
	
	Если ЗначениеЗаполнено(Форма.ДокументОснование) И МассивТипов.Найти(ТипДокументаОснования) = Неопределено Тогда
		МассивТипов.Добавить(ТипДокументаОснования);
	КонецЕсли;
	
	Форма.ТипыОснований.ЗагрузитьЗначения(МассивТипов);
	
КонецПроцедуры

// Формируем строковое представление автомобилей документа
// Параметры:
// 	ОбъектДок - ДокуметОбъект - источник таблицы с транспортными средствами     
// Возвращаемое значение:
//  Строка - представление транспортного средства
//
Функция ТранспортныеСредстваСтрокой(ОбъектДок) Экспорт
		
    СтрокиАвто = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ПолучитьСтрокиТС(
		ОбъектДок.ТранспортныеСредства,
		ЗначениеМетаданных("Saby_ВидыТС.Автомобиль"));
	
	МассивНаименований = Новый Массив;
	Для Каждого СтрокаАвто Из СтрокиАвто Цикл
		МассивНаименований.Добавить(СтрокаАвто.РегистрационныйНомер);
	КонецЦикла;
	
	АвтоСтрокой = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(МассивНаименований, ", ");
	
	Возврат АвтоСтрокой;
	
КонецФункции

// Получение из регистра состояний активного этапа
//
// Параметры:
// 	СсылкаНаДокумент - ДокументСсылка - ссылка на документ для поиска
//
// Возвращаемое значение:
//  Строка - активный этап строкой
//
Функция АктивныйЭтапСтрокой(Знач СсылкаНаДокумент) Экспорт
	
	РезультатФункции = "";
	
	ДанныеСостояния = ДанныеСостоянияОбъекта(СсылкаНаДокумент);
	Если ДанныеСостояния <> Неопределено Тогда
		РезультатФункции = ДанныеСостояния.АктивныйЭтап;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

// Изменяет признак не просмотренного документа
//
// Параметры:
// 	СсылкаНаДокумент - ДокументСсылка - ссылка на документ для поиска
//
Процедура УбратьПризнакНеПросмотрен(Знач СсылкаНаДокумент) Экспорт
	
	ДанныеСостояния = ДанныеСостоянияОбъекта(СсылкаНаДокумент);
	Если ДанныеСостояния = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИД",           ДанныеСостояния.UID);
	Параметры.Вставить("АктивныйЭтап", ДанныеСостояния.АктивныйЭтап);
	Параметры.Вставить("Ссылка",       СсылкаНаДокумент);
	Параметры.Вставить("Состояние",    ДанныеСостояния.Состояние);
	Параметры.Вставить("Аккаунт",      ДанныеСостояния.Аккаунт);
	Параметры.Вставить("СбисИД",       ""); // сброс "Не просмотрен"
	
	ОбновитьДанныеСостояния(Параметры);
	
КонецПроцедуры

#Область ДатаВремяНаФорме

// Дополнение массива реквизитов объекта и его составных частей на форме
// реквизитов формы, отдельно хранящее время и дату
// Параметры:
//  Массив - Массив - исходный массив для дополнения новыми записями 
//  РевизитОбъекта - Строка - ревизит объекта  
//	РеквизитДата - Строка - реквизит формы с датой
//	РеквизитВремя - Строка - реквизит формы с временем
//
Процедура ДобавитьРеквизитВМассивДатаВремя(Массив, РевизитОбъекта, РеквизитДата = "", РеквизитВремя = "") Экспорт
	
	Если Не ЗначениеЗаполнено(РеквизитДата) Тогда
		РеквизитДата = СтрЗаменить(РевизитОбъекта, "ДатаВремя", "Дата");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РеквизитВремя) Тогда 
		РеквизитВремя = СтрЗаменить(РевизитОбъекта, "ДатаВремя", "Время");
	КонецЕсли;
		
	Структура = Новый Структура;
	Структура.Вставить("РеквизитОбъекта", РевизитОбъекта);
	Структура.Вставить("РеквизитДата",    РеквизитДата);
	Структура.Вставить("РеквизитВремя",   РеквизитВремя);
	
	Массив.Добавить(Структура);
	
КонецПроцедуры

// Разбор реквизита объекта на составные части в реквизиты формы (дата и время отдельно)
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма источник вызова  
//	МассивРеквизитов - Массив - реквизиты объекта и формы для разбора
//
Процедура РазложитьДатуВремяПоРеквизитам(Форма, МассивРеквизитов) Экспорт
	
	Объект = Форма.Объект;
	
	Для Каждого ЭлементМассива Из МассивРеквизитов Цикл
		
		РеквизитОбъекта = Объект[ЭлементМассива.РеквизитОбъекта];
		
		Форма[ЭлементМассива.РеквизитДата]  = РеквизитОбъекта;
		Форма[ЭлементМассива.РеквизитВремя] = РеквизитОбъекта;
		
	КонецЦикла;
	
КонецПроцедуры

// Сборка даты реквизита объекта из составных частей в реквизитов формы (дата и время отдельно)
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма источник вызова  
//	МассивРеквизитов - Массив - реквизиты объекта и формы для сборки
//  ТекущийОбъект - ДокументОбъект - объект документа
//
Процедура СобратьДатуВремяПоРеквизитам(Форма, МассивРеквизитов, ТекущийОбъект = Неопределено) Экспорт
	
	Если ТекущийОбъект <> Неопределено Тогда
		Объект = ТекущийОбъект;
	Иначе
		Объект = Форма.Объект;
	КонецЕсли;
	
	Для Каждого ЭлементМассива Из МассивРеквизитов Цикл
		
		РеквизитОбъекта = ЭлементМассива.РеквизитОбъекта;
		
		Если Не ЗначениеЗаполнено(Форма[ЭлементМассива.РеквизитДата]) Тогда
			Объект[РеквизитОбъекта] = "";
			Продолжить;
		КонецЕсли;
		
		ДатаСтрока  = Формат(Форма[ЭлементМассива.РеквизитДата],  "ДФ=yyyyMMdd");
		ВремяСтрока = Формат(Форма[ЭлементМассива.РеквизитВремя], "ДФ=HHmmss");
		
		ДатаИВремя = Дата(ДатаСтрока + ВремяСтрока);
 
		Объект[РеквизитОбъекта] = ДатаИВремя;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // ДатаВремяНаФорме

#Область АктивныйЭтапНаФорме

// Отображение на форме данных по активному этапу документа и состоянию
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма на которой необходимо показать данные
//
Процедура ОтобразитьНаФормеСбисЭтапИСостояние(Форма) Экспорт
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	ФормаОбъект = МодульТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	ДанныеЭтапа = АктивныйЭтапДокумента(ФормаОбъект.Ссылка);
	
	Форма.АктивныйЭтапСтрокой               = ДанныеЭтапа.АктивныйЭтап;
	Форма.Элементы.КартинкаСтатус.Подсказка = ДанныеЭтапа.Состояние;
	Форма.ИндексКартинки                    = ДанныеЭтапа.ИндексКартинки;
	Форма.Состояние                         = ДанныеЭтапа.Состояние;
	Если ТипЗнч(ДанныеЭтапа.Состояние) = Тип("Структура") Тогда
		Форма.Элементы.КартинкаСтатус.Подсказка = ДанныеЭтапа.Состояние.Наименование;
	КонецЕсли;
	
	ДОЗавершенОтрицательно = 5;
	ДОЗавершенПоложительно = 6;
	ДОСОшибкой             = 3;
	
	Форма.Элементы.АктивныйЭтап.Видимость = ЗначениеЗаполнено(ДанныеЭтапа.АктивныйЭтап);
	
	Если Форма.Элементы.Найти("КомментарийУведомление") <> Неопределено Тогда 
		Если МодульТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(ФормаОбъект, "КомментарийУведомление") Тогда
			КомментарийУведомление = ФормаОбъект.КомментарийУведомление;
		Иначе
			КомментарийУведомление = Форма.КомментарийУведомление;
		КонецЕсли;
		Форма.Элементы.КомментарийУведомление.Видимость = (Форма.ИндексКартинки = ДОЗавершенОтрицательно)
			Или ЗначениеЗаполнено(КомментарийУведомление);
	КонецЕсли;
	
	ИмяМетаданных = МодульТНОбщегоНазначенияКлиентСервер.ИмяМетаданныхПоФорме(Форма);
	
	ТекущийТитул = МодульКода("Saby_ТНОбщегоНазначенияСервер").РезультатФункцииТитулов(
		ИмяМетаданных, "ЗначениеПоПредставлению", ДанныеЭтапа.АктивныйЭтап);
	
	// проверка на динамический это титул или нет
	ЭтоДТ = МодульКода("Saby_ТНОбщегоНазначенияСервер").РезультатФункцииТитулов(
		ИмяМетаданных, "ЭтоДинамическийТитул", ТекущийТитул);
	
	Видимость   = Истина;
	Доступность = Ложь;
	Если Форма.ИндексКартинки = ДОСОшибкой Тогда  
		
		Доступность = Истина;
		Картинка    = КартинкаИнтерфейсаСервер(Форма, "Saby_СостояниеОшибка");
		
	// ДО завершен положительно
	ИначеЕсли Форма.ИндексКартинки = ДОЗавершенПоложительно Тогда
		Картинка = КартинкаИнтерфейсаСервер(Форма, "Saby_СостояниеЗавершеноПоложительно");
		
	// ДО завершен отрицательно
	ИначеЕсли Форма.ИндексКартинки = ДОЗавершенОтрицательно Тогда
		Картинка = КартинкаИнтерфейсаСервер(Форма, "Saby_СостояниеЗавершеноОтрицательно");
		
	Иначе
		
		Картинка     = Новый Картинка();
		Видимость    = Ложь;
		Доступность  = Не ЭтоДТ; // выгрузка в сбис на ДТ не доступна
		
	КонецЕсли;
	
	Форма.Элементы.КартинкаСтатус.Картинка         = Картинка;
	Форма.Элементы.ФормаВыгрузитьВСбис.Доступность = Доступность;
	Форма.Элементы.КартинкаСтатус.Видимость        = Видимость;
	
КонецПроцедуры

#КонецОбласти

#Область РегламентныеЗадания

// Запуск регламентного задания по ключу
//
// Параметры:
//   Ключ - Строка - ключ для регламентного задания
//
Процедура ВыполнитьРегламентноеЗаданиеЭПД(Ключ) Экспорт
		
	Если Ключ = "Saby_Загрузка ЭПД" Тогда
		МодульКода("Saby_ТНЗагрузкаСервер").ЗагрузитьДокументыЭПДПоРегламентномуЗаданию();
	ИначеЕсли Ключ = "Saby_Выгрузка ЭПД" Тогда
		МодульКода("Saby_ТНВыгрузкаСервер").ВыгрузитьДокументыЭПДПоРегламентномуЗаданию();
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Добавление регламентных заданий подсистемы ЭПД
//
// Параметры:
//   МассивЗаданий - Массив - данные по регламентным заданиям, которые нужно дополнить новыми значениями
//
Процедура ДобавитьРегламентныеЗаданияЭПД(МассивЗаданий) Экспорт
		
	ИменаФоновыхЗаднийЭПД = Новый Массив;
	ИменаФоновыхЗаднийЭПД.Добавить("Загрузка ЭПД");
	ИменаФоновыхЗаднийЭПД.Добавить("Выгрузка ЭПД");
	
	Для Каждого ФоновоеЗаданиеЭПД Из ИменаФоновыхЗаднийЭПД Цикл
		
		Ключ = "Saby_" + ФоновоеЗаданиеЭПД;
		
		ИнфоОЗадании = Новый Структура;
		ИнфоОЗадании.Вставить("Наименование", ФоновоеЗаданиеЭПД);
		ИнфоОЗадании.Вставить("Ключ",         Ключ);
		ИнфоОЗадании.Вставить("Расписание",   Неопределено);
		МассивЗаданий.Добавить(ИнфоОЗадании);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // РегламентныеЗадания

#Область include_etrn_base_CommonModule_ЗначениеПеречисления
#КонецОбласти // include_etrn_base_CommonModule_ЗначениеПеречисления

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

// Получает данные указанного водителя, включая водительское удостоверение.
//
// Параметры:
//  Водитель - СправочникСсылка.ФизическиеЛица - ссылка на водителя для получения данных.
//
// Возвращаемое значение:
//   Структура - см. ФиоИВУФизЛиц + "ВУ"
//
Функция ДанныеВодителяПоСсылке(Водитель) Экспорт

	Массив = Новый Массив;
	Массив.Добавить(Водитель);
	
	Водители = ФиоИВУФизЛиц(Массив);	
	ВодительДанные = Водители[Водитель];
	
	ВодительскоеУдостоверение = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ПредставлениеВУ(ВодительДанные);
	ВодительДанные.Вставить("ВУ", ВодительскоеУдостоверение);
	
	Возврат ВодительДанные;
	
КонецФункции

// Записывает строку JSON в указанный файл.
// Напрямую нигде не вызывается, а используется при отладке, когда онлайн возвращает большой JSON,
// который не помещается в табло.
//
// Параметры:
//  ИмяФайла - Строка - полный путь к файлу для записи.
//  СтрокаJSON - Строка - строка в формате JSON
//
// Возвращаемое значение:
//   Булево - всегда Истина.
//
Функция ЗаписатьJSONВФайл(ИмяФайла, СтрокаJSON) Экспорт
	
	Запись = Новый ЗаписьJSON();
	Запись.ОткрытьФайл(ИмяФайла);
	Запись.ЗаписатьБезОбработки(СтрокаJSON);
	Запись.Закрыть();
	
	Возврат Истина;
	
КонецФункции

// Получает таблицу значений из менеджера временных таблиц.
// Напрямую не вызывается, но используется для отладки запросов с менеджером временных таблиц.
//
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц
//  ИмяТаблицы - Строка - имя таблицы из менеджера временных таблиц
//
// Возвращаемое значение:
//   ТаблицаЗначений - талица данных из менеджера временных таблиц
//
Функция ПолучитьТаблицуМенеджераВременныхТаблиц(МенеджерВременныхТаблиц, ИмяТаблицы) Экспорт
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросДанных.Текст = СтрЗаменить("ВЫБРАТЬ * ИЗ &ИмяТаблицы", "&ИмяТаблицы", ИмяТаблицы);
	
	Возврат ЗапросДанных.Выполнить().Выгрузить();
	
КонецФункции

// Получает ссылку на объект информационной базы по навигационной ссылке.
//
// Параметры:
//  НавигационнаяСсылка - Строка - навигационная ссылка на объект информационной базы.
//
// Возвращаемое значение:
//   ЛюбаяСсылка - ссылка на объект информационной базы
//
Функция СсылкаПоНавигационнойСсылке(НавигационнаяСсылка) Экспорт
	
	РезультатФункции = Неопределено;
	
	ПрефиксМетаданных = "e1cib/data/";
	ПрефиксСсылки     = "?ref=";
	
	ПозицияМетаданных     = Найти(НавигационнаяСсылка, ПрефиксМетаданных) + СтрДлина(ПрефиксМетаданных);
    ПозицияПрефиксаСсылки = Найти(НавигационнаяСсылка, ПрефиксСсылки);
	
	Если ПозицияПрефиксаСсылки <> 0 Тогда
		ПозицияСсылки         = ПозицияПрефиксаСсылки + СтрДлина(ПрефиксСсылки);
		
		ИдентификаторСсылки   = Сред(НавигационнаяСсылка, ПозицияСсылки);
		
		ПредставлениеТипа     = Сред(НавигационнаяСсылка, ПозицияМетаданных, ПозицияПрефиксаСсылки - ПозицияМетаданных);
		ПустаяСсылка          = ПредопределенноеЗначение(ПредставлениеТипа + ".ПустаяСсылка");
		
	    ШаблонЗначения = ЗначениеВСтрокуВнутр(ПустаяСсылка);
	    ЗначениеСсылки = СтрЗаменить(ШаблонЗначения, "00000000000000000000000000000000", ИдентификаторСсылки);
	    РезультатФункции = ЗначениеИзСтрокиВнутр(ЗначениеСсылки);
		
		Если Не СсылкаСуществует(РезультатФункции) Тогда
			РезультатФункции = ПустаяСсылка;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

// Проверяет физическое наличие записи в информационной базе данных о переданном значении ссылки.
//
// Параметры:
//  ПроверяемаяСсылка - ЛюбаяСсылка - значение любой ссылки информационной базы данных.
// 
// Возвращаемое значение:
//  Булево - Истина, если существует.
//
Функция СсылкаСуществует(ПроверяемаяСсылка) Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	1 КАК Поле1
		|ИЗ
		|	&ИмяТаблицы КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка = &Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ПроверяемаяСсылка.Метаданные().ПолноеИмя());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ПроверяемаяСсылка);
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

// Получает данные печатной формы документа с онлайна.
//
// Параметры:
//  СсылкаНаДокумент - ДокументСсылка.Saby_ТранспортнаяНакладная - ссылка на документ ЭТрН
//  ЗагрузкаТребуется - Структура - результат выгрузки на клиенте
//  УникальныйИдентификаторФормы - УникальныйИдентификатор - идентификатор формы,
//    в хранилище которой будет помещены двоичные данные.
//
// Возвращаемое значение:
//   Структура - структура данных печатной формы полученной с онлайна.
//     * АдресДвоичныхДанных - Строка - адрес двоичных данных печатной формы
//     * СсылкаНаПрисоединенныйФайл - СправочникСсылка.Saby_ТранспортнаяНакладнаяПрисоединенныеФайлы - ссылка на
//       справочник с присоединенным файлом печатной формы.
//     * ОписаниеОшибки - Строка - описание ошибки получения печтаной формы с онлайна.
//     * СписокДокументов - Массив - данные документов с ошибкой.
//
Функция ДанныеПечатнойФормыДляОткрытия(СсылкаНаДокумент, ЗагрузкаТребуется, УникальныйИдентификаторФормы = Неопределено) Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("АдресДвоичныхДанных",        Неопределено);
	РезультатФункции.Вставить("СсылкаНаПрисоединенныйФайл", Неопределено);
	РезультатФункции.Вставить("ОписаниеОшибки",             "");
	РезультатФункции.Вставить("СписокДокументов",           Неопределено);
	РезультатФункции.Вставить("СсылкаНаДокумент",           СсылкаНаДокумент);
	
	Если Не ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
		РезультатФункции.ОписаниеОшибки = "Документ еще ни разу не сохранен. Печатная форма недоступна.";
		Возврат РезультатФункции;
	КонецЕсли;
	
	МодульТНЗагрузкаСервер = МодульКода("Saby_ТНЗагрузкаСервер");
	
	ПараметрыЗагрузки = МодульТНЗагрузкаСервер.ПараметрыЗагрузкиИзСбис(
		СсылкаНаДокумент, , УникальныйИдентификаторФормы, ЗагрузкаТребуется);
	
	Если Не ЗначениеЗаполнено(ПараметрыЗагрузки.УИДСбис) Тогда
		РезультатФункции.ОписаниеОшибки = "Документ не выгружен в СБИС. Печатная форма недоступна.";
		РезультатФункции.СписокДокументов = ПараметрыЗагрузки.СписокДокументов;
		Возврат РезультатФункции;
	КонецЕсли;
	
	ХранитьПечатныеФормыВБД = Ложь;
	
	ЗагруженныеДанныеПечатнойФормыДляОткрытия(ХранитьПечатныеФормыВБД, ПараметрыЗагрузки, РезультатФункции);
	
	Если Не ПараметрыЗагрузки.ЗагрузкаТребуется Тогда
		ПараметрыЗагрузки.ОбъектОбработки = Неопределено;
		РезультатФункции.Вставить("ПараметрыЗагрузки", ПараметрыЗагрузки);
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

// Получает данные ссылки в СБИС документа с онлайна.
//
// Параметры:
//  СсылкаНаДокумент - ДокументСсылка - ссылка на документ ЭТрН
//  ОбменНаКлиенте - Структура - результат выгрузки на клиенте
//
// Возвращаемое значение:
//   Структура - структура данных документа с онлайна.
//     * СсылкаНаДокументВСБИС - Строка - URL адрес документа на online.sbis.ru
//     * ОписаниеОшибки - Строка - описание ошибки получения ссылки на документ с онлайна.
//     * СписокДокументов - Массив - данные документов с ошибкой.
//
Функция ДанныеДляОткрытияДокументаВСБИС(СсылкаНаДокумент, ОбменНаКлиенте = Ложь) Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("СсылкаНаДокументВСБИС", "");
	РезультатФункции.Вставить("ОписаниеОшибки" ,       "");
	РезультатФункции.Вставить("СписокДокументов",      Неопределено);
	
	Если Не ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
		РезультатФункции.ОписаниеОшибки = Нстр("ru = 'Документ еще не был записан. Открытие в SABY не доступно.'");
		Возврат РезультатФункции;
	КонецЕсли;
	
	ПараметрыЗагрузки = МодульКода("Saby_ТНЗагрузкаСервер").ПараметрыЗагрузкиИзСбис(СсылкаНаДокумент);
	
	Если Не ЗначениеЗаполнено(ПараметрыЗагрузки.УИДСбис) Тогда
		РезультатФункции.ОписаниеОшибки = Нстр("ru = 'Документ не выгружен в СБИС. Открытие в SABY не доступно.'");
		РезультатФункции.СписокДокументов = ПараметрыЗагрузки.СписокДокументов;
		Возврат РезультатФункции;
	КонецЕсли;
	
	URL_АдресСБИС    = МодульКода("Saby_ОбщегоНазначенияКлиентСервер").URL_АдресСБИС();
	АлиасДоменаСБИС  = МодульКода("Saby_ОбщегоНазначенияКлиентСервер").АлиасДоменаСБИС();
	URL_АдресСервиса = СтрЗаменить(ПараметрыЗагрузки.context_params.api_url, АлиасДоменаСБИС, URL_АдресСБИС);
								
	СсылкаНаДокументВСБИС = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(
		"%1/opendoc.html?guid=%2&client=%3", URL_АдресСервиса, ПараметрыЗагрузки.УИДСбис, ПараметрыЗагрузки.context_params.account);
								
	РезультатФункции.СсылкаНаДокументВСБИС = СсылкаНаДокументВСБИС;
	
	Возврат РезультатФункции;
	
КонецФункции

// Получает все необходимые данные физ лиц для обмена с онлайном.
//
// Параметры:
//  МассивФизлиц - Массив - массив из ссылок СправочникСсылка.ФизическиеЛица
//
// Возвращаемое значение:
//   Соответствие - ключами соответствия являются ссылки на ФизЛицо, а значениями - структура данных физлица.
//     Структура данных физлица содержит:
//       * Фамилия - Строка
//       * Имя - Строка
//       * Отчество - Строка
//       * ИНН - Строка
//       * Номер - Строка - номер водительского удостоверения
//       * Серия - Строка - серия водительского удостоверения
//       * ДатаВыдачи - Дата - дата выдачи водительского удостоверения
//       * Телефоны - Строка
//
Функция ФиоИВУФизЛиц(МассивФизлиц) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ПолучитьДанныеФизлиц(МассивФизлиц, МенеджерВременныхТаблиц);
	ПолучитьВУФизлиц(МассивФизлиц, МенеджерВременныхТаблиц);
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	РеквизитыФизлиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РеквизитыФизлиц.Наименование КАК Наименование,
	|	РеквизитыФизлиц.ИНН КАК ИНН,
	|	РеквизитыФизлиц.Фамилия КАК Фамилия,
	|	РеквизитыФизлиц.Имя КАК Имя,
	|	РеквизитыФизлиц.Отчество КАК Отчество,
	|	ЕСТЬNULL(ВУФизлиц.Номер, """") КАК Номер,
	|	ЕСТЬNULL(ВУФизлиц.Серия, """") КАК Серия,
	|	ЕСТЬNULL(ВУФизлиц.ДатаВыдачи, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаВыдачи
	|ИЗ
	|	РеквизитыФизлиц КАК РеквизитыФизлиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВУФизлиц КАК ВУФизлиц
	|		ПО РеквизитыФизлиц.ФизическоеЛицо = ВУФизлиц.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелефоныЭлектроннаяПочтаФизлиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТелефоныЭлектроннаяПочтаФизлиц.Представление КАК Представление
	|ИЗ
	|	ТелефоныЭлектроннаяПочтаФизлиц КАК ТелефоныЭлектроннаяПочтаФизлиц
	|ГДЕ
	|	ТелефоныЭлектроннаяПочтаФизлиц.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелефоныЭлектроннаяПочтаФизлиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТелефоныЭлектроннаяПочтаФизлиц.Представление КАК Представление
	|ИЗ
	|	ТелефоныЭлектроннаяПочтаФизлиц КАК ТелефоныЭлектроннаяПочтаФизлиц
	|ГДЕ
	|	ТелефоныЭлектроннаяПочтаФизлиц.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)";
	
	РезультатЗапроса        = ЗапросДанных.ВыполнитьПакет();
	Выборка                 = РезультатЗапроса[0].Выбрать();
	ВыборкаТелефоны         = РезультатЗапроса[1].Выбрать();
	ВыборкаЭлектроннаяПочта = РезультатЗапроса[2].Выбрать();
	
	МенеджерВременныхТаблиц.Закрыть();
	
	Соответствие = Новый Соответствие;
	Пока Выборка.Следующий() Цикл 
		
		ВыборкаТелефоны.Сбросить();
		ВыборкаЭлектроннаяПочта.Сбросить();
		
		Если ПустаяСтрока(Выборка.Фамилия) Тогда
			ИсточникФИО = ФИОИзСтроки(СокрЛП(Выборка.Наименование));
		Иначе
			ИсточникФИО = Выборка;
		КонецЕсли;
		
		Структура = Новый Структура;
		Структура.Вставить("Фамилия",    ИсточникФИО.Фамилия);
		Структура.Вставить("Имя",        ИсточникФИО.Имя);
		Структура.Вставить("Отчество",   ИсточникФИО.Отчество);
		Структура.Вставить("ИНН",        Выборка.ИНН);
		Структура.Вставить("Номер",      Выборка.Номер);
		Структура.Вставить("Серия",      Выборка.Серия);
		Структура.Вставить("ДатаВыдачи", Выборка.ДатаВыдачи);
		
	    Отбор = Новый Структура;
		Отбор.Вставить("ФизическоеЛицо", Выборка.ФизическоеЛицо);
		
		МассивТелефонов = Новый Массив;
		Пока ВыборкаТелефоны.НайтиСледующий(Отбор) Цикл
			МассивТелефонов.Добавить(ВыборкаТелефоны.Представление);
		КонецЦикла;
				
		МассивЭлектроннаяПочта = Новый Массив;
		Пока ВыборкаЭлектроннаяПочта.НайтиСледующий(Отбор) Цикл
			МассивЭлектроннаяПочта.Добавить(ВыборкаЭлектроннаяПочта.Представление);
		КонецЦикла;
		
		Структура.Вставить("Телефоны",         ЗначениеВСтрокуВнутр(МассивТелефонов));
		Структура.Вставить("ЭлектроннаяПочта", ЗначениеВСтрокуВнутр(МассивЭлектроннаяПочта));
		
		Соответствие.Вставить(Выборка.ФизическоеЛицо, Структура);
		
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции

// Преобразует ФИО из строки в структуру
// Параметры:
//  СтрокаФИО - Строка - ФИО строкой
// Возвращаемое значение:
//  Структура - Фамилия, Имя, Отчество полученные из строки
//
Функция ФИОИзСтроки(СтрокаФИО) Экспорт
	
	// Убираем ИП
	ФИОБезИП = СтрЗаменить(СтрокаФИО, "ИП", "");
	ФИОБезИП = СтрЗаменить(ФИОБезИП,  ",",  "");
	ФИОБезИП = СтрЗаменить(ФИОБезИП,  "Индивидуальный предприниматель", "");
	
	ЧастиФИО = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(ФИОБезИП, " ", Ложь);
	
	ПозицияИмя      = 1;
	ПозицияОтчество = 2;
	
	Фамилия  = ?(ЧастиФИО.Количество(),                   ЧастиФИО[0], "");
	Имя      = ?(ЧастиФИО.Количество() > ПозицияИмя,      ЧастиФИО[1], "");
	Отчество = ?(ЧастиФИО.Количество() > ПозицияОтчество, ЧастиФИО[2], "");

	Структура = Новый Структура;
	Структура.Вставить("Фамилия",  Фамилия);
	Структура.Вставить("Имя",      Имя);
	Структура.Вставить("Отчество", Отчество);
	
	Возврат Структура;
	
КонецФункции

// Возвращает структуру синонимов доступных баз
//
// Возвращаемое значение:
//   Структура - значениями являются синонимы конфигураций
//
Функция ДоступныеКонфигурации() Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Бух3", "Бухгалтерия предприятия, редакция 3.0");
	РезультатФункции.Вставить("ЕРП",  "1С:ERP Управление предприятием 2");
	РезультатФункции.Вставить("КА",   "1С:Комплексная автоматизация 2");
	РезультатФункции.Вставить("УТ",   "Управление торговлей, редакция 11");
	РезультатФункции.Вставить("УАТ",  "Управление автотранспортом Проф, редакция 2.2");
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ЭтоДоступнаяКонфигурация(СинонимКонфигурации, ДоступныеКонфигурации) Экспорт
	
	РезультатФункции = Ложь;
	
	Для Каждого КлючЗначение Из ДоступныеКонфигурации Цикл
		Если КлючЗначение.Значение = СинонимКонфигурации Тогда
			РезультатФункции = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

// Возвращает с структуру данных ответственного лица
//
// Возвращаемое значение:
//   Структура - см. в самой функции
//
Функция СтруктураДанныхОтветственногоЛица() Экспорт

	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Роль",                  ЗначениеМетаданных("Saby_РолиОтветственных.ПустаяСсылка"));
	РезультатФункции.Вставить("Фамилия",               "");
	РезультатФункции.Вставить("Имя",                   "");
	РезультатФункции.Вставить("Отчество",              "");
	РезультатФункции.Вставить("ИНН",                   "");
	РезультатФункции.Вставить("Серия",                 "");
	РезультатФункции.Вставить("Номер",                 "");
	РезультатФункции.Вставить("ДатаВыдачи",            "");
	РезультатФункции.Вставить("ДатаОкончанияДействия", "");
	РезультатФункции.Вставить("Телефоны",              "");
	РезультатФункции.Вставить("ЭлектроннаяПочта",      "");
	РезультатФункции.Вставить("Должность",             "");

	Возврат РезультатФункции;
	
КонецФункции

// Обновляет декорацию с ошибкой
// Параметры:
//  Форма - Форма - источник вызова
//  ПараметрыВыделенияДекорации - Структура - параметры выделения
//
Процедура ОбновитьДекорацию(Форма, Знач ПараметрыВыделенияДекорации) Экспорт
	
	Для Каждого ИмяДекорации Из ПараметрыВыделенияДекорации.Выделить Цикл
		Форма.Элементы[ИмяДекорации].ЦветРамки = ЦветаСтиля.ЦветАктивности;
		Форма.Элементы[ИмяДекорации].Подсказка = "Необходимо заполнить";
	КонецЦикла;
	
	Для Каждого ИмяДекорации Из ПараметрыВыделенияДекорации.ОтменитьВыделение Цикл
		Форма.Элементы[ИмяДекорации].ЦветРамки = ЦветаСтиля.ЦветФонаФормы;
		Форма.Элементы[ИмяДекорации].Подсказка = "";
	КонецЦикла;
	
КонецПроцедуры

#Область ПолучениеДанныхПоСсылке

// Возвращает все необходимые реквизиты транспортных средств из базы
//
// Параметры:
//  МассивСсылокТС - Массив - массив ссылок на транспортные средства
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц с таблицей ТаблицаТранспортныхСредств
//
// Возвращаемое значение:
//   Соответствие - соответствие структур реквизитов по ссылкам на транспортные средства.
//     * Ключ - СправочникСсылка - ссылка на транспортное средство
//     * Значение - Структура - см. функцию Saby_ТНОбщегоНазначенияКлиентСервер.СтруктураТранспортногоСредства()
//
Функция РеквизитыТранспортныхСредств(МассивСсылокТС, МенеджерВременныхТаблиц = Неопределено) Экспорт
	
	Если МассивСсылокТС = Неопределено И МенеджерВременныхТаблиц = Неопределено Тогда
		Возврат Новый Соответствие;
	КонецЕсли;
	
	ИмяКонфигурации = ИмяКонфигурации();
	ДоступныеКонфигурации = ДоступныеКонфигурации();
	
	Если ИмяКонфигурации = ДоступныеКонфигурации.УАТ Тогда
		Возврат ДанныеТранспортныхСредствИзКонфигурацииУАТ(МассивСсылокТС, МенеджерВременныхТаблиц);
	Иначе
		Возврат ДанныеТранспортныхСредствИзДругихКонфигураций(МассивСсылокТС, МенеджерВременныхТаблиц);
	КонецЕсли;
	
КонецФункции

#КонецОбласти // ПолучениеДанныхПоСсылке

#Область Заполнение_С_Контролем

Функция ПараметрыСвойстваСКонтролем(Приемник, Источник) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Приемник",  Приемник);
	Параметры.Вставить("Источник",  Источник);
	
	Возврат Параметры;
	
КонецФункции

Процедура ЗаполнитьСвойствоСКонтролем(Параметры, ИмяРеквизита, ИмяСвойства = "", ВЧисло = Ложь) Экспорт
	
	Приемник = Параметры.Приемник; 
	Источник = Параметры.Источник;  
	
	Если ПустаяСтрока(ИмяСвойства) Тогда 
		ИмяСвойства = ИмяРеквизита;
	КонецЕсли;
		
	// Проверка На значениение заполнено не совсем корректно, так как данные могут затираться
	Если Источник[ИмяСвойства] <> Неопределено Тогда
		Если ВЧисло Тогда
			Приемник[ИмяРеквизита] = ЧислоИзСтроки(Источник[ИмяСвойства]);
		Иначе                                             		
			Приемник[ИмяРеквизита] = Источник[ИмяСвойства]; 		
		КонецЕсли;	                                      
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Заполнение_С_Контролем

#Область ПроцедурыОбновления

// Операции для выполнения перехода на версию 23.6200 с более ранней версии.
//
// Возвращаемое значение:
//   Структура - структура результат перехода.
//
Функция РезультатПереходаНа236200() Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ОК",             Истина);
	РезультатФункции.Вставить("ОписаниеОшибки", "");
	
	ИмяКонфигурации = ИмяКонфигурации();
	ДоступныеКонфигурации = ДоступныеКонфигурации();
	
	Если ИмяКонфигурации <> ДоступныеКонфигурации.УАТ Тогда
		РезультатФункции = ПеренестиДанныеТранспортныхСредствВДокумент();
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

// Операции для выполнения перехода на версию 24.2160 с более ранней версии.
//
// Возвращаемое значение:
//   Структура - структура результат перехода.
//
Функция РезультатПереходаНа242160() Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ОК",             Истина);
	РезультатФункции.Вставить("ОписаниеОшибки", "");
	
	РезультатПереносаСторон = МодульКода("Справочники.Saby_ДокументыЭПД").ПеренестиДанныеСторонНепосредственноВДокумент();
	
	РезультатФункции.ОК = РезультатФункции.ОК И РезультатПереносаСторон.ОК;
	РезультатФункции.ОписаниеОшибки = РезультатФункции.ОписаниеОшибки + РезультатПереносаСторон.ОписаниеОшибки;
	
	РезультатЗаполненияРолиВодителя = МодульКода("Документы.Saby_ТранспортнаяНакладная").ЗаполнитьРольВодителяВоВсехДокументах();
	
	РезультатФункции.ОК = РезультатФункции.ОК И РезультатЗаполненияРолиВодителя.ОК;
	РезультатФункции.ОписаниеОшибки = РезультатФункции.ОписаниеОшибки + РезультатЗаполненияРолиВодителя.ОписаниеОшибки;
	
	РезультатПереносаОтветственного = МодульКода("Документы.Saby_ТранспортнаяНакладная").ПеренестиОтветственногоВТабличнуюЧасть();
	
	РезультатФункции.ОК = РезультатФункции.ОК И РезультатПереносаОтветственного.ОК;
	РезультатФункции.ОписаниеОшибки = РезультатФункции.ОписаниеОшибки + РезультатПереносаОтветственного.ОписаниеОшибки;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // ПроцедурыОбновления

// Возвращает общие параметры для загрузки и выгрузки
//
// Параметры:
//  context_params - Структура - параметры подключения к СБИС
//
// Возвращаемое значение:
//   Структура - основные параметры используемые для выгрузки и загрузки
//
Функция ОбщиеПараметрыВыгрузкиЗагрузки(context_params = Неопределено) Экспорт
	
	Контекст = context_params;
	Если Контекст = Неопределено Тогда
		Контекст = МодульКода("Saby_Core").ПроверитьНаличиеПараметровПодключения();
	КонецЕсли;
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("context_params",  Контекст);
	РезультатФункции.Вставить("ОбъектОбработки", ОбъектОбработки());
	РезультатФункции.Вставить("ТранспортБлокли", ОбъектОбработки());
	
	Saby_ХранитьПечатныеФормыВБД = Ложь;
	
	РезультатФункции.Вставить("ЗагружатьПечатнуюФорму", Saby_ХранитьПечатныеФормыВБД);
	
	Возврат РезультатФункции;
	
КонецФункции

// Извлекает текстовое представление ошибки из json-структуры ошибки полученной от СБИС
//
// Параметры:
//  Описание - Строка - json с описанием ошибки, полученной от СБИС
//
// Возвращаемое значение:
//   Строка - описание ошибки
//
Функция ПричинаОшибкиИзЯдра(Описание) Экспорт
	
	Попытка
		Структура = ЗначениеИзСтрокиВнутр(Описание);
	Исключение
		// может прийти не десеарилизуемая строка
		Возврат Описание;
	КонецПопытки;
    	
	Если ТипЗнч(Структура.detail) = Тип("Строка") Тогда
		ПодробноеОписание = Структура.detail;
	ИначеЕсли ЗначениеЗаполнено(Структура.detail) Тогда
		ПодробноеОписание = Структура.detail["error"]["details"];
	Иначе
		ПодробноеОписание = "";
	КонецЕсли;
	
	ТекстОшибки = Структура.message + ?(ПустаяСтрока(ПодробноеОписание), "", " - " + ПодробноеОписание);
	
	Возврат ТекстОшибки;
	
КонецФункции

// Обновляет данные в регистре Saby_Состояние по документу.
//
// Параметры:
//  Параметры - Структура - данные для записи
//
Процедура ОбновитьДанныеСостояния(Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Параметры.ИД) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("Аккаунт") Или Не ЗначениеЗаполнено(Параметры.Аккаунт) Тогда
		Аккаунт = АвторизованныйАккаунтСтрокой();
	Иначе
		Аккаунт = Параметры.Аккаунт;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Ссылка) = Тип("Структура") Тогда
		Параметры.Ссылка.Идентификатор       = Параметры.ИД;
		Параметры.Ссылка.ТекущийЭтап         = Параметры.АктивныйЭтап;
		Параметры.Ссылка.АктивныйЭтапСтрокой = Параметры.АктивныйЭтап;
	Иначе
		МодульКода("РегистрыСведений.Saby_Состояние").ОбновитьПоUID(
			Параметры.ИД, ,
			Аккаунт,
			СокрЛП(Параметры.АктивныйЭтап),
			Параметры.Состояние,
			Параметры.Ссылка, , ,
			Параметры.СбисИД);
	КонецЕсли;
	
КонецПроцедуры

// Считывает данные из регистра Saby_Состояние по документу.
//
// Параметры:
//  Ссылка - ДокументСсылка - документ для получения данных
// 
// Возвращаемое значение:
//   Выборка - найденная из регистра запись или неопределенно
//
Функция ДанныеСостоянияОбъекта(Ссылка) Экспорт
	
	Если ТипЗнч(Ссылка) = Тип("Структура") Тогда
		Данные = Новый Структура;
		Данные.Вставить("SbisId",       "");
		Данные.Вставить("UID",          Ссылка.Идентификатор);
		Данные.Вставить("Аккаунт",      "");
		Данные.Вставить("АктивныйЭтап", Ссылка.АктивныйЭтапСтрокой);
		Данные.Вставить("Объект",       Неопределено);
		Данные.Вставить("Сервис",       "");
		Данные.Вставить("Состояние",    Ссылка.Состояние);
	Иначе
		Аккаунт = АвторизованныйАккаунтСтрокой();
		Данные = МодульКода("РегистрыСведений.Saby_Состояние").ПрочитатьПоОбъекту(Ссылка, , Аккаунт);
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

// Записывает ошибки загрузки документов в журнал регистрации.
//
// Параметры:
//  СписокДокументов - Соответствие - ошибки по документам
//
Процедура ЗаписьОшибокВЖурналРегистрации(СписокДокументов) Экспорт
	
	Если ТипЗнч(СписокДокументов) <> Тип("Соответствие") Тогда
		Возврат; 
	КонецЕсли;
		
	Если СписокДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ОшибкиДокумента Из СписокДокументов Цикл
		
		МассивОшибок = ОписанияОшибок(ОшибкиДокумента.Значение.СписокОшибок);
		
		Описание = "Не выгружен документ " + СокрЛП(ОшибкиДокумента.Ключ);
		Текст    = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(МассивОшибок, Символы.ПС);
		
		ЗаписатьОшибкуВЖурналРегистрации(ОшибкиДокумента.Ключ, Описание, Текст);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьРеквизитыИзБД(СтруктураДанных, СсылкаНаДокумент, ПсевдонимыРеквизитов, ТекстЗапросаРеквизитов) Экспорт
		
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапросаРеквизитов);
	
	НужныеКолонки = Новый Соответствие;
	Для Каждого ПсевдонимРеквизита Из ПсевдонимыРеквизитов Цикл
		НужныеКолонки.Вставить(ПсевдонимРеквизита, Истина);
	КонецЦикла;
	
	Колонки = СхемаЗапроса.ПакетЗапросов[0].Колонки;
	
	ИндексКолонки = 0;
	Пока ИндексКолонки < Колонки.Количество() Цикл
		
		Псевдоним = Колонки[ИндексКолонки].Псевдоним;
		
		Если НужныеКолонки[Псевдоним] = Неопределено Тогда
			Колонки.Удалить(ИндексКолонки);
		Иначе
			ИндексКолонки = ИндексКолонки + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	ЗапросДанных.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	
	МассивДанных = МодульКода("Saby_ТНОбщегоНазначенияСервер").ТаблицаЗначенийВМассивЭПД(ЗапросДанных.Выполнить().Выгрузить());
	
	Для Каждого КлючЗначение Из МассивДанных[0] Цикл
		СтруктураДанных.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьТаблицыИзБД(СтруктураДанных, СсылкаНаДокумент, Таблицы,
		ШаблонЗапросаТаблиц, НестандартныеЗапросыТаблиц) Экспорт
	
	МассивЗапросов = Новый Массив;
	
	ЗапросДанных = Новый Запрос;
	
	СоответствиеПакетныхЗапросов = Новый Соответствие;
	Для Каждого ИмяТаблицы Из Таблицы Цикл
		
		ТекстЗапроса = НестандартныеЗапросыТаблиц.Получить(ИмяТаблицы);
		Если ТекстЗапроса = Неопределено Тогда
			ТекстЗапроса = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(ШаблонЗапросаТаблиц, ИмяТаблицы);
		КонецЕсли;
		
		Пакеты = СтрЧислоВхождений(ТекстЗапроса, ";");
		Если Пакеты > 0 Тогда
			СоответствиеПакетныхЗапросов.Вставить(ИмяТаблицы, Пакеты);
		КонецЕсли;	
			
		МассивЗапросов.Добавить(ТекстЗапроса);
		
	КонецЦикла;
	
	РазделительЗапросов = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ЗапросДанных.Текст = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(МассивЗапросов, РазделительЗапросов);
	ЗапросДанных.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	
	Результат = ЗапросДанных.ВыполнитьПакет();
	
	Индекс = 0;
	Для Каждого ИмяТаблицы Из Таблицы Цикл
				
		ПакетыЗапроса = СоответствиеПакетныхЗапросов[ИмяТаблицы];
		Сдвиг = ?(ЗначениеЗаполнено(ПакетыЗапроса), ПакетыЗапроса, 0);
		
		НомерТаблицыВЗапросе = Индекс + Сдвиг;
		
		СтруктураДанных.Вставить(ИмяТаблицы, Результат[НомерТаблицыВЗапросе].Выгрузить());
		Индекс = НомерТаблицыВЗапросе + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Функция РеквизитыДокументовСбис(МассивДокументов) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивДокументов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументыЭПД.Наименование КАК Наименование,
	|	ДокументыЭПД.Номер КАК Номер,
	|	ДокументыЭПД.Дата КАК Дата,
	|	ДокументыЭПД.Дата КАК ДатаВремя,
	|	ДокументыЭПД.ДопСведения КАК ДополнительныеСведения,
	|	ДокументыЭПД.Ссылка КАК Ссылка,
	|	ДокументыЭПД.Тип КАК Тип
	|ИЗ
	|	Справочник.Saby_ДокументыЭПД КАК ДокументыЭПД
	|ГДЕ
	|	ДокументыЭПД.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыЭПДДанныеЮрЛиц.Ссылка КАК Ссылка,
	|	ДокументыЭПДДанныеЮрЛиц.НаименованиеОрганизации КАК НаименованиеОрганизации,
	|	ДокументыЭПДДанныеЮрЛиц.ИНН КАК ИНН,
	|	ДокументыЭПДДанныеЮрЛиц.КПП КАК КПП,
	|	ДокументыЭПДДанныеЮрЛиц.СтранаРегистрации КАК СтранаРегистрации,
	|	ДокументыЭПДДанныеЮрЛиц.ОГРН КАК ОГРН,
	|	ДокументыЭПДДанныеЮрЛиц.ЮрФизЛицо КАК ЮрФизЛицо
	|ИЗ
	|	Справочник.Saby_ДокументыЭПД.ДанныеЮрЛиц КАК ДокументыЭПДДанныеЮрЛиц
	|ГДЕ
	|	ДокументыЭПДДанныеЮрЛиц.Ссылка В(&МассивСсылок)";
	
	ИмяКонфигурации = ИмяКонфигурации();
	ДоступныеКонфигурации = ДоступныеКонфигурации();
	
	Если ИмяКонфигурации = ДоступныеКонфигурации.УАТ Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"ДокументыЭПДСтороны.Сторона.РегистрационныйНомер",
			"&ПустаяСтрока");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаДокументов = РезультатЗапроса[0].Выбрать();
	РеквизитыСторон   = РезультатЗапроса[1].Выгрузить();
	
	Соответствие = Новый Соответствие;
	
	КолонкиИсключения = Новый Массив;
	КолонкиИсключения.Добавить("Ссылка");
	
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ПараметрыСтроки = СтрокаТаблицыЗначенийВСтруктуруЭПД(
			ВыборкаДокументов, КолонкиИсключения);
		ПараметрыСтроки.Дата = МодульКода("Saby_ТНВыгрузкаСервер").ЗначениеДатыВремяДляВыгрузки(ПараметрыСтроки.Дата, Истина);
		
		// добавим стороны документа
		Отбор = Новый Структура;
		Отбор.Вставить("Ссылка", ВыборкаДокументов.Ссылка);
		НайденныеСтроки = РеквизитыСторон.НайтиСтроки(Отбор);
		
		МассивСторон = ПараметрыСторонДокумента(НайденныеСтроки);
		
		ПараметрыСтроки.Вставить("Стороны", МассивСторон);
		
		Соответствие.Вставить(ВыборкаДокументов.Ссылка, ПараметрыСтроки);
		
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции

Функция РеквизитыДокументаСбисИзСтрокиТаблицы(СтрокаДокумента) Экспорт
	
	КолонкиИсключения = Новый Массив;
	КолонкиИсключения.Добавить("Путь");
	
	ПараметрыСтроки = СтрокаТаблицыЗначенийВСтруктуруЭПД(СтрокаДокумента, КолонкиИсключения);
	
	ПараметрыСтроки.Дата = МодульКода("Saby_ТНВыгрузкаСервер").ЗначениеДатыВремяДляВыгрузки(ПараметрыСтроки.Дата, Истина);
		
	МассивСторон = ПараметрыСторонДокумента(СтрокаДокумента.ДанныеЮрЛиц);
		
	ПараметрыСтроки.Вставить("Стороны", МассивСторон);
	
	Возврат ПараметрыСтроки;
	
КонецФункции

Процедура ДозаполнитьНовыеЭлементыДаннымиСОнлайна(ТекстЗапроса, МенеджерСправочника) Экспорт
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст = ТекстЗапроса;
	
	ЗапросДанных.УстановитьПараметр("Идентификатор", МенеджерСправочника.ИдентификаторДозаполнения());
	
	МассивНаименований = Новый Массив;
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Пока ВыборкаДанных.Следующий() Цикл
		Наименование = МенеджерСправочника.ПреобразоватьНаименование(ВыборкаДанных.Наименование);
		МассивНаименований.Добавить(Наименование);
	КонецЦикла;
	
	Если МассивНаименований.Количество() > 0 Тогда
		
		МенеджерСправочника.ЗагрузитьСОнлайна(МассивНаименований);
		
		ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
		Пока ВыборкаДанных.Следующий() Цикл
			ОбъектСправочника = ВыборкаДанных.Ссылка.ПолучитьОбъект();
			ОбъектСправочника.Идентификатор = "";
			ОбъектСправочника.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураПредставленияТитула(ТипТитула, КодТитула, ИмяПодстановки = "") Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("КодТитула",      КодТитула);
	РезультатФункции.Вставить("ИмяПодстановки", ИмяПодстановки);
	
	Имя           = "";
	Представление = "";
	Если Не ТипТитула.Пустая() Тогда
		МетаданныеПеречисления = ТипТитула.Метаданные();
		ИндексТипа = Перечисления[МетаданныеПеречисления.Имя].Индекс(ТипТитула);
		МетаданныеПеречисления = МетаданныеПеречисления.ЗначенияПеречисления[ИндексТипа];
		Имя           = МетаданныеПеречисления.Имя;
		Представление = МетаданныеПеречисления.Синоним;
	КонецЕсли;
	
	РезультатФункции.Вставить("Имя",           Имя);
	РезультатФункции.Вставить("Представление", Представление);
	
	Возврат РезультатФункции;
	
КонецФункции

// Установка отбора в динамическом списке по аккаунту
//  Параметры:
// 	 Список - ДинамическийСписок - список для установки отбора
//
Процедура УстановитьПараметрАккаунтаВСписке(Список) Экспорт
	
	Аккаунт = АвторизованныйАккаунтСтрокой();
	
	Использование = Истина;
	
	ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Аккаунт"));
	Если ЗначениеПараметраКомпоновкиДанных <> Неопределено Тогда
		Если Использование И ЗначениеПараметраКомпоновкиДанных.Значение <> Аккаунт Тогда
			ЗначениеПараметраКомпоновкиДанных.Значение = Аккаунт;
		КонецЕсли;
		Если ЗначениеПараметраКомпоновкиДанных.Использование <> Использование Тогда
			ЗначениеПараметраКомпоновкиДанных.Использование = Использование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Найдем ответсвенного по роли или идентификатору строки
// Параметры:
// 	ОтветственныеЛица   - ТаблицаЗначений - ответсвенные лица документа ЭПД  
// 	РольСтрокой         - Строка          - представление роли из перечисления "Saby_РолиОтветственных"  
//  ИдентификаторСтроки - Число           - идентификатор строки ТаблицыЗначений
//   
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений - строка с нужной ролью
//
Функция СтруктураОтветственного(Знач ОтветственныеЛица, Знач РольСтрокой, Знач ИдентификаторСтроки = Неопределено) Экспорт
	
	РезультатФункции = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтруктураОтветственногоЛица();
	
	Если ИдентификаторСтроки = Неопределено Тогда
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Роль", ЗначениеМетаданных("Saby_РолиОтветственных." + РольСтрокой));
		
		НайденныеСтроки = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").НайтиСтрокиУниверсально(
			ОтветственныеЛица, СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(РезультатФункции, НайденныеСтроки[0]);
			ИдентификаторСтроки = ПолучитьИдентификаторУниверсально(ОтветственныеЛица, НайденныеСтроки[0]);
			РезультатФункции.ИдентификаторСтроки = ИдентификаторСтроки;
		Иначе
			РезультатФункции.Роль = СтруктураПоиска.Роль;
		КонецЕсли;
		
	Иначе
		
		СтрокаОтветственного = НайтиПоИдентификаторуУниверсально(ОтветственныеЛица, ИдентификаторСтроки);
	
		ЗаполнитьЗначенияСвойств(РезультатФункции, СтрокаОтветственного);
		РезультатФункции.ИдентификаторСтроки = ИдентификаторСтроки;
		
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция АвторизованныйАккаунтСтрокой() Экспорт
	
	Аккаунт = "";
	context_params = МодульКода("Saby_Core").ПроверитьНаличиеПараметровПодключения();
	Если context_params <> Неопределено Тогда
		Аккаунт = context_params.account;
	КонецЕсли;
	
	Возврат Аккаунт;
	
КонецФункции

// Возвращает структуру, содержащую значения реквизитов, прочитанные из информационной базы по ссылке на объект.
// Рекомендуется использовать вместо обращения к реквизитам объекта через точку от ссылки на объект
// для быстрого чтения отдельных реквизитов объекта из базы данных.
//
// Если необходимо зачитать реквизит независимо от прав текущего пользователя,
// то следует использовать предварительный переход в привилегированный режим.
//
// Параметры:
//  Ссылка    - ЛюбаяСсылка - объект, значения реквизитов которого необходимо получить.
//            - Строка      - полное имя предопределенного элемента, значения реквизитов которого необходимо получить.
//  Реквизиты - Строка - имена реквизитов, перечисленные через запятую, в формате
//                       требований к свойствам структуры.
//                       Например, "Код, Наименование, Родитель".
//            - Структура
//            - ФиксированнаяСтруктура - в качестве ключа передается
//                       псевдоним поля для возвращаемой структуры с результатом, а в качестве
//                       значения (опционально) фактическое имя поля в таблице.
//                       Если ключ задан, а значение не определено, то имя поля берется из ключа.
//                       Допускается указание имени поля через точку, но при этом параметр КодЯзыка для такого поля
//                       учитываться не будет.
//            - Массив из Строка
//            - ФиксированныйМассив из Строка - имена реквизитов в формате требований к свойствам структуры.
//  ВыбратьРазрешенные - Булево - если Истина, то запрос к объекту выполняется с учетом прав пользователя;
//                                если есть ограничение на уровне записей, то все реквизиты вернутся со 
//                                значением Неопределено; если нет прав для работы с таблицей, то возникнет исключение;
//                                если Ложь, то возникнет исключение при отсутствии прав на таблицу 
//                                или любой из реквизитов.
//  КодЯзыка - Строка - код языка для мультиязычного реквизита. Значение по умолчанию - основной язык конфигурации.
//
// Возвращаемое значение:
//  Структура - содержит имена (ключи) и значения затребованных реквизитов.
//              Если в параметр Реквизиты передана пустая строка, то возвращается пустая структура.
//              Если в параметр Ссылка передана пустая ссылка, то возвращается структура, 
//              соответствующая именам реквизитов со значениями Неопределено.
//              Если в параметр Ссылка передана ссылка несуществующего объекта (битая ссылка), 
//              то все реквизиты вернутся со значением Неопределено.
//
Функция ЗначенияРеквизитовОбъекта(Ссылка, Знач Реквизиты, ВыбратьРазрешенные = Ложь, Знач КодЯзыка = Неопределено) Экспорт
	
	Попытка
		ПолноеИмяОбъектаМетаданных = Ссылка.Метаданные().ПолноеИмя(); 
	Исключение
		ВызватьИсключение МодульКода("Saby_ОбщегоНазначенияКлиентСервер").ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неверный первый параметр %1 в функции %2: 
				|Значение должно быть ссылкой или именем предопределенного элемента.';
				|en = 'Invalid value of the %1 parameter, function %2:
				|The value must contain predefined item name or reference.'"),
			"Ссылка", "Saby_ТНОбщегоНазначенияСервер.ЗначенияРеквизитовОбъекта");
	КонецПопытки;
	
	СтруктураПолей = Новый Структура;
	МультиязычныеРеквизиты = Новый Соответствие;
	СуффиксЯзыка = "";
	
	СформироватьСтруктуруРеквизитов(Реквизиты, СтруктураПолей);
	
	// Подготовка результата (после выполнения запроса переопределится).
	Результат = Новый Структура;
	
	// Формирование текста запроса к выбираемым полям.
	ТекстЗапросаПолей = "";
	Для каждого КлючИЗначение Из СтруктураПолей Цикл
		
		ИмяПоля = ?(ЗначениеЗаполнено(КлючИЗначение.Значение),
						КлючИЗначение.Значение,
						КлючИЗначение.Ключ);
		ПсевдонимПоля = КлючИЗначение.Ключ;
		
		Если МультиязычныеРеквизиты[ИмяПоля] <> Неопределено Тогда
			ИмяПоля = ИмяПоля + СуффиксЯзыка;
		КонецЕсли;
		
		ТекстЗапросаПолей = 
			ТекстЗапросаПолей + ?(ПустаяСтрока(ТекстЗапросаПолей), "", ",") + "
			|	" + ИмяПоля + " КАК " + ПсевдонимПоля;
		
		// Предварительное добавление поля по псевдониму в возвращаемый результат.
		Результат.Вставить(ПсевдонимПоля);
		
	КонецЦикла;
	
	// Если предопределенного нет в ИБ.
	// - приведение результата к отсутствию объекта в ИБ или передаче пустой ссылки.
	Если Ссылка = Неопределено Тогда 
		Возврат Результат;
	КонецЕсли;
	
	РеквизитыВМассив(Реквизиты, СтруктураПолей);
	
	РеквизитыЧерезТочку = Новый Массив;
	Для Индекс = -Реквизиты.ВГраница() По 0 Цикл
		ИмяПоля = Реквизиты[ -Индекс];
		Если Найти(ИмяПоля, ".") Тогда
			РеквизитыЧерезТочку.Добавить(ИмяПоля);
			Реквизиты.Удалить(-Индекс);
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьРезультат(Ссылка, Реквизиты, ВыбратьРазрешенные, КодЯзыка, Результат, СтруктураПолей);
	
	Если Не ЗначениеЗаполнено(РеквизитыЧерезТочку) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Реквизиты = РеквизитыЧерезТочку;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|&ТекстЗапросаПолей КАК ТекстЗапросаПолей
		|ИЗ
		|	&ПолноеИмяОбъектаМетаданных КАК ПсевдонимЗаданнойТаблицы
		|ГДЕ
		|	ПсевдонимЗаданнойТаблицы.Ссылка = &Ссылка";
	
	Если Не ВыбратьРазрешенные Тогда 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ", "");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаПолей КАК ТекстЗапросаПолей", ТекстЗапросаПолей);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолноеИмяОбъектаМетаданных", ПолноеИмяОбъектаМетаданных);
	
	// Выполнение запроса.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// Заполнение реквизитов.
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает значения реквизита, прочитанного из информационной базы по ссылке на объект.
// Рекомендуется использовать вместо обращения к реквизитам объекта через точку от ссылки на объект
// для быстрого чтения отдельных реквизитов объекта из базы данных.
//
// Если необходимо зачитать реквизит независимо от прав текущего пользователя,
// то следует использовать предварительный переход в привилегированный режим.
//
// Параметры:
//  Ссылка    - ЛюбаяСсылка - объект, значения реквизитов которого необходимо получить.
//            - Строка      - полное имя предопределенного элемента, значения реквизитов которого необходимо получить.
//  ИмяРеквизита       - Строка - имя получаемого реквизита.
//                                Допускается указание имени реквизита через точку, но при этом параметр КодЯзыка для
//                                такого реквизита учитываться не будет.
//  ВыбратьРазрешенные - Булево - если Истина, то запрос к объекту выполняется с учетом прав пользователя;
//                                если есть ограничение на уровне записей, то возвращается Неопределено;
//                                если нет прав для работы с таблицей, то возникнет исключение;
//                                если Ложь, то возникнет исключение при отсутствии прав на таблицу
//                                или любой из реквизитов.
//  КодЯзыка - Строка - код языка для мультиязычного реквизита. Значение по умолчанию - основной язык конфигурации.
//
// Возвращаемое значение:
//  Произвольный - если в параметр Ссылка передана пустая ссылка, то возвращается Неопределено.
//                 Если в параметр Ссылка передана ссылка несуществующего объекта (битая ссылка), 
//                 то возвращается Неопределено.
//
Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита, ВыбратьРазрешенные = Ложь, Знач КодЯзыка = Неопределено) Экспорт
	
	Если ПустаяСтрока(ИмяРеквизита) Тогда 
		ВызватьИсключение МодульКода("Saby_ОбщегоНазначенияКлиентСервер").ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неверный второй параметр %1 в функции %2: 
				|Имя реквизита должно быть заполнено.';
				|en = 'Invalid value of the %1 parameter, function %2:
				|The attribute name cannot be empty.'"), "ИмяРеквизита", "Saby_ТНОбщегоНазначенияСервер.ЗначениеРеквизитаОбъекта");
	КонецЕсли;
	
	Результат = ЗначенияРеквизитовОбъекта(Ссылка, ИмяРеквизита, ВыбратьРазрешенные, КодЯзыка);
	Возврат Результат[СтрЗаменить(ИмяРеквизита, ".", "")];
	
КонецФункции 

// Возвращает значения реквизитов, прочитанные из информационной базы для нескольких объектов.
// Рекомендуется использовать вместо обращения к реквизитам объекта через точку от ссылки на объект
// для быстрого чтения отдельных реквизитов объекта из базы данных.
//
// Если необходимо зачитать реквизит независимо от прав текущего пользователя,
// то следует использовать предварительный переход в привилегированный режим.
//
// Параметры:
//  Ссылки - Массив из ЛюбаяСсылка
//         - ФиксированныйМассив из ЛюбаяСсылка - ссылки на объекты.
//           Если массив пуст, то результатом будет пустое соответствие.
//  Реквизиты - Строка - имена реквизитов перечисленные через запятую, в формате требований к свойствам
//                       структуры. Например, "Код, Наименование, Родитель".
//            - Массив из Строка
//            - ФиксированныйМассив из Строка - имена реквизитов в формате требований к свойствам структуры.
//  ВыбратьРазрешенные - Булево - если Истина, то запрос к объектам выполняется с учетом прав пользователя;
//                                если какой-либо объект будет исключен из выборки по правам, то этот объект
//                                будет исключен и из результата;
//                                если Ложь, то возникнет исключение при отсутствии прав на таблицу
//                                или любой из реквизитов.
//  КодЯзыка - Строка - код языка для мультиязычного реквизита. Значение по умолчанию - основной язык конфигурации.
//
// Возвращаемое значение:
//  Соответствие из КлючИЗначение - список объектов и значений их реквизитов:
//   * Ключ - ЛюбаяСсылка - ссылка на объект;
//   * Значение - Структура:
//    ** Ключ - Строка - имя реквизита;
//    ** Значение - Произвольный - значение реквизита.
// 
Функция ЗначенияРеквизитовОбъектов(Ссылки, Знач Реквизиты, ВыбратьРазрешенные = Ложь, Знач КодЯзыка = Неопределено) Экспорт
	
	Если ТипЗнч(Реквизиты) = Тип("Массив") Или ТипЗнч(Реквизиты) = Тип("ФиксированныйМассив") Тогда
		Реквизиты = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(Реквизиты, ",");
	КонецЕсли;
	
	Если ПустаяСтрока(Реквизиты) Тогда 
		ВызватьИсключение МодульКода("Saby_ОбщегоНазначенияКлиентСервер").ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неверный второй параметр %1 в функции %2: 
				|Поле объекта должно быть указано.';
				|en = 'Invalid value of the %1 parameter, function %2:
				|The object field must be specified.'"), "Реквизиты", "Saby_ТНОбщегоНазначенияСервер.ЗначенияРеквизитовОбъектов");
	КонецЕсли;
	
	Если Найти(Реквизиты, ".") <> 0 Тогда 
		ВызватьИсключение МодульКода("Saby_ОбщегоНазначенияКлиентСервер").ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неверный второй параметр %1 в функции %2: 
				|Обращение через точку не поддерживается.';
				|en = 'Invalid value of the %1 parameter, function %2:
				|Dot syntax is not supported.'"), "Реквизиты", "Saby_ТНОбщегоНазначенияСервер.ЗначенияРеквизитовОбъектов");
	КонецЕсли;
	
	ЗначенияРеквизитов = Новый Соответствие;
	Если Ссылки.Количество() = 0 Тогда
		Возврат ЗначенияРеквизитов;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодЯзыка) Тогда
		КодЯзыка = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(КодЯзыка, "_", Истина)[0];
	КонецЕсли;
	
	РеквизитыТекстЗапроса = Реквизиты;
	
	СсылкиПоТипам = Новый Соответствие;
	Для Каждого Ссылка Из Ссылки Цикл
		Тип = ТипЗнч(Ссылка);
		Если СсылкиПоТипам[Тип] = Неопределено Тогда
			СсылкиПоТипам[Тип] = Новый Массив;
		КонецЕсли;
		ЭлементПоТипу = СсылкиПоТипам[Тип]; // Массив
		ЭлементПоТипу.Добавить(Ссылка);
	КонецЦикла;
	
	ТекстыЗапросов = Новый Массив;
	ПараметрыЗапроса = Новый Структура;
	
	СформироватьТекстыЗапросов(СсылкиПоТипам, ТекстыЗапросов, ПараметрыЗапроса, ВыбратьРазрешенные, РеквизитыТекстЗапроса);
	
	ТекстЗапроса = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(
		ТекстыЗапросов, Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КодЯзыка", КодЯзыка);
	Для Каждого Параметр Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат = Новый Структура(Реквизиты);
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		ЗначенияРеквизитов[Выборка.Ссылка] = Результат;
		
	КонецЦикла;
	
	Возврат ЗначенияРеквизитов;
	
КонецФункции

// Преобразует таблицу значений в массив структур.
// Может использоваться для передачи на клиент данных в том случае, если таблица
// значений содержит только такие значения, которые могут
// быть переданы с сервера на клиент.
//
// Полученный массив содержит структуры, каждая из которых повторяет
// структуру колонок таблицы значений.
//
// Не рекомендуется использовать для преобразования таблиц значений
// с большим количеством строк.
//
// Параметры:
//  ТаблицаЗначений - ТаблицаЗначений - исходная таблица значений.
//
// Возвращаемое значение:
//  Массив - коллекция строк таблицы в виде структур.
//
Функция ТаблицаЗначенийВМассивЭПД(ТаблицаЗначений) Экспорт
	
	Массив = Новый Массив();
	СтруктураСтрокой = "";
	НужнаЗапятая = Ложь;
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		Если НужнаЗапятая Тогда
			СтруктураСтрокой = СтруктураСтрокой + ",";
		КонецЕсли;
		СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
		НужнаЗапятая = Истина;
	КонецЦикла;
	Для Каждого Строка Из ТаблицаЗначений Цикл
		НоваяСтрока = Новый Структура(СтруктураСтрокой);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		Массив.Добавить(НоваяСтрока);
	КонецЦикла;
	Возврат Массив;

КонецФункции

// Единая точка входа для всего функционала описанного в модулях перечислений
//
// Параметры:
//  ИмяМетаданных - Строка - имя метаданных документа
//  ИмяФункции - Строка - имя функции описанной в данном модуле
//  Параметр1 - Произвольный - первый параметр вызываемой функции
//  Параметр2 - Произвольный - второй параметр вызываемой функции
//  Параметр3 - Произвольный - третий параметр вызываемой функции
//
// Возвращаемое значение:
//   Произвольный - зависит от конкретной вызванной функции
//
Функция РезультатФункцииТитулов(ИмяМетаданных, ИмяФункции,
		Параметр1 = Неопределено, Параметр2 = Неопределено, Параметр3 = Неопределено) Экспорт
		
	Если ИмяМетаданных = "Saby_ТранспортнаяНакладная" Тогда
		Возврат МодульКода("Перечисления.Saby_ТипТитулаЭтрН").РезультатФункцииТитуловЭТрН(ИмяФункции, Параметр1, Параметр2, Параметр3);
	ИначеЕсли ИмяМетаданных = "Saby_ПутевойЛист" Тогда
		Возврат МодульКода("Перечисления.Saby_ТипТитулаЭПЛ").РезультатФункцииТитуловЭПЛ(ИмяФункции, Параметр1, Параметр2, Параметр3);
	ИначеЕсли ИмяМетаданных = "Saby_ЗаказНаПеревозку" Тогда
		Возврат МодульКода("Перечисления.Saby_ТипТитулаЗнП").РезультатФункцииТитуловЗнП(ИмяФункции, Параметр1, Параметр2, Параметр3);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПрочитатьПоОбъекту(ОбъектИС, Сервис = "", Аккаунт = "") Экспорт
	
	Возврат Новый Структура("Объект,Сервис,Аккаунт,АктивныйЭтап,Состояние,UID,SbisId");
	
КонецФункции

#Область Пользователи

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции работы с пользователями.

// Только для внутреннего использования.
//
// Возвращаемое значение:
//  СправочникСсылка.Пользователи
//  СправочникСсылка.ВнешниеПользователи
//
Функция АвторизованныйПользователь() Экспорт
	
	// BSLLS:SetPrivilegedMode-off
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		ТекстОшибки = ТекстСообщенияТекущийПользовательНедоступенВСеансеБезРазделителей();
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	// BSLLS:SetPrivilegedMode-on
	
	АвторизованныйПользователь = ПараметрыСеанса.ТекущийВнешнийПользователь;
	
	Если ЗначениеЗаполнено(ПараметрыСеанса.ТекущийПользователь) Тогда
		АвторизованныйПользователь = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	Возврат АвторизованныйПользователь;
	
	КонецФункции
	
#КонецОбласти // Пользователи

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияСервер_ПараметрыДаты
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияСервер_ПараметрыДаты

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеАдресов

Процедура ОбработатьЭлементАдресаКвартиры(ИсходнаяСтруктура, СтруктураАдреса)
	
	Если ИсходнаяСтруктура.Свойство("apartments") Тогда
		Для Каждого Данные Из ИсходнаяСтруктура.apartments Цикл
			Если Данные.type = "Г" Тогда
				СтруктураАдреса.Структура.Город = Данные.number;
			ИначеЕсли Данные.type = "Ул" Тогда
				СтруктураАдреса.Структура.Улица = Данные.number;
			ИначеЕсли Данные.type = "Квартира" Тогда
				СтруктураАдреса.Структура.Квартира = Данные.number;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьЭлементАдресаЗдания(ИсходнаяСтруктура, СтруктураАдреса)
	
	Если ИсходнаяСтруктура.Свойство("buildings") Тогда
		Для Каждого Данные Из ИсходнаяСтруктура.buildings Цикл
			Если Данные.type = "Корпус" Тогда
				СтруктураАдреса.Структура.Корпус = Данные.number;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ЗаполнениеАдресов

Функция НаименованиеДлительнойОперации(Ключ)
	Наименование = Новый Соответствие;
	Наименование.Вставить("ВыгрузитьВСбис", НСтр("ru = 'Загрузка документов в СБИС';
			                 					|en = 'Download documents to SBIS'"));
	Наименование.Вставить("ОбновитьДокументИзСбис", НСтр("ru = 'Синхронизация документов со СБИС';
			                 							|en = 'Document synchronization to SBIS'"));
	Возврат Наименование.Получить(Ключ);	
КонецФункции

#Область ФизЛица

Процедура ПолучитьДанныеФизлиц(МассивФизлиц, МенеджерВременныхТаблиц)
	
	НоваяЗапись = Справочники.ФизическиеЛица.СоздатьЭлемент();
	ЕстьФИО = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ЕстьРеквизитИлиСвойство(НоваяЗапись, "Фамилия");
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.УстановитьПараметр("МассивФизлиц",            МассивФизлиц);
	
	ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ЕстьФИО Тогда
		ЗапросДанных.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВложенныйЗапрос.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВложенныйЗапрос.Фамилия КАК Фамилия,
		|	ВложенныйЗапрос.Имя КАК Имя,
		|	ВложенныйЗапрос.Отчество КАК Отчество,
		|	ВложенныйЗапрос.Порядок КАК Порядок
		|ПОМЕСТИТЬ ФИОФизлиц
		|ИЗ
		|	(ВЫБРАТЬ
		|		ФИОФизическихЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
		|		ФИОФизическихЛицСрезПоследних.Фамилия КАК Фамилия,
		|		ФИОФизическихЛицСрезПоследних.Имя КАК Имя,
		|		ФИОФизическихЛицСрезПоследних.Отчество КАК Отчество,
		|		1 КАК Порядок
		|	ИЗ
		|		РегистрСведений.ФИОФизическихЛиц.СрезПоследних(, ФизическоеЛицо В (&МассивФизлиц)) КАК ФИОФизическихЛицСрезПоследних
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ФизическиеЛица.Ссылка,
		|		ФизическиеЛица.Фамилия,
		|		ФизическиеЛица.Имя,
		|		ФизическиеЛица.Отчество,
		|		2
		|	ИЗ
		|		Справочник.ФизическиеЛица КАК ФизическиеЛица
		|	ГДЕ
		|		ФизическиеЛица.Ссылка В (&МассивФизлиц)) КАК ВложенныйЗапрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВложенныйЗапрос.Порядок
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо";
	Иначе
		ЗапросДанных.Текст =
		"ВЫБРАТЬ
		|	ФИОФизическихЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ФИОФизическихЛицСрезПоследних.Фамилия КАК Фамилия,
		|	ФИОФизическихЛицСрезПоследних.Имя КАК Имя,
		|	ФИОФизическихЛицСрезПоследних.Отчество КАК Отчество
		|ПОМЕСТИТЬ ФИОФизлиц
		|ИЗ
		|	РегистрСведений.ФИОФизическихЛиц.СрезПоследних(, ФизическоеЛицо В (&МассивФизлиц)) КАК ФИОФизическихЛицСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо";
	КонецЕсли;
	
	ЗапросДанных.Выполнить();
	
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
	|	ФизическиеЛица.Наименование КАК Наименование,
	|	ФизическиеЛица.ИНН КАК ИНН,
	|	ЕСТЬNULL(ФИОФизлиц.Фамилия, """") КАК Фамилия,
	|	ЕСТЬNULL(ФИОФизлиц.Имя, """") КАК Имя,
	|	ЕСТЬNULL(ФИОФизлиц.Отчество, """") КАК Отчество
	|ПОМЕСТИТЬ РеквизитыФизлиц
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФИОФизлиц КАК ФИОФизлиц
	|		ПО (ФИОФизлиц.ФизическоеЛицо = ФизическиеЛица.Ссылка)
	|ГДЕ
	|	ФизическиеЛица.Ссылка В(&МассивФизлиц)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК ФизическоеЛицо,
	|	ФизическиеЛицаКонтактнаяИнформация.Тип КАК Тип,
	|	ФизическиеЛицаКонтактнаяИнформация.Представление КАК Представление
	|ПОМЕСТИТЬ ТелефоныЭлектроннаяПочтаФизлиц
	|ИЗ
	|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	|ГДЕ
	|	ФизическиеЛицаКонтактнаяИнформация.Ссылка В(&МассивФизлиц)
	|	И ФизическиеЛицаКонтактнаяИнформация.Тип В (ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон), ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ФИОФизлиц";
	
	ЗапросДанных.Выполнить();
	
КонецПроцедуры

Процедура ПолучитьВУФизлиц(МассивФизлиц, МенеджерВременныхТаблиц)
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.УстановитьПараметр("МассивФизлиц", МассивФизлиц);
	ЗапросДанных.УстановитьПараметр(
		"ВодительскоеУдостоверение", Справочники.ВидыДокументовФизическихЛиц.ВодительскоеУдостоверение);
	
	ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	ДокументыФизическихЛицСрезПоследних.Физлицо КАК ФизическоеЛицо,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК Серия,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК Номер,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи КАК ДатаВыдачи
	|ПОМЕСТИТЬ ВУФизлиц
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(
	|			,
	|			Физлицо В (&МассивФизлиц)
	|				И ВидДокумента = &ВодительскоеУдостоверение) КАК ДокументыФизическихЛицСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо";
	
	ЗапросДанных.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область ЮрЛица

Процедура ПолучитьДанныеЮрЛицИзБазы(МассивЮрЛиц, МенеджерВременныхТаблиц)
	
	ИмяКонфигурации = ИмяКонфигурации();
	ДоступныеКонфигурации = ДоступныеКонфигурации();
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ИмяКонфигурации = ДоступныеКонфигурации.ЕРП Тогда
		
		ЗапросДанных.Текст =
		"ВЫБРАТЬ
		|	ДополнительныеСведения.Объект КАК Ссылка,
		|	ДополнительныеСведения.Значение КАК Значение
		|ПОМЕСТИТЬ ДопСведения
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Объект В(&МассивЮрЛиц)
		|	И ДополнительныеСведения.Свойство.Наименование = &ИмяСвойстваКодФилиала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Ссылка,
		|	Контрагенты.Наименование КАК Наименование,
		|	Контрагенты.ИНН КАК ИНН,
		|	Контрагенты.КПП КАК КПП,
		|	Контрагенты.СтранаРегистрации.Наименование КАК СтранаРегистрации,
		|	Контрагенты.РегистрационныйНомер КАК ОГРН,
		|	ВЫБОР
		|		КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
		|			ТОГДА ""ЮрЛицо""
		|		КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
		|			ТОГДА ""ИндивидуальныйПредприниматель""
		|		КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент)
		|			ТОГДА ""ЮрЛицоНеРезидент""
		|		КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
		|			ТОГДА ""ИндивидуальныйПредприниматель""
		|	КОНЕЦ КАК ЮрФизЛицо,
		|	ЕСТЬNULL(ДопСведения.Значение, """") КАК КодФилиала
		|ПОМЕСТИТЬ ДанныеЮрЛицИзБазы
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДопСведения КАК ДопСведения
		|		ПО Контрагенты.Ссылка = ДопСведения.Ссылка
		|ГДЕ
		|	Контрагенты.Ссылка В(&МассивЮрЛиц)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.Ссылка,
		|	Организации.Наименование,
		|	Организации.ИНН,
		|	Организации.КПП,
		|	Организации.СтранаРегистрации.Наименование,
		|	Организации.ОГРН,
		|	ВЫБОР
		|		КОГДА Организации.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
		|			ТОГДА ""ЮрЛицо""
		|		КОГДА Организации.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
		|			ТОГДА ""ИндивидуальныйПредприниматель""
		|		КОГДА Организации.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент)
		|			ТОГДА ""ЮрЛицоНеРезидент""
		|		КОГДА Организации.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
		|			ТОГДА ""ИндивидуальныйПредприниматель""
		|	КОНЕЦ КАК ЮрФизЛицо,
		|	ЕСТЬNULL(ДопСведения.Значение, """")
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДопСведения КАК ДопСведения
		|		ПО Организации.Ссылка = ДопСведения.Ссылка
		|ГДЕ
		|	Организации.Ссылка В(&МассивЮрЛиц)";
		
	Иначе
		
		ЗапросДанных.Текст =
		"ВЫБРАТЬ
		|	ДополнительныеСведения.Объект КАК Ссылка,
		|	ДополнительныеСведения.Значение КАК Значение
		|ПОМЕСТИТЬ ДопСведения
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Объект В(&МассивЮрЛиц)
		|	И ДополнительныеСведения.Свойство.Наименование = &ИмяСвойстваКодФилиала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Ссылка,
		|	Контрагенты.Наименование КАК Наименование,
		|	Контрагенты.ИНН КАК ИНН,
		|	Контрагенты.КПП КАК КПП,
		|	Контрагенты.СтранаРегистрации.Наименование КАК СтранаРегистрации,
		|	Контрагенты.РегистрационныйНомер КАК ОГРН,
		|	ВЫБОР
		|		КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
		|			ТОГДА ""ЮрЛицо""
		|		КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|			ТОГДА ""ИндивидуальныйПредприниматель""
		|	КОНЕЦ КАК ЮрФизЛицо,
		|	ЕСТЬNULL(ДопСведения.Значение, """") КАК КодФилиала
		|ПОМЕСТИТЬ ДанныеЮрЛицИзБазы
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДопСведения КАК ДопСведения
		|		ПО Контрагенты.Ссылка = ДопСведения.Ссылка
		|ГДЕ
		|	Контрагенты.Ссылка В(&МассивЮрЛиц)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.Ссылка,
		|	Организации.Наименование,
		|	Организации.ИНН,
		|	Организации.КПП,
		|	Организации.СтранаРегистрации.Наименование,
		|	Организации.ОГРН,
		|	ВЫБОР
		|		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
		|			ТОГДА ""ЮрЛицо""
		|		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|			ТОГДА ""ИндивидуальныйПредприниматель""
		|	КОНЕЦ,
		|	ЕСТЬNULL(ДопСведения.Значение, """")
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДопСведения КАК ДопСведения
		|		ПО Организации.Ссылка = ДопСведения.Ссылка
		|ГДЕ
		|	Организации.Ссылка В(&МассивЮрЛиц)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДопСведения";
		
		Если ИмяКонфигурации = ДоступныеКонфигурации.УАТ Тогда
			ЗапросДанных.Текст = СтрЗаменить(ЗапросДанных.Текст, "Контрагенты.РегистрационныйНомер", "&ПустаяСтрока");
		КонецЕсли;
		
		Если ИмяКонфигурации = ДоступныеКонфигурации.Бух3 Тогда
			ЗапросДанных.Текст = СтрЗаменить(
				ЗапросДанных.Текст,
				"	ВЫБОР
				|		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
				|			ТОГДА ""ЮрЛицо""
				|		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
				|			ТОГДА ""ФизЛицо""
				|	КОНЕЦ",
				"	ВЫБОР
				|		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
				|			ТОГДА ""ЮрЛицо""
				|			ИНАЧЕ ""ИндивидуальныйПредприниматель""
				|	КОНЕЦ");
			ЗапросДанных.Текст = СтрЗаменить(
				ЗапросДанных.Текст,
				"	ВЫБОР
				|		КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
				|			ТОГДА ""ЮрЛицо""
				|		КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
				|			ТОГДА ""ИндивидуальныйПредприниматель""
				|	КОНЕЦ",
				"	ВЫБОР
				|		КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
				|			ТОГДА ""ЮрЛицо""
				|			ИНАЧЕ ""ИндивидуальныйПредприниматель""
				|	КОНЕЦ");
		КонецЕсли;
		
	КонецЕсли;
	
	ЗапросДанных.УстановитьПараметр("ПустаяСтрока", "");
	ЗапросДанных.УстановитьПараметр("МассивЮрЛиц",  МассивЮрЛиц);
	
	ЗапросДанных.УстановитьПараметр(
		"ИмяСвойстваКодФилиала",
		МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ИмяСвойстваКодФилиала());
	
	ЗапросДанных.Выполнить();
	
КонецПроцедуры

Функция ПараметрыСторонДокумента(Стороны)
	
	МассивСторон = Новый Массив;
	Для Каждого Сторона Из Стороны Цикл
		
		ПараметрыСтороны = Новый Структура;
		ПараметрыСтороны.Вставить("Наименование", Сторона.НаименованиеОрганизации);
		
		// наименование органа исполнительной власти выдавшего документ
		// пока не храним это значение
		Если Сторона.ЮрФизЛицо = ЗначениеМетаданных("Saby_ЮрФизЛицо.ЮрЛицо")
			Или Сторона.ЮрФизЛицо = ЗначениеЮрФизЛицаКонфигурации("ЮрЛицо") Тогда
			
			ПараметрыСтороны.Вставить("ЮЛ", Новый Структура);
			ПараметрыСтороны.ЮЛ.Вставить("ИНН", Сторона.ИНН);
			
		ИначеЕсли Сторона.ЮрФизЛицо = ЗначениеМетаданных("Saby_ЮрФизЛицо.ЮрЛицоНеРезидент")
			Или Сторона.ЮрФизЛицо = ЗначениеЮрФизЛицаКонфигурации("ЮрЛицоНеРезидент") Тогда 
			
			СтруктураИнЛица = МодульКода("Saby_ТНВыгрузкаСервер").ШаблонИностранногоЛица();
			
			// Поиск будет только по наименованию
			СтруктураИнЛица.Страна       = СокрЛП(Сторона.СтранаРегистрации);
			СтруктураИнЛица.Наименование = Сторона.НаименованиеОрганизации;
			
			ПараметрыСтороны.Вставить("ИностранноеЛицо", СтруктураИнЛица);
			
		Иначе  
			
			// Физ или ИП
			ПараметрыСтороны.Вставить("ФЛ", Новый Структура);
			ПараметрыСтороны.ФЛ.Вставить("ИНН", Сторона.ИНН);
			
		КонецЕсли;
		
		МассивСторон.Добавить(ПараметрыСтороны);
		
	КонецЦикла;
	
	Возврат МассивСторон;
	
КонецФункции

#КонецОбласти

Функция ПараметрыСоответствия()
	
	Соответствие = Новый Соответствие;

	Структура = Новый Структура;
	Структура.Вставить("Соответствие",    Соответствие);
	Структура.Вставить("ИмяПеречисления", "");
	Структура.Вставить("ПоискПоЗначению", Ложь);
	
	Возврат Структура;
	
КонецФункции

Процедура ДополнитьСоответствие(Параметры, ИмяЗначения, Значение)
	
	Соответствие    = Параметры.Соответствие;
	ИмяПеречисления = Параметры.ИмяПеречисления;
	
	Если ЗначениеЗаполнено(ИмяПеречисления) Тогда
		ЗначениеПеречисления = ЗначениеМетаданных(ИмяПеречисления + "." + ИмяЗначения); 
	Иначе 
		ЗначениеПеречисления = ИмяЗначения;
	КонецЕсли;
	
	Если Параметры.ПоискПоЗначению Тогда 
		Соответствие.Вставить(Значение, ЗначениеПеречисления);
	Иначе 
		Соответствие.Вставить(ЗначениеПеречисления, Значение);
	КонецЕсли;
		
КонецПроцедуры

Процедура ЗачиститьШтрафы(ОбъектДок, КлючиОтметокДляУдаления)
	
	Если Не МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ЕстьРеквизитИлиСвойство(ОбъектДок, "Штрафы") Тогда
		Возврат;
	КонецЕсли;
	
	ИндексШтрафы = ОбъектДок.Штрафы.Количество() - 1;
	Пока ИндексШтрафы >= 0 Цикл
				
		СтрокаШтраф = ОбъектДок.Штрафы[ИндексШтрафы];
			
		ЕстьСтрока = КлючиОтметокДляУдаления.Получить(СтрокаШтраф.КлючСтроки_Отметки);
		Если ЕстьСтрока <> Неопределено Тогда
			ОбъектДок.Штрафы.Удалить(ИндексШтрафы);
		КонецЕсли;
		
		ИндексШтрафы = ИндексШтрафы - 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗачиститьАкты(ОбъектДок, КлючиОтметокДляУдаления)
	
	Если Не МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ЕстьРеквизитИлиСвойство(ОбъектДок, "Акты") Тогда
		Возврат;
	КонецЕсли;
	
	ИндексАкты = ОбъектДок.Акты.Количество() - 1;
	Пока ИндексАкты >= 0 Цикл
				
		СтрокаАкт = ОбъектДок.Акты[ИндексАкты];
			
		ЕстьСтрока = КлючиОтметокДляУдаления.Получить(СтрокаАкт.КлючСтроки_Отметки);
		Если ЕстьСтрока <> Неопределено  Тогда
			ОбъектДок.Акты.Удалить(ИндексАкты);
		КонецЕсли;
		
		ИндексАкты = ИндексАкты - 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьВремяВМассивДаты(МассивИтог, ЧастьВремя, ЕстьВремя, ФорматБезСмещения, СмещениеВСекундах)
	
	Если Не ЕстьВремя Тогда
		Возврат;
	КонецЕсли;
	
	ЧислоДвоеточийВДате = 2;
	
	Если ФорматБезСмещения Тогда 
		
		Время = ЧастьВремя;    
		Если СтрЧислоВхождений(ЧастьВремя, ":") = ЧислоДвоеточийВДате Тогда
			РазделительВремя = ":";
		Иначе
			РазделительВремя = ".";
		КонецЕсли;
		
	Иначе 
		
		// может быть +/-
		Если Найти(ЧастьВремя, "+") > 0 Тогда
			РазделительСмещение = "+";  
		Иначе 
			РазделительСмещение = "-";
		КонецЕсли;
				
		МассивВремени = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(ЧастьВремя, РазделительСмещение);
		Время         = МассивВремени[0];
		Смещение      = МассивВремени[1];
		
		РазделительВремя = ":";
		
		СмещениеВходящее = Дата("00010101" + СтрЗаменить(Смещение, РазделительВремя, ""));
		СмещениеСеанса   = Дата("00010101") + СмещениеСтандартногоВремени(ЧасовойПоясСеанса());
		
		СмещениеВСекундах = СмещениеВходящее - СмещениеСеанса;
		
	КонецЕсли;
	
	ЧастиВремени = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(Время, РазделительВремя);
	МассивИтог.Добавить(ЧастиВремени[0]);
	МассивИтог.Добавить(ЧастиВремени[1]);
	МассивИтог.Добавить(ЧастиВремени[2]);
	
КонецПроцедуры

Функция ДатаБезВремениВСтандартномФормате(ДатаВходящая)
	
	// старый формат
	// 2023-02-02
	// 2023.01.03
	
	Если Найти(ДатаВходящая, "-") <> 0 Тогда
		МассивЧастейДаты = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(ДатаВходящая, "-");
	Иначе
		МассивЧастейДаты = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(ДатаВходящая, ".");
	КонецЕсли;
	
	// формат может быть перевернут 03.01.2023
	КоличествоСимволовГод = 4;
	ФорматПеревернут = СтрДлина(МассивЧастейДаты[0]) = КоличествоСимволовГод;
	
	Если ФорматПеревернут Тогда
		КоличествоУдалить = МассивЧастейДаты.Количество();
		МассивЧастейДаты.Добавить(МассивЧастейДаты[2]);
		МассивЧастейДаты.Добавить(МассивЧастейДаты[1]);
		МассивЧастейДаты.Добавить(МассивЧастейДаты[0]);
		Пока КоличествоУдалить <> 0 Цикл
			МассивЧастейДаты.Удалить(0);
			КоличествоУдалить = КоличествоУдалить - 1;
		КонецЦикла;
	КонецЕсли;
	
	Возврат МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(МассивЧастейДаты, ".");
	
КонецФункции

#Область ПолучениеДанныхПоСсылкеИзБазы

Функция ДанныеТранспортныхСредствИзКонфигурацииУАТ(МассивСсылокТС, МенеджерВременныхТаблиц)
	
	РезультатФункции = Новый Соответствие;
	Если МассивСсылокТС = Неопределено И МенеджерВременныхТаблиц = Неопределено Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	ОчиститьМенеджерВременныхТаблиц = Ложь;
	Если МенеджерВременныхТаблиц = Неопределено Тогда
		ОчиститьМенеджерВременныхТаблиц = Истина;
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ЗапросДанных = Новый Запрос;
		ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		ЗапросДанных.Текст = 
		"ВЫБРАТЬ
		|	уатТС.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ТаблицаТранспортныхСредств
		|ИЗ
		|	Справочник.уатТС КАК уатТС
		|ГДЕ
		|	уатТС.Ссылка В(&МассивСсылокТС)";
		ЗапросДанных.УстановитьПараметр("МассивСсылокТС", МассивСсылокТС);
		ЗапросДанных.Выполнить();
	КонецЕсли;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	ТранспортныеСредства.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ТранспортныеСредства.Ссылка.ВидМоделиТС = ЗНАЧЕНИЕ(Перечисление.уатВидыМоделейТС.Автотранспорт) ТОГДА ЗНАЧЕНИЕ(Перечисление.Saby_ВидыТС.Автомобиль)
	|		КОГДА ТранспортныеСредства.Ссылка.ВидМоделиТС = ЗНАЧЕНИЕ(Перечисление.уатВидыМоделейТС.Прицеп) ТОГДА ЗНАЧЕНИЕ(Перечисление.Saby_ВидыТС.Прицеп)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.Saby_ВидыТС.ПустаяСсылка)
	|	КОНЕЦ КАК Вид,
	|	ТранспортныеСредства.Ссылка.ГосударственныйНомер КАК РегистрационныйНомер,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ТранспортныеСредства.Ссылка.ТипТС) КАК Тип,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ТранспортныеСредства.Ссылка.Марка) КАК Марка,
	|	ТранспортныеСредства.Ссылка.VIN КАК ВИН,
	|	ЕСТЬNULL(РегистрСведенийуатДокументыТС.Серия, """") + ЕСТЬNULL(РегистрСведенийуатДокументыТС.Номер, """") КАК НомерСТС,
	|	ТранспортныеСредства.Ссылка.Грузоподъемность КАК Грузоподъемность,
	|	ТранспортныеСредства.Ссылка.ПолезныйРазмерВысота / 1000 * ТранспортныеСредства.Ссылка.ПолезныйРазмерДлина / 1000 * ТранспортныеСредства.Ссылка.ПолезныйРазмерШирина / 1000 КАК Вместимость,
	|	ВЫБОР
	|		КОГДА ТранспортныеСредства.Ссылка.ТипВладенияТС = ЗНАЧЕНИЕ(Перечисление.уатТипыВладенияТС.Собственность) ТОГДА ЗНАЧЕНИЕ(Перечисление.Saby_ТипыВладенияТС.Собственность)
	|		КОГДА ТранспортныеСредства.Ссылка.ТипВладенияТС = ЗНАЧЕНИЕ(Перечисление.уатТипыВладенияТС.СовместнаяСобственностьСупругов) ТОГДА ЗНАЧЕНИЕ(Перечисление.Saby_ТипыВладенияТС.СовместнаяСобственностьСупругов)
	|		КОГДА ТранспортныеСредства.Ссылка.ТипВладенияТС = ЗНАЧЕНИЕ(Перечисление.уатТипыВладенияТС.Аренда) ТОГДА ЗНАЧЕНИЕ(Перечисление.Saby_ТипыВладенияТС.Аренда)
	|		КОГДА ТранспортныеСредства.Ссылка.ТипВладенияТС = ЗНАЧЕНИЕ(Перечисление.уатТипыВладенияТС.Лизинг) ТОГДА ЗНАЧЕНИЕ(Перечисление.Saby_ТипыВладенияТС.Лизинг)
	|		КОГДА ТранспортныеСредства.Ссылка.ТипВладенияТС = ЗНАЧЕНИЕ(Перечисление.уатТипыВладенияТС.БезвозмездноеПользование) ТОГДА ЗНАЧЕНИЕ(Перечисление.Saby_ТипыВладенияТС.БезвозмездноеПользование)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.Saby_ТипыВладенияТС.ПустаяСсылка)
	|	КОНЕЦ КАК ТипВладения
	|ИЗ
	|	ТаблицаТранспортныхСредств КАК ТранспортныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уатРегистрационныеДокументы КАК РегистрСведенийуатДокументыТС
	|		ПО ТранспортныеСредства.Ссылка = РегистрСведенийуатДокументыТС.ВладелецДокументов
	|			И РегистрСведенийуатДокументыТС.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
	|			И РегистрСведенийуатДокументыТС.ДатаОкончания <= &ТекущаяДата
	|			И ВидДокумента = ЗНАЧЕНИЕ(Справочник.уатВидыДокументов.СТС)";
	
	ЗапросДанных.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Пока ВыборкаДанных.Следующий() Цикл
		СтруктураТС = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтруктураТранспортногоСредства();
		ЗаполнитьЗначенияСвойств(СтруктураТС, ВыборкаДанных);
		РезультатФункции.Вставить(ВыборкаДанных.Ссылка, СтруктураТС);
	КонецЦикла;
	
	Если ОчиститьМенеджерВременныхТаблиц Тогда
		МенеджерВременныхТаблиц.Закрыть();
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ДанныеТранспортныхСредствИзДругихКонфигураций(МассивСсылокТС, МенеджерВременныхТаблиц = Неопределено)
	
	РезультатФункции = Новый Соответствие;
	Если МассивСсылокТС = Неопределено И МенеджерВременныхТаблиц = Неопределено Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер");

	ЕстьВид = МодульТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(
		Справочники.ТранспортныеСредства.ПустаяСсылка(), "Вид");
	
	Если Не ЕстьВид Тогда
		Возврат ДанныеТранспортныхСредствИзДругихКонфигурацийБезРасширения(МассивСсылокТС);
	КонецЕсли;
	
	ОчиститьМенеджерВременныхТаблиц = Ложь;
	Если МенеджерВременныхТаблиц = Неопределено Тогда
		ОчиститьМенеджерВременныхТаблиц = Истина;
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ЗапросДанных = Новый Запрос;
		ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		ЗапросДанных.Текст = 
		"ВЫБРАТЬ
		|	ТранспортныеСредства.Ссылка КАК Ссылка,
		|	ТранспортныеСредства.Вид КАК Вид
		|ПОМЕСТИТЬ ТаблицаТранспортныхСредств
		|ИЗ
		|	Справочник.ТранспортныеСредства КАК ТранспортныеСредства
		|ГДЕ
		|	ТранспортныеСредства.Ссылка В(&МассивСсылокТС)";
		ЗапросДанных.УстановитьПараметр("МассивСсылокТС", МассивСсылокТС);
		ЗапросДанных.Выполнить();
	КонецЕсли;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	ТранспортныеСредства.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ТранспортныеСредства.Ссылка.Вид = ЗНАЧЕНИЕ(Перечисление.Saby_ВидыТС.ПустаяСсылка)
	|		ТОГДА ТранспортныеСредства.Вид
	|		ИНАЧЕ ТранспортныеСредства.Ссылка.Вид
	|	КОНЕЦ КАК Вид,
	|	ТранспортныеСредства.Ссылка.Код КАК РегистрационныйНомер,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ТранспортныеСредства.Ссылка.Тип) КАК Тип,
	|	ТранспортныеСредства.Ссылка.Марка КАК Марка,
	|	ТранспортныеСредства.Ссылка.VIN КАК ВИН,
	|	ТранспортныеСредства.Ссылка.НомерСТС КАК НомерСТС,
	|	ТранспортныеСредства.Ссылка.ГрузоподъемностьВТоннах КАК Грузоподъемность,
	|	ТранспортныеСредства.Ссылка.ВместимостьВКубическихМетрах КАК Вместимость,
	|	ТранспортныеСредства.Ссылка.ТипВладения КАК ТипВладения,
	|	ТранспортныеСредства.Ссылка.ДокументыПодтверждающиеВладение.(
	|		ДокументВладения КАК ДокументВладения
	|	) КАК ОснованияВладения
	|ИЗ
	|	ТаблицаТранспортныхСредств КАК ТранспортныеСредства";
	
	ЕстьРегистрационныйЗнак = МодульТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(
		Справочники.ТранспортныеСредства.ПустаяСсылка(), "РегистрационныйЗнак");
	
	Если ЕстьРегистрационныйЗнак Тогда
		ЗапросДанных.Текст = СтрЗаменить(
			ЗапросДанных.Текст,
			"ТранспортныеСредства.Ссылка.Код",
			"ТранспортныеСредства.Ссылка.РегистрационныйЗнак");
	КонецЕсли;
		
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Пока ВыборкаДанных.Следующий() Цикл
		СтруктураТС = МодульТНОбщегоНазначенияКлиентСервер.СтруктураТранспортногоСредства();
		ЗаполнитьЗначенияСвойств(СтруктураТС, ВыборкаДанных, , "ОснованияВладения");
		ВыборкаОснованийВладения = ВыборкаДанных.ОснованияВладения.Выбрать();
		МассивОснований = Новый Массив;
		Пока ВыборкаОснованийВладения.Следующий() Цикл
			МассивОснований.Добавить(ВыборкаОснованийВладения.ДокументВладения);
		КонецЦикла;
		Если МассивОснований.Количество() > 0 Тогда
			СтруктураТС.ОснованияВладения = ЗначениеВСтрокуВнутр(МассивОснований);
		КонецЕсли;
		РезультатФункции.Вставить(ВыборкаДанных.Ссылка, СтруктураТС);
	КонецЦикла;
	
	Если ОчиститьМенеджерВременныхТаблиц Тогда
		МенеджерВременныхТаблиц.Закрыть();
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ДанныеТранспортныхСредствИзДругихКонфигурацийБезРасширения(МассивСсылокТС)
	
	РезультатФункции = Новый Соответствие;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.УстановитьПараметр("МассивСсылокТС", МассивСсылокТС);
	
	ЗапросДанных.Текст = 
	"ВЫБРАТЬ *
	|ИЗ
	|	Справочник.ТранспортныеСредства КАК ТранспортныеСредства
	|ГДЕ
	|	ТранспортныеСредства.Ссылка В(&МассивСсылокТС)";
	
	ПустаяСсылка = Справочники.ТранспортныеСредства.ПустаяСсылка();
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер");

	ЕстьРегистрационныйЗнак = МодульТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(
		ПустаяСсылка, "РегистрационныйЗнак");
			
	ЕстьГрузоподъемность = МодульТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(
		ПустаяСсылка, "ГрузоподъемностьВТоннах");
			
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Пока ВыборкаДанных.Следующий() Цикл
		
		СтруктураТС = МодульТНОбщегоНазначенияКлиентСервер.СтруктураТранспортногоСредства();
		ЗаполнитьЗначенияСвойств(СтруктураТС, ВыборкаДанных);
		
		Если ЗначениеЗаполнено(СтруктураТС.Тип) Тогда
			СтруктураТС.Тип = СокрЛП(СтруктураТС.Тип);
		КонецЕсли;	
		
		Если ЕстьГрузоподъемность Тогда 
			СтруктураТС.Грузоподъемность = ВыборкаДанных.ГрузоподъемностьВТоннах; 
			СтруктураТС.Вместимость      = ВыборкаДанных.ВместимостьВКубическихМетрах;
		КонецЕсли;	
		
		Если ЕстьРегистрационныйЗнак Тогда
			СтруктураТС.РегистрационныйНомер = ВыборкаДанных.РегистрационныйЗнак;
		Иначе 
			СтруктураТС.РегистрационныйНомер = ВыборкаДанных.Код;
		КонецЕсли;
				
		РезультатФункции.Вставить(ВыборкаДанных.Ссылка, СтруктураТС);
		
	КонецЦикла;
		
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // ПолучениеДанныхПоСсылкеИзБазы

#Область ДействияПриОбновлении

Функция ПеренестиДанныеТранспортныхСредствВДокумент()
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ОК",             Истина);
	РезультатФункции.Вставить("ОписаниеОшибки", "");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	ВложенныйЗапрос.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ВложенныйЗапрос.Вид КАК Вид
	|ПОМЕСТИТЬ ТранспортныеСредстваИзДокументов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Saby_ТранспортнаяНакладная.Ссылка КАК Ссылка,
	|		Saby_ТранспортнаяНакладная.Отправитель_ТранспортноеСредство КАК ТранспортноеСредство,
	|		ЗНАЧЕНИЕ(Перечисление.Saby_ВидыТС.Автомобиль) КАК Вид
	|	ИЗ
	|		Документ.Saby_ТранспортнаяНакладная КАК Saby_ТранспортнаяНакладная
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.Saby_ТранспортнаяНакладная.ТранспортныеСредства КАК Saby_ТранспортнаяНакладнаяТранспортныеСредства
	|			ПО (Saby_ТранспортнаяНакладнаяТранспортныеСредства.Ссылка = Saby_ТранспортнаяНакладная.Ссылка)
	|				И Saby_ТранспортнаяНакладная.Отправитель_ТранспортноеСредство = Saby_ТранспортнаяНакладнаяТранспортныеСредства.Прицеп
	|	ГДЕ
	|		Saby_ТранспортнаяНакладная.Отправитель_ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.ТранспортныеСредства.ПустаяСсылка)
	|		И Saby_ТранспортнаяНакладнаяТранспортныеСредства.Прицеп ЕСТЬ NULL
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Saby_ТранспортнаяНакладнаяТранспортныеСредства.Ссылка,
	|		Saby_ТранспортнаяНакладнаяТранспортныеСредства.Прицеп,
	|		ЗНАЧЕНИЕ(Перечисление.Saby_ВидыТС.Прицеп)
	|	ИЗ
	|		Документ.Saby_ТранспортнаяНакладная.ТранспортныеСредства КАК Saby_ТранспортнаяНакладнаяТранспортныеСредства
	|	ГДЕ
	|		Saby_ТранспортнаяНакладнаяТранспортныеСредства.Прицеп <> ЗНАЧЕНИЕ(Справочник.ТранспортныеСредства.ПустаяСсылка)
	|		И Saby_ТранспортнаяНакладнаяТранспортныеСредства.Вид = ЗНАЧЕНИЕ(Перечисление.Saby_ВидыТС.ПустаяСсылка)) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТранспортныеСредстваИзДокументов.ТранспортноеСредство КАК Ссылка,
	|	ТранспортныеСредстваИзДокументов.Вид КАК Вид
	|ПОМЕСТИТЬ ТаблицаТранспортныхСредств
	|ИЗ
	|	ТранспортныеСредстваИзДокументов КАК ТранспортныеСредстваИзДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТранспортныеСредстваИзДокументов.Ссылка КАК Ссылка,
	|	ТранспортныеСредстваИзДокументов.ТранспортноеСредство КАК ТранспортноеСредство
	|ИЗ
	|	ТранспортныеСредстваИзДокументов КАК ТранспортныеСредстваИзДокументов
	|ИТОГИ ПО
	|	Ссылка";
		
	ВыборкаПоДокументам = ЗапросДанных.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Если Не ВыборкаПоДокументам.Следующий() Тогда
		Возврат РезультатФункции;
	Иначе
		ВыборкаПоДокументам.Сбросить();
	КонецЕсли;
	
	РеквизитыТранспортныхСредств = РеквизитыТранспортныхСредств(Неопределено, МенеджерВременныхТаблиц);
	
	ОписанияОшибок = Новый Массив;
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		ОбъектДокумента = ВыборкаПоДокументам.Ссылка.ПолучитьОбъект();
		
		ОбъектДокумента.ТранспортныеСредства.Очистить();
		
		ВыборкаПоТранспортнымСредствам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТранспортнымСредствам.Следующий() Цикл
			РеквизитыТС = РеквизитыТранспортныхСредств.Получить(ВыборкаПоТранспортнымСредствам.ТранспортноеСредство);
			НоваяСтрокаТС = ОбъектДокумента.ТранспортныеСредства.Добавить();
			НоваяСтрокаТС.Прицеп = ВыборкаПоТранспортнымСредствам.ТранспортноеСредство;
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТС, РеквизитыТС);
		КонецЦикла;
		
		Попытка
			ОбъектДокумента.Записать();
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			РезультатФункции.ОК = Ложь;
			ОписанияОшибок.Добавить(ИнформацияОбОшибке.Описание);
		КонецПопытки;
		
	КонецЦикла;
	
	Если Не РезультатФункции.ОК Тогда
		РезультатФункции.ОписаниеОшибки = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(
			ОписанияОшибок, Символы.ПС);
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // ДействияПриОбновлении

#Область ФЛК

// Проверяет заполненность реквизитов документа.
//
// Параметры:
//  ДанныеДокумента - Структура - структура реквизитов и табличных часте документа
//  ПараметрыПроверкиРеквизитов - Структура - структура параметров для проверки реквизитов на заполненность
//    Для реквизитов табличных частей реквизиты указываются через точку. Сначала имя табличной части а потом реквизит.
//  Ошибки - Массив - массив структур ошибок. Расхождения по реквизитам.
//
Процедура ПроверитьЗаполненНеЗаполнен(ДанныеДокумента, ПараметрыПроверкиРеквизитов, Ошибки)
	
	ТабличныеЧасти = Новый Структура;
	
	// Проверка реквизитов объекта и заполнение структуры по реквизитам табличных частей.
	Для Каждого Реквизит Из ПараметрыПроверкиРеквизитов.ПроверяемыеРеквизиты Цикл
		
		ПозицияТочки = Найти(Реквизит.РеквизитОбъекта, ".");
		
		Если ПозицияТочки > 0 Тогда 
			// В случае если указан реквизит табличной части
			ПроверкаРеквизитаТабличнойЧасти(Реквизит, ПозицияТочки, ТабличныеЧасти);
		Иначе 
			// В случае если указан реквизит объекта
			ПроверкаРеквизитаОбъекта(Реквизит, ДанныеДокумента, ПараметрыПроверкиРеквизитов, Ошибки);
		КонецЕсли;
		
	КонецЦикла;
	
	ПроверитьЗаполненНеЗаполненРеквизитТЧ(ДанныеДокумента, ПараметрыПроверкиРеквизитов, Ошибки, ТабличныеЧасти);
	
	ПараметрыПроверкиРеквизитов.ПроверяемыеРеквизиты.Очистить();
	
КонецПроцедуры

Процедура ПроверитьЗаполненНеЗаполненРеквизитТЧ(ДанныеДокумента, ПараметрыПроверкиРеквизитов, Ошибки, ТабличныеЧасти)
	
	// Проверка реквизитов в табличных частях
	Для Каждого ТабличнаяЧасть Из ТабличныеЧасти Цикл
		
		ИмяТабличнойЧасти = ТабличнаяЧасть.Ключ;
		ТЧ = ДанныеДокумента[ИмяТабличнойЧасти];
		МассивРеквизитов = ТабличнаяЧасть.Значение;
		
		// Цикл по всем строкам табличной части.
		Для ИндексСтроки = 0 По ТЧ.Количество() - 1 Цикл
			
			ИмяТЧ = ПараметрыПроверкиРеквизитов.ДокументМенеджер.ИмяТЧДокумента(
				ИмяТабличнойЧасти, ТЧ[ИндексСтроки], ДанныеДокумента);
			
			// Цикл по всем проверяемым реквизитам для текущей табличной части.
			Для Каждого Реквизит Из МассивРеквизитов Цикл
				
				ИмяРеквизита = Реквизит.РеквизитОбъекта;
				
				ПроверятьРеквизитТЧДокумента = ПараметрыПроверкиРеквизитов.ДокументМенеджер.ПроверятьРеквизитТЧДокумента(
					ТЧ[ИндексСтроки], ИмяТЧ, Реквизит.РеквизитОбъекта, ДанныеДокумента);
				
				Если Не ПроверятьРеквизитТЧДокумента
					Или ЗначениеЗаполнено(ТЧ[ИндексСтроки][ИмяРеквизита]) Тогда
					Продолжить;
				КонецЕсли;
				
				РеквизитДляПроверки = ПараметрыПроверкиРеквизитов.ДокументМенеджер.ИзменитьРеквизитТЧ(
					ТЧ[ИндексСтроки], ИмяТЧ, Реквизит, ДанныеДокумента);
				
				ПолеОшибки = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(
					"Объект.%1",
				    ИмяТабличнойЧасти + "[" + Формат(ИндексСтроки, "ЧН=0; ЧГ=0") + "]." + РеквизитДляПроверки.РеквизитОбъекта);

				ДобавитьОшибкуПользователю(
					Ошибки,
					ПолеОшибки,
					РеквизитДляПроверки.Ошибка,
					ИмяТЧ + "." + РеквизитДляПроверки.РеквизитОбъекта,
					РеквизитДляПроверки.ЭлементФормы,
					ИндексСтроки);
					
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверкаРеквизитаТабличнойЧасти(Реквизит, ПозицияТочки, ТабличныеЧасти)
	
	ДлинаСтроки       = СтрДлина(Реквизит.РеквизитОбъекта);
	ИмяТабличнойЧасти = Лев(Реквизит.РеквизитОбъекта, ПозицияТочки - 1);
	ИмяРеквизита      = Прав(Реквизит.РеквизитОбъекта, ДлинаСтроки - ПозицияТочки);
	
	Если ТабличныеЧасти.Свойство(ИмяТабличнойЧасти) Тогда
		ПроверяемыеРеквизитыТЧ = ТабличныеЧасти[ИмяТабличнойЧасти];
	Иначе
		ПроверяемыеРеквизитыТЧ = Новый Массив;
		ТабличныеЧасти.Вставить(ИмяТабличнойЧасти, ПроверяемыеРеквизитыТЧ);
	КонецЕсли;
	
	Реквизит.РеквизитОбъекта = ИмяРеквизита;
	
	ПроверяемыеРеквизитыТЧ.Добавить(Реквизит);
	
КонецПроцедуры

Процедура ПроверкаРеквизитаОбъекта(Реквизит, ДанныеДокумента, ПараметрыПроверкиРеквизитов, Ошибки)
	
	Если Не ПараметрыПроверкиРеквизитов.ДокументМенеджер.ПроверятьРеквизитДокумента(Реквизит.РеквизитОбъекта, ДанныеДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеРеквизитаЗаполнено = ЗначениеЗаполнено(ДанныеДокумента[Реквизит.РеквизитОбъекта]);
	
	Если Не ЗначениеРеквизитаЗаполнено Тогда
		
		ДобавитьОшибкуПользователю(
			Ошибки,
			МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД("Объект.%1", Реквизит),
			Реквизит.Ошибка,
			Реквизит.РеквизитОбъекта,
			Реквизит.ЭлементФормы);
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ДобавитьОшибкуТипаВладения(СтрокаТС, Ошибки, СоответствиеРеквизитовОшибок)
	
	Если СтрокаТС.Вид = ЗначениеМетаданных("Saby_ВидыТС.Автомобиль") Тогда
		СтруктураДанныхОшибки = СоответствиеРеквизитовОшибок.Получить("ТранспортноеСредствоДокумент");
		ДобавитьОшибкуПользователю(
			Ошибки,
			"ТранспортноеСредствоСтрокой",
			СтруктураДанныхОшибки.Ошибка,
			СтруктураДанныхОшибки.РеквизитОбъекта,
			СтруктураДанныхОшибки.ЭлементФормы);
	Иначе
		СтруктураДанныхОшибки = СоответствиеРеквизитовОшибок.Получить("Прицепы.ПрицепДокумент");
		ДобавитьОшибкуПользователю(
			Ошибки,
			МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД("Объект.ТранспортныеСредства[%1]", СтрокаТС.НомерСтроки),
			СтруктураДанныхОшибки.Ошибка,
			СтруктураДанныхОшибки.РеквизитОбъекта,
			СтруктураДанныхОшибки.ЭлементФормы,
			СтрокаТС.НомерСтроки - 1);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ФЛК

#Область Лента

Процедура ОбновитьДанныеЛенты(Форма)
	
	Форма.Лента.Очистить();
	
	ИмяМетаданных = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ИмяМетаданныхПоФорме(Форма);
	
	ФормаОбъект = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ФормаОбъект(Форма);

	ТекущиеЭтапыДокумента = МодульКода("Документы." + ИмяМетаданных).ТекущиеЭтапыДокумента(Форма);
	ДанныеДляЛенты = МодульКода("РегистрыСведений.Saby_ДанныеТитулов").ДанныеДляЛенты(
		ФормаОбъект, ИмяМетаданных, ТекущиеЭтапыДокумента);
	
	КоличествоВидимость = РазмерЛентыВТитулах();
	
	Для Каждого СтруктураДанных Из ДанныеДляЛенты Цикл
		
		СтрокаЛенты = Форма.Лента.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЛенты, СтруктураДанных);
		
		ЗаполнитьПризнакТекущегоЭтапа(Форма, ИмяМетаданных, СтрокаЛенты);
		
		ЗаполнитьНаименованиеТитула(СтрокаЛенты, ИмяМетаданных);
		
		СтрокаЛенты.Видимость = КоличествоВидимость > 0;
		КоличествоВидимость   = КоличествоВидимость - 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНаименованиеТитула(СтрокаСобытия, ИмяМетаданных)
	
	ПредставленияТитула = РезультатФункцииТитулов(ИмяМетаданных, "ПредставленияТитула", СтрокаСобытия.ТипТитула);	
	СтрокаСобытия.НаименованиеТитула = ПредставленияТитула.Имя;
	
КонецПроцедуры

Процедура СдвинутьНаСтроку(Лента, СтрокаЛенты)
	
	КоличествоВидимость = РазмерЛентыВТитулах();
	
	ИндексСтроки = Лента.Индекс(СтрокаЛенты);
	
	Индекс = Лента.Количество() - 1;
	Пока Индекс >= 0 Цикл
		СтрокаЛенты = Лента[Индекс];
		Если Индекс <= ИндексСтроки Тогда
			СтрокаЛенты.Видимость = КоличествоВидимость > 0;
			КоличествоВидимость   = КоличествоВидимость - 1;
		КонецЕсли;
		Индекс = Индекс - 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура СдвинутьЛентуВправо(Лента)
	
	Если Лента.Количество() <= РазмерЛентыВТитулах() Или Лента[Лента.Количество() - 1].Видимость Тогда
		Возврат;
	КонецЕсли;
	
	Для ИндексНачала = 0 По Лента.Количество() - 1 Цикл
		Если Лента[ИндексНачала].Видимость Тогда
			Лента[ИндексНачала].Видимость = Ложь;
			ИндексНачала = ИндексНачала + 1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПоследнийИндекс = Мин(ИндексНачала + РазмерЛентыВТитулах() - 1, Лента.Количество() - 1);
	
	Для Индекс = ИндексНачала По ПоследнийИндекс Цикл
		Лента[Индекс].Видимость = Истина;
	КонецЦикла;
	
КонецПроцедуры

Процедура СдвинутьЛентуВлево(Лента)
	
	Если Лента.Количество() <= РазмерЛентыВТитулах() Или Лента[0].Видимость Тогда
		Возврат;
	КонецЕсли;
	
	Для ИндексНачала = 0 По Лента.Количество() - 1 Цикл
		Если Лента[ИндексНачала].Видимость Тогда
			ИндексНачала = ИндексНачала - 1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПоследнийИндекс = Мин(ИндексНачала + РазмерЛентыВТитулах() - 1, Лента.Количество() - 1);
	
	Для Индекс = ИндексНачала По Лента.Количество() - 1 Цикл
		Лента[Индекс].Видимость = Индекс <= ПоследнийИндекс;
	КонецЦикла;
	
КонецПроцедуры

Функция РазмерЛентыВТитулах()
	
	Возврат 6;
	
КонецФункции

Процедура ЗаполнитьПризнакТекущегоЭтапа(Форма, ИмяМетаданных, СтрокаЛенты)
	
	ДокументооборотЗавершен = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ДокументооборотЗавершен(Форма);
	ТекущиеЭтапыДокумента   = МодульКода("Документы." + ИмяМетаданных).ТекущиеЭтапыДокумента(Форма);
	
	Если ТекущиеЭтапыДокумента.Количество() = 0 Тогда
		ЕстьТитулВТекущихЭтапах = РезультатФункцииТитулов(ИмяМетаданных, "ЭтоПервыйТитул", СтрокаЛенты.ТипТитула);
	Иначе
		ЕстьТитулВТекущихЭтапах = Не ЗначениеЗаполнено(СтрокаЛенты.ИдентификаторТитула)
			И ТекущиеЭтапыДокумента.Найти(СтрокаЛенты.ТипТитула) <> Неопределено;
	КонецЕсли;
	
	Если СтрокаЛенты.ТипТитула = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.ИзменениеСтоимости")
		Или СтрокаЛенты.ТипТитула = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.СогласованиеСтоимости") Тогда
		СтрокаЛенты.ТекущийТитул = ЕстьТитулВТекущихЭтапах;
	Иначе
		СтрокаЛенты.ТекущийТитул = Не ДокументооборотЗавершен И ЕстьТитулВТекущихЭтапах;
	КонецЕсли;
	
КонецПроцедуры

#Область Отрисовка

Процедура ОчиститьЛенту(Форма)
	
	Пока Форма.Элементы.ГруппаЛента.ПодчиненныеЭлементы.Количество() <> 0 Цикл
		Форма.Элементы.Удалить(Форма.Элементы.ГруппаЛента.ПодчиненныеЭлементы.Получить(0));
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьКнопку(Форма, ИмяКоманды)
	
	Если Форма.Лента.Количество() = 0 Или Форма.Лента.Количество() <= РазмерЛентыВТитулах() Тогда
		Возврат;
	КонецЕсли;
		
	КнопкаФормы = Форма.Элементы.Добавить("Кнопка" + ИмяКоманды, Тип("КнопкаФормы"), Форма.Элементы.ГруппаЛента);
	КнопкаФормы.РастягиватьПоВертикали = Истина;
	КнопкаФормы.ИмяКоманды = ИмяКоманды;
	КнопкаФормы.Отображение = ОтображениеКнопки.Картинка;
	КнопкаФормы.ОтображениеФигуры = ОтображениеФигурыКнопки.ПриАктивности;
	
КонецПроцедуры

Процедура ДобавитьДекорациюЛинию(Форма, СтрокаЛенты = Неопределено, Ширина = 3, ИмяДекорации = "ДекорацияЛиния")
	
	Серая   = Истина;
	Красная = Ложь;
	
	ПолноеИмяДекорации = ИмяДекорации;
	Если СтрокаЛенты <> Неопределено Тогда
		ПолноеИмяДекорации = ПолноеИмяДекорации + Формат(СтрокаЛенты.ПолучитьИдентификатор(), "ЧДЦ=0; ЧГ=");
		Серая   = Не ЗначениеЗаполнено(СтрокаЛенты.Период) И Не СтрокаЛенты.ТекущийТитул;
		Красная = Форма.ТитулОтрицательноЗавершен(СтрокаЛенты);
	КонецЕсли;
	
	ДекорацияФормы = Форма.Элементы.Добавить(ПолноеИмяДекорации, Тип("ДекорацияФормы"), Форма.Элементы.ГруппаЛента);
	ДекорацияФормы.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	ДекорацияФормы.РастягиватьПоГоризонтали = Ложь;
	ДекорацияФормы.Ширина = Ширина;
	ДекорацияФормы.Рамка = Форма.Элементы.Стили.Рамка;
	
	Если Красная Тогда
		ДекорацияФормы.ЦветРамки = ЦветаСтиля.ЦветОсобогоТекста;
	ИначеЕсли Серая Тогда
		ДекорацияФормы.ЦветРамки = ЦветаСтиля.ЦветРамкиКнопки;
	Иначе
		ДекорацияФормы.ЦветРамки = WebЦвета.ЖелтоЗеленый;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьДекорациюТитул(Форма, СтрокаЛенты)
	
	ИдентификаторСтроки = Формат(СтрокаЛенты.ПолучитьИдентификатор(), "ЧДЦ=0; ЧН=0; ЧГ=");
	
	ГруппаТитул = Форма.Элементы.Добавить(
		"Группа_" + ИдентификаторСтроки,
		Тип("ГруппаФормы"),
		Форма.Элементы.ГруппаЛента);
		
	ГруппаТитул.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		
	ГруппаТитул.ОтображатьЗаголовок                = Ложь;
	ГруппаТитул.Группировка                        = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаТитул.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	ГруппаТитул.ВертикальныйИнтервал               = ИнтервалМеждуЭлементамиФормы.Нет;
	
	ДекорацияФормы = Форма.Элементы.Добавить(
		"Титул_" + ИдентификаторСтроки,
		Тип("ДекорацияФормы"),
		Форма.Элементы[ГруппаТитул.Имя]);
		
	ДекорацияФормы.Вид = ВидДекорацииФормы.Картинка;
	ДекорацияФормы.Гиперссылка = Истина;
		
	МассивДанныхПодсказки = Новый Массив;
	ПериодСтрокой = ?(ЗначениеЗаполнено(СтрокаЛенты.Период), Формат(СтрокаЛенты.Период, "ДФ='dd.MM.yyyy ЧЧ:мм'"), "");
	
	Если Форма.ТитулОтрицательноЗавершен(СтрокаЛенты) Тогда
		ЦветТитула = ЦветаСтиля.ЦветОсобогоТекста;
		МассивДанныхПодсказки.Добавить("Красный");
	Иначе
		ЦветТитула = WebЦвета.ТемноЗеленый;
		МассивДанныхПодсказки.Добавить("Зеленый");
	КонецЕсли;
	
	Если СтрокаЛенты.ТекущийТитул Тогда
		Картинка = КартинкаИнтерфейсаСервер(Форма, "Saby_ТекущийТитул");
		МассивДанныхПодсказки.Добавить("Saby_ТекущийТитул");
	ИначеЕсли ЗначениеЗаполнено(СтрокаЛенты.ИдентификаторТитула) Тогда
		Картинка = КартинкаИнтерфейсаСервер(Форма, "Saby_АктивныйТитул");
		МассивДанныхПодсказки.Добавить("Saby_АктивныйТитул");
	Иначе
		Картинка = КартинкаИнтерфейсаСервер(Форма, "Saby_НеактивныйТитул");
		ЦветТитула = ЦветаСтиля.ЦветРамкиКнопки;
		МассивДанныхПодсказки[0] = "Серый";
		МассивДанныхПодсказки.Добавить("Saby_НеактивныйТитул");
	КонецЕсли;
	
	ДекорацияФормы.Картинка = Картинка;
	
	ФорматированнаяСтрока = Новый ФорматированнаяСтрока(
		Новый ФорматированнаяСтрока(СтрокаЛенты.Представление, ШрифтыСтиля.ОбычныйШрифтТекста, ЦветТитула),
		Символы.ПС,
		Новый ФорматированнаяСтрока(ПериодСтрокой, ШрифтыСтиля.МелкийШрифтТекста, WebЦвета.Серый));
		
	МассивДанныхПодсказки.Добавить(СтрокаЛенты.Представление);
	Если ЗначениеЗаполнено(ПериодСтрокой) Тогда
		МассивДанныхПодсказки.Добавить(ПериодСтрокой);
	КонецЕсли;
		
	ДекорацияФормы.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	ДекорацияФормы.Подсказка            = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(
		МассивДанныхПодсказки, "/");
	
	ДекорацияФормы.УстановитьДействие("Нажатие", "ДекорацияТитулОбработкаНавигационнойСсылки");
	
	ДекорацияФормыТекст = Форма.Элементы.Добавить(
		"НадписьТитула_" + ИдентификаторСтроки,
		Тип("ДекорацияФормы"),
		Форма.Элементы[ГруппаТитул.Имя]);
		
	ДекорацияФормыТекст.Заголовок               = ФорматированнаяСтрока;
	ДекорацияФормыТекст.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
	ДекорацияФормыТекст.Ширина                  = ШиринаПоТитулу(СтрокаЛенты.ТипТитула);
	
КонецПроцедуры

Функция ШиринаПоТитулу(ТипТитула) Экспорт
	
	РезультатФункции = 10;
	
	Если ТипТитула = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.Переадресовка") Тогда
		
		РезультатФункции = 12;
		
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ДобавитьПодсказку(Форма)
	
	ДекорацияФормы = Форма.Элементы.Добавить("ПодсказкаЛенты", Тип("ДекорацияФормы"), Форма.Элементы.ГруппаЛента);
	ДекорацияФормы.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	ДекорацияФормы.Подсказка = "При нажатии на картинку, происходит переход к данным выбранного титула";
	
КонецПроцедуры

#КонецОбласти // Отрисовка

#КонецОбласти // Лента

Функция АдресКонтрагентаИлиОрганизации(ВидИлиТипКИ)
	
	Возврат ВидИлиТипКИ = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации
		Или ВидИлиТипКИ = Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации
		Или ВидИлиТипКИ = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента
		Или ВидИлиТипКИ = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
	
КонецФункции

Функция ОписанияОшибок(СписокОшибок)
	
	РезультатФункции = Новый Массив;
	
	Для Каждого СтруктураОшибки Из СписокОшибок Цикл
		
		РезультатФункции.Добавить(СтруктураОшибки.ТекстДляОднойОшибки);
		
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ЗаполнитьТаблицуЗначений(Форма, ДанныеЗаполнения, РеквизитФормы, ЗначениеИсточник)
	
	Форма[РеквизитФормы.Имя].Очистить();
	
	Если Не ЗначениеЗаполнено(ЗначениеИсточник) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеИсточник) = Тип("Массив") Тогда
		Массив = ЗначениеИсточник;
	Иначе
		Массив = ЗначениеИзСтрокиВнутр(ЗначениеИсточник);
	КонецЕсли;
	
	Для Каждого Значение Из Массив Цикл 
		
		НС = Форма[РеквизитФормы.Имя].Добавить();
		
		ЕстьОдноименныйРеквизит = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ЕстьРеквизитИлиСвойство(НС, РеквизитФормы.Имя);
		
		Если ТипЗнч(Значение) = Тип("Строка") Тогда
			
			Если ЕстьОдноименныйРеквизит Тогда 
				ИмяКолонки = РеквизитФормы.Имя;
			Иначе 
				ИмяКолонки = Лев(РеквизитФормы.Имя, СтрДлина(РеквизитФормы.Имя) - 1);
			КонецЕсли;
			
			НС[ИмяКолонки] = Значение;
			
		Иначе
			
			// Структура
			Для Каждого КлючЗначение Из Значение Цикл
				НС[КлючЗначение.Ключ] = КлючЗначение.Значение;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция АктивныйЭтапДокумента(Ссылка)
	
	ИндексКартинки = 0;
	АктивныйЭтап   = "";
	
	ДанныеСостояния = ДанныеСостоянияОбъекта(Ссылка);
	Если ДанныеСостояния <> Неопределено Тогда
		
		АктивныйЭтап = ДанныеСостояния.АктивныйЭтап;
		
		Состояние = ДанныеСостояния.Состояние;
		
		Если ТипЗнч(Состояние) = Тип("Структура") Тогда
			ИндексКартинки = Состояние.ИндексКартинки;
		ИначеЕсли ЗначениеЗаполнено(Состояние) Тогда
			ИндексКартинки = МодульКода("Saby_ТНОбщегоНазначенияСервер").ЗначениеРеквизитаОбъекта(Состояние, "ИндексКартинки");
		Иначе
			ИндексКартинки = 0;
		КонецЕсли;
		
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("АктивныйЭтап",   АктивныйЭтап);
	Структура.Вставить("ИндексКартинки", ИндексКартинки);
	Структура.Вставить("Состояние",      Состояние);
	
	Возврат Структура;
	
КонецФункции

#Область ЗаполнениеНаОснованииINI

Функция РезультатЗаполненияНаОснованииINI(ПараметрыЗаполнения) Экспорт
	
	Если ПараметрыЗаполнения.НаКлиенте Тогда
		РезультатРасчета = ПараметрыЗаполнения.РезультатРасчетаИНИ;
	Иначе
		РезультатРасчета = МодульКода("Saby_ТНОбщегоНазначенияСервер").РезультатРасчетаINI(ПараметрыЗаполнения, ОбъектОбработки());
	КонецЕсли;
	
	Если РезультатРасчета.Результат.Ошибка Тогда
		Возврат РезультатРасчета.Результат;
	Иначе
		РезультатФункции = РезультатРасчета.Результат;
		ДанныеИзИНИ      = РезультатРасчета.ДанныеИзИНИ;
	КонецЕсли;
	
	МодульКода("Документы." + ПараметрыЗаполнения.ИмяМетаданных).СформироватьНовыеОснования(РезультатФункции, ДанныеИзИНИ);
	МодульКода("Документы." + ПараметрыЗаполнения.ИмяМетаданных).ДополнитьПодстановки(ДанныеИзИНИ, ПараметрыЗаполнения);
	
	ПервыйТитул = РезультатФункцииТитулов(ПараметрыЗаполнения.ИмяМетаданных, "ПервыйТитул");
	ПредставленияТитула = РезультатФункцииТитулов(ПараметрыЗаполнения.ИмяМетаданных, "ПредставленияТитула", ПервыйТитул);
	
	МассивПодстановок = Новый Массив;
	МассивПодстановок.Добавить(ДанныеИзИНИ);
	
	Подстановки = Новый Соответствие;
	ПодстановкаПервогоТитула = Новый Соответствие;
	ПодстановкаПервогоТитула.Вставить(ПредставленияТитула.ИмяПодстановки, МассивПодстановок);
	Подстановки.Вставить("Подстановки", ПодстановкаПервогоТитула);
	
	ПараметрыЗагрузки = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыЗагрузки();
	ПараметрыЗагрузки.ДанныеДокумента          = Подстановки;
	ПараметрыЗагрузки.Ссылка                   = ПараметрыЗаполнения.Ссылка;
	ПараметрыЗагрузки.ИдСбис                   = Неопределено;
	ПараметрыЗагрузки.ИзДокумента              = Истина;
	ПараметрыЗагрузки.ЭтоЗаполнениеСПомощьюINI = Истина;
	ПараметрыЗагрузки.ЭтоПервоеОснование       = ПараметрыЗаполнения.ЭтоПервоеОснование;
	ПараметрыЗагрузки.ИмяМетаданных            = ПараметрыЗаполнения.ИмяМетаданных;
	ПараметрыЗагрузки.НоменклатураГруза        = ПараметрыЗаполнения.НоменклатураГруза;
	ПараметрыЗагрузки.КлючСтроки               = ПараметрыЗаполнения.КлючСтроки;
	ПараметрыЗагрузки.СопроводительныеДокументы = ПараметрыЗаполнения.СопроводительныеДокументы;
	
	ПараметрыЗаполненияНаОсновании = Новый Структура;
	ПараметрыЗаполненияНаОсновании.Вставить("РольКонтрагента", ПараметрыЗаполнения.РольКонтрагента);
	ПараметрыЗагрузки.ПараметрыОснования = ПараметрыЗаполненияНаОсновании;
	
	Если ПараметрыЗаполнения.ЭтоВыбранныйДокумент Тогда
		ПараметрыЗагрузки.ДокументОснование = ПараметрыЗаполнения.Основание;
	КонецЕсли;
	
	МодульКода("Saby_ТНЗагрузкаСервер").ЗагрузкаДанныхДокумента(ПараметрыЗагрузки);
	
	УстановитьЗапретЗаполнения(ПараметрыЗаполнения);
	
	РезультатФункции.ТекстСообщения = "Заполнение на основании завершено";
	
	Если РезультатФункции.Ошибка Тогда
		ЗаписатьОшибкуВЖурналРегистрации(
			ПараметрыЗаполнения.Ссылка,
			"Заполнение не выполнено",
			РезультатФункции.ТекстСообщения);
	КонецЕсли;
	
	РезультатФункции.Вставить("Ссылка",           ПараметрыЗагрузки.Ссылка);
	РезультатФункции.Вставить("ЗапретЗаполнения", ПараметрыЗаполнения.ЗапретЗаполнения);
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ДанныеОснованийДляЗаполненияНаОснованииINI(ДокументОснование, ПараметрыЗаполнения) Экспорт
	
	ШаблонИмениИНИ = ПараметрыЗаполнения.ШаблонИмениИНИ;
	
	ИмяМетаданных = ИмяМетаданныхДокумента(ДокументОснование);
	РезультатФункции = Новый Массив;
	ИмяИНИ = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(ШаблонИмениИНИ, ИмяМетаданных);
	РезультатФункции.Добавить(СтруктураОснования(ДокументОснование, ИмяИНИ));
	
	ДобавитьТранспортнуюНакладную(РезультатФункции, ДокументОснование, ШаблонИмениИНИ);
	
	ДобавитьОснованияПоТранспортнойНакладной(РезультатФункции, ДокументОснование, ШаблонИмениИНИ);
	
	ДобавитьОснованияПоПоручениюЭкспедитору(РезультатФункции, ДокументОснование, ШаблонИмениИНИ);
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ДобавитьТранспортнуюНакладную(МассивОснований, ДокументОснование, ШаблонИмениINI)
	
	Если Метаданные.Документы.Найти("ТранспортнаяНакладная") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	ТранспортнаяНакладная.Ссылка КАК Ссылка 
	|ИЗ
	|	Документ.ТранспортнаяНакладная КАК ТранспортнаяНакладная
	|ГДЕ
	|	ТранспортнаяНакладная.ЗаданиеНаПеревозку = &ДокументОснование";
	
	ЗапросДанных.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда
		
		ИмяИНИ = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(ШаблонИмениINI, "ТранспортнаяНакладная");
		МассивОснований.Добавить(СтруктураОснования(ВыборкаДанных.Ссылка, ИмяИНИ));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьОснованияПоТранспортнойНакладной(МассивОснований, ДокументОснование, ШаблонИмениINI)
	
	Если Метаданные.Документы.Найти("ТранспортнаяНакладная") = Неопределено
		Или ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.ТранспортнаяНакладная") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	ТранспортнаяНакладнаяДокументыОснования.ДокументОснование КАК Ссылка
	|ИЗ
	|	Документ.ТранспортнаяНакладная.ДокументыОснования КАК ТранспортнаяНакладнаяДокументыОснования
	|ГДЕ
	|	ТранспортнаяНакладнаяДокументыОснования.Ссылка = &ДокументОснование
	|	И НЕ ТранспортнаяНакладнаяДокументыОснования.Ссылка.ПометкаУдаления";
	
	ЗапросДанных.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	ОтборОснования = Новый Структура("Основание", ДокументОснование);
	
	СтрокаОснования = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").НайтиСтрокиУниверсально(
		МассивОснований, ОтборОснования)[0];
	ИндексВставки   = МассивОснований.Найти(СтрокаОснования);
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Пока ВыборкаДанных.Следующий() Цикл
		
		ИмяМетаданных = ИмяМетаданныхДокумента(ВыборкаДанных.Ссылка);
		
		ИмяИНИ = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(ШаблонИмениINI, ИмяМетаданных);
		МассивОснований.Вставить(ИндексВставки, СтруктураОснования(ВыборкаДанных.Ссылка, ИмяИНИ));
		
	КонецЦикла;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	ТранспортнаяНакладная.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку
	|ИЗ
	|	Документ.ТранспортнаяНакладная КАК ТранспортнаяНакладная
	|ГДЕ
	|	ТранспортнаяНакладная.Ссылка = &ДокументОснование
	|	И ТранспортнаяНакладная.ЗаданиеНаПеревозку <> &ПустаяСсылка";
	
	ДокПустаяСсылка = Документы["ЗаданиеНаПеревозку"].ПустаяСсылка();
		    
	ЗапросДанных.УстановитьПараметр("ДокументОснование", ДокументОснование);
	ЗапросДанных.УстановитьПараметр("ПустаяСсылка",      ДокПустаяСсылка);
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда
		ИмяИНИ = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(ШаблонИмениINI, "ЗаданиеНаПеревозку");
		МассивОснований.Добавить(СтруктураОснования(ВыборкаДанных.ЗаданиеНаПеревозку, ИмяИНИ));
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьОснованияПоПоручениюЭкспедитору(МассивОснований, ДокументОснование, ШаблонИмениINI)
	
	Если Метаданные.Документы.Найти("ПоручениеЭкспедитору") = Неопределено
		Или ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.ПоручениеЭкспедитору") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
	|ГДЕ
	|	ЗаданиеНаПеревозкуРаспоряжения.Распоряжение = &Распоряжение";
	
	ЗапросДанных.УстановитьПараметр("Распоряжение", ДокументОснование);
	
	ОтборОснования = Новый Структура("Основание", ДокументОснование);
	
	СтрокаОснования = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").НайтиСтрокиУниверсально(
		МассивОснований, ОтборОснования)[0];
	ИндексВставки   = МассивОснований.Найти(СтрокаОснования);
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда
		ИмяМетаданных = ИмяМетаданныхДокумента(ВыборкаДанных.Ссылка);
		ИмяИНИ = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(ШаблонИмениINI, ИмяМетаданных);
		СтруктураОснования = СтруктураОснования(ВыборкаДанных.Ссылка, ИмяИНИ);
		МассивОснований.Вставить(ИндексВставки, СтруктураОснования);
	КонецЕсли;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоручениеЭкспедиторуОснования.Основание КАК Основание
	|ИЗ
	|	Документ.ПоручениеЭкспедитору.Основания КАК ПоручениеЭкспедиторуОснования
	|ГДЕ
	|	ПоручениеЭкспедиторуОснования.Ссылка = &Ссылка
	|	И ПоручениеЭкспедиторуОснования.Основание ССЫЛКА Документ.ПриобретениеТоваровУслуг";
	
	ЗапросДанных.УстановитьПараметр("Ссылка", ДокументОснование);
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Пока ВыборкаДанных.Следующий() Цикл
		ИмяМетаданных = ИмяМетаданныхДокумента(ВыборкаДанных.Основание);
		ИмяИНИ = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(ШаблонИмениINI, ИмяМетаданных);
		СтруктураОснования = СтруктураОснования(ВыборкаДанных.Основание, ИмяИНИ);
		МассивОснований.Вставить(ИндексВставки, СтруктураОснования);
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьЗапретЗаполнения(ПараметрыЗаполнения)
	
	Если ПараметрыЗаполнения.ИмяМетаданных = "Saby_ТранспортнаяНакладная" Тогда
		МодульКода("Документы." + ПараметрыЗаполнения.ИмяМетаданных).УстановитьЗапретЗаполненияЭТрН(ПараметрыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура СвернутьГруз(ОбъектСсылка, ПараметрыЗаполнения, РезультатФункции) Экспорт
	
	НоменклатураГруза = ПараметрыЗаполнения.НоменклатураГруза;
	СтруктураПараметров = ПараметрыГруппировки();
	
	Если ТипЗнч(ОбъектСсылка) = Тип("Структура") Тогда
		ОбъектДок = ОбъектСсылка;
	Иначе
		ОбъектДок = ОбъектСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	ЭтоЭПЛ = МодульКода("Saby_ТНОбщегоНазначенияСервер").ИмяМетаданныхДокумента(ОбъектСсылка) = "Saby_ПутевойЛист";
	Если ЭтоЭПЛ Тогда
		// в ЭПЛ нет груза
		Возврат;
	КонецЕсли;	
	
	ЭтоЭТрН = МодульКода("Saby_ТНОбщегоНазначенияСервер").ИмяМетаданныхДокумента(ОбъектСсылка) = "Saby_ТранспортнаяНакладная";	
	
	Если НеГруппироватьГруз(ОбъектДок, НоменклатураГруза, СтруктураПараметров, РезультатФункции) Тогда
		Возврат;
	КонецЕсли;
	
	Грузы = ОбъектДок.Грузы.Выгрузить();
	
	КолонкиДляЗапроса = Новый Массив;
	Для Каждого Колонка Из Грузы.Колонки Цикл
		СтрокаЗапроса = "Грузы.%1 КАК %1";
		КолонкиДляЗапроса.Добавить(МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(СтрокаЗапроса, Колонка.Имя));
	КонецЦикла;
	
	СуммируемыеКолонки = Новый Массив;
	СуммируемыеКолонки.Добавить("Высота");
	СуммируемыеКолонки.Добавить("Длина");
	СуммируемыеКолонки.Добавить("Количество");
	СуммируемыеКолонки.Добавить("КоличествоПаллет");
	СуммируемыеКолонки.Добавить("МассаБрутто");
	СуммируемыеКолонки.Добавить("МассаНетто");
	СуммируемыеКолонки.Добавить("Объем");
	СуммируемыеКолонки.Добавить("Стоимость");
	СуммируемыеКолонки.Добавить("Ширина");
	
	Грузы.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	
	Для Каждого КлючЗначение Из НоменклатураГруза Цикл
		Грузы[КлючЗначение.Ключ].Номенклатура = КлючЗначение.Значение;
	КонецЦикла;
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	Грузы.Номенклатура КАК Номенклатура,
	|	Грузы.Наименование КАК КритерийГруппировки,
	|	1 КАК Колонки
	|ПОМЕСТИТЬ ТаблицаГрузов
	|ИЗ
	|	&Грузы КАК Грузы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаГрузов.КритерийГруппировки КАК КритерийГруппировки,
	|	2 КАК Колонки
	|ИЗ
	|	ТаблицаГрузов КАК ТаблицаГрузов
	|ИТОГИ
	|	СУММА(Колонки)
	|ПО
	|	КритерийГруппировки";
	
	РазделительСтрок = ",
	|	";
	
	КолонкиПервойТаблицы = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(КолонкиДляЗапроса, РазделительСтрок);
	ЗапросДанных.Текст = СтрЗаменить(ЗапросДанных.Текст, "1 КАК Колонки", КолонкиПервойТаблицы);
	КолонкиВторойТаблицы = СтрЗаменить(КолонкиПервойТаблицы, "Грузы.", "ТаблицаГрузов.");
	ЗапросДанных.Текст = СтрЗаменить(ЗапросДанных.Текст, "2 КАК Колонки", КолонкиВторойТаблицы);
	
	КолонкиСуммыДляЗапроса = Новый Массив;
	Для Каждого ИмяКолонки Из СуммируемыеКолонки Цикл
		СтрокаЗапроса = "СУММА(%1)";
		КолонкиСуммыДляЗапроса.Добавить(МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(СтрокаЗапроса, ИмяКолонки));
	КонецЦикла;
	КолонкиСуммыДляЗапроса = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(
		КолонкиСуммыДляЗапроса, РазделительСтрок);
	ЗапросДанных.Текст = СтрЗаменить(ЗапросДанных.Текст, "СУММА(Колонки)", КолонкиСуммыДляЗапроса);
	
	ЗапросДанных.УстановитьПараметр("Грузы", Грузы);
	
	ЗаполнитьКритерийГруппировки(СтруктураПараметров, ЗапросДанных, Грузы);
	
	РезультатЗапроса = ЗапросДанных.Выполнить();
	
	Грузы.Очистить();
	СуммируемыеКолонки = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрСоединитьЭПД(СуммируемыеКолонки, ",");
	
	Погрузка_КоличествоМест = 0;
	Погрузка_Масса          = 0;
	
	ВыборкаПоКритериюГруппировки = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоКритериюГруппировки.Следующий() Цикл
		
		СтрокаГруза = Грузы.Добавить();
		
		ВыборкаДанных = ВыборкаПоКритериюГруппировки.Выбрать();
		Если ВыборкаДанных.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(СтрокаГруза, ВыборкаДанных);
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаГруза, ВыборкаПоКритериюГруппировки, СуммируемыеКолонки);
		
		ЗаполнитьНаименованиеСгруппированногоГруза(
			СтрокаГруза, СтруктураПараметров, ВыборкаПоКритериюГруппировки.КритерийГруппировки);
		
		Погрузка_КоличествоМест = Погрузка_КоличествоМест + ВыборкаПоКритериюГруппировки.Количество;
		Погрузка_Масса          = Погрузка_Масса + ВыборкаПоКритериюГруппировки.МассаБрутто;
		
	КонецЦикла;
	
	ОбъектДок.Грузы.Загрузить(Грузы);
	
	Если ЭтоЭТрН Тогда
		
		ОбъектДок.Погрузка_КоличествоМест = Погрузка_КоличествоМест;
		ОбъектДок.Погрузка_Масса          = ?(Погрузка_Масса = 0, "", Формат(Погрузка_Масса, "ЧГ="));
		
	КонецЕсли;
	
	Если ТипЗнч(ОбъектДок) <> Тип("Структура") Тогда
		ОбъектДок.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция НеГруппироватьГруз(ОбъектДок, НоменклатураГруза, СтруктураПараметров, РезультатФункции)
	
	НеЗаполненаНоменклатураДляГруппировки = СтруктураПараметров.ГруппироватьПоРеквизитуНоменклатуры
		И НоменклатураГруза.Количество() = 0;
	
	Если НеЗаполненаНоменклатураДляГруппировки Тогда
		РезультатФункции.ТекстЗаголовка = "Не выполнена группировка товаров";
		РезультатФункции.Вставить("ТекстСообщения", "Не заполнена номенклатура в файле настроек");
	КонецЕсли;
	
	Возврат СтруктураПараметров = Неопределено
		Или Не СтруктураПараметров.ГруппироватьТоварыИзДокументовОснований
		Или ОбъектДок.Грузы.Количество() = 0
		Или (НеЗаполненаНоменклатураДляГруппировки);
	
КонецФункции

Функция ПараметрыГруппировки()
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураПараметров = ХранилищеОбщихНастроек.Загрузить("Saby", "ПараметрыГруппировкиПозицийОснований");
	
	Если СтруктураПараметров = Неопределено Тогда
		СтруктураПараметров = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыГруппировкиПозицийОснований();
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции

Процедура ЗаполнитьКритерийГруппировки(СтруктураПараметров, ЗапросДанных, Грузы)
	
	ПодстрокаЗамены = "ТаблицаГрузов.КритерийГруппировки";
	
	Если СтруктураПараметров.ГруппироватьВОднуПозицию Тогда
		Если ЗначениеЗаполнено(СтруктураПараметров.НаименованиеОбщейПозиции) Тогда
			Наименование = СтруктураПараметров.НаименованиеОбщейПозиции;
		Иначе
			Наименование = Грузы[0].Наименование;
		КонецЕсли;
		НаименованиеОбщейПозиции = """%1""";
		НаименованиеОбщейПозиции = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(
			НаименованиеОбщейПозиции, Наименование);
		ЗапросДанных.Текст = СтрЗаменить(ЗапросДанных.Текст, ПодстрокаЗамены, НаименованиеОбщейПозиции);
	ИначеЕсли СтруктураПараметров.ГруппироватьПоРеквизитуНоменклатуры Тогда
		ЗапросДанных.Текст = СтрЗаменить(
			ЗапросДанных.Текст,
			ПодстрокаЗамены,
			"ТаблицаГрузов.Номенклатура." + СтруктураПараметров.РеквизитНоменклатуры);
	Иначе
		ЗапросДанных.Текст = СтрЗаменить(ЗапросДанных.Текст, ПодстрокаЗамены, "ТаблицаГрузов.Наименование");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНаименованиеСгруппированногоГруза(СтрокаГруза, СтруктураПараметров, КритерийГруппировки)
	
	Если СтруктураПараметров.ГруппироватьВОднуПозицию И ЗначениеЗаполнено(СтруктураПараметров.НаименованиеОбщейПозиции) Тогда
		СтрокаГруза.Наименование = СтруктураПараметров.НаименованиеОбщейПозиции;
	КонецЕсли;
	
	Если СтруктураПараметров.ГруппироватьПоРеквизитуНоменклатуры Тогда
		СтрокаГруза.Наименование = Строка(КритерийГруппировки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ЗаполнениеНаОснованииINI

#Область ПолучениеРеквизитовОбъектов

Процедура СформироватьСтруктуруРеквизитов(Реквизиты, СтруктураПолей)
	
	// Разбор реквизитов, если второй параметр Строка.
	Если ТипЗнч(Реквизиты) = Тип("Строка") Тогда
		Реквизиты = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрРазделитьЭПД(Реквизиты, ",", Ложь);
		Для Индекс = 0 По Реквизиты.ВГраница() Цикл
			Реквизиты[Индекс] = СокрЛП(Реквизиты[Индекс]);
		КонецЦикла;
	КонецЕсли;
	
	// Приведение реквизитов к единому формату.
	Если ТипЗнч(Реквизиты) = Тип("Структура")
		Или ТипЗнч(Реквизиты) = Тип("ФиксированнаяСтруктура") Тогда
		
		Для Каждого КлючИЗначение Из Реквизиты Цикл
			СтруктураПолей.Вставить(КлючИЗначение.Ключ, СокрЛП(КлючИЗначение.Значение));
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Реквизиты) = Тип("Массив")
		Или ТипЗнч(Реквизиты) = Тип("ФиксированныйМассив") Тогда
		
		Для Каждого Реквизит Из Реквизиты Цикл
			Реквизит = СокрЛП(Реквизит);
			ПсевдонимПоля = СтрЗаменить(Реквизит, ".", "");
			СтруктураПолей.Вставить(ПсевдонимПоля, Реквизит);
		КонецЦикла;
	Иначе
		ШаблонИсключения = НСтр("ru = 'Неверный тип второго параметра %1 в функции %2: %3.';
			|en = 'Invalid type of parameter %1 in function %2: %3.'");
		ВызватьИсключение МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(
			ШаблонИсключения,
			"Реквизиты",
			"Saby_ТНОбщегоНазначенияСервер.ЗначенияРеквизитовОбъекта",
			Строка(ТипЗнч(Реквизиты)));
	КонецЕсли;
	
КонецПроцедуры

Процедура РеквизитыВМассив(Реквизиты, СтруктураПолей)
	
	Если Тип("Структура") = ТипЗнч(Реквизиты)
		Или Тип("ФиксированнаяСтруктура") = ТипЗнч(Реквизиты) Тогда
		Реквизиты = Новый Массив;
		Для Каждого КлючИЗначение Из СтруктураПолей Цикл
			ИмяПоля = ?(ЗначениеЗаполнено(КлючИЗначение.Значение),
						КлючИЗначение.Значение,
						КлючИЗначение.Ключ);
			Реквизиты.Добавить(ИмяПоля);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРезультат(Ссылка, Реквизиты, ВыбратьРазрешенные, КодЯзыка, Результат, СтруктураПолей)
	
	Если ЗначениеЗаполнено(Реквизиты) Тогда
		
		МассивСсылок = Новый Массив;
	    МассивСсылок.Добавить(Ссылка);
		
		ЗначенияРеквизитовОбъекта = ЗначенияРеквизитовОбъектов(
			МассивСсылок, Реквизиты, ВыбратьРазрешенные, КодЯзыка)[Ссылка];
		Если ЗначенияРеквизитовОбъекта <> Неопределено Тогда
			Для Каждого КлючИЗначение Из СтруктураПолей Цикл
				ИмяПоля = ?(ЗначениеЗаполнено(КлючИЗначение.Значение),
							КлючИЗначение.Значение,
							КлючИЗначение.Ключ);
				Если Найти(ИмяПоля, ".") = 0 И ЗначенияРеквизитовОбъекта.Свойство(ИмяПоля) Тогда
					Результат[КлючИЗначение.Ключ] = ЗначенияРеквизитовОбъекта[ИмяПоля];
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьТекстыЗапросов(СсылкиПоТипам, ТекстыЗапросов, ПараметрыЗапроса, ВыбратьРазрешенные, РеквизитыТекстЗапроса)
	
	ИменаОбъектовМетаданных = Новый Массив;
	
	Для Каждого СсылкиПоТипу Из СсылкиПоТипам Цикл
		Тип = СсылкиПоТипу.Ключ;
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
		Если ОбъектМетаданных = Неопределено Тогда
			ВызватьИсключение МодульКода("Saby_ОбщегоНазначенияКлиентСервер").ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Неверный первый параметр %1 в функции %2: 
					|Значения массива должны быть ссылками.';
					|en = 'Invalid value of the %1 parameter, function %2:
					|The array values must be of the Ref type.'"), "Ссылки", "Saby_ТНОбщегоНазначенияСервер.ЗначенияРеквизитовОбъектов");
		КонецЕсли;
		
		ПолноеИмяОбъектаМетаданных = ОбъектМетаданных.ПолноеИмя();
		ИменаОбъектовМетаданных.Добавить(ПолноеИмяОбъектаМетаданных);
		
		ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Ссылка КАК Ссылка,
			|	&Реквизиты КАК Реквизиты
			|ИЗ
			|	&ПолноеИмяОбъектаМетаданных КАК ПсевдонимЗаданнойТаблицы
			|ГДЕ
			|	ПсевдонимЗаданнойТаблицы.Ссылка В (&Ссылки)";
		Если Не ВыбратьРазрешенные Или ТекстыЗапросов.Количество() > 0 Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ", "");
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Реквизиты КАК Реквизиты", РеквизитыТекстЗапроса);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолноеИмяОбъектаМетаданных", ПолноеИмяОбъектаМетаданных);
		ИмяПараметра = "Ссылки" + СтрЗаменить(ПолноеИмяОбъектаМетаданных, ".", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Ссылки", "&" + ИмяПараметра);
		ПараметрыЗапроса.Вставить(ИмяПараметра, СсылкиПоТипу.Значение);
		
		ТекстыЗапросов.Добавить(ТекстЗапроса);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // ПолучениеРеквизитовОбъектов

Функция СформироватьИмяМетаданных(Тип, Имя)
	
	Если Тип = "Документы" Тогда 
		ТипМетаданных = "ДокументСсылка";
	ИначеЕсли Тип = "Справочники" Тогда
		ТипМетаданных = "СправочникСсылка";
	ИначеЕсли Тип = "Перечисления" Тогда
		ТипМетаданных = "ПеречислениеСсылка";
	Иначе
		ТипМетаданных = "";
	КонецЕсли;	
	
	ИмяТипа = ТипМетаданных + "." + Имя;
	
	Возврат ИмяТипа;
	
КонецФункции

Функция ИмяФункцииФоновогоЗадания(ИмяДляРасширения, ИмяДляВнешнейОбработки, СтруктураПараметров, ПараметрыВыполнения)
	
	РезультатФункции = "";

	Если СтруктураПараметров.ДопПараметры.ДвоичныеДанныеОбработки = Неопределено Тогда
		РезультатФункции = ИмяДляРасширения;
	Иначе
		ПараметрыВыполнения.ЗапуститьВФоне = Ложь;
		ПараметрыВыполнения.ЗапуститьНеВФоне = Истина;
		РезультатФункции = ИмяДляВнешнейОбработки;
	КонецЕсли;
	
	Если Не ПараметрыВыполнения.Свойство("ВнешнийОтчетОбработка") Тогда
		ПараметрыВыполнения.Вставить("ВнешнийОтчетОбработка", Неопределено);
	КонецЕсли;
	
	Возврат РезультатФункции;

КонецФункции

Функция ПолучитьИдентификаторУниверсально(Коллекция, ЭлементКоллекции)
	
	Если ТипЗнч(Коллекция) = Тип("ДанныеФормыКоллекция") Тогда
		Возврат ЭлементКоллекции.ПолучитьИдентификатор();
	ИначеЕсли ТипЗнч(Коллекция) = Тип("Массив") Тогда
		Возврат Коллекция.Найти(ЭлементКоллекции);
	ИначеЕсли ТипЗнч(Коллекция) = Тип("ТаблицаЗначений") Тогда
		Возврат Коллекция.Индекс(ЭлементКоллекции);
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Функция НайтиПоИдентификаторуУниверсально(Коллекция, Идентификатор)
	
	Если ТипЗнч(Коллекция) = Тип("ДанныеФормыКоллекция") Тогда
		Возврат Коллекция.НайтиПоИдентификатору(Идентификатор);
	Иначе
		Возврат Коллекция[Идентификатор];
	КонецЕсли;
	
КонецФункции

Функция ТекстСообщенияТекущийПользовательНедоступенВСеансеБезРазделителей()
	
	Возврат МодульКода("Saby_ОбщегоНазначенияКлиентСервер").ПодставитьПараметрыВСтроку(
	НСтр("ru = 'Недопустимое получение параметра сеанса %1
	|в сеансе без указания всех разделителей.';
	|en = 'Couldn''t get session parameter for user ""%1"".
	|Not all session delimiters are specified.'"),
	"ТекущийПользователь");
	
КонецФункции

Процедура ЗагруженныеДанныеПечатнойФормыДляОткрытия(ХранитьПечатныеФормыВБД, ПараметрыЗагрузки, РезультатФункции)
	
	МодульТНЗагрузкаСервер = МодульКода("Saby_ТНЗагрузкаСервер");
	
	Если ХранитьПечатныеФормыВБД Тогда
		
		СсылкаНаПрисоединенныйФайл = МодульТНЗагрузкаСервер.ЗагрузитьПечатнуюФормуДокументаИзСбис(
			ПараметрыЗагрузки,
			,
			ХранитьПечатныеФормыВБД);

		Если Не ЗначениеЗаполнено(СсылкаНаПрисоединенныйФайл) Тогда
			РезультатФункции.ОписаниеОшибки = "Не удалось получить печатную форму из СБИС.";
		Иначе
			РезультатФункции.СсылкаНаПрисоединенныйФайл = СсылкаНаПрисоединенныйФайл;
		КонецЕсли;
		
	Иначе
		
		АдресДвоичныхДанныхФайла = МодульТНЗагрузкаСервер.АдресДвоичныхДанныхФайла(ПараметрыЗагрузки);
		Если ПараметрыЗагрузки.ЗагрузкаТребуется И Не ЗначениеЗаполнено(АдресДвоичныхДанныхФайла) Тогда
			РезультатФункции.ОписаниеОшибки = "Не удалось получить печатную форму из СБИС.";
		Иначе
			РезультатФункции.АдресДвоичныхДанных = АдресДвоичныхДанныхФайла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

#Область include_etrn_base_CommonModule_ОпределениеМодуляКода
#КонецОбласти // include_etrn_base_CommonModule_ОпределениеМодуляКода

#Область include_etrn_base_CommonModule_ОпределениеМодуляКодаКлиентСервер
#КонецОбласти // include_etrn_base_CommonModule_ОпределениеМодуляКодаКлиентСервер

#Область include_etrn_base_CommonModule_ЗначениеМетаданных
#КонецОбласти // include_etrn_base_CommonModule_ЗначениеМетаданных

#Область include_etrn_base_CommonModule_ОбъектОбработки
#КонецОбласти // include_etrn_base_CommonModule_ОбъектОбработки

#Область include_etrn_base_CommonModule_КартинкаИнтерфейсаСервер
#КонецОбласти // include_etrn_base_CommonModule_КартинкаИнтерфейсаСервер

#Область include_etrn_base_CommonModule_ВыполнениеФункцииНаСервере
#КонецОбласти // include_etrn_base_CommonModule_ВыполнениеФункцииНаСервере

