
// Генерация нового ключа таблицы
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - источник для генерации
//  ИмяТЧ - Строка - название таблицы
// Возвращаемое значение:  
//  Число - значение нового ключа
//
&НаКлиенте
Функция НовыйКлючСтроки(Форма, ИмяТЧ) Экспорт
	
	// Если в табл. части уже присутствуют строки, то новое «свободное» значение ключа
	// рассчитывается от максимального существующего значения.	
	ИмяКлюча = "МаксимальныйКлючСтроки_" + ИмяТЧ;	
	
	Форма[ИмяКлюча] = Форма[ИмяКлюча] + 1;						
	Возврат Форма[ИмяКлюча];
			
КонецФункции

&НаКлиенте
Функция ПараметрыКлючаСвязиСтрок(ИмяТЧ, ИсходнаяТЧ, НомерСтроки, НоваяСтрока, ИмяКолонкиКлюча = Неопределено) Экспорт

	ПараметрыСвязи = Новый Структура;
	
	ПараметрыСвязи.Вставить("ИмяТЧ",           ИмяТЧ);
	ПараметрыСвязи.Вставить("ИсходнаяТЧ",      ИсходнаяТЧ);
	ПараметрыСвязи.Вставить("НомерСтроки",     НомерСтроки);
	ПараметрыСвязи.Вставить("НоваяСтрока",     НоваяСтрока);
	ПараметрыСвязи.Вставить("ИмяКолонкиКлюча", ИмяКолонкиКлюча);
	
	Возврат ПараметрыСвязи;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьКлючСвязиСтроки(Форма, ПараметрыСвязи) Экспорт
		
	Если Не ПараметрыСвязи.НоваяСтрока Тогда 
		Возврат;
	КонецЕсли;
	
	Если Форма.ЭтоВО Тогда 
		ФормаОбъект = Форма;
	Иначе
		ФормаОбъект = Форма.Объект;		
	КонецЕсли;
	
	ИмяТЧ       = ПараметрыСвязи.ИмяТЧ;
	ИсходнаяТЧ  = ПараметрыСвязи.ИсходнаяТЧ;
	НомерСтроки = ПараметрыСвязи.НомерСтроки;
	
	// Проверка корректности номера строки 
	// При удалении строк расходится номер строки и индекс. Индекс сдвигается, а номер строки только увеличивается 
	Если НомерСтроки >= ФормаОбъект[ИмяТЧ].Количество() Тогда 
		
		// считаем что новая строка это самая последняя в таблице		
		НовыйНомерСтроки = ФормаОбъект[ИмяТЧ].Количество() - 1;		
		ТекущиеДанные    = ФормаОбъект[ИмяТЧ][НовыйНомерСтроки];
		
	Иначе
		ТекущиеДанные = ФормаОбъект[ИмяТЧ][НомерСтроки];
	КонецЕсли;
			
	ТекущиеДанныеИсходнойТЧ = Форма.Элементы[ИсходнаяТЧ].ТекущиеДанные;
	
	КлючСтроки = ТекущиеДанныеИсходнойТЧ.КлючСтроки;
	
	ИмяКлюча = ПараметрыСвязи.ИмяКолонкиКлюча;
	Если ИмяКлюча = Неопределено Тогда
		Если ИсходнаяТЧ = "Водители" Тогда
			ИмяКлюча = "КлючСтроки_ОтветственныеЛица";
		Иначе
			ИмяКлюча = "КлючСтроки_" + ИсходнаяТЧ;
		КонецЕсли;
	КонецЕсли;
	
	Если ТекущиеДанные[ИмяКлюча] <> КлючСтроки Тогда 		
		ТекущиеДанные[ИмяКлюча] = КлючСтроки;		
	КонецЕсли;
		
КонецПроцедуры

