
&НаКлиенте
Функция ТекстСообщенияКоманды(Ключ)
	
	ТекстСообщения = Новый Соответствие;
	
	КлючиСтруктурыСообщений = "НетРезультата,Успешно,ВыполненоСОшибками";
	
	ЗагрузитьВСбис = Новый Структура(КлючиСтруктурыСообщений);
	ЗагрузитьВСбис.НетРезультата      = "Выгрузка не производилась";
	ЗагрузитьВСбис.Успешно            = "В СБИС выгружено %1 документов";
	ЗагрузитьВСбис.ВыполненоСОшибками = "В СБИС выгружено документов %1 из %2";
	
	ТекстСообщения.Вставить("ЗагрузитьВСбис",                  ЗагрузитьВСбис);
	ТекстСообщения.Вставить("ЗагрузитьВСбисДинамическийТитул", ЗагрузитьВСбис);
	
	ПрочитатьДокумент = Новый Структура(КлючиСтруктурыСообщений);
	ПрочитатьДокумент.НетРезультата      = "Загрузка не производилась";
	ПрочитатьДокумент.Успешно            = "Документ обновлен";
	ПрочитатьДокумент.ВыполненоСОшибками = "Возникла ошибка! Документ не загружен";
	
	ТекстСообщения.Вставить("ПрочитатьДокумент", ПрочитатьДокумент);
	
	ОбновитьДокументИзСбис = Новый Структура(КлючиСтруктурыСообщений);
	ОбновитьДокументИзСбис.НетРезультата      = "Загрузка не производилась. Данных для обновления нет";
	ОбновитьДокументИзСбис.Успешно            = "Из СБИС загружено %1 документов";
	ОбновитьДокументИзСбис.ВыполненоСОшибками = "Из СБИС загружено документов %1 из %2";
	
	ТекстСообщения.Вставить("ОбновитьДокументИзСбис", ОбновитьДокументИзСбис);
	
	Возврат ТекстСообщения.Получить(Ключ);
	
КонецФункции

&НаКлиенте
Процедура ОповеститьПользователяОРезультатах(Форма, ИмяКоманды, РезультатОбработки) Экспорт
	
	ТекстСообщения = ТекстСообщенияКоманды(ИмяКоманды);
	ТекстЗаголовка = "Завершено";
	Картинка       = Неопределено;
	
	Если ИмяКоманды = "ВыбранДокументОснованиеINI"
		Или ИмяКоманды = "ЗаполнитьИзINI" Тогда 
		
		ТекстЗаголовка = РезультатОбработки.ТекстЗаголовка;
		Текст          = РезультатОбработки.ТекстСообщения;
		
		Если РезультатОбработки.Свойство("Картинка")
			И ЗначениеЗаполнено(РезультатОбработки.Картинка) Тогда
			
			Картинка = РезультатОбработки.Картинка;
			
		КонецЕсли;
		
	ИначеЕсли ТекстСообщения = Неопределено Тогда
		// если не чего сообщать то завершаем
		Возврат;
		
	ИначеЕсли РезультатОбработки.Итог.Всего = 0 Тогда
		
		Текст    = ТекстСообщения.НетРезультата;
		Картинка = БиблиотекаКартинок.ВосклицательныйЗнакСерый;
		
	ИначеЕсли РезультатОбработки.Итог.Успех Тогда
		Картинка = КартинкаИнтерфейса(Форма, "Saby_Успешно32");
		УспешноеВыполнениеПоУмолчанию(РезультатОбработки, ТекстСообщения, Текст, Картинка);
	Иначе
		ВыполнениеСОшибкамиПоУмолчанию(РезультатОбработки, ТекстСообщения, Текст, Картинка);
	КонецЕсли;
	
	// по умолчанию
	Если Картинка = Неопределено Тогда
		Картинка = КартинкаИнтерфейса(Форма, "Saby_Ошибка32");
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		ТекстЗаголовка,
		,
		Текст,
		Картинка);
	
КонецПроцедуры

&НаКлиенте
Процедура УспешноеВыполнениеПоУмолчанию(РезультатОбработки, ТекстСообщения, Текст, Картинка)
	
	Если Найти(ТекстСообщения.Успешно, "%") > 0 Тогда
		Текст = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(
			ТекстСообщения.Успешно, Формат(РезультатОбработки.Итог.Количество, "ЧН=; ЧГ="));
	Иначе
		Текст = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(ТекстСообщения.Успешно);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеСОшибкамиПоУмолчанию(РезультатОбработки, ТекстСообщения, Текст, Картинка)
	
	Если Найти(ТекстСообщения.ВыполненоСОшибками, "%") > 0 Тогда
		Текст = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(
			ТекстСообщения.ВыполненоСОшибками,
			Формат(РезультатОбработки.Итог.Количество, "ЧН=; ЧГ="),
			Формат(РезультатОбработки.Итог.Всего, "ЧГ="));
	Иначе
		Текст = ТекстСообщения.ВыполненоСОшибками;
	КонецЕсли;
	
КонецПроцедуры

