   
#Область ПрограммныйИнтерфейс

// Авторизация в сервисе
// Параметры:
//  Команда - Строка - имя команды
//	ЭтаФорма - ФормаКлиентскогоПриложения - источник авторизации
//
// Возвращаемое значение:
//  Структура - данные авторизации, в противном случае Неопределено
Функция АвторизацияSaby(Команда, ЭтаФорма) Экспорт
	
	context_params	= Saby_Core.ПроверитьНаличиеПараметровПодключения();
	
	Если context_params = Неопределено Тогда
		
	    Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Вы не авторизованы! Выполните авторизацию и повторите команду";
		Сообщение.Сообщить();	
		
		ОткрытьФорму("Обработка.SABY.Форма.Вход", , , , , , , РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		Возврат Неопределено;
						
	КонецЕсли;	
	
	Возврат context_params;
		
КонецФункции

#Область КомандыСбис

// Выполнить обмен данными из Формы списка 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - источник вызова выполнения 
//  ИмяКоманды - Строка - имя команды, которую требуется выполнить  
//  ДополнительныеПараметры - Структура - дополнительные параметры команды
//
Процедура ОбменДаннымиСБИСИзФормыСписка(Форма, ИмяКоманды, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	
	ИмяМетаданных = Saby_ТНОбщегоНазначенияКлиентСервер.ИмяМетаданныхПоФорме(Форма);
	
	ДополнительныеПараметры.Вставить("ИмяМетаданных", ИмяМетаданных);
	
	Если ИмяКоманды = "ВыгрузитьВСбис"
		Или ИмяКоманды = "ОбновитьАктивныйЭтап" Тогда
		
		ЭтоВыгрузить    = (ИмяКоманды = "ВыгрузитьВСбис");
		ЭтоОбновитьЭтап = (ИмяКоманды = "ОбновитьАктивныйЭтап");
		
		МассивДокументов = ДокументыДляОбработки(Форма, ЭтоВыгрузить);
		
		ДанныеДляОбработки = Новый Массив;
		Для Каждого СсылкаНаДокумент Из МассивДокументов Цикл
			
			Если ЭтоВыгрузить Тогда 
				
				ПараметрыКоманды = Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыВыгрузки(СсылкаНаДокумент);
				
			Иначе 
				
				ПараметрыКоманды = Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыЗагрузки(СсылкаНаДокумент);
				ПараметрыКоманды.ТекущийТитул       = Форма.АктивныйЭтапСтрокой;
				ПараметрыКоманды.ТолькоАктивныйЭтап = Истина;

			КонецЕсли;
					
			ПараметрыКоманды.ИмяМетаданных = ДополнительныеПараметры.ИмяМетаданных;
			
			ДанныеДляОбработки.Добавить(ПараметрыКоманды);
			
		КонецЦикла;
		
		Если ЭтоОбновитьЭтап Тогда
			Форма.АктивныйЭтапСтрокой = "";
		КонецЕсли;
		
	Иначе
		ДанныеДляОбработки = Новый Массив;
	КонецЕсли;
	
	ВыполнитьКомандуСбис(Форма, ИмяКоманды, ДанныеДляОбработки, ДополнительныеПараметры);
	
КонецПроцедуры

// Универсальное выполнение команд
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - источник вызова выполнения
//	ИмяКоманды - Строка - имя команды, которую требуется выполнить
//	МассивДокументов - Массив - документы для выполнения команды
//  ДополнительныеПараметры - Структура - дополнительные параметры команды
//
Процедура ВыполнитьКомандуСбис(Форма, ИмяКоманды, МассивДокументов, ДополнительныеПараметры = Неопределено) Экспорт
	
	ДопПараметры = Новый Структура;		
	ДопПараметры.Вставить("ИмяКоманды", ИмяКоманды);
	
	Если ЭтоЗагрузкаЧтениеИлиОбновление(ИмяКоманды) Тогда
		
		Если МассивДокументов.Количество() > 1 Тогда 
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Данная команда предназначена для выполнения по 1 документу. 
				|Будет использован первый выбранный документ из списка";
			Сообщение.Сообщить();			
		КонецЕсли;
				
		// Авторизация
		context_params = АвторизацияSaby(ИмяКоманды, Форма); 
		Если context_params <> Неопределено Тогда
			
			Если ЗначениеЗаполнено(МассивДокументов) Тогда 
				МассивДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(МассивДокументов[0]);
			КонецЕсли;
			
			ДопПараметры.Вставить("МассивДокументов", МассивДокументов);
			ДопПараметры.Вставить("context_params",   context_params);			
			ДопПараметры.Вставить("ДопПараметры",     ДополнительныеПараметры);
			ДопПараметры.Вставить("Источник",         Форма.УникальныйИдентификатор);
			ВыполнитьКомандуВФоне(Форма, ДопПараметры);
			
		КонецЕсли;
		
	ИначеЕсли ИмяКоманды = "ОткрытьВСбис" Тогда  		
		
		Массив = Новый Массив;
		Массив.Добавить(МассивДокументов[0]);
		// Источник = Массив;
		
		ПараметрыФормы = Новый Структура;		
		ПараметрыФормы.Вставить("Форма", Форма);
		
		Saby_КомандыОбменаДляФормыКлиент.ПриНажатииОткрытьВСБИСПолучитьUID(ИмяКоманды, ПараметрыФормы);
		
	ИначеЕсли ИмяКоманды = "АктуальныйСтатусВерсии" Тогда 
		
		ДопПараметры.Вставить("ДопПараметры", ДополнительныеПараметры);
		
		ПараметрыОжидания = Новый Структура;
		ПараметрыОжидания.Вставить("ВыводитьОкноОжидания", Ложь);
		ДопПараметры.Вставить("ПараметрыОжидания",  ПараметрыОжидания);
		ДопПараметры.Вставить("Источник",           Форма.УникальныйИдентификатор);
		
		ВыполнитьКомандуВФоне(Форма, ДопПараметры);
				
	Иначе
		
		ВызватьИсключение("Для команды " + ИмяКоманды + " не предусмотрен обработчик! Обратитесь к администратору");
			
	КонецЕсли;
	
КонецПроцедуры

// Фоновое выполнене команды
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - источник вызова выполнения
//  СтруктураПараметров - Структура - параметры для выполнения команды
//
Процедура ВыполнитьКомандуВФоне(Форма, СтруктураПараметров) Экспорт
	
	ДлительнаяОперация = Saby_ТНОбщегоНазначенияСервер.ДлительнаяОперация(
		СтруктураПараметров, 
		Форма.УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма); 
	Если СтруктураПараметров.Свойство("ПараметрыОжидания") Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыОжидания, СтруктураПараметров.ПараметрыОжидания);
	КонецЕсли;	
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультат", Saby_ТНОбщегоНазначенияКлиент, СтруктураПараметров);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

// Вывести ошибку или оповестить источник о успешном выполнении команды
// Параметры:
//	Результат - Структура - данные по результатам выполнения команды 
//  ДополнительныеПараметры - Структура - параметры для обработки результата
//
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	ИмяКоманды = ДополнительныеПараметры.ИмяКоманды;
	Источник   = ДополнительныеПараметры.Источник;
	
	Если ЭтоЗагрузкаЧтениеИлиОбновление(ИмяКоманды) Тогда
		
		ОбработатьРезультатКоманды(Результат, ИмяКоманды, Источник); 
		
		// Обновить форму списка после каких то изменений
		Оповестить("Saby_СписокДокументовОбновить", , Источник);   
		
	ИначеЕсли ИмяКоманды = "АктуальныйСтатусВерсии" Тогда	
		
		РезультатОбработки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);		
		
		Оповестить("Saby_АктуальныйСтатусВерсии", РезультатОбработки, Источник);
		
	Иначе		
		Возврат;		
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

// Переход документа на следующий этап
// Параметры:
//  Ссылка - ДокументСсылка.Saby_ТранспортнаяНакладная - ссылка на документ
//  Форма - ФормаКлиентскогоПриложения - источник перехода
//  Этапы - Структура - параметры для последующего перехода
Процедура СменаЭтапа(Ссылка, Форма, Этапы) Экспорт
	
	СписокДокументовСБИС = Новый Массив();
	СписокДокументов1С   = Новый Массив();
	
	СписокДокументов1С.Добавить(Ссылка);
		
	ИД = Saby_ТНОбщегоНазначенияСервер.ИдентификаторВСбис(Ссылка);
		
	ДокументСБИС = Новый Соответствие;
	ДокументСБИС.Вставить("Идентификатор", ИД);   // ид сбис 
	ДокументСБИС.Вставить("Документ1С",    Ссылка);
	ДокументСБИС.Вставить("ДокументСБИС",  Неопределено);
	ДокументСБИС.Вставить("Этап",          Этапы);
	
	СписокДокументовСБИС.Добавить(ДокументСБИС);
	
	ПараметрыВФорму = Новый Структура;
	ПараметрыВФорму.Вставить("СписокДокументовСБИС",          СписокДокументовСБИС);
	ПараметрыВФорму.Вставить("СписокДокументов1С",            СписокДокументов1С);
	ПараметрыВФорму.Вставить("МожноПереотправитьВСБИС",       Ложь);
	ПараметрыВФорму.Вставить("МожноОбновитьВСБИС",            Истина);
	ПараметрыВФорму.Вставить("ПрисоединенныеФайлыРасширения", Истина);
	
	ОткрытьФорму("Обработка.SABY.Форма.ВыполнитьДействие", ПараметрыВФорму, Форма, Новый УникальныйИдентификатор());
		
КонецПроцедуры

// Вывод ошибок в форме по результатам команды "ВыполнитьДействие" 
// из формы обработки SABY ВыполнитьДействие
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - источник перехода
//	Ссылка - ДокументСсылка.Saby_ТранспортнаяНакладная - ссылка на источник выполнения 
//  Параметр - Структура - данные по ошибке 
//           - Строка - описание ошибки
//
Процедура ОбработатьОшибкиИзФормыВыполнитьДействие(Форма, Ссылка, Параметр) Экспорт
	
	МассивОшибок = Новый Массив;
	
	ЭтоСтруктура = ТипЗнч(Параметр.data) = Тип("Структура");
	
	Если ЭтоСтруктура Тогда
		Результат = Параметр.data;
	Иначе
		Результат = Параметр.data["Результат"];
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		
		Для Каждого Строка Из Результат Цикл
			
			ИндексОшибки = 0;
			
			СтруктураОшибки = ДанныеОшибки(Строка, ЭтоСтруктура, ИндексОшибки);
			МассивОшибок.Добавить(СтруктураОшибки);
						
			ИндексОшибки = ИндексОшибки + 1;
			
		КонецЦикла;
		
	Иначе
		
		ТекстОшибки = Результат.message + " - (" + Результат.detail + ")";
		
		СтруктураОшибки = Новый Структура;
		СтруктураОшибки.Вставить("ИндексСтроки",        0);
		СтруктураОшибки.Вставить("ТекстДляОднойОшибки", ТекстОшибки);
		СтруктураОшибки.Вставить("ПолеОшибки",          "");
		СтруктураОшибки.Вставить("ГруппаОшибок",        Неопределено);
		МассивОшибок.Добавить(СтруктураОшибки);
		
	КонецЕсли;
	
	// если нет ошибок по нашему документу ошибки не показываем
	Если Не МассивОшибок.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОшибок = Новый Структура;
	СтруктураОшибок.Вставить("СписокОшибок", МассивОшибок);
	СтруктураОшибок.Вставить("ГруппыОшибок", Новый Соответствие);
	
	ИмяМетаданных = Saby_ТНОбщегоНазначенияКлиентСервер.ИмяМетаданныхПоФорме(Форма);
	ПараметрыДокументаДляАПИ = Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыДокументаДляАПИ(ИмяМетаданных);
	
	СтруктураОшибок.Вставить("ИмяМетаданных", ИмяМетаданных);
	СтруктураОшибок.Вставить("Тип",           ПараметрыДокументаДляАПИ.Тип);
	
	СписокДокументов = Новый Соответствие;
	СписокДокументов.Вставить(Ссылка, СтруктураОшибок);
	
	ОткрытьПервыйДокументИзСоответствия(СписокДокументов);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("СписокДокументов",     СписокДокументов);
	ПараметрыФормы.Вставить("ЭтоДинамическийТитул", Ложь);
	
	Оповестить("Saby_СписокДокументов", ПараметрыФормы);
	
КонецПроцедуры

// Проверка что документ не изменен
// Параметры:
// 	ПроцедураДляВыполнения - Структура - данные для проверки, в том числе форма источник
//  ЗаписыватьБезВопроса- Булево - запись документа без вопросов
//
Процедура ПроверитьМодифицированностьИПродолжитьВыполнение(ПроцедураДляВыполнения, ЗаписыватьБезВопроса = Ложь) Экспорт
	
	Форма = ПроцедураДляВыполнения.Модуль;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                  Форма);
	ДополнительныеПараметры.Вставить("ПроцедураДляВыполнения", ПроцедураДляВыполнения);
	
	Если Не НеобходимоЗаписатьФорму(Форма) Тогда
		ПослеВопросаОЗаписиФормы(КодВозвратаДиалога.Нет, ДополнительныеПараметры);
	ИначеЕсли ЗаписыватьБезВопроса Тогда
		ПослеВопросаОЗаписиФормы(КодВозвратаДиалога.Да, ДополнительныеПараметры);
	Иначе
		ПроцедураПослеВопросаОЗаписиФормы = Новый ОписаниеОповещения(
			"ПослеВопросаОЗаписиФормы",
			Saby_ТНОбщегоНазначенияКлиент,
			ДополнительныеПараметры);
		
		ПоказатьВопрос(
			ПроцедураПослеВопросаОЗаписиФормы,
			"Перед выполнением операции необходимо записать изменения.",
			РежимДиалогаВопрос.ДаНетОтмена,
			,
			КодВозвратаДиалога.Отмена,
			"Записать изменения?");

	КонецЕсли;
	
КонецПроцедуры

// Получение ПФ из БД или онлайна, с последующим отрытием пользователю на просмотр
// Параметры:
//	СсылкаНаДокумент - ДокументСсылка.Saby_ТранспортнаяНакладная - ссылка на источник выполнения 
//  УникальныйИдентификаторФормы - УникальныйИдентифкатор - УИД формы источника
//
Процедура СоздатьИОткрытьПечатнуюФорму(СсылкаНаДокумент, УникальныйИдентификаторФормы) Экспорт
	
	Результат = Saby_ТНОбщегоНазначенияСервер.ДанныеПечатнойФормыДляОткрытия(
		СсылкаНаДокумент, УникальныйИдентификаторФормы);
	
	Если ЗначениеЗаполнено(Результат.АдресДвоичныхДанных) Тогда
		
		ФайловаяСистемаКлиент.ОткрытьФайл(Результат.АдресДвоичныхДанных, , "ЭПД.pdf");
		
	ИначеЕсли ЗначениеЗаполнено(Результат.СсылкаНаПрисоединенныйФайл) Тогда
		
		ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаДляОткрытия(
			Результат.СсылкаНаПрисоединенныйФайл,
			Неопределено,
			УникальныйИдентификаторФормы);
		
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
		
	Иначе
		
		Если Результат.СписокДокументов = Неопределено Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = Результат.ОписаниеОшибки;
			Сообщение.Сообщить();
		Иначе
			ОткрытьПервыйДокументИзСоответствия(Результат.СписокДокументов);
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("СписокДокументов",     Результат.СписокДокументов);
			ПараметрыФормы.Вставить("ЭтоДинамическийТитул", Ложь);
			
			Оповестить("Saby_СписокДокументов", ПараметрыФормы);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработкаОшибок

// Выводим описание и активизируем проблемный элемент формы
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - источник вызова команды
//  СоответствиеТаблицФормыИОбъекта - Соответствие - необходимо заполнить, когда имя таблицы на форме не соответствует
//    имени таблицы в объекте.
//
Процедура РасшифровкаЗаписиТаблицыОшибок(Форма, СоответствиеТаблицФормыИОбъекта = Неопределено) Экспорт
	
	ТаблицаОшибокПриАктивизацииСтроки(Форма, СоответствиеТаблицФормыИОбъекта);
	
	ПараметрыВыделенияДекорации = ПараметрыВыделенияДекорации(Форма);
	Если ПараметрыВыделенияДекорации.НужноОбновитьДекорацию Тогда
		Форма.ОбновитьДекорацию(ПараметрыВыделенияДекорации);
	КонецЕсли;
	
КонецПроцедуры

// Обработка активизици сток таблицы ошибок, привязка к элементам формы
//
// Параметры:
//	Форма - Форма - источник вызова команды
//  СоответствиеТаблицФормыИОбъекта - Соответствие - необходимо заполнить, когда имя таблицы на форме не соответствует
//    имени таблицы в объекте.
//
Процедура ТаблицаОшибокПриАктивизацииСтроки(Форма, СоответствиеТаблицФормыИОбъекта = Неопределено) Экспорт
	
	ТекущиеДанные = Форма.Элементы.ТаблицаОшибок.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ПодробноеОписаниеОшибки = ТекущиеДанные.Ошибка;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.РеквизитОбъекта) Тогда
		Возврат;
	КонецЕсли;
	
	МассивИмен = СтрРазделить(ТекущиеДанные.РеквизитОбъекта, ".");
	
	УровеньИерархии = 2;
	Если МассивИмен.Количество() = УровеньИерархии Тогда
		ИмяТаблицы  = МассивИмен[0];
		ИмяЭлемента = ТекущиеДанные.ЭлементФормы;
	Иначе
	    ИмяТаблицы  = "";
		ИмяЭлемента = ТекущиеДанные.ЭлементФормы;
	КонецЕсли;
	
	ИмяТаблицыОбъекта = СтрЗаменить(ИмяТаблицы, "_Выгрузка", "");
	
	УстановитьАктивнуюСтрокуВОсновнойТаблице(Форма, ИмяТаблицы, ТекущиеДанные.ИндексСтроки);
	
	ИмяТаблицыФормы = СтрЗаменить(ИмяТаблицыОбъекта, "ТранспортныеСредства", "Прицепы");
	
	Если Не ЭтоТаблицаФормы(Форма, ИмяТаблицыФормы) Тогда
		Форма.ТекущийЭлемент = Форма.Элементы[ИмяЭлемента];
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы = СтрокаТаблицыСОшибкой(Форма, ИмяТаблицыОбъекта, ТекущиеДанные, СоответствиеТаблицФормыИОбъекта);
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ИмяТаблицыОбъекта = "ТранспортныеСредства" Тогда
		Автомобиль = ПредопределенноеЗначение("Перечисление.Saby_ВидыТС.Автомобиль");
		Если СтрокаТаблицы.Вид = Автомобиль Тогда
			ТекущиеДанные.ЭлементФормы = "ТранспортноеСредствоСтрокой";
			Форма.ТекущийЭлемент = Форма.Элементы.ТранспортноеСредствоСтрокой;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ИдентификаторСтроки = СтрокаТаблицы.ПолучитьИдентификатор();
	
	Форма.ТекущийЭлемент = Форма.Элементы[ИмяТаблицыФормы];
	Форма.Элементы[ИмяТаблицыФормы].ТекущаяСтрока  = ИдентификаторСтроки;
	Форма.Элементы[ИмяТаблицыФормы].ТекущийЭлемент = Форма.Элементы[ИмяЭлемента];
	
	Если ИмяТаблицыФормы = "Отметки" Или ИмяТаблицыФормы = "Отметки_Выгрузка" Тогда
		Форма.ОтборОтметокПоТипу(ИдентификаторСтроки);
		Форма.Элементы[ИмяТаблицыФормы].ТекущаяСтрока = ИдентификаторСтроки;
	КонецЕсли;
	
КонецПроцедуры

// Параметры для декорации адресов погрузки и доставки
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - источник вызова
//
// Возвращаемое значение:
// 	Структура - параметры декорации
//
Функция ПараметрыВыделенияДекорации(Форма) Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("НужноОбновитьДекорацию", Ложь);
	РезультатФункции.Вставить("Выделить",               Новый Массив);
	РезультатФункции.Вставить("ОтменитьВыделение",      Новый Массив);
	
	ТекущиеДанные = Форма.Элементы.ТаблицаОшибок.ТекущиеДанные;
	ЭлементФормы = ?(ТекущиеДанные = Неопределено, Неопределено, ТекущиеДанные.ЭлементФормы);
	
	ПереченьДекораций = Новый Массив;
	ПереченьДекораций.Добавить("ДекорацияЗаполнитьАдресПогрузки");
	ПереченьДекораций.Добавить("ДекорацияЗаполнитьАдресДоставки");
	ПереченьДекораций.Добавить("ТранспортноеСредствоСтрокой");
	ПереченьДекораций.Добавить("ВодительСтрокой");
	ПереченьДекораций.Добавить("Оформил");
	ПереченьДекораций.Добавить("МедосмотрВыездСтрокой");
	ПереченьДекораций.Добавить("МедосмотрЗаездСтрокой");
	ПереченьДекораций.Добавить("ТехосмотрСтрокой");
	ПереченьДекораций.Добавить("ДекорацияЗаполнитьМестоОтправления");
	ПереченьДекораций.Добавить("ОрганизацияСтрокой");
	ПереченьДекораций.Добавить("ОдометрВыездСтрокой");
	ПереченьДекораций.Добавить("ОдометрЗаездСтрокой");
	ПереченьДекораций.Добавить("ДекорацияЗаполнитьЛицензияДоРейса");
	ПереченьДекораций.Добавить("ДекорацияЗаполнитьПодачаТС");
	ПереченьДекораций.Добавить("ОтправительСтрокой");
	ПереченьДекораций.Добавить("ПеревозчикСтрокой");
	ПереченьДекораций.Добавить("ОтветственныйЗаПеревозкуСтрокой");
	ПереченьДекораций.Добавить("ВодителиСтрокой");
	ПереченьДекораций.Добавить("ОформительСтрокой");
	ПереченьДекораций.Добавить("ПолучательСтрокой");
	ПереченьДекораций.Добавить("ЗаказчикСтрокой");
	ПереченьДекораций.Добавить("ВладелецОбъектаСтрокой");
	ПереченьДекораций.Добавить("ОтгрузчикСтрокой");
	
	Для Каждого ИмяДекорации Из ПереченьДекораций Цикл
		ЭлементДекорации = Форма.Элементы.Найти(ИмяДекорации);
		Если ЭлементДекорации = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Выделена = ЭлементДекорации.Подсказка <> "";
		
		Если Выделена И ИмяДекорации <> ЭлементФормы Тогда
			РезультатФункции.НужноОбновитьДекорацию = Истина;
			РезультатФункции.ОтменитьВыделение.Добавить(ИмяДекорации);
		ИначеЕсли Не Выделена И ИмяДекорации = ЭлементФормы Тогда
			РезультатФункции.НужноОбновитьДекорацию = Истина;
			РезультатФункции.Выделить.Добавить(ИмяДекорации); 
		Иначе 
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // ОбработкаОшибок

#Область ДанныеЮрЛицИнтерфейс

// Универсальный обработчик начала выбора телефона
// Параметры:
// 	Форма - Форма - источник вызова 
// 	ПараметрыВыбораТелефона - Структура - параметры 
// 	СтандартнаяОбработка - Булево - стандратный параметр
// 	ЭтоОсновной - Булево - это основной номер телефона
//
Процедура ТелефонНачалоВыбора(Форма, ПараметрыВыбораТелефона, СтандартнаяОбработка, ЭтоОсновной = Ложь) Экспорт
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Значение", ПараметрыВыбораТелефона.Значение);
	
	НайденныеСтроки = ФормаОбъект.КонтактныеДанные.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() > 0 Тогда
		ЗначениеТелефона = НайденныеСтроки[0].Структура;
	Иначе
		ЗначениеТелефона = "";
	КонецЕсли;
	
	ДополнительныеПараметры = ПараметрыОткрытияФормыКИ(Форма, "Телефон", ПараметрыВыбораТелефона.Роль);
	
	Если ЗначениеЗаполнено(ЗначениеТелефона) Тогда
		ДополнительныеПараметры.Значение = ЗначениеТелефона;
	Иначе
		ДополнительныеПараметры.Значение = Saby_ТНОбщегоНазначенияСервер.КонтактнаяИнформацияПоПредставлению(
			ПараметрыВыбораТелефона.Значение, ДополнительныеПараметры.Тип);
	КонецЕсли;
	
	Если ЭтоОсновной Тогда
		ДополнительныеПараметры.ИдентификаторСтроки = ИдентификаторОсновнойСтроки(
			ФормаОбъект, ДополнительныеПараметры.Тип, ПараметрыВыбораТелефона.Роль);
	Иначе
		Если ПараметрыВыбораТелефона.Свойство("ИдентификаторСтроки") Тогда
			ДополнительныеПараметры.ИдентификаторСтроки = ПараметрыВыбораТелефона.ИдентификаторСтроки;
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьФормуКонтактнойИнформации(ДополнительныеПараметры);
	
КонецПроцедуры

// Универсальный обработчик при измении телефона
// Параметры:
// 	Форма - Форма - источник вызова 
// 	Представление - Строка - телефон строкой    
//  Роль - Перечисление.Saby_РолиКонтрагентов - роль контрагента в транспортной накладной
// 	ЭтоОсновной - Булево - это основной номер телефона
//  Идентификатор - Идентификатор - идентификатор строки 
//
Процедура ТелефонПриИзменении(Форма, Представление, Роль, ЭтоОсновной = Ложь, Идентификатор = Неопределено) Экспорт
	
	ИдентификаторСтроки = Идентификатор;
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	ТипТелефон = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон");
	
	Значение = Saby_ТНОбщегоНазначенияСервер.КонтактнаяИнформацияПоПредставлению(
		Представление,
		ТипТелефон);
	
	СтруктураТелефона = Новый Структура;
	СтруктураТелефона.Вставить("Представление", Представление);
	СтруктураТелефона.Вставить("Значение",      Значение);
	
	ДопПараметрыОбновленияКИ = Saby_ТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(
		Роль, , , СтруктураТелефона);
	
	Если ЭтоОсновной Тогда
		ДопПараметрыОбновленияКИ.ИдентификаторСтроки = ИдентификаторОсновнойСтроки(ФормаОбъект, ТипТелефон, Роль);
	Иначе
		Если ИдентификаторСтроки <> Неопределено Тогда
			ДопПараметрыОбновленияКИ.ИдентификаторСтроки = ИдентификаторСтроки;
		КонецЕсли;
	КонецЕсли;
	
	Saby_ТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(Форма, ДопПараметрыОбновленияКИ);
	
КонецПроцедуры

// Универсальный обработчик начала выбора Адреса
// Параметры:
// 	Форма - Форма - источник вызова 
// 	ПараметрыВыбораАдреса - Структура - параметры 
// 	СтандартнаяОбработка - Булево - стандратный параметр
//
Процедура АдресНачалоВыбора(Форма, ПараметрыВыбораАдреса, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаJSON = ПараметрыВыбораАдреса.Значение;
		
	Если ЗначениеЗаполнено(СтрокаJSON) Тогда
		
		#Если ВебКлиент Тогда
			Адрес = Saby_ТНОбщегоНазначенияСервер.ПрочитатьJSONДляВебКлиента(СтрокаJSON);
		#Иначе
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
			Адрес = ПрочитатьJSON(ЧтениеJSON);
			ЧтениеJSON.Закрыть();
		#КонецЕсли
		
		Если ТипЗнч(Адрес) = Тип("Структура") И Адрес.Свойство("Типовой") Тогда
			ЗначениеАдреса = Адрес.Типовой;
		Иначе 
			ЗначениеАдреса = "";
		КонецЕсли;
	
	Иначе
		ЗначениеАдреса = "";
	КонецЕсли;
	
	ДополнительныеПараметры = ПараметрыОткрытияФормыКИ(Форма, "Адрес", ПараметрыВыбораАдреса.Роль);
	ДополнительныеПараметры.Значение = ЗначениеАдреса;
	Если ПараметрыВыбораАдреса.Свойство("ТолькоПросмотр") Тогда
		ДополнительныеПараметры.ТолькоПросмотр = ПараметрыВыбораАдреса.ТолькоПросмотр;
	КонецЕсли;
	ОткрытьФормуКонтактнойИнформации(ДополнительныеПараметры);
	
КонецПроцедуры

// Универсальный обработчик после выборка КИ
// Параметры:
//	Результат - Структура - результат выбора КИ 
//  ДополнительныеПараметры - Структура - доп параметры команды
//
Процедура ПослеВыбораКИ(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Или ДополнительныеПараметры.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ТипСтрокой = "Телефон" Тогда
		ДопПараметры = Saby_ТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(
			ДополнительныеПараметры.Роль, , , Результат);

		Если ДополнительныеПараметры.Свойство("ИдентификаторСтроки") Тогда
			ДопПараметры.ИдентификаторСтроки = ДополнительныеПараметры.ИдентификаторСтроки;
		КонецЕсли;
		Saby_ТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(ДополнительныеПараметры.Форма, ДопПараметры);
	ИначеЕсли ДополнительныеПараметры.ТипСтрокой = "Адрес" Тогда
		ДопПараметры = Saby_ТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(
			ДополнительныеПараметры.Роль, , , Результат);

		Saby_ТНОбщегоНазначенияКлиентСервер.ОбновитьАдресаПогрузкиДоставки(ДополнительныеПараметры.Форма, ДопПараметры);
		ДополнительныеПараметры.Форма.ОбновитьВидимостьИДоступностьЭлементовНаКлиенте();
	Иначе
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Форма.Модифицированность = Истина;
	
КонецПроцедуры

// Новые контактные данные при измнении номера телефона 
// Параметры:
// 	Форма - Форма - форма источник вызова 
// 	ТекущиеДанные - Структура - изменяемые данные
// 	Роль - Перечисление.Saby_РолиКонтрагентов - роль контрагента в транспортной накладной
//       - Массив - данные роли
//
Процедура ЗначениеКонтактныхДанныхПриИзменении(Форма, ТекущиеДанные, Роль) Экспорт
	
	Если ТекущиеДанные.Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон") Тогда
		
		ТелефонПриИзменении(Форма, ТекущиеДанные.Значение, Роль, Ложь, ТекущиеДанные.ПолучитьИдентификатор());
		
	КонецЕсли;
	
КонецПроцедуры

// Универсальный обработчик контактных данных при начале выбора
// Параметры:
// 	Форма - Форма - источник вызова 
//	ТекущиеДанные - ДанныеСтроки - текущая строка таблицы КИ
// 	Роль - Перечисление.Saby_РолиКонтрагентов - роль контрагента в транспортной накладной 
// 	СтандартнаяОбработка - Булево - стандратный параметр
//
Процедура ЗначениеКонтактныхДанныхНачалоВыбора(Форма, ТекущиеДанные, Роль, СтандартнаяОбработка) Экспорт
	
	Если ТекущиеДанные.Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон") Тогда
		
		ПараметрыВыбораТелефона = Новый Структура;
		ПараметрыВыбораТелефона.Вставить("Роль",                Роль);
		ПараметрыВыбораТелефона.Вставить("Значение",            ТекущиеДанные.Значение);
		ПараметрыВыбораТелефона.Вставить("ИдентификаторСтроки", ТекущиеДанные.ПолучитьИдентификатор());
		
		ТелефонНачалоВыбора(Форма, ПараметрыВыбораТелефона, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает событие выбора юр лица в таблице
//
// Параметры:
// 	Форма - Форма - источник вызова 
//  СтандартныеПараметрыСобытия - Структура - стандартные параметры события Выбор
//   * Элемент - ТаблицаФормы - таблица, в которой выбирается юр лицо
//   * ВыбраннаяСтрока - ДанныеФормыЭлементКоллекции - Значение выбранной строки
//   * Поле - ПолеФормы - активное поле (колонка)
//  ДополнительныеПараметры - Структура - дополнительные параметры необходимые для обработчика
//   * Роль - Строка - имя роли юр лица
//   * ТолькоПросмотр - Булево - признак открытия данных юр лица на просмотр
//
Процедура ЮрЛицоВТаблицеВыбор(Форма, СтандартныеПараметрыСобытия, ДополнительныеПараметры) Экспорт
	
	ИмяКолонки = СтрЗаменить(СтандартныеПараметрыСобытия.Поле.Имя, СтандартныеПараметрыСобытия.Элемент.Имя, "");
		
	ИдентификаторСтрокиДанныхЮрЛиц = Неопределено;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("КлючСтроки", СтандартныеПараметрыСобытия.Элемент.ТекущиеДанные.КлючСтроки_ДанныеЮрЛиц);
	Если СтруктураПоиска.КлючСтроки = 0 Тогда
		СтруктураПоиска.Вставить(
			"Роль",
			ПредопределенноеЗначение("Перечисление.Saby_РолиКонтрагентов." + ДополнительныеПараметры.Роль));
	КонецЕсли;
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	НайденныеСтроки = ФормаОбъект.ДанныеЮрЛиц.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() > 0 Тогда
		ИдентификаторСтрокиДанныхЮрЛиц = НайденныеСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
	ПараметрыРеквизитаФормы = Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыРеквизитаФормы(
		ИмяКолонки,
		СтандартныеПараметрыСобытия.Поле.Имя,
		ИдентификаторСтрокиДанныхЮрЛиц);
	
	ПараметрыРеквизитаФормы.ЭтоСтрокаТаблицы            = Истина;
	ПараметрыРеквизитаФормы.ИмяИсходнойТаблицы          = СтандартныеПараметрыСобытия.Элемент.Имя;
	ПараметрыРеквизитаФормы.ИдентификаторИсходнойСтроки = СтандартныеПараметрыСобытия.ВыбраннаяСтрока;
	ПараметрыРеквизитаФормы.ТолькоПросмотр              = ДополнительныеПараметры.ТолькоПросмотр;
		
	Saby_ТНОбщегоНазначенияКлиент.ЮрЛицоНажатие(
		Форма,
		ДополнительныеПараметры.Роль,
		ПараметрыРеквизитаФормы,
		ДополнительныеПараметры.ПроцедураПослеИзмененияЮрЛица);
	
КонецПроцедуры

// Универсальный обработчик события Нажатие для полей представления юр лица
//
// Параметры:
// 	Форма - Форма - источник вызова 
//  РольСтрокой - Строка - имя роли организации
//  ПараметрыРеквизитаФормы - Структура - см. функцию Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыРеквизитаФормы
//  ПроцедураПослеИзмененияЮрЛица - ОписаниеОповещения - процедура, которую нужно выполнить после изменения юр лиц.
//
Процедура ЮрЛицоНажатие(Форма, РольСтрокой, ПараметрыРеквизитаФормы,
		ПроцедураПослеИзмененияЮрЛица = Неопределено) Экспорт
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	ПараметрыФормы = Saby_ТНОбщегоНазначенияКлиентСервер.ДанныеЮрЛицаПоРоли(
		ФормаОбъект.ДанныеЮрЛиц, РольСтрокой, ПараметрыРеквизитаФормы);
		
	Если Saby_ТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(ФормаОбъект, "КонтактныеДанные") Тогда
		Saby_ТНОбщегоНазначенияКлиентСервер.ДобавитьКонтактныеДанныеПоРоли(
			ПараметрыФормы, ФормаОбъект.КонтактныеДанные, ПараметрыРеквизитаФормы);
	КонецЕсли;
	
	ТолькоПросмотр = ПараметрыРеквизитаФормы.ТолькоПросмотр
		Или Форма.Элементы[ПараметрыРеквизитаФормы.ИмяЭлемента].ТолькоПросмотр;
		
	Если Saby_ТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(ФормаОбъект, "СсылкаНаДокумент") Тогда
		ТипДокументаИсточника = ТипЗнч(ФормаОбъект.СсылкаНаДокумент);
	Иначе 
		ТипДокументаИсточника = ТипЗнч(ФормаОбъект.Ссылка);
	КонецЕсли;
		
	ПараметрыФормы.Вставить("Наименование",          "Организация");
	ПараметрыФормы.Вставить("РольСтрокой",           РольСтрокой);
	ПараметрыФормы.Вставить("ЭлементФормы",          ПараметрыРеквизитаФормы);
	ПараметрыФормы.Вставить("ТипДокументаИсточника", ТипДокументаИсточника);
	ПараметрыФормы.Вставить("ТолькоПросмотр",        ТолькоПросмотр);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма",                         Форма);
	ДопПараметры.Вставить("ПроцедураПослеИзмененияЮрЛица", ПроцедураПослеИзмененияЮрЛица);
	Оповещение = Новый ОписаниеОповещения("ПослеВводаДанныхЮрЛица", Saby_ТНОбщегоНазначенияКлиент, ДопПараметры);
	
	ОткрытьФорму(
		"ОбщаяФорма.Saby_ОрганизацииЭПД",
		ПараметрыФормы,
	    Форма, , , , 
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

#КонецОбласти // ДанныеЮрЛицИнтерфейс

// Преобразует строку табличной части формы в структуру.
// Свойства структуры и их значения совпадают с колонками переданной строки.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - источник данных строки
//  ИмяТаблицы - Строка - таблица, где находится строка преобразования
//  СтрокаТаблицы - СтрокаТаблицыЗначений - строка для преобразования
//  МассивКолонокИсключений - Массив - колонки для исключения
//
// Возвращаемое значение:
//  Структура - преобразованная строка таблицы значений.
//
Функция СтрокаТаблицыФормыВСтруктуру(Форма, ИмяТаблицы, СтрокаТаблицы, МассивКолонокИсключений = Неопределено) Экспорт
	
	КолонкиТаблицы = Форма.Элементы[ИмяТаблицы].ПодчиненныеЭлементы; 
	
	Структура = Новый Структура;
	Для Каждого Колонка Из КолонкиТаблицы Цикл 
		
		ИмяКолонки = Колонка.Имя; 
		Если СтрНачинаетсяС(ИмяКолонки, ИмяТаблицы) Тогда 
			ИмяКолонки = СтрЗаменить(ИмяКолонки, ИмяТаблицы, "");
		КонецЕсли;
				
		НельзяДобавитьВСтруктуру = МассивКолонокИсключений <> Неопределено 
			И МассивКолонокИсключений.Найти(ИмяКолонки) <> Неопределено
			Или КолонкиТаблицы.Найти(Колонка.Имя) = Неопределено;
			
		Если НельзяДобавитьВСтруктуру Тогда 
			Продолжить;
		КонецЕсли;
					
	    Структура.Вставить(ИмяКолонки, СтрокаТаблицы[ИмяКолонки]);
	
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции

#Область ТранспортныеСредстваИнтерфейс

// Обработчик стандартного события Нажатие для транспортного средства
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с транспорными средствами
//  СтандартнаяОбработка - Булево - признак использования стандартной обработки нажатия
//  ПроцедураОбновленияДанныхТС - ОписаниеОповещения - процедура, выполняемая после выбора транспортного средства
//  ТолькоПросмотр - Булево - признак доступности изменений на форме транспортного средства
//
Процедура ТранспортноеСредствоСтрокойНажатие(Форма, СтандартнаяОбработка,
		ПроцедураОбновленияДанныхТС, ТолькоПросмотр = Ложь) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытияФормыТС = ПараметрыОткрытияФормыТС(ПроцедураОбновленияДанныхТС, ТолькоПросмотр);
	ОткрытьФормуТранспортногоСредства(Форма, ПараметрыОткрытияФормыТС);
		
КонецПроцедуры

// Обработчик стандартного события Нажатие для прицепов
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с транспорными средствами
//  СтандартнаяОбработка - Булево - признак использования стандартной обработки нажатия
//
Процедура ПрицепыСтрокойНажатие(Форма, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;	
	Форма.ТекущийЭлемент = Форма.Элементы.Прицепы;
	
КонецПроцедуры

// Обработчик стандартного события Выбор для прицепов
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с транспорными средствами
//  ВыбраннаяСтрока - Число - идентификатор выбранной строки прицепа
//  СтандартнаяОбработка - Булево - признак использования стандартной обработки выбора
//  ПроцедураОбновленияДанныхТС - ОписаниеОповещения - процедура, выполняемая после выбора прицепа
//  ТолькоПросмотр - Булево - признак доступности изменений на форме транспортного средства
//
Процедура ПрицепыВыбор(Форма, ВыбраннаяСтрока, СтандартнаяОбработка,
		ПроцедураОбновленияДанныхТС, ТолькоПросмотр = Ложь) Экспорт
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	СтрокаПрицепа = ФормаОбъект.ТранспортныеСредства.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытияФормыТС = ПараметрыОткрытияФормыТС(ПроцедураОбновленияДанныхТС, ТолькоПросмотр);
	ПараметрыОткрытияФормыТС.ЭтоПрицеп = Истина;
	ПараметрыОткрытияФормыТС.СтрокаТС = СтрокаПрицепа;
	
	ОткрытьФормуТранспортногоСредства(Форма, ПараметрыОткрытияФормыТС);
	
КонецПроцедуры

// Обработчик стандартного события ПриНачалеРедактирования для прицепов
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с транспорными средствами
//  ПроцедураОбновленияДанныхТС - ОписаниеОповещения - процедура, выполняемая после обработки самого события
//
Процедура ПрицепыПриНачалеРедактирования(Форма, ПроцедураОбновленияДанныхТС) Экспорт
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	Форма.Элементы.Прицепы.ТекущиеДанные.Вид = ПредопределенноеЗначение("Перечисление.Saby_ВидыТС.Прицеп");
	ТекущаяСтрока = ФормаОбъект.ТранспортныеСредства.НайтиПоИдентификатору(Форма.Элементы.Прицепы.ТекущаяСтрока);
	
	ПараметрыОткрытияФормыТС = ПараметрыОткрытияФормыТС(ПроцедураОбновленияДанныхТС);
	ПараметрыОткрытияФормыТС.ЭтоПрицеп = Истина;
	ПараметрыОткрытияФормыТС.СтрокаТС = ТекущаяСтрока;
	
	ОткрытьФормуТранспортногоСредства(Форма, ПараметрыОткрытияФормыТС);
	
КонецПроцедуры

#КонецОбласти // ТранспортныеСредстваИнтерфейс

// Открывает форму изменения/ просмотра данных водителя или ответственного лица 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма источник вызова 
//  ПараметрыФормы - Структура - параметры для запуска формы водителя/отвественного  
//  ИмяОповещения - Строка - имя оповещения при получении результата  
// 
Процедура ИзменитьДанныеВодителяОтветственногоЛица(Форма, ПараметрыФормы, ИмяОповещения) Экспорт
					
	Если ПараметрыФормы.Свойство("РольСтрокой") Тогда
		
		ИмяПеречисления = "Перечисление.Saby_РолиОтветственных." + ПараметрыФормы.РольСтрокой;		
		ПараметрыФормы.Вставить("Роль", ПредопределенноеЗначение(ИмяПеречисления));
		
	КонецЕсли;

	ПараметрыФормы.Вставить("ИмяМетаданных", Saby_ТНОбщегоНазначенияКлиентСервер.ИмяМетаданныхПоФорме(Форма));
	
	ОткрытьОбщуюФормуСПараметрами(Форма, "Saby_ОтветственныйЭПД", ПараметрыФормы, ИмяОповещения);
		
КонецПроцедуры

// Открывает данные ответственного лица для редактирования
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма источник вызова
//  СтруктураОтветственногоЛица - Структура - см. Saby_ТНОбщегоНазначенияКлиентСервер.СтруктураОтветственногоЛица()
//  РольСтрокой - Строка - роль ответственного лица строкой
//
Процедура НачатьРедактированиеОтветственногоЛица(Форма, СтруктураОтветственногоЛица, РольСтрокой) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                       Форма);
	ДополнительныеПараметры.Вставить("СтруктураОтветственногоЛица", СтруктураОтветственногоЛица);
	ДополнительныеПараметры.Вставить("РольСтрокой",                 РольСтрокой);
	ПроцедураПослеРедактированияОтветственногоЛица = Новый ОписаниеОповещения(
		"ПослеРедактированияОтветственногоЛица", Saby_ТНОбщегоНазначенияКлиент, ДополнительныеПараметры);
	
	СтруктураОтветственногоЛица.Вставить("ИмяМетаданных", Saby_ТНОбщегоНазначенияКлиентСервер.ИмяМетаданныхПоФорме(Форма));
	
	СтруктураОтветственногоЛица.Вставить("Наименование", РольСтрокой);
	
	ОткрытьФорму(
		"ОбщаяФорма.Saby_ОтветственныйЭПД",
		СтруктураОтветственногоЛица,
	    Форма, , , , 
		ПроцедураПослеРедактированияОтветственногоЛица,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Выполняет действия после редактирования ответственного лица
//
// Параметры:
//  Результат - Структура - см. Saby_ТНОбщегоНазначенияКлиентСервер.СтруктураОтветственногоЛица()
//  ДополнительныеПараметры - Структура - см. НачатьРедактированиеОтветственногоЛица()
//
Процедура ПослеРедактированияОтветственногоЛица(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(ДополнительныеПараметры.Форма);
	
	СтрокаОтветственного = Неопределено;
	Если ДополнительныеПараметры.СтруктураОтветственногоЛица.ИдентификаторСтроки <> Неопределено Тогда
		СтрокаОтветственного = ФормаОбъект.ОтветственныеЛица.НайтиПоИдентификатору(
			ДополнительныеПараметры.СтруктураОтветственногоЛица.ИдентификаторСтроки);
	КонецЕсли;
	
	Если Результат.Удалить Тогда
		Если СтрокаОтветственного <> Неопределено Тогда
			ФормаОбъект.ОтветственныеЛица.Удалить(СтрокаОтветственного);
		КонецЕсли;
	Иначе
		Если СтрокаОтветственного = Неопределено Тогда
			СтрокаОтветственного = ФормаОбъект.ОтветственныеЛица.Добавить();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаОтветственного, Результат);
		
		Если СтрокаОтветственного.Роль = ПредопределенноеЗначение("Перечисление.Saby_РолиОтветственных.Водитель") Тогда
			Saby_ТНОбщегоНазначенияКлиентСервер.ОбновитьВодителейНаФорме(ДополнительныеПараметры.Форма);
		КонецЕсли;
	КонецЕсли;
	
	ДополнительныеПараметры.Форма.ПослеРедактированияОтветственногоЛица(Результат, ДополнительныеПараметры.РольСтрокой);
	
КонецПроцедуры

#Область ДокументОснование

// Зачистка документа основания по команде из формы
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма источник вызова
//
Процедура ОчиститьДокументОснование(Форма) Экспорт
	
	Если ЗначениеЗаполнено(Форма.ДокументОснование) Тогда
		
		Форма.Модифицированность = Истина;
		
		Форма.Объект.ДокументОснование_Идентификатор    = "";
		Форма.Объект.ДокументОснование_ОбъектМетаданных = "";
		
		Форма.ДокументОснование = Неопределено;
		Форма.Элементы.ДекорацияДокументОснование.Заголовок = "Выбрать документ основание";
		
		Форма.Элементы.ОчиститьДокументОснование.Доступность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработка нажатия на декорацию документа основания
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма источник вызова
//
Процедура ДокументОснованиеНажатие(Форма) Экспорт
	
	Если ЗначениеЗаполнено(Форма.ДокументОснование) Тогда 
		ПоказатьЗначение(, Форма.ДокументОснование);
	Иначе
		
		Если Форма.ТипыОснований.Количество() = 0 Тогда	
			
			ДопПараметры = Новый Структура;
			ДопПараметры.Вставить("Ссылка", Форма.Объект.Ссылка);
			
			ОткрытьФорму("ОбщаяФорма.Saby_ВыборТиповЭПД", ДопПараметры, Форма);
			
		Иначе 
			
			ДопПараметры = Новый Структура;
			ДопПараметры.Вставить("Форма",     Форма);
			
			Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораТипаДанныхДокументаОснования", ЭтотОбъект, ДопПараметры);						
			Форма.ТипыОснований.ПоказатьВыборЭлемента(Оповещение, "Выбор типа данных");
						
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

// Обработка выбора типа документа основания
// Параметры:
// 	ВыбранныйЭлемент - ТипМетаданных - выбранный тип документа основания   
//	ДопПараметры - Структура - дополнительные параметры выбора
//
Процедура ОбработкаВыбораТипаДанныхДокументаОснования(ВыбранныйЭлемент, ДопПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		
		ДокОснование = ДопПараметры.Форма.ДокументОснование;
		
		Оповещение = Новый ОписаниеОповещения("ПослеВыбораЗначенияДокументаОснования", ЭтотОбъект, ДопПараметры);		
		ПоказатьВводЗначения(Оповещение, ДокОснование, "Выберите основание", ВыбранныйЭлемент.Значение);
				
	КонецЕсли;
	
КонецПроцедуры

// Обработка выбора значения документа основания
// Параметры:
//	Значение - ДокументСсылка - выбранный документ как источник для заполнения данных
//  ДопПараметры - Структура - дополнительные параметры
//
Процедура ПослеВыбораЗначенияДокументаОснования(Значение, ДопПараметры) Экспорт
	
	Форма = ДопПараметры.Форма;
		
	Если Форма.Объект.Ссылка.Пустая() Тогда
		ЗаписатьДанныеДокумента(Форма);
	КонецЕсли;		
		
	Если Значение <> Неопределено Тогда
		
		Форма.Модифицированность = Истина;
		
		Форма.ДокументОснование = Значение;
		Форма.Элементы.ДекорацияДокументОснование.Заголовок = СокрЛП(Значение);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.ДокументОснование) Тогда		
		
		Форма.Элементы.ОчиститьДокументОснование.Доступность = Истина;				
        Оповестить("ВыбранДокументОснованиеINI", , Форма);
		
	Иначе 
		
		Форма.Объект.ДокументОснование_Идентификатор    = "";
		Форма.Объект.ДокументОснование_ОбъектМетаданных = "";
		
		Форма.Элементы.ОчиститьДокументОснование.Доступность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Запись документа из фомы
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма источник вызова
//
Процедура ЗаписатьДанныеДокумента(Форма) Экспорт
	
	РежимЗаписи = ?(Форма.Объект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
	
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписи);	
		
	Форма.Записать(ПараметрыЗаписи);
	
КонецПроцедуры

// Обработка для стандартного события формы документов ОбработкаОповещения
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма источник вызова
// 	ИмяСобытия - Строка - имя события оповещения 
//	Параметр - Структура - параметры оповещения 
//	Источник - Произвольный - источник оповещения 
//
Процедура ОбработатьОповещенияДляФормы(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если Не ОповещениеДляЭтойФормы(Форма, ИмяСобытия, Параметр, Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "Saby_Ошибки" Тогда 
		
		ОчиститьСообщения();
		
		ДанныеОшибки = Saby_ТНОбщегоНазначенияСервер.ДесериализоватьЗначение(Параметр.Ошибки);
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(ДанныеОшибки);
		
	ИначеЕсли ИмяСобытия = "Saby_ОбновитьДокумент" Тогда
		
		Форма.ОбработатьОповещениеОбновитьДокумент(Параметр);
		
	ИначеЕсли ИмяСобытия = "Saby_ВыполнитьПереход" Тогда
		
		ОбработатьОповещениеВыполнитьПереход(Форма, Параметр);
		
	ИначеЕсли ИмяСобытия = "Saby_ЗавершениеВыполнитьДействие" Тогда
		
		ОбработатьОповещениеЗавершениеВыполнитьДействие(Форма, Параметр);
		
	ИначеЕсли ИмяСобытия = "Saby_ИзмененыТипыДокументовОснований" Тогда
		
		Форма.ОграничитьТипыДокументовОснований();
		
	// ИначеЕсли ИмяСобытия = "Saby_ПрочитанДинамическийТитул" Тогда
		
		// Форма.ПослеЗакрытияФормыТитула(Параметр);
		
	ИначеЕсли ИмяСобытия = "Saby_СписокДокументов" Тогда
		
		Если Не Параметр.ЭтоДинамическийТитул Тогда
			Форма.ОбновитьДанныеПоОшибкамНаСервере(Параметр.СписокДокументов.Получить(Форма.Объект.Ссылка));
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Saby_СписокДокументовОбновить" Тогда  	
		
		Форма.Элементы.Список.Обновить();
		
	ИначеЕсли ИмяСобытия = "ВыбранДокументОснованиеINI" Тогда
		
		Результат = Форма.ЗаполнениеНаОснованииINI();		
		ОповеститьПользователяОРезультатах("ВыбранДокументОснованиеINI", Результат);
				
	ИначеЕсли ИмяСобытия = "Saby_АктуальныйСтатусВерсии" Тогда
		
		ОтобразитьНаличиеОбновлений(Форма, Параметр);
		
	ИначеЕсли ИмяСобытия = "ОбновитьЛенту" Тогда
		
		Форма.ОбновитьЛентуНаСервере();
				
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

// Получения выделенных строк - ссылок на документы для последующей обработки
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - источник данных списка
//  ЗаполнитьАктивныйЭтап - Булево - признак запонения активного этапа на форме
// Возвращаемое значение:
//  Массив - ссылки на документы для обработки
//
Функция ДокументыДляОбработки(Форма, ЗаполнитьАктивныйЭтап = Ложь) Экспорт
	
	ВыделенныеСтроки = Форма.Элементы.Список.ВыделенныеСтроки;
	
	Массив = Новый Массив;
	Для Каждого Строка Из ВыделенныеСтроки Цикл
		Если ЗаполнитьАктивныйЭтап И Форма.АктивныйЭтапСтрокой = "" Тогда
			Форма.АктивныйЭтапСтрокой = Saby_ТНОбщегоНазначенияСервер.АктивныйЭтапСтрокой(Строка);
		КонецЕсли;
		Массив.Добавить(Строка);
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции

#Область Лента

// Обработчик события ОбработкаНавигационнойСсылки для декорации титула в визуализации ленты в документе.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма источник вызова
//  Элемент - ДекорацияФормы - декорация титула
//  СтандартнаяОбработка - Булево - признак продолжения стандартной обработки
//
Процедура ОбработкаНавигационнойСсылкиЛенты(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	МассивПодстрок = СтрРазделить(Элемент.Имя, "_");
	
	ИдентификаторСтроки = Число(МассивПодстрок[МассивПодстрок.ВГраница()]);
	
	СтрокаЛенты = Форма.Лента.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если СтрокаЛенты.Основной Тогда
		Форма.АктивизироватьРеквизитИзТитула(СтрокаЛенты);
	Иначе
		Форма.ОткрытьФормуТитула(
			СтрокаЛенты.НаименованиеТитула,
			СтрокаЛенты.ИдентификаторТитула,
			СтрокаЛенты.Описание);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Лента

// Проверяет возможность добавления новой строки подчиненной таблицы
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма источник вызова
//  ИмяОсновнойТаблицы - Строка - имя таблицы, от которой зависит текущая
//  Отказ - Булево - признак продолжения стандартного обработчика события
//
Процедура ПередНачаломДобавленияСтрокиСвязаннойТаблицы(Форма, ИмяОсновнойТаблицы, Отказ) Экспорт
		
	ИдентификаторСтроки = Форма.Элементы[ИмяОсновнойТаблицы].ТекущаяСтрока;
	Если ИдентификаторСтроки = Неопределено Тогда 
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбрана строка в основной таблице " + ИмяОсновнойТаблицы;
		Сообщение.Сообщить();
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

// Проверка наличия обновлений расширения и генерация элементов предупреждения и ссылки на скачивание
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма для обновления элементов
//  СтатусВерсии - Структура - данные о текущей версии
//
Процедура ОтобразитьНаличиеОбновлений(Форма, СтатусВерсии) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если СтатусВерсии = Неопределено Тогда
		Элементы.ГруппаОбновление.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ДанныеЭлементов = ОбработкаШаблонаСтатусаВерсии(СтатусВерсии.Шаблон);
	Если Не ЗначениеЗаполнено(ДанныеЭлементов) Тогда 
		Возврат;
	КонецЕсли;	
		
	// Картинка
	Картинка = Элементы.КартинкаОбновление;
	Картинка.Картинка       = БиблиотекаКартинок.Saby_СостояниеОшибка;
	Картинка.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
	
	// Надпись
	Надпись = Элементы.СтатусОбновление;
	Надпись.Заголовок = ДанныеЭлементов.Заголовок;
	Если ЗначениеЗаполнено(ДанныеЭлементов.ЦветТекста) Тогда
		Надпись.ЦветТекста = ДанныеЭлементов.ЦветТекста;
	КонецЕсли;	
	
	// Ссылка на скачивание
	Если ДанныеЭлементов.Гиперссылка Тогда		
		
		Гиперссылка = Элементы.СсылкаОбновление;
		Гиперссылка.Доступность = Истина;
		Гиперссылка.Заголовок        = "Скачать";
		Гиперссылка.Подсказка        = ДанныеЭлементов.Ссылка;
				
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти    

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет действия после вопроса о сохранении объекта
//
// Параметры:
//  Результат - КодВозвратаДиалога - выбранный ответ в форме вопроса
//  ДополнительныеПараметры - Структура - контекст, переданный из вызывающей процедуры
//
Процедура ПослеВопросаОЗаписиФормы(Результат, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если НеобходимоЗаписатьФорму(Форма) И Результат = КодВозвратаДиалога.Да Тогда
		Форма.Записать();
	КонецЕсли;
	
	Если Результат <> КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ПроцедураДляВыполнения, Результат);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обновление кэша локальных сертификатов перед выполнением подписания и отправки динамического титула
//
// Параметры:
//  ПроцедураПродолжения - ОписаниеОповещения - процедура для выполнения после обневления кэша
//  СсылкаНаДокумент - ДокументСсылка.Saby_ТранспортнаяНакладная - ссылка на документ,
//    из которого выполняется отправка титула
//
Процедура ОбновитьКЭШЛокальныхСертификатовИПродолжитьВыполнение(ПроцедураПродолжения, СсылкаНаДокумент) Экспорт
	
	СтруктураДокумента = Новый Структура();
	СтруктураДокумента.Вставить("Документ1С", СсылкаНаДокумент);
	СтруктураДокумента.Вставить("Этап", Новый Массив);
	
	СтруктураЭтапа = Новый Структура;
	СтруктураЭтапа.Вставить("Действие", Новый Массив);
	СтруктураДокумента.Этап.Добавить(СтруктураЭтапа);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СписокДокументовСБИС", Новый Массив);
	ПараметрыФормы.СписокДокументовСБИС.Добавить(СтруктураДокумента);
	
	ФормаВыполнитьДействие = ПолучитьФорму("Обработка.SABY.Форма.ВыполнитьДействие", ПараметрыФормы);
	ФормаВыполнитьДействие.ОбновитьКэшЛокальныхСертификатов(ПроцедураПродолжения);
	
КонецПроцедуры

// Выполняет действия после открытия формы выбора транспортного средства
//
// Параметры:
//  СтруктураТранспортногоСредства - Структура - структура параметров транспортного средства.
//    Совпадает с табличной частию ТранспортныеСредства в документе Saby_ТранспортнаяНакладная.
//  ДополнительныеПараметры - Структура - контекст из вызывающей процедуры
//
Процедура ПослеОткрытияФормыТранспортногоСредства(СтруктураТранспортногоСредства, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	УдалитьСтрокиПоВиду(ФормаОбъект.ТранспортныеСредства);
	
	Если ДополнительныеПараметры.ТолькоПросмотр Или Не ЗначениеЗаполнено(СтруктураТранспортногоСредства) Тогда
		Возврат;
	КонецЕсли;
	
	Автомобиль = ПредопределенноеЗначение("Перечисление.Saby_ВидыТС.Автомобиль");
	
	Если СтруктураТранспортногоСредства.Вид = Автомобиль Тогда
		СтрокаАвтомобиля = СтрокаАвтомобиля(ФормаОбъект.ТранспортныеСредства);
		Если СтрокаАвтомобиля = Неопределено Тогда
			СтрокаАвтомобиля = ФормаОбъект.ТранспортныеСредства.Добавить();
		КонецЕсли;
		ИдентификаторСтроки = СтрокаАвтомобиля.ПолучитьИдентификатор();
	Иначе
		ИдентификаторСтроки = Форма.Элементы.Прицепы.ТекущаяСтрока;
	КонецЕсли;
	
	СтрокаТранспортногоСредства = ФормаОбъект.ТранспортныеСредства.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ЗаполнитьЗначенияСвойств(СтрокаТранспортногоСредства, СтруктураТранспортногоСредства);
	
	Если Не ЗначениеЗаполнено(СтрокаТранспортногоСредства.РегистрационныйНомер) Тогда
		ФормаОбъект.ТранспортныеСредства.Удалить(СтрокаТранспортногоСредства);
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	
	Если ДополнительныеПараметры.ПроцедураОбновленияДанныхТС <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ПроцедураОбновленияДанныхТС);
	Иначе
		Saby_ТНОбщегоНазначенияКлиентСервер.ОбновитьТранспортныеСредстваНаФорме(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьДокументыСБИС(Форма, ИмяКоманды, ИмяМетаданных) Экспорт
	
	ДатаОбновления = Saby_ТНОбщегоНазначенияСервер.ПоследнееУспешноЗагруженноеИзменение(ИмяМетаданных);
	Если Не ЗначениеЗаполнено(ДатаОбновления) Тогда 
		Saby_ТНОбщегоНазначенияКлиент.ОбновитьДокументыЗаПериод(Форма, ИмяКоманды);			
	Иначе 
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("Начало", ДатаОбновления);
		
		// Обновляем все документы разом
		// не важно выделенны строки или нет
		ОбменДаннымиСБИСИзФормыСписка(
			Форма, 
			ИмяКоманды, 
			ДопПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьДокументыЗаПериод(Форма, ИмяКоманды, НеОбновлятьДатуИзменения = Ложь) Экспорт
		
	// Обновить документы с даты
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяКоманды", ИмяКоманды);
	ДополнительныеПараметры.Вставить("Форма",      Форма);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеВводаПериодаЗагрузки",
		Saby_ТНОбщегоНазначенияКлиент,
		ДополнительныеПараметры);
	
	ОткрытьФорму(
		"ОбщаяФорма.Saby_НастройкиПериодаЭПД", ,
		Форма, , , ,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс); 
	
КонецПроцедуры

Процедура ПослеВводаПериодаЗагрузки(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(РезультатЗакрытия) Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Обновление не выполнено! Не указан период обновления документов";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;	
	
    ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Начало",    РезультатЗакрытия.Начало);
	ДопПараметры.Вставить("Окончание", РезультатЗакрытия.Окончание);
			
	ОбменДаннымиСБИСИзФормыСписка(
		ДополнительныеПараметры.Форма,
		ДополнительныеПараметры.ИмяКоманды,
		ДопПараметры);
	
КонецПроцедуры

Процедура ПослеВводаДанныхЮрЛица(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРеквизитаФормы = Результат.ЭлементФормы;
	
	ОбновитьДанныеЮрЛиц(ДополнительныеПараметры.Форма, Результат, ПараметрыРеквизитаФормы);
	
	ДополнительныеПараметры.Форма.Модифицированность = Истина;
	
	Если ДополнительныеПараметры.ПроцедураПослеИзмененияЮрЛица <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ПроцедураПослеИзмененияЮрЛица, Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ТекстСообщенияКоманды(Ключ) 
	
	ТекстСообщения = Новый Соответствие;
	
	КлючиСтруктурыСообщений = "НетРезультата,Успешно,ВыполненоСОшибками";
	
	ЗагрузитьВСбис = Новый Структура(КлючиСтруктурыСообщений);
	ЗагрузитьВСбис.НетРезультата      = "Выгрузка не производилась";
	ЗагрузитьВСбис.Успешно            = "В СБИС выгружено %1 документов";
	ЗагрузитьВСбис.ВыполненоСОшибками = "В СБИС выгружено документов %1 из %2";
	
	ТекстСообщения.Вставить("ЗагрузитьВСбис", ЗагрузитьВСбис);

	ПрочитатьДокумент = Новый Структура(КлючиСтруктурыСообщений);
	ПрочитатьДокумент.НетРезультата      = "Загрузка не производилась";
	ПрочитатьДокумент.Успешно            = "Документ обновлен";
	ПрочитатьДокумент.ВыполненоСОшибками = "Возникла ошибка! Документ не загружен";
	
	ТекстСообщения.Вставить("ПрочитатьДокумент", ПрочитатьДокумент);
		
	ОбновитьДокументИзСбис = Новый Структура(КлючиСтруктурыСообщений);
	ОбновитьДокументИзСбис.НетРезультата      = "Загрузка не производилась. Данных для обновления нет";
	ОбновитьДокументИзСбис.Успешно            = "Из СБИС загружено %1 документов";
	ОбновитьДокументИзСбис.ВыполненоСОшибками = "Из СБИС загружено документов %1 из %2";
	
	ТекстСообщения.Вставить("ОбновитьДокументИзСбис", ОбновитьДокументИзСбис);
	
	Возврат ТекстСообщения.Получить(Ключ);
	
КонецФункции

Процедура ОбработатьРезультатКоманды(Результат, ИмяКоманды, Источник)
	
	РезультатОбработки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	ОповеститьПослеОбработкиРезультатаКоманды(ИмяКоманды, РезультатОбработки, Источник);
	
	ОповеститьПользователяОРезультатах(ИмяКоманды, РезультатОбработки);
	
КонецПроцедуры

Процедура ОповеститьПослеОбработкиРезультатаКоманды(ИмяКоманды, РезультатОбработки, Источник)
	
	Если РезультатОбработки.Ошибки.Количество() Тогда
		
		Если Не ЭтоВыгрузкаДинамическогоТитула(ИмяКоманды) Тогда
			ОткрытьПервыйДокументИзСоответствия(РезультатОбработки.Ошибки);
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("СписокДокументов",     РезультатОбработки.Ошибки);
		ПараметрыФормы.Вставить("ЭтоДинамическийТитул", ЭтоВыгрузкаДинамическогоТитула(ИмяКоманды));
		
		Оповестить("Saby_СписокДокументов", ПараметрыФормы);
		
	КонецЕсли;
	
	Если Не РезультатОбработки.Итог.Успех Тогда
		Возврат;
	КонецЕсли;
	
	МассивСсылок = РезультатОбработки.Успешные;
	Для Каждого Элемент Из МассивСсылок Цикл
		
		// одиночная команда
		Если ИмяКоманды = "ПрочитатьДокумент"
			Или ИмяКоманды = "ОбновитьАктивныйЭтап"
			Или ИмяКоманды = "ЗагрузитьВСбисДинамическийТитул" Тогда
			
			Оповестить("Saby_ОбновитьДокумент", , Источник);
			
		ИначеЕсли ИмяКоманды = "ВыгрузитьВСбис" Тогда
			
			Оповестить("Saby_ВыполнитьПереход", Элемент, Источник);
			
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОповеститьПользователяОРезультатах(ИмяКоманды, РезультатОбработки)
	
	ТекстСообщения = ТекстСообщенияКоманды(ИмяКоманды);
	ТекстЗаголовка = "Завершено";
	
	Если ИмяКоманды = "ВыбранДокументОснованиеINI" Тогда 
		
		ТекстЗаголовка = РезультатОбработки.ТекстЗаголовка;
		Текст          = РезультатОбработки.ТекстСообщения;
	    Картинка       = РезультатОбработки.Картинка;
		
	ИначеЕсли ТекстСообщения = Неопределено Тогда
		// если не чего сообщать то завершаем
		Возврат;
				
	ИначеЕсли РезультатОбработки.Итог.Всего = 0 Тогда
		
		Текст    = ТекстСообщения.НетРезультата;
		Картинка = БиблиотекаКартинок.ВосклицательныйЗнакСерый;
					
	ИначеЕсли РезультатОбработки.Итог.Успех Тогда
		
		Картинка = БиблиотекаКартинок.Saby_Успешно32;
		
		Если СтрНайти(ТекстСообщения.Успешно, "%") > 0 Тогда
			Текст = СтрШаблон(ТекстСообщения.Успешно, Формат(РезультатОбработки.Итог.Количество, "ЧН=; ЧГ="));
		Иначе
			Текст = СтрШаблон(ТекстСообщения.Успешно);
		КонецЕсли;
		
	Иначе
		
		Картинка = БиблиотекаКартинок.Saby_Ошибка32;
		
		Если СтрНайти(ТекстСообщения.ВыполненоСОшибками, "%") > 0 Тогда
			Текст = СтрШаблон(
				ТекстСообщения.ВыполненоСОшибками,
				Формат(РезультатОбработки.Итог.Количество, "ЧН=; ЧГ="),
				Формат(РезультатОбработки.Итог.Всего, "ЧГ="));
		Иначе
			Текст = ТекстСообщения.ВыполненоСОшибками;
		КонецЕсли;
		
	КонецЕсли;
		
	ПоказатьОповещениеПользователя(
		ТекстЗаголовка,
		,
		Текст,
		Картинка);
	
КонецПроцедуры

Процедура ОткрытьПервыйДокументИзСоответствия(СоответствиеДокументов)
	
	Для Каждого КлючЗначение Из СоответствиеДокументов Цикл
		СсылкаНаДокумент      = КлючЗначение.Ключ;
		СтруктураДанныхОшибок = КлючЗначение.Значение;
		Если Не ЕстьСсылкаНаДокумент(СсылкаНаДокумент, СтруктураДанныхОшибок) Тогда
			Продолжить;
		КонецЕсли;
		ПараметрыФормыДокумента = Новый Структура;
		ПараметрыФормыДокумента.Вставить("Ключ", СсылкаНаДокумент);
		ИмяФормы = СтрШаблон("Документ.%1.ФормаОбъекта", СтруктураДанныхОшибок.ИмяМетаданных);
		ОткрытьФорму(ИмяФормы, ПараметрыФормыДокумента);
		Прервать;
	КонецЦикла;
	
КонецПроцедуры

Функция ЕстьСсылкаНаДокумент(СсылкаНаДокумент, СтруктураДанныхОшибок)
	
	Возврат ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка." + СтруктураДанныхОшибок.ИмяМетаданных)
		И ЗначениеЗаполнено(СсылкаНаДокумент);
	
КонецФункции

Функция ИдентификаторОсновнойСтроки(ФормаОбъект, Тип, Роль)
	
	МассивРолей = Saby_ТНОбщегоНазначенияКлиентСервер.МассивРолей(Роль);
	
	РезультатФункции = Неопределено;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Основной", Истина);
	СтруктураПоиска.Вставить("Роль",     МассивРолей[0].Роль);
	НайденныеСтроки = ФормаОбъект.КонтактныеДанные.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() > 0 Тогда
		РезультатФункции = НайденныеСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ПараметрыОткрытияФормыКИ(Форма, Тип, Роль)
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Форма",          Форма);
	РезультатФункции.Вставить("Тип",            ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации." + Тип));
	РезультатФункции.Вставить("ТипСтрокой",     Тип);
	РезультатФункции.Вставить("Роль",           Роль);
	РезультатФункции.Вставить("Значение",       "");
	РезультатФункции.Вставить("ТолькоПросмотр", Ложь);
	
	РезультатФункции.Вставить("ИдентификаторСтроки", Неопределено);
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ОткрытьФормуКонтактнойИнформации(ДополнительныеПараметры)
	
	ПараметрыВидаКИ = Saby_ТНОбщегоНазначенияСервер.ПараметрыВидаКонтактнойИнформации(ДополнительныеПараметры.Тип);
	
	ПараметрыВидаКИ.Наименование = ДополнительныеПараметры.ТипСтрокой;
	
	ПараметрыКИ = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ПараметрыВидаКИ, ДополнительныеПараметры.Значение);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораКИ", ЭтотОбъект, ДополнительныеПараметры);
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыКИ, ЭтотОбъект, Оповещение);
	
КонецПроцедуры  

Процедура УстановитьАктивнуюСтрокуВОсновнойТаблице(Форма, ИмяТаблицы, ИндексСтроки)
	
	ЭтоАкты           = ИмяТаблицы = "Акты";
	ЭтоШтрафы         = ИмяТаблицы = "Штрафы";
	ЭтоАктыВыгрузка   = ИмяТаблицы = "Акты_Выгрузка";
	ЭтоШтрафыВыгрузка = ИмяТаблицы = "Штрафы_Выгрузка";
	
	ЭтоНужнаяТаблица = ЭтоАкты Или ЭтоШтрафы Или ЭтоАктыВыгрузка Или ЭтоШтрафыВыгрузка;
	Если Не ЭтоНужнаяТаблица Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицыОбъекта = СтрЗаменить(ИмяТаблицы, "_Выгрузка", "");
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	КлючСтроки = ФормаОбъект[ИмяТаблицыОбъекта][ИндексСтроки].КлючСтроки_Отметки;
	Отбор = Новый Структура;
	Отбор.Вставить("КлючСтроки", КлючСтроки);
	НайденныеСтрокиОтметок = ФормаОбъект.Отметки.НайтиСтроки(Отбор);
	Если НайденныеСтрокиОтметок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСтрокиОтметок = НайденныеСтрокиОтметок[0].ПолучитьИдентификатор();
	
	Если ЭтоАкты Или ЭтоШтрафы Тогда
		Форма.ТекущийЭлемент = Форма.Элементы.Отметки;
		Форма.Элементы.Отметки.ТекущаяСтрока  = ИдентификаторСтрокиОтметок;
		Форма.ОтборОтметокПоТипу(ИдентификаторСтрокиОтметок);
		Форма.Элементы.Отметки.ТекущаяСтрока  = ИдентификаторСтрокиОтметок;
	ИначеЕсли ЭтоАктыВыгрузка Или ЭтоШтрафыВыгрузка Тогда
		Форма.ТекущийЭлемент = Форма.Элементы.Отметки_Выгрузка;
		Форма.Элементы.Отметки_Выгрузка.ТекущаяСтрока  = ИдентификаторСтрокиОтметок;
		Форма.ОтборОтметокПоТипу(ИдентификаторСтрокиОтметок);
		Форма.Элементы.Отметки_Выгрузка.ТекущаяСтрока  = ИдентификаторСтрокиОтметок;
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеОшибки(Строка, ЭтоСтруктура, ИндексОшибки)
	
	ОписаниеОшибки = "";
	
	Если ЭтоСтруктура Тогда
		ОписаниеОшибки = Строка.message;
	Иначе
		
		ДокументСБИС = Строка["ДокументСБИС"];
		ДеталиОшибки = ДокументСБИС["detail"];
		Если Не ЗначениеЗаполнено(ДеталиОшибки) Тогда
			Ошибка = ДокументСБИС["message"];
		Иначе
			Ошибка = ДеталиОшибки;
		КонецЕсли;
		
		Если ТипЗнч(Ошибка) = Тип("Строка") Тогда
			ОписаниеОшибки = Ошибка;
		Иначе
			ОписаниеОшибки = ДеталиОшибки["error"]["details"];
		КонецЕсли;
		
	КонецЕсли;
				
	СтруктураОшибки = Новый Структура;
	СтруктураОшибки.Вставить("ИндексСтроки",        ИндексОшибки);
	СтруктураОшибки.Вставить("ТекстДляОднойОшибки", ОписаниеОшибки);
	СтруктураОшибки.Вставить("ПолеОшибки",          "");
	СтруктураОшибки.Вставить("ГруппаОшибок",        Неопределено);
		
	Возврат СтруктураОшибки;
	
КонецФункции

Функция СтрокаТаблицыСОшибкой(Форма, Знач ИмяТаблицыОбъекта, ТекущиеДанные, СоответствиеТаблицФормыИОбъекта)
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	Если СоответствиеТаблицФормыИОбъекта <> Неопределено
		И СоответствиеТаблицФормыИОбъекта[ИмяТаблицыОбъекта] <> Неопределено Тогда
		ИмяТаблицыОбъекта = СоответствиеТаблицФормыИОбъекта[ИмяТаблицыОбъекта];
	КонецЕсли;
	
	Если ТекущиеДанные.ИндексСтроки < ФормаОбъект[ИмяТаблицыОбъекта].Количество() Тогда
		Возврат ФормаОбъект[ИмяТаблицыОбъекта][ТекущиеДанные.ИндексСтроки];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ЭтоТаблицаФормы(Форма, ИмяТаблицыФормы)
	
	Если ИмяТаблицыФормы = "" Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Форма.Элементы.Найти(ИмяТаблицыФормы) <> Неопределено;
	
КонецФункции

#Область ТранспортныеСредства

Функция ПараметрыОткрытияФормыТС(ПроцедураОбновленияДанныхТС = Неопределено, ТолькоПросмотр = Ложь)
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ЭтоПрицеп",                   Ложь);
	РезультатФункции.Вставить("ТолькоПросмотр",              ТолькоПросмотр);
	РезультатФункции.Вставить("СтрокаТС",                    Неопределено);
	РезультатФункции.Вставить("ПроцедураОбновленияДанныхТС", ПроцедураОбновленияДанныхТС);
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ОткрытьФормуТранспортногоСредства(Форма, ПараметрыОткрытияФормыТС)
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	Если ПараметрыОткрытияФормыТС.СтрокаТС = Неопределено Тогда
		ПараметрыОткрытияФормыТС.СтрокаТС = СтрокаАвтомобиля(ФормаОбъект.ТранспортныеСредства);
	КонецЕсли;
	
	Если ПараметрыОткрытияФормыТС.СтрокаТС <> Неопределено Тогда
		ПараметрыФормы = Saby_ТНОбщегоНазначенияКлиент.СтрокаТаблицыФормыВСтруктуру(
			Форма, "Прицепы", ПараметрыОткрытияФормыТС.СтрокаТС);
	Иначе
		ПараметрыФормы = Новый Структура;
	КонецЕсли;
	
	ТолькоПросмотр = ПараметрыОткрытияФормыТС.ТолькоПросмотр Или Форма.ТолькоПросмотр;
	Если Форма.Элементы.Найти("Страница_Основное") <> Неопределено Тогда
		ТолькоПросмотр = ТолькоПросмотр Или Форма.Элементы.Страница_Основное.ТолькоПросмотр;
	КонецЕсли;
	Если Форма.Элементы.Найти("ГруппаТС") <> Неопределено Тогда
		ТолькоПросмотр = ТолькоПросмотр Или Форма.Элементы.ГруппаТС.ТолькоПросмотр;
	КонецЕсли;
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("СсылкаНаДокумент", Неопределено);
		
	ЗаполнитьЗначенияСвойств(СтруктураСвойств, Форма);
	
	Если Saby_ТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(ФормаОбъект, "СсылкаНаДокумент") Тогда
		ТипДокументаИсточника = ТипЗнч(ФормаОбъект.СсылкаНаДокумент);
	Иначе 
		ТипДокументаИсточника = ТипЗнч(ФормаОбъект.Ссылка);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ТолькоПросмотр",        ТолькоПросмотр);
	ПараметрыФормы.Вставить("ЭтоПрицеп",             ПараметрыОткрытияФормыТС.ЭтоПрицеп); 
    ПараметрыФормы.Вставить("ТипДокументаИсточника", ТипДокументаИсточника);	
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ТолькоПросмотр",              ПараметрыФормы.ТолькоПросмотр);
	ДопПараметры.Вставить("Форма",                       Форма);
	ДопПараметры.Вставить("ПроцедураОбновленияДанныхТС", ПараметрыОткрытияФормыТС.ПроцедураОбновленияДанныхТС);
	
	ПроцедураПослеОткрытияФормыТранспортногоСредства = Новый ОписаниеОповещения(
		"ПослеОткрытияФормыТранспортногоСредства", Saby_ТНОбщегоНазначенияКлиент, ДопПараметры);
	
	ОткрытьФорму(
		"ОбщаяФорма.Saby_ТранспортноеСредствоЭПД",
		ПараметрыФормы,
	    Форма, , , , 
		ПроцедураПослеОткрытияФормыТранспортногоСредства,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Функция СтрокаАвтомобиля(ТранспортныеСредства)
	
	Автомобиль = ПредопределенноеЗначение("Перечисление.Saby_ВидыТС.Автомобиль");
	
	СтрокиАвтомобиля = Saby_ТНОбщегоНазначенияКлиентСервер.ПолучитьСтрокиТС(
		ТранспортныеСредства,
		Автомобиль);
		
	Если СтрокиАвтомобиля.Количество() > 0 Тогда
		РезультатФункции = СтрокиАвтомобиля[0];
	Иначе
		РезультатФункции = Неопределено;
	КонецЕсли;	
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура УдалитьСтрокиПоВиду(ТранспортныеСредства, Вид = Неопределено)
	
	Если Вид = Неопределено Тогда
		Вид = ПредопределенноеЗначение("Перечисление.Saby_ВидыТС.ПустаяСсылка");
	КонецЕсли;
	
	ИндексСтрокиТС = 0;
	Пока ИндексСтрокиТС < ТранспортныеСредства.Количество() Цикл
		СтрокаТС = ТранспортныеСредства[ИндексСтрокиТС];
		Если СтрокаТС.Вид = Вид Тогда
			ТранспортныеСредства.Удалить(ИндексСтрокиТС);
		Иначе
			ИндексСтрокиТС = ИндексСтрокиТС + 1;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // ТранспортныеСредства

Функция ЭтоЗагрузкаЧтениеИлиОбновление(ИмяКоманды)
	
	Возврат ИмяКоманды = "ВыгрузитьВСбис"
		Или ИмяКоманды = "ЗагрузитьВСбисДинамическийТитул"
		Или ИмяКоманды = "ПрочитатьДокумент"
		Или ИмяКоманды = "ОбновитьАктивныйЭтап"  
		Или ИмяКоманды = "ОбновитьДокументИзСбис";
	
КонецФункции

Функция ЭтоВыгрузкаДинамическогоТитула(ИмяКоманды)
	
	Возврат ИмяКоманды = "ЗагрузитьВСбисДинамическийТитул";
	
КонецФункции

Функция НеобходимоЗаписатьФорму(Форма)
	
	Возврат Форма.Модифицированность
		Или (Форма.Параметры.Свойство("Ключ") И Не ЗначениеЗаполнено(Форма.Параметры.Ключ));
	
КонецФункции

Процедура ОткрытьОбщуюФормуСПараметрами(Форма, ИмяФормы, ПараметрыФормы, ИмяОповещения)
	
	ДопПараметры = Новый Структура;  	
	Оповещение   = Новый ОписаниеОповещения(ИмяОповещения, Форма, ДопПараметры);
	
	ОткрытьФорму(
		"ОбщаяФорма." + ИмяФормы,
		ПараметрыФормы,
	    Форма, , , , 
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#Область ОбработкаОповещений

// Проверка, что оповещение пришло для нужного источника
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма источник вызова
// 	ИмяСобытия - Строка - имя события оповещения 
//	Параметр - Структура - параметры оповещения 
//	Источник - Произвольный - источник оповещения 
//
// Возвращаемое значение:
// 	Булево - если Истина то оповещение предназначено для данной формы
//
Функция ОповещениеДляЭтойФормы(Форма, ИмяСобытия, Параметр, Источник)
	
	ЭтоНужнаяФорма = (Источник = Форма) Или (Источник = Форма.УникальныйИдентификатор);
	ЭтоФормаСписка = ЭтоФормаСписка(Форма);
	
	Если ИмяСобытия = "Saby_СписокДокументовОбновить" 
		Или ИмяСобытия = "Saby_АктуальныйСтатусВерсии" Тогда
		РезультатФункции = ЭтоФормаСписка И ЭтоНужнаяФорма;
	ИначеЕсли ИмяСобытия = "Saby_СписокДокументов" Тогда
		РезультатФункции = Не ЭтоФормаСписка;
	ИначеЕсли ИмяСобытия = "Saby_Ошибки" Тогда
		РезультатФункции = Не ЭтоФормаСписка И Параметр.Ключ = Форма.Объект.Ссылка;
	ИначеЕсли ИмяСобытия = "ОбновитьЛенту" Тогда
		РезультатФункции = Не ЭтоФормаСписка И Источник = Форма.Объект.Ссылка;
	Иначе
		РезультатФункции = ПроверкаСобытияФормы(ИмяСобытия, ЭтоНужнаяФорма, ЭтоФормаСписка);
	КонецЕсли;		
			
	Возврат РезультатФункции;
	
КонецФункции

Функция ПроверкаСобытияФормы(ИмяСобытия, ЭтоНужнаяФорма, ЭтоФормаСписка)
	
	Возврат (ИмяСобытия = "Saby_ВыполнитьПереход" И ЭтоНужнаяФорма)
		Или (ИмяСобытия = "Saby_ЗавершениеВыполнитьДействие" И ЭтоНужнаяФорма)
		Или СобытияФормыОбъекта(ИмяСобытия, ЭтоНужнаяФорма, ЭтоФормаСписка);
	
КонецФункции

Функция СобытияФормыОбъекта(ИмяСобытия, ЭтоНужнаяФорма, ЭтоФормаСписка)
	
	НужнаяФормаОбъекта = ЭтоНужнаяФорма И Не ЭтоФормаСписка;
	
	Возврат (ИмяСобытия = "Saby_ОбновитьДокумент" И НужнаяФормаОбъекта)
		Или (ИмяСобытия = "Saby_ИзмененыТипыДокументовОснований" И НужнаяФормаОбъекта)
		Или (ИмяСобытия = "Saby_ПрочитанДинамическийТитул" И НужнаяФормаОбъекта)
		Или (ИмяСобытия = "ВыбранДокументОснованиеINI" И НужнаяФормаОбъекта);
	
КонецФункции

Процедура ОбработатьОповещениеВыполнитьПереход(Форма, Параметр)
	
	Если ЗначениеЗаполнено(Параметр.Этап) Тогда 
		Если Saby_ТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(Форма, "Объект") Тогда
			Ссылка = Форма.Объект.Ссылка;
		ИначеЕсли Параметр.Свойство("Ссылка") Тогда
			Ссылка = Параметр.Ссылка;
		Иначе
			Ссылка = Параметр.Ключ;
		КонецЕсли;
		СменаЭтапа(Ссылка, Форма, Параметр.Этап);
	Иначе 
		
		Форма.ОбработатьОповещениеВыполнитьПереход();
		
		КартинкаСообщения = БиблиотекаКартинок["Saby_Ошибка32"];
		
		ПоказатьОповещениеПользователя(
			"Ошибка",
			,
			"Нет доступных действий",
			КартинкаСообщения,
			СтатусОповещенияПользователя.Важное,
			Новый УникальныйИдентификатор);
							
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОповещениеЗавершениеВыполнитьДействие(Форма, Параметр)
	
	Если Не ЭтоФормаСписка(Форма) Тогда
		Форма.ОбработатьОповещениеЗавершениеВыполнитьДействие(Параметр);
		Возврат;
	КонецЕсли;
	
	Если Форма.Элементы.Список.ВыделенныеСтроки.Количество() Тогда 
		ТекущаяСсылка = Форма.Элементы.Список.ВыделенныеСтроки[0];
	Иначе 
		ТекущаяСсылка = Неопределено;
	КонецЕсли;
	
	Если Параметр.status = "error" Тогда
		
		ОбработатьОшибкиИзФормыВыполнитьДействие(Форма, ТекущаяСсылка, Параметр);
		Форма.Элементы.Список.Обновить();
		
	Иначе
		
		ОбменДаннымиСБИСИзФормыСписка(Форма, "ОбновитьАктивныйЭтап");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработкаОповещений

Функция ЭтоФормаСписка(Форма)
	
	Возврат Форма.Элементы.Найти("Список") <> Неопределено;
	
КонецФункции

#Область ДанныеЮрЛиц

Процедура ОбновитьДанныеЮрЛиц(Форма, СтруктураДанныхЮрЛиц, ПараметрыРеквизитаФормы)
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	СтрокаЮрЛиц = СтрокаЮрЛиц(ПараметрыРеквизитаФормы, ФормаОбъект, СтруктураДанныхЮрЛиц.Роль);
	ЗаполнитьЗначенияСвойств(СтрокаЮрЛиц, СтруктураДанныхЮрЛиц);
	
	КлючСтроки = СтрокаЮрЛиц.КлючСтроки;
	Если СтруктураДанныхЮрЛиц.Удалить Тогда
		ФормаОбъект.ДанныеЮрЛиц.Удалить(СтрокаЮрЛиц);
		КлючСтроки = 0;
	КонецЕсли;
	
	Если ПараметрыРеквизитаФормы.ЭтоСтрокаТаблицы Тогда
		ИсходнаяСтрока = ФормаОбъект[ПараметрыРеквизитаФормы.ИмяИсходнойТаблицы].НайтиПоИдентификатору(
			ПараметрыРеквизитаФормы.ИдентификаторИсходнойСтроки);
		Если ИсходнаяСтрока <> Неопределено Тогда
			ИсходнаяСтрока[ПараметрыРеквизитаФормы.ИмяРеквизита] = СтрокаЮрЛиц.НаименованиеОрганизации;
			
			ЕстьКлюч = Saby_ТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(ИсходнаяСтрока, "КлючСтроки_ДанныеЮрЛиц");
				
			Если ЕстьКлюч Тогда
				ИсходнаяСтрока.КлючСтроки_ДанныеЮрЛиц = КлючСтроки;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Форма[ПараметрыРеквизитаФормы.ИмяРеквизита] = Saby_ТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли(
			СтрокаЮрЛиц.НаименованиеОрганизации);
	КонецЕсли;
	
	Saby_ТНОбщегоНазначенияКлиентСервер.ЗаполнитьДополнительныеПоляЮрЛица(
		Форма, СтрокаЮрЛиц, ПараметрыРеквизитаФормы.ДополнительныеПоля, СтруктураДанныхЮрЛиц.РольСтрокой);
	
	ОбновитьКонтактныеДанные(Форма, СтруктураДанныхЮрЛиц, СтрокаЮрЛиц, ПараметрыРеквизитаФормы);
	
КонецПроцедуры

Функция СтрокаЮрЛиц(ПараметрыРеквизитаФормы, ФормаОбъект, Роль)
	
	Если ПараметрыРеквизитаФормы.ЭтоСтрокаТаблицы Тогда
		Если ПараметрыРеквизитаФормы.ИдентификаторСтроки = Неопределено Тогда
			РезультатФункции = ФормаОбъект.ДанныеЮрЛиц.Добавить();
			РезультатФункции.КлючСтроки = Saby_ТНОбщегоНазначенияКлиентСервер.НовыйКлючОсновнойСтроки(
				ФормаОбъект, "ДанныеЮрЛиц");
		Иначе
			РезультатФункции = ФормаОбъект.ДанныеЮрЛиц.НайтиПоИдентификатору(ПараметрыРеквизитаФормы.ИдентификаторСтроки);
		КонецЕсли;
	Иначе
		Отбор = Новый Структура;
		Отбор.Вставить("Роль", Роль);
		НайденныеСтроки = ФормаОбъект.ДанныеЮрЛиц.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() Тогда
			РезультатФункции = НайденныеСтроки[0];
		Иначе
			РезультатФункции = ФормаОбъект.ДанныеЮрЛиц.Добавить();
			РезультатФункции.КлючСтроки = Saby_ТНОбщегоНазначенияКлиентСервер.НовыйКлючОсновнойСтроки(
				ФормаОбъект, "ДанныеЮрЛиц");
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ОбновитьКонтактныеДанные(Форма, СтруктураДанныхЮрЛиц, СтрокаЮрЛиц, ПараметрыРеквизитаФормы)
	
	ФормаОбъект = Saby_ТНОбщегоНазначенияКлиентСервер.ФормаОбъект(Форма);
	
	Если Не Saby_ТНОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойство(ФормаОбъект, "КонтактныеДанные") Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Роль",                   СтруктураДанныхЮрЛиц.Роль);
	Отбор.Вставить("КлючСтроки_ДанныеЮрЛиц", СтрокаЮрЛиц.КлючСтроки);
	
	НайденныеСтроки = ФормаОбъект.КонтактныеДанные.НайтиСтроки(Отбор);
	Для Каждого СтрокаДляУдаления Из НайденныеСтроки Цикл
		ФормаОбъект.КонтактныеДанные.Удалить(СтрокаДляУдаления);
	КонецЦикла;
	
	Для Каждого СтруктураКонтактныхДанных Из СтруктураДанныхЮрЛиц.КонтактныеДанные Цикл
		СтрокаКонтактныхДанных = ФормаОбъект.КонтактныеДанные.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКонтактныхДанных, СтруктураКонтактныхДанных);
		СтрокаКонтактныхДанных.КлючСтроки_ДанныеЮрЛиц = СтрокаЮрЛиц.КлючСтроки;
	КонецЦикла;
	
	РолиДляОбновления = Новый Массив;
	СтруктураРоли = Saby_ТНОбщегоНазначенияКлиентСервер.СтруктураДанныхРолей();
	ЗаполнитьЗначенияСвойств(СтруктураРоли, СтрокаЮрЛиц);
	СтруктураРоли.РольСтрокой             = СтруктураДанныхЮрЛиц.РольСтрокой;
	СтруктураРоли.ПараметрыРеквизитаФормы = ПараметрыРеквизитаФормы;
	РолиДляОбновления.Добавить(СтруктураРоли);
	
	ДопПараметры = Saby_ТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(РолиДляОбновления);
	Saby_ТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(Форма, ДопПараметры);
	
	Saby_ТНОбщегоНазначенияКлиентСервер.НастроитьДополнительныеКонтакты(Форма, СтруктураДанныхЮрЛиц.РольСтрокой);
	
КонецПроцедуры

#КонецОбласти // ДанныеЮрЛиц

Функция ОбработкаШаблонаСтатусаВерсии(Шаблон)
	
	Структура = Новый Структура;
	Структура.Вставить("Заголовок", "");
	Структура.Вставить("ЦветТекста");
	Структура.Вставить("Гиперссылка", Истина);
	Структура.Вставить("Ссылка", "https://sbis.ru/help/integration/1C_set/etrn/install");
	
	ВерсияАктуальна     = (Шаблон = Неопределено Или Шаблон = 0);
	ДоступнаНоваяВерсия = (Шаблон = 1 Или Шаблон = - 1);
	ВерсияУстарела      = 2;
	ВерсияНеСовместима  = 3;
	ВерсияСОшибками     = 4;
	
	Если ВерсияАктуальна Тогда
		Возврат Неопределено;
		
	ИначеЕсли ДоступнаНоваяВерсия Тогда		
		Структура.Заголовок   = "Доступна новая версия";
	
	ИначеЕсли Шаблон = ВерсияУстарела Тогда
		Структура.Заголовок = "Версия устарела";
		
	ИначеЕсли Шаблон = ВерсияНеСовместима Тогда		
		Структура.Заголовок = "ВЕРСИЯ НЕ СОВМЕСТИМА";
		
	ИначеЕсли Шаблон = ВерсияСОшибками Тогда		
		Структура.Заголовок   = "ВЕРСИЯ СОДЕРЖИТ КРИТИЧЕСКИЕ ОШИБКИ";
		Структура.ЦветТекста  = WebЦвета.Красный;
		
	Иначе
		
		Структура.Заголовок   = "Ошибка при получении информации о версии";
		Структура.ЦветТекста  = WebЦвета.Красный;
		Структура.Гиперссылка = Ложь;
						
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

#КонецОбласти   
