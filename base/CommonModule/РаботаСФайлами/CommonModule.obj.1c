
&Вместо("ХранилищеФайлаИзИнформационнойБазы")
Функция Saby_ХранилищеФайлаИзИнформационнойБазы(ФайлСсылка)
		
	// Значение из расширения 
	Если Saby_ОбщегоНазначенияКлиентСервер.ПрисоединенныйФайлВРасширении(ФайлСсылка) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДвоичныеДанныеФайлов.Файл КАК Файл,
		|	ДвоичныеДанныеФайлов.ДвоичныеДанныеФайла КАК ДвоичныеДанныеФайла
		|ИЗ
		|	РегистрСведений.Saby_ДвоичныеДанныеФайлов КАК ДвоичныеДанныеФайлов
		|ГДЕ
		|	ДвоичныеДанныеФайлов.Файл = &ФайлСсылка";
		
		Запрос.УстановитьПараметр("ФайлСсылка", ФайлСсылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Возврат ?(Выборка.Следующий(), Выборка.ДвоичныеДанныеФайла, Неопределено);
		
	Иначе 			
		Результат = ПродолжитьВызов(ФайлСсылка);
		Возврат Результат;		
	КонецЕсли;
	
КонецФункции

&Вместо("ДобавитьФайл")
Функция Saby_ДобавитьФайл(ПараметрыФайла, 
	Знач АдресФайлаВоВременномХранилище, 
	Знач АдресВременногоХранилищаТекста, 
	Знач Описание, 
	Знач НоваяСсылкаНаФайл)
	
	Если ЗначениеЗаполнено(НоваяСсылкаНаФайл)
		И Saby_ОбщегоНазначенияКлиентСервер.ПрисоединенныйФайлВРасширении(НоваяСсылкаНаФайл) Тогда
		
		Результат = Saby_ДобавитьФайлДляРасширения(
			ПараметрыФайла,
			АдресФайлаВоВременномХранилище,
			АдресВременногоХранилищаТекста,
			Описание,
			НоваяСсылкаНаФайл);
		
	Иначе
		
		Результат = ПродолжитьВызов(
			ПараметрыФайла,
			АдресФайлаВоВременномХранилище,
			АдресВременногоХранилищаТекста,
			Описание,
			НоваяСсылкаНаФайл);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Создает объект в справочнике расширения для хранения файла и заполняет его реквизиты переданными свойствами.
//
// Параметры:
//   ПараметрыФайла                 - Структура - см. РаботаСФайлами.ПараметрыДобавленияФайла.
//   АдресФайлаВоВременномХранилище - Строка - адрес, указывающий на двоичные данные во временном хранилище.
//   АдресВременногоХранилищаТекста - Строка - адрес, указывающий на извлеченный текст из файла во временном хранилище.
//   Описание                       - Строка - текстовое описание файла.
//   НоваяСсылкаНаФайл              - Неопределено - если у владельца файла только один справочник хранения файлов.
//                                  - ОпределяемыйТип.ПрисоединенныйФайл - ссылка на справочник хранения файлов,
//                                    которую следует использовать для добавляемого файла.
//                                    Должна соответствовать одному из типов справочников хранения файлов владельца
//                                    файлов. Ссылка может быть получена функцией НоваяСсылкаНаФайл.
// 
// Возвращаемое значение:
//   ОпределяемыйТип.ПрисоединенныйФайл - ссылка на созданный присоединенный файл.
//
Функция Saby_ДобавитьФайлДляРасширения(
	ПараметрыФайла, 
	Знач АдресФайлаВоВременномХранилище, 
	Знач АдресВременногоХранилищаТекста, 
	Знач Описание, Знач НоваяСсылкаНаФайл)

	ВладелецФайлов     = ПараметрыФайла.ВладелецФайлов;
	ИмяБезРасширения   = ПараметрыФайла.ИмяБезРасширения;
	РасширениеБезТочки = ПараметрыФайла.РасширениеБезТочки;

	Если Не ЗначениеЗаполнено(ВладелецФайлов) Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не задано значение параметра %1 в %2.';
		|en = 'Parameter value %1 is not set in %2.'"), "ПараметрыФайла.ВладелецФайлов", "РаботаСФайлами.ДобавитьФайл");
	КонецЕсли;

	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаВоВременномХранилище); // ДвоичныеДанные
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("РаботаСФайлами.ДобавитьФайл",
	"АдресФайлаВоВременномХранилище", ДвоичныеДанные, Тип("ДвоичныеДанные"));

	ГруппаФайлов = Неопределено;
	Если ПараметрыФайла.Свойство("ГруппаФайлов")
		И ЗначениеЗаполнено(ПараметрыФайла.ГруппаФайлов)
		И Не РаботаСФайламиСлужебный.ЭтоПапкаФайлов(ВладелецФайлов) Тогда

		ГруппаФайлов = ПараметрыФайла.ГруппаФайлов;
	КонецЕсли;

	ИмяИРасширение = ИмяИРасширениеФайла(РасширениеБезТочки, ИмяБезРасширения);
	ИмяБезРасширения   = ИмяИРасширение.ИмяБезРасширения;
	РасширениеБезТочки = ИмяИРасширение.РасширениеБезТочки;
	
	ВремяИзмененияУниверсальное = ПараметрыФайла.ВремяИзмененияУниверсальное;
	Если Не ЗначениеЗаполнено(ВремяИзмененияУниверсальное)
		Или ВремяИзмененияУниверсальное > ТекущаяУниверсальнаяДата() Тогда
		ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
	КонецЕсли;
	
	ИмяСправочника = ИмяСправочника(НоваяСсылкаНаФайл, ВладелецФайлов);
	
	ХранитьВерсии      = СтрСравнить(ИмяСправочника, Метаданные.Справочники.Файлы.Имя) = 0;	
	ПрисоединенныйФайл = СсылкаНаФайл(НоваяСсылкаНаФайл, ИмяСправочника);
	
	ПрисоединенныйФайл.ВладелецФайла                = ВладелецФайлов;
	ПрисоединенныйФайл.ДатаМодификацииУниверсальная = ВремяИзмененияУниверсальное;
	ПрисоединенныйФайл.ДатаСоздания                 = ТекущаяДатаСеанса();
	ПрисоединенныйФайл.Описание                     = Описание;
	ПрисоединенныйФайл.Наименование                 = ИмяБезРасширения;
	ПрисоединенныйФайл.Размер                       = ДвоичныеДанные.Размер();
	ПрисоединенныйФайл.Расширение                   = РасширениеБезТочки;
	ПрисоединенныйФайл.ТипХраненияФайла             = ТипФайлаХранения(ПрисоединенныйФайл);
	ПрисоединенныйФайл.Изменил                      = ПараметрыФайла.Автор;
	ПрисоединенныйФайл.ХранитьВерсии                = ХранитьВерсии;

	Если ГруппаФайлов <> Неопределено Тогда
		ПрисоединенныйФайл.Родитель = ГруппаФайлов;
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(ПрисоединенныйФайл, ПараметрыФайла);

	ПрисоединенныйФайл.Том        = Справочники.ТомаХраненияФайлов.ПустаяСсылка();
	ПрисоединенныйФайл.ПутьКФайлу = "";
	ПрисоединенныйФайл.Заполнить(Неопределено);

	ИспользованиеПолнотекстовогоПоиска = Метаданные.СвойстваОбъектов.ИспользованиеПолнотекстовогоПоиска.Использовать;
	ИзвлекатьТекст = Метаданные.Справочники[ИмяСправочника].ПолнотекстовыйПоиск = ИспользованиеПолнотекстовогоПоиска;

	Если ИзвлекатьТекст Тогда
		РезультатИзвлеченияТекста = РаботаСФайламиСлужебный.ИзвлечьТекст(АдресВременногоХранилищаТекста,
			ДвоичныеДанные, ПрисоединенныйФайл.Расширение);

		ПрисоединенныйФайл.ТекстХранилище = РезультатИзвлеченияТекста.ТекстХранилище;
		ПрисоединенныйФайл.СтатусИзвлеченияТекста = РезультатИзвлеченияТекста.СтатусИзвлеченияТекста;
	Иначе
		ПрисоединенныйФайл.ТекстХранилище = Новый ХранилищеЗначения("");
		ПрисоединенныйФайл.СтатусИзвлеченияТекста = Перечисления.СтатусыИзвлеченияТекстаФайлов.НеИзвлечен;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ПрисоединенныйФайл.Изменил) Тогда
		ПрисоединенныйФайл.Изменил = Пользователи.АвторизованныйПользователь();
	КонецЕсли;

	Контекст = РаботаСФайламиСлужебный.КонтекстОбновленияФайла(
		ПрисоединенныйФайл, 
		АдресФайлаВоВременномХранилище, 
		НоваяСсылкаНаФайл);
				
	ЗаписатьДанныеФайла(
		ПрисоединенныйФайл, 
		Контекст, 
		ИмяБезРасширения, 
		ХранитьВерсии, 
		РасширениеБезТочки, 
		АдресВременногоХранилищаТекста);	
		
	РаботаСФайламиПереопределяемый.ПриСозданииФайла(ПрисоединенныйФайл.Ссылка);
	Возврат ПрисоединенныйФайл.Ссылка;

КонецФункции   

// Формирует имя справочника для файла
// Параметры:
//   НоваяСсылкаНаФайл - Неопределено - если у владельца файла только один справочник хранения файлов.
//                     - ОпределяемыйТип.ПрисоединенныйФайл - ссылка на элемент справочника хранения файлов,
//                       которую следует использовать для добавляемого файла.
//                       Должна соответствовать одному из типов справочников хранения файлов владельца
//                       файлов. Ссылка может быть получена функцией НоваяСсылкаНаФайл.
//
// 	 ВладелецФайлов    - ЛюбаяСсылка - владелец присоединенного файла
// Возвращаемое значение:
//   Строка - Имя справочника для создания присоединенного файла.
//
Функция ИмяСправочника(НоваяСсылкаНаФайл, ВладелецФайлов)
	
	ЗаголовокОшибки = НСтр("ru = 'Ошибка при добавлении присоединенного файла.';
		|en = 'Error adding attached file.'");
	
	Если НоваяСсылкаНаФайл = Неопределено Тогда
		
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В этом случае параметр ""%1"" должен быть указан.';
			|en = 'In this case, parameter ""%1"" is required.'"), "НоваяСсылкаНаФайл");
		
		ИмяСправочника = РаботаСФайламиСлужебный.ИмяСправочникаХраненияФайлов(ВладелецФайлов, "", ЗаголовокОшибки, Описание);
		
		НоваяСсылкаНаФайл = Справочники[ИмяСправочника].ПолучитьСсылку();
		
	Иначе
		
		Если Не Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(НоваяСсылкаНаФайл))
			И Не Saby_ОбщегоНазначенияКлиентСервер.ПрисоединенныйФайлВРасширении(НоваяСсылкаНаФайл)
			Или Не ЗначениеЗаполнено(НоваяСсылкаНаФайл) Тогда
			
			ВызватьИсключение НСтр("ru = 'Ошибка при добавлении присоединенного файла.
			|Ссылка на новый файл не заполнена.';
			|en = 'Error adding attached file.
			|A reference to the new file is required.'");
		КонецЕсли;
		
		ИмяСправочника = РаботаСФайламиСлужебный.ИмяСправочникаХраненияФайлов(
			ВладелецФайлов, НоваяСсылкаНаФайл.Метаданные().Имя, ЗаголовокОшибки);
		
	КонецЕсли;
	
	Возврат ИмяСправочника;
	
КонецФункции

// Записывает новый присоединенный файл в БД
//
// Параметры:
//   ПрисоединенныйФайл             - Неопределено - если у владельца файла только один справочник хранения файлов.
//                                  - ОпределяемыйТип.ПрисоединенныйФайл - ссылка на справочник хранения файлов,
//                                    которую следует использовать для добавляемого файла.
//                                    Должна соответствовать одному из типов справочников хранения файлов владельца
//                                    файлов. Ссылка может быть получена функцией НоваяСсылкаНаФайл.
// 
//   Контекст                       - Структура - КонтекстОбновленияФайла
//   ИмяБезРасширения               - Строка - имя файла, без расширения
//   ХранитьВерсии                  - Булево - признак хранения версий 
//   РасширениеБезТочки             - Строка - расширение файла
//   АдресВременногоХранилищаТекста - Строка - адрес, указывающий на извлеченный текст из файла во временном хранилище.
//
//
Процедура ЗаписатьДанныеФайла(ПрисоединенныйФайл, Контекст, 
	ИмяБезРасширения, ХранитьВерсии, РасширениеБезТочки, АдресВременногоХранилищаТекста)
		
	МенеджерФайла = РаботаСФайламиСлужебный.МенеджерФайлов(ПрисоединенныйФайл);
	МенеджерФайла.ПередОбновлениемДанныхФайла(Контекст);

	НачатьТранзакцию();
	Попытка

		МенеджерФайла.ПередЗаписьюДанныхФайла(Контекст, ПрисоединенныйФайл);
		ПрисоединенныйФайл.Записать();

		Если ХранитьВерсии Тогда

			Если ПрисоединенныйФайл.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
				СвойстваФайла = РаботаСФайламиВТомахСлужебный.СвойстваФайлаВТоме(ПрисоединенныйФайл.Ссылка);
				АдресФайлаВоВременномХранилище = РаботаСФайламиВТомахСлужебный.ПолноеИмяФайлаВТоме(СвойстваФайла);
				ИсходныйФайл = Новый Файл(АдресФайлаВоВременномХранилище);
				СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией", ИсходныйФайл);
			Иначе
				СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
				СведенияОФайле.РасширениеБезТочки = РасширениеБезТочки;
				СведенияОФайле.ИмяБезРасширения   = ИмяБезРасширения;
				СведенияОФайле.Размер             = ПрисоединенныйФайл.Размер;
			КонецЕсли;

			СведенияОФайле.АдресВременногоХранилищаФайла   = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			Контекст, 
			"ПутьКФайлу",
			АдресФайлаВоВременномХранилище);
			СведенияОФайле.АдресВременногоХранилищаТекста  = АдресВременногоХранилищаТекста;
			СведенияОФайле.ЗаписатьВИсторию                = Истина;

			Версия = РаботаСФайламиСлужебный.СоздатьВерсию(ПрисоединенныйФайл.Ссылка, СведенияОФайле);
			РаботаСФайламиСлужебный.ОбновитьВерсиюВФайле(ПрисоединенныйФайл.Ссылка, Версия, АдресВременногоХранилищаТекста);
		Иначе
			МенеджерФайла.ПриОбновленииДанныхФайла(Контекст, ПрисоединенныйФайл.Ссылка);
		КонецЕсли;

		ЗафиксироватьТранзакцию();

	Исключение

		ОтменитьТранзакцию();

		ИнформацияОбОшибке = ИнформацияОбОшибке();

		ШаблонСообщения = НСтр("ru = 'Ошибка при добавлении присоединенного файла ""%1"":
		|%2';
		|en = 'Error adding attached file ""%1"":
		|%2'");
		КомментарийЖурналаРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонСообщения,
		ИмяБезРасширения + "." + РасширениеБезТочки,
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));

		ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Файлы.Добавление присоединенного файла';
		|en = 'Files.Add attached file'",
		ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		КомментарийЖурналаРегистрации);

		МенеджерФайла.ПослеОбновленияДанныхФайла(Контекст, Ложь);

		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонСообщения,
		ИмяБезРасширения + "." + РасширениеБезТочки,
		КраткоеПредставлениеОшибки(ИнформацияОбОшибке));

	КонецПопытки;

	МенеджерФайла.ПослеОбновленияДанныхФайла(Контекст, Истина);
	
КонецПроцедуры

// Создание или получение существующей ссылки на присоединенный файл
// 	Параметры:
//   НоваяСсылкаНаФайл - Неопределено - если у владельца файла только один справочник хранения файлов.
//                     - ОпределяемыйТип.ПрисоединенныйФайл - ссылка на элемент справочника хранения файлов,
//                       которую следует использовать для добавляемого файла.
//                       Должна соответствовать одному из типов справочников хранения файлов владельца
//                       файлов. Ссылка может быть получена функцией НоваяСсылкаНаФайл.
//
//	ИмяСправочника     - Строка - имя справочника присоединного файла
//
// Возвращаемое значение:
//  Ссылка - ссылка на элемент справочника хранения файлов
//
Функция СсылкаНаФайл(НоваяСсылкаНаФайл, ИмяСправочника)
	
	ПрисоединенныйФайл = Справочники[ИмяСправочника].СоздатьЭлемент(); // ОпределяемыйТип.ПрисоединенныйФайлОбъект

	Если ОбщегоНазначения.СсылкаСуществует(НоваяСсылкаНаФайл)
		И Saby_ОбщегоНазначенияКлиентСервер.ПрисоединенныйФайлВРасширении(НоваяСсылкаНаФайл) Тогда
		ПрисоединенныйФайл = НоваяСсылкаНаФайл.ПолучитьОбъект();
	Иначе
		ПрисоединенныйФайл.УстановитьСсылкуНового(НоваяСсылкаНаФайл);
	КонецЕсли;
	
	Возврат ПрисоединенныйФайл;
	
КонецФункции

// Разделение полного имени на имя файла и расширения файла
// Параметры:
//	РасширениеБезТочки - Строка - расширение файла(не форматированное)
//  ИмяБезРасширения   - Строка - имя файла без расширение (не форматированное)
//                                                        
// Возвращаемое значение:
// 	Структура - имя и расширения файла после форматирования 
//
Функция ИмяИРасширениеФайла(РасширениеБезТочки, ИмяБезРасширения)
	
	Если Не ЗначениеЗаполнено(РасширениеБезТочки) Тогда
		ЧастиИмениФайла = СтрРазделить(ИмяБезРасширения, ".", Ложь);
		Если ЧастиИмениФайла.Количество() > 1 Тогда
			РасширениеБезТочки = ЧастиИмениФайла[ЧастиИмениФайла.Количество() - 1];
			ИмяБезРасширения = Лев(ИмяБезРасширения, СтрДлина(ИмяБезРасширения) - (СтрДлина(РасширениеБезТочки) + 1));
		КонецЕсли;
	КонецЕсли;
	
	ИмяБезРасширения   = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяБезРасширения);
	РасширениеБезТочки = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(РасширениеБезТочки); 
	
	Структура = Новый Структура;
	Структура.Вставить("ИмяБезРасширения",   ИмяБезРасширения);
	Структура.Вставить("РасширениеБезТочки", РасширениеБезТочки); 
	
	Возврат Структура;
	
КонецФункции 

// Получение типа файла для новых и ранних версий
// Параметры:
//	ПрисоединенныйФайл - Неопределено - если у владельца файла только один справочник хранения файлов.
//                     - ОпределяемыйТип.ПрисоединенныйФайл - ссылка на элемент справочника хранения файлов,
//                       которую следует использовать для добавляемого файла.
//                       Должна соответствовать одному из типов справочников хранения файлов владельца
//                       файлов. Ссылка может быть получена функцией НоваяСсылкаНаФайл.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ТипыХраненияФайлов
//
Функция ТипФайлаХранения(ПрисоединенныйФайл)
	
	Попытка
		ТипХраненияФайла = РаботаСФайламиСлужебный.ТипХраненияФайла(ПрисоединенныйФайл.Размер,
			ПрисоединенныйФайл.Расширение);
	Исключение
		// Обратная совместимость с более ранними версиями
		ТипХраненияФайла = РаботаСФайламиСлужебный.ТипХраненияФайлов(ПрисоединенныйФайл.Размер,
			ПрисоединенныйФайл.Расширение);
	КонецПопытки;
	
	Возврат ТипХраненияФайла;
	
КонецФункции
