
#Область include_etrn_src_vo3_Document_ТранспортнаяНакладная_DocumentForm_ФормаДокумента_Переменные
#КонецОбласти // include_etrn_src_vo3_Document_ТранспортнаяНакладная_DocumentForm_ФормаДокумента_Переменные

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСтруктуруДокумента(Параметры);
	
	ИсточникДанных = ИсточникДанных();
	
	Если Не ЗначениеЗаполнено(ИсточникДанных.Ссылка) Или ТипЗнч(ИсточникДанных.Ссылка) = Тип("Структура") Тогда
		ПриЧтенииСозданииПослеЗаписиНаСервере();
	Иначе
		МодульКода("Saby_ТНОбщегоНазначенияСервер").УбратьПризнакНеПросмотрен(ИсточникДанных.Ссылка);
	КонецЕсли;
	
	ОграничитьТипыДокументовОснованийНаСервере();
	
	// Решение проблемы с тем что значения типов КИ
	// имеют разные идентификаторы в различных конфигурациях
	УстановкаСпискаДоступныхТиповКИНаФорме();
	
	НастроитьУсловноеОформление();
	
	НастроитьФормуДляСтарыхПлатформ();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	
	// установим отборы по таблицам для корректного отображения информации
	УстановитьОтборыПоТаблицам();
	ИзменитьТекстПодсказкиГрузоотправитель_ОсуществляетОтгрузку();
	
	СсылкаНаДокумент = ИсточникДанныхКлиент().Ссылка;
	Если ТипЗнч(СсылкаНаДокумент) <> Тип("Структура") Тогда
		ОповеститьОбИзменении(СсылкаНаДокумент);
		Если СсылкаНаДокумент.Пустая() Тогда
			ЗаполнитьДатыПогрузки(ИсточникДанныхКлиент());
		КонецЕсли;
	КонецЕсли;
	
	ОтобразитьВодителейДляВебКлиента();
	
	Если ЭтаФорма.ДвоичныеДанныеОбработки <> Неопределено И ТипЗнч(ЭтаФорма.ДвоичныеДанныеОбработки) = Тип("Строка") Тогда
		
		ЭтаФорма.ДвоичныеДанныеОбработки = "";
		ЭтаФорма.Ссылка.ДвоичныеДанныеОбработки = ЭтаФорма.ДвоичныеДанныеОбработки;
		
	КонецЕсли;
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		Если ЗначениеЗаполнено(ЭтаФорма.Идентификатор) Тогда
			ОбменДаннымиСБИС("ПрочитатьДокумент");
		КонецЕсли;
		
		ЭтаФорма.ТранспортИнтеграции = ЭтаФорма.ВладелецФормы.ТранспортИнтеграции;
		ЭтаФорма.BlocklyExecutor     = ЭтаФорма.ВладелецФормы.BlocklyExecutor;
		
		Если ЗначениеЗаполнено(ЭтаФорма.ДокументОснование) Тогда
			ДопПараметры = Новый Структура;
			ДопПараметры.Вставить("Форма", ЭтаФорма);
			МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПослеВыбораЗначенияДокументаОснования(
				ЭтаФорма.ДокументОснование, ДопПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Обработчики для ВО	
	Если ЭтаФорма.ЭтоВО И ЭтаФорма.Открыта() Тогда 
		
		// закрыть форму
		Если ИмяСобытия = "ЗакрытьСБИС"
			Или ИмяСобытия = "ПереоткрытьФормуЭПД" Тогда							
			ЭтаФорма.Закрыть();
		КонецЕсли;	
				
	КонецЕсли;
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ОбработатьОповещенияДляФормы(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииПослеЗаписиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ЗаполнитьЮрЛицоКопированием("ВладелецОбъекта", "Отправитель");
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Составитель_НаОснованииДокумент = ЭтаФорма.Оформитель_Основание;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПриЧтенииСозданииПослеЗаписиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОтборОтметокПоТипу();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбменСбис(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяКоманды", Команда.Имя);
	
	ПроцедураОбменДаннымиСБИС = Новый ОписаниеОповещения("ОбменДаннымиСБИС", ЭтаФорма, ДополнительныеПараметры);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПроверитьМодифицированностьИПродолжитьВыполнение(ПроцедураОбменДаннымиСБИС);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтоимость(Команда)
	
	ОткрытьФормуТитула("ИзменениеСтоимости");
	
КонецПроцедуры

&НаКлиенте
Процедура СогласоватьСтоимость(Команда)
	
	ОткрытьФормуТитула("СогласованиеСтоимости");
	
КонецПроцедуры

&НаКлиенте
Процедура Переадресовка(Команда)
	
	ОткрытьФормуТитула("Переадресовка");
	
КонецПроцедуры

&НаКлиенте
Процедура ПереадресовкаУведомление(Команда)
	
	ОткрытьФормуТитула("ПереадресовкаУведомление");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменаВодителя(Команда)
	
	ОткрытьФормуТитула("ЗаменаВодителяТС");
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ФормаОбъект = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ФормаОбъект(ЭтаФорма);
	
	Если ЭтаФорма.ЭтоВО Тогда
		НомерЭТрН = ФормаОбъект.НомерСбис;
	Иначе
		НомерЭТрН = ФормаОбъект.Номер;
	КонецЕсли;
	
	ЗаголовокПечатнойФормы = "Транспортная накладная №%1 от %2";
	ЗаголовокПечатнойФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").СтрШаблонЭПД(
		ЗаголовокПечатнойФормы, НомерЭТрН, Формат(ФормаОбъект.Дата, "ДФ=dd.MM.yyyy"));
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПечатьДокумента(ЭтаФорма, Команда.Имя, ЗаголовокПечатнойФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСкрытьОшибки(Команда)
	
	Элементы.ФормаПоказатьСкрытьОшибки.Пометка = Не Элементы.ФормаПоказатьСкрытьОшибки.Пометка;
	
	ОбновитьВнешнийВидЭлементовОшибокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоОшибкам(Команда)
	
	ПроцедураОбновитьДанныеПоОшибкамНаКлиенте = Новый ОписаниеОповещения(
		"ОбновитьДанныеПоОшибкамНаКлиенте",
		ЭтаФорма);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПроверитьМодифицированностьИПродолжитьВыполнение(
		ПроцедураОбновитьДанныеПоОшибкамНаКлиенте, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыгрузкуПоДаннымГрузоотправителя(Команда)
	
	ЭтаФорма.Модифицированность = Истина;
	
	ИсточникДанных = ИсточникДанныхКлиент();
	
	Выгрузка_МассаБрутто = МодульКодаКлиент("Saby_ТНОбщегоНазначенияСервер").ЧислоИзСтроки(ИсточникДанных.Погрузка_Масса);
	
	ИсточникДанных.Выгрузка_ДатаВремя         = ИсточникДанных.Отправитель_ДоставитьДо;
	ИсточникДанных.Выгрузка_ДатаВремяПрибыл   = ИсточникДанных.Отправитель_ДоставитьДо;
	ИсточникДанных.Выгрузка_ДатаВремяУбыл     = ИсточникДанных.Отправитель_ДоставитьДо;

	ИсточникДанных.Выгрузка_КоличествоМест    = ИсточникДанных.Погрузка_КоличествоМест;
	ИсточникДанных.Выгрузка_МассаМетодРасчета = ИсточникДанных.Погрузка_МассаМетодРасчета;
	ИсточникДанных.Выгрузка_Состояние         = ИсточникДанных.Погрузка_Состояние;
	ИсточникДанных.Выгрузка_МассаБрутто       = Выгрузка_МассаБрутто;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьСтруктуруДокумента();
	ПриЧтенииСозданииПослеЗаписиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИныеПолучателиДокумента(Команда)
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ОткрытьИныхПолучателей(ЭтаФорма, Элементы.Страница_Основное.ТолькоПросмотр);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы 

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область ДокументОснование

&НаКлиенте
Процедура ДекорацияДокументОснование(Элемент)
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ДокументОснованиеНажатие(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДокументОснование(Команда)
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ОчиститьДокументОснованиеНаФорме(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаДокументовОснований(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ссылка", ИсточникДанныхКлиент().Ссылка);
	
	ПараметрыОткрытияФормыОбработки = ПараметрыОткрытияФормыОбработки("ОбщаяФорма.Saby_ВыборТиповЭПД", ПараметрыФормы, ЭтаФорма);
	ОткрытьФормуОбработки(ПараметрыОткрытияФормыОбработки);
	
КонецПроцедуры

#КонецОбласти // ДокументОснование

#Область ЮрЛица

&НаКлиенте
Процедура ОтправительСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПроцедураПослеИзмененияОтправителя = Новый ОписаниеОповещения("ПослеИзмененияОтправителя", ЭтаФорма);
	
	ПараметрыРеквизитаФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыРеквизитаФормы(
		Элемент.Имя, Элемент.Имя, , ЭтаФорма.ЭтоВО);
		
	ПараметрыРеквизитаФормы.ТолькоПросмотр = Элементы.Страница_Основное.ТолькоПросмотр;
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЮрЛицоНажатие(
		ЭтаФорма,
		"Отправитель",
		ПараметрыРеквизитаФормы,
		ПроцедураПослеИзмененияОтправителя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияОтправителя(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЭтаФорма.Грузоотправитель_ОсуществляетОтгрузку Тогда
		ЗаполнитьЮрЛицоКопированием("Отгрузчик", "Отправитель");
	КонецЕсли;
	
	ОбновитьАдресаПогрузкиВыгрузки("Отправитель");
	
	ЗаполнитьИныхПолучателей();
	
КонецПроцедуры

&НаКлиенте
Процедура Отправитель_ТелефонПриИзменении(Элемент)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Отправитель");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТелефонПриИзменении(ЭтаФорма, Отправитель_Телефон, РолиЮрЛиц, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Отправитель_ТелефонНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Отправитель");
	
	ПараметрыВыбораТелефона = Новый Структура;
	ПараметрыВыбораТелефона.Вставить("Роль",     РолиЮрЛиц);
	ПараметрыВыбораТелефона.Вставить("Значение", Отправитель_Телефон);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТелефонНачалоВыбора(
		ЭтаФорма, ПараметрыВыбораТелефона, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставительСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПроцедураПослеИзмененияСоставителя = Новый ОписаниеОповещения("ПослеИзмененияСоставителя", ЭтаФорма);
	
	ПараметрыРеквизитаФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыРеквизитаФормы(
		Элемент.Имя, Элемент.Имя, , ЭтаФорма.ЭтоВО);
		
	ПараметрыРеквизитаФормы.ДополнительныеПоля = "Основание";
	ПараметрыРеквизитаФормы.ТолькоПросмотр = Элементы.Страница_Основное.ТолькоПросмотр;
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЮрЛицоНажатие(
		ЭтаФорма, "Оформитель", ПараметрыРеквизитаФормы, ПроцедураПослеИзмененияСоставителя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияСоставителя(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьЮрЛицоКопированием("Отгрузчик", "Оформитель");
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПроцедураПослеИзмененияПолучателя = Новый ОписаниеОповещения("ПослеИзмененияПолучателя", ЭтаФорма);
	
	ПараметрыРеквизитаФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыРеквизитаФормы(
		Элемент.Имя, Элемент.Имя, , ЭтаФорма.ЭтоВО);
	
	ПараметрыРеквизитаФормы.ТолькоПросмотр = Элементы.Страница_Основное.ТолькоПросмотр
		Или Элементы.Группа_Грузополучатель.ТолькоПросмотр;
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЮрЛицоНажатие(
		ЭтаФорма, 
		"Получатель", 
		ПараметрыРеквизитаФормы, 
		ПроцедураПослеИзмененияПолучателя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияПолучателя(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьАдресаПогрузкиВыгрузки("Получатель");
	
	ЗаполнитьИныхПолучателей();
	
КонецПроцедуры

&НаКлиенте
Процедура Получатель_ТелефонПриИзменении(Элемент)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Получатель");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТелефонПриИзменении(ЭтаФорма, Получатель_Телефон, РолиЮрЛиц, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Получатель_ТелефонНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Получатель");
	
	ПараметрыВыбораТелефона = Новый Структура;
	ПараметрыВыбораТелефона.Вставить("Роль",     РолиЮрЛиц);
	ПараметрыВыбораТелефона.Вставить("Значение", Получатель_Телефон);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТелефонНачалоВыбора(
		ЭтаФорма, ПараметрыВыбораТелефона, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыРеквизитаФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыРеквизитаФормы(
		Элемент.Имя, Элемент.Имя, , ЭтаФорма.ЭтоВО);
		
	ПараметрыРеквизитаФормы.ТолькоПросмотр = Элементы.Страница_Основное.ТолькоПросмотр;
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЮрЛицоНажатие(ЭтаФорма, "Перевозчик", ПараметрыРеквизитаФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура Перевозчик_ТелефонПриИзменении(Элемент)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Перевозчик");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТелефонПриИзменении(ЭтаФорма, Перевозчик_Телефон, РолиЮрЛиц, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Перевозчик_ТелефонНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Перевозчик");
	
	ПараметрыВыбораТелефона = Новый Структура;
	ПараметрыВыбораТелефона.Вставить("Роль",     РолиЮрЛиц);
	ПараметрыВыбораТелефона.Вставить("Значение", Перевозчик_Телефон);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТелефонНачалоВыбора(
		ЭтаФорма, ПараметрыВыбораТелефона, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыРеквизитаФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыРеквизитаФормы(
		Элемент.Имя, Элемент.Имя, , ЭтаФорма.ЭтоВО);
	
	ПараметрыРеквизитаФормы.ТолькоПросмотр = Элементы.Страница_Основное.ТолькоПросмотр;
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЮрЛицоНажатие(ЭтаФорма, "Заказчик", ПараметрыРеквизитаФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецОбъектаСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыРеквизитаФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыРеквизитаФормы(
		Элемент.Имя, Элемент.Имя, , ЭтаФорма.ЭтоВО);
		
	ПараметрыРеквизитаФормы.ТолькоПросмотр = Элементы.Страница_Основное.ТолькоПросмотр;
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЮрЛицоНажатие(ЭтаФорма, "ВладелецОбъекта", ПараметрыРеквизитаФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура Владелец_ТелефонПриИзменении(Элемент)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "ВладелецОбъекта");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТелефонПриИзменении(ЭтаФорма, ВладелецОбъекта_Телефон, РолиЮрЛиц, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Владелец_ТелефонНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "ВладелецОбъекта");
	
	ПараметрыВыбораТелефона = Новый Структура;
	ПараметрыВыбораТелефона.Вставить("Роль",     РолиЮрЛиц);
	ПараметрыВыбораТелефона.Вставить("Значение", ВладелецОбъекта_Телефон);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТелефонНачалоВыбора(
		ЭтаФорма, ПараметрыВыбораТелефона, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтгрузчикСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыРеквизитаФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыРеквизитаФормы(
		Элемент.Имя, Элемент.Имя, , ЭтаФорма.ЭтоВО);
		
	ПараметрыРеквизитаФормы.ТолькоПросмотр = Элементы.ГруппаПогрузкаШапка.ТолькоПросмотр
		Или Не Элементы.Группа_ТелОтгрузка.Доступность;
		
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЮрЛицоНажатие(ЭтаФорма, "Отгрузчик", ПараметрыРеквизитаФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура Отгрузка_ТелефонПриИзменении(Элемент)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Отгрузчик");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТелефонПриИзменении(ЭтаФорма, Отгрузчик_Телефон, РолиЮрЛиц, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Отгрузка_ТелефонНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Отгрузчик");
	
	ПараметрыВыбораТелефона = Новый Структура;
	ПараметрыВыбораТелефона.Вставить("Роль",     РолиЮрЛиц);
	ПараметрыВыбораТелефона.Вставить("Значение", Отгрузчик_Телефон);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТелефонНачалоВыбора(
		ЭтаФорма, ПараметрыВыбораТелефона, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Грузоотправитель_ГрузополучательПриИзменении(Элемент)
	
	ЭтаФорма.Модифицированность = Истина;
	
	ИмяРолиПолучатель = "Получатель";

	Если Грузоотправитель_Грузополучатель Тогда
		ЗаполнитьЮрЛицоКопированием(ИмяРолиПолучатель, "Отправитель");
	Иначе
		ОчиститьЮрЛицо(ИмяРолиПолучатель);
	КонецЕсли;
	
	ОбновитьАдресаПогрузкиВыгрузки(ИмяРолиПолучатель);
	
	ОбновитьВидимостьИДоступностьЭлементов();
	
	ЗаполнитьИныхПолучателей();
	
КонецПроцедуры

&НаКлиенте
Процедура Грузоотправитель_ОсуществляетОтгрузкуПриИзменении(Элемент)
	
	ЭтаФорма.Модифицированность = Истина;
	
	Если Не Грузоотправитель_НеЯвляетсяОтправителем Тогда
		
		Если Грузоотправитель_ОсуществляетОтгрузку Тогда
			ЗаполнитьЮрЛицоКопированием("Отгрузчик", "Отправитель");
		Иначе
			ОчиститьЮрЛицо("Отгрузчик");
		КонецЕсли;
				
	КонецЕсли;
	
	ИзменитьТекстПодсказкиГрузоотправитель_ОсуществляетОтгрузку();
	
	ОбновитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура Грузоотправитель_НеЯвляетсяОтправителемПриИзменении(Элемент)
	
	Если ОтправительСтрокой = "Заполнить" Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Переключение недоступно. Необходимо заполнить поле Отправитель.";
		Сообщение.Сообщить();
		Грузоотправитель_НеЯвляетсяОтправителем = Не Грузоотправитель_НеЯвляетсяОтправителем;
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.Модифицированность = Истина;
	
	Если Не Грузоотправитель_НеЯвляетсяОтправителем Тогда
		
		ОчиститьЮрЛицо("Оформитель");
		ЭтаФорма.Оформитель_Основание = "";
		
	Иначе
		
		ЭтаФорма.Грузоотправитель_ОсуществляетОтгрузку = Ложь;
		
	КонецЕсли;
	
	ПослеИзмененияСоставителя(Неопределено, Неопределено);
	
	ОбновитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура Владелец_ТипПриИзменении(Элемент)
	
	ИсточникДанных = ИсточникДанныхКлиент();
	
	Если СокрЛП(ИсточникДанных.Владелец_Тип) <> "Неизвестен" Тогда
		Если ЗначениеЗаполнено(ИсточникДанных.Владелец_НеизвестенПричина) Тогда
			ИсточникДанных.Владелец_НеизвестенПричина = "";
		КонецЕсли;
	КонецЕсли;
	
	Если СокрЛП(ИсточникДанных.Владелец_Тип) = "Отправитель" Тогда
		ЗаполнитьЮрЛицоКопированием("ВладелецОбъекта", "Отправитель");
	Иначе
		ОчиститьЮрЛицо("ВладелецОбъекта");
	КонецЕсли;
	
	ОбновитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура Грузоотправитель_ЭкспедиторПриИзменении(Элемент)
	
	ИсточникДанных = ИсточникДанныхКлиент();
	
	Если НЕ ИсточникДанных.Отправитель_Экспедитор Тогда
		ОчиститьЮрЛицо("Заказчик");
		ИсточникДанных.Заказчик_Договор = "";
	КонецЕсли;
	
	ОбновитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

#КонецОбласти // ЮрЛица

#Область РаботаСКонтактнойИнформацией

&НаКлиенте
Процедура ОтборИнформацииПоКонтактнымДанным(ИмяТЧ, Значение)
	
	СтруктураЮрЛица = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ДанныеЮрЛицаПоРоли(
		ИсточникДанныхКлиент().ДанныеЮрЛиц, Значение);
	
	СтруктураПоиска = Новый Структура;
	
	Если СтруктураЮрЛица.Заполнена Тогда
		
		Роль = СтруктураЮрЛица.Роль;
		СтруктураПоиска.Вставить("КлючСтроки_ДанныеЮрЛиц", СтруктураЮрЛица.КлючСтроки);
		
	Иначе
		
		Роль = ЗначениеМетаданныхКлиент("Saby_РолиКонтрагентов." + Значение);
		
	КонецЕсли;
	
	СтруктураПоиска.Вставить("Роль", Роль);
	
	Элементы[ИмяТЧ].ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураПоиска);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНовуюСтрокуКонтактнойИнформации(ИмяТЧ, Роль)
	
	Роль = ЗначениеМетаданныхКлиент("Saby_РолиКонтрагентов.ПустаяСсылка");
	
	Отбор = Новый Структура;
	Отбор.Вставить("Роль", Роль);
	
	НайденныеСтроки = ИсточникДанныхКлиент().КонтактныеДанные.НайтиСтроки(Отбор);
	Для Каждого Строка Из НайденныеСтроки Цикл
		
		ОтборСтрок = Элементы[ИмяТЧ].ОтборСтрок;
		
		Строка.Роль = ОтборСтрок.Роль;
		
		Если ОтборСтрок.Свойство("КлючСтроки_ДанныеЮрЛиц") Тогда
			Ключ = Элементы[ИмяТЧ].ОтборСтрок.КлючСтроки_ДанныеЮрЛиц;
		Иначе
			
			ФормаОбъект             = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ФормаОбъект(ЭтаФорма);
			ПараметрыРеквизитаФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыРеквизитаФормы(
				"КонтактныеДанные", ИмяТЧ);
			
			СтрокаЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").СтрокаЮрЛиц(ПараметрыРеквизитаФормы, ФормаОбъект, Роль);
			Ключ        = СтрокаЮрЛиц.КлючСтроки;
			
		КонецЕсли;
		
		Строка.КлючСтроки_ДанныеЮрЛиц = Ключ;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Погрузка_АдресНажатие(Элемент, СтандартнаяОбработка)
	
	ПараметрыВыбораАдреса = Новый Структура;
	ПараметрыВыбораАдреса.Вставить("Роль",     "Отправитель");
	ПараметрыВыбораАдреса.Вставить("Значение", ИсточникДанныхКлиент().АдресПогрузкиЗначение);
	ПараметрыВыбораАдреса.Вставить("Текст",    ИсточникДанныхКлиент().Погрузка_Адрес);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").АдресНачалоВыбора(ЭтаФорма, ПараметрыВыбораАдреса, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиНажатие(Элемент, СтандартнаяОбработка)
	
	ПараметрыВыбораАдреса = Новый Структура;
	ПараметрыВыбораАдреса.Вставить("Роль",     "Получатель");
	ПараметрыВыбораАдреса.Вставить("Значение", ИсточникДанныхКлиент().АдресДоставкиЗначение);
	ПараметрыВыбораАдреса.Вставить("Текст",    ИсточникДанныхКлиент().Отправитель_АдресДоставки);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").АдресНачалоВыбора(ЭтаФорма, ПараметрыВыбораАдреса, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // РаботаСКонтактнойИнформацией

#Область ТранспортныеСредства

&НаКлиенте
Процедура ТранспортноеСредствоСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	ПроцедураПослеОбновленияДанныхТранспортныхСредств = Новый ОписаниеОповещения(
		"ПослеОбновленияДанныхТранспортныхСредств",
		ЭтаФорма);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТранспортноеСредствоНажатие(
		ЭтаФорма,
		СтандартнаяОбработка,
		ПроцедураПослеОбновленияДанныхТранспортныхСредств);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрицепыСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПрицепыНажатие(ЭтаФорма, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрицепыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПроцедураПослеОбновленияДанныхТранспортныхСредств = Новый ОписаниеОповещения(
		"ПослеОбновленияДанныхТранспортныхСредств",
		ЭтаФорма);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТаблицаПрицеповВыбор(
		ЭтаФорма,
		ВыбраннаяСтрока,
		СтандартнаяОбработка,
		ПроцедураПослеОбновленияДанныхТранспортныхСредств);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрицепыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И ЭтаФорма.ЭтоВО Тогда
		
		ТекДанные = Элементы.Прицепы.ТекущиеДанные;
		ТекДанные.КлючСтроки =
			МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").НовыйКлючСтроки(ЭтаФорма, "ТранспортныеСредства");
			
	КонецЕсли;
	
	ПроцедураПослеОбновленияДанныхТранспортныхСредств = Новый ОписаниеОповещения(
		"ПослеОбновленияДанныхТранспортныхСредств",
		ЭтаФорма);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ТаблицаПрицеповПриНачалеРедактирования(
		ЭтаФорма,
		ПроцедураПослеОбновленияДанныхТранспортныхСредств);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрицепыПослеУдаления(Элемент)
	
	ПослеОбновленияДанныхТранспортныхСредств(Неопределено, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрицепыПередУдалением(Элемент, Отказ)
	
	Если ЭтаФорма.ЭтоВО Тогда
		ЗапускУдаленияСвязанныхСтрок("Прицепы", "ТранспортныеСредства", Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ТранспортныеСредства

#Область ВодителиПутевыеЛисты

&НаКлиенте
Процедура ВодителиПриАктивизацииСтроки(Элемент)
	
	Водители_УстановитьОтборПоВсемСвязаннымТаблицам();
	
КонецПроцедуры

&НаКлиенте
Процедура ВодителиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураОтветственного = МодульКодаКлиент("Saby_ТНОбщегоНазначенияСервер").СтруктураОтветственного(
		ИсточникДанныхКлиент().ОтветственныеЛица, "", ВыбраннаяСтрока);
	СтруктураОтветственного.Вставить("ТолькоПросмотр", Элементы.Страница_Основное.ТолькоПросмотр);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").НачатьРедактированиеОтветственногоЛица(
		ЭтаФорма, СтруктураОтветственного, "Водитель");
	
КонецПроцедуры

&НаКлиенте
Процедура Водители_УстановитьОтборПоВсемСвязаннымТаблицам()
	
	ТекущиеДанные = Элементы.Водители.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("КлючСтроки_ОтветственныеЛица", ТекущиеДанные.КлючСтроки);
		
		Отбор = Новый ФиксированнаяСтруктура(СтруктураПоиска);
		Элементы.ПутевыеЛисты.ОтборСтрок = Отбор;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВодителиПередУдалением(Элемент, Отказ)
	
	ЗапускУдаленияСвязанныхСтрок("Водители", "ОтветственныеЛица", Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВодителиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		ТекДанные = Элементы.Водители.ТекущиеДанные;
		ТекДанные.КлючСтроки = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").НовыйКлючСтроки(ЭтаФорма, "ОтветственныеЛица");
		
		// Переустановим отбор
		Водители_УстановитьОтборПоВсемСвязаннымТаблицам();
		
		Для Каждого СтруктураОтбора Из Элемент.ОтборСтрок Цикл
			Элемент.ТекущиеДанные[СтруктураОтбора.Ключ] = СтруктураОтбора.Значение;
		КонецЦикла;
		
		СтруктураОтветственного = МодульКодаКлиент("Saby_ТНОбщегоНазначенияСервер").СтруктураОтветственного(
			ИсточникДанныхКлиент().ОтветственныеЛица, "", Элемент.ТекущаяСтрока);
		
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").НачатьРедактированиеОтветственногоЛица(
			ЭтаФорма, СтруктураОтветственного, "Водитель");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ПроверкаВозможностиВводаСтроки("Водители", Отказ);
	
	КопированиеСвязанныхДанныхДокументов(Копирование, Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	МодульКлиент = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент");
	
	Если Не Копирование Тогда
		
		Если ЭтаФорма.ЭтоВО Тогда
			ИмяТаблицы = "ДокументыЭПД";
		Иначе
			ИмяТаблицы = Элемент.Имя;
		КонецЕсли;	
		
		ПараметрыСвязи = МодульКлиент.ПараметрыКлючаСвязиСтрок(ИмяТаблицы, "Водители", Элемент.ТекущаяСтрока, НоваяСтрока);
		
		МодульКлиент.ДобавитьКлючСвязиСтроки(ЭтаФорма, ПараметрыСвязи);
		
	КонецЕсли;
	
	Если НоваяСтрока И ЭтаФорма.ЭтоВО Тогда
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		ТекущиеДанные.Тип        = "ПутевойЛист";
		ТекущиеДанные.КлючСтроки = МодульКлиент.НовыйКлючСтроки(ЭтаФорма, "ДокументыЭПД");
		
		ПараметрыДокумента = МодульКодаКлиент("ЭтаФорма").ПараметрыДокументаЭПД("ПутевойЛист", "Путевой лист");
		
		ИндексЭПД = ЭтаФорма.ДокументыЭПД.Индекс(ТекущиеДанные);
		
		МодульКодаКлиент("ЭтаФорма").ОткрытьДокументЭПД(ПараметрыДокумента, Неопределено, ИндексЭПД);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВодителиСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭтаФорма.ТекущийЭлемент = Элементы.Водители;
	
КонецПроцедуры

&НаКлиенте
Процедура ВодителиПослеУдаления(Элемент)
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ОбновитьВодителейНаФорме(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыПередУдалением(Элемент, Отказ)
	
	Если ЭтаФорма.ЭтоВО Тогда
		ЗапускУдаленияСвязанныхСтрок("ПутевыеЛисты", "ДокументыЭПД", Отказ)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		ПараметрыДокумента = МодульКодаКлиент("ЭтаФорма").ПараметрыДокументаЭПД("ПутевойЛист", "Путевой лист");
		ПараметрыДокумента.Вставить("ТолькоПросмотр", Элементы.Страница_Основное.ТолькоПросмотр);
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		ИндексЭПД = ЭтаФорма.ДокументыЭПД.Индекс(ТекущиеДанные);
		
		МодульКодаКлиент("ЭтаФорма").ОткрытьДокументЭПД(ПараметрыДокумента, СтандартнаяОбработка, ИндексЭПД);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ВодителиПутевыеЛисты 

&НаКлиенте
Процедура Группа_СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ОтборОтметокПоТипу();
	
КонецПроцедуры

#Область ТаблицаОшибок

&НаКлиенте
Процедура ТаблицаОшибокПриАктивизацииСтроки(Элемент)
	
	ПоказатьОшибкуЗаполнения();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОшибокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьОшибкуЗаполнения();
	
КонецПроцедуры

#КонецОбласти // ТаблицаОшибок

&НаКлиенте
Процедура КодФилиалаПриИзменении(Элемент)
	
	ОбновитьКодФилиалаНаФорме(ЭтаФорма.КодФилиала);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтправительКонтактныеДанные

&НаКлиенте
Процедура ОтправительКонтактныеДанныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	СтруктураДанныхЮрЛица = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ДанныеЮрЛицаПоРоли(
		ИсточникДанныхКлиент().ДанныеЮрЛиц, "Отправитель");
	
	Если Не СтруктураДанныхЮрЛица.Заполнена Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан Отправитель!";
		Сообщение.Поле  = "ОтправительСтрокой";
		Сообщение.Сообщить();
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактныеДанныеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока Тогда
		ОбработатьНовуюСтрокуКонтактнойИнформации(Элемент.Имя, "Отправитель");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактныеДанныеПослеУдаления(Элемент)
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	РолиЮрЛиц = МодульТНОбщегоНазначенияКлиентСервер.РолиЮрЛиц(ЭтаФорма, "Отправитель");
	
	ДопПараметрыОбновленияКИ = МодульТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(РолиЮрЛиц);
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(ЭтаФорма, ДопПараметрыОбновленияКИ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактныеДанныеЗначениеПриИзменении(Элемент)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Отправитель");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЗначениеКонтактныхДанныхПриИзменении(
		ЭтаФорма, Элементы.ОтправительКонтактныеДанные.ТекущиеДанные, РолиЮрЛиц);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактныеДанныеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Отправитель");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЗначениеКонтактныхДанныхНачалоВыбора(
		ЭтаФорма, Элементы.ОтправительКонтактныеДанные.ТекущиеДанные, РолиЮрЛиц, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыОтправительКонтактныеДанные

#Область ОбработчикиСобытийЭлементовТаблицыФормыПолучательКонтактныеДанные

&НаКлиенте
Процедура ПолучательКонтактныеДанныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр) 
	
	СтруктураДанныхЮрЛица = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ДанныеЮрЛицаПоРоли(
		ИсточникДанныхКлиент().ДанныеЮрЛиц, "Получатель");
	
	Если Не СтруктураДанныхЮрЛица.Заполнена Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан Получатель!";
		Сообщение.Поле  = "ПолучательСтрокой";
		Сообщение.Сообщить();
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактныеДанныеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока Тогда
		ОбработатьНовуюСтрокуКонтактнойИнформации(Элемент.Имя, "Получатель");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактныеДанныеПослеУдаления(Элемент)
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	РолиЮрЛиц = МодульТНОбщегоНазначенияКлиентСервер.РолиЮрЛиц(ЭтаФорма, "Получатель");
	
	ДопПараметрыОбновленияКИ = МодульТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(РолиЮрЛиц);
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(ЭтаФорма, ДопПараметрыОбновленияКИ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактныеДанныеЗначениеПриИзменении(Элемент)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Получатель");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЗначениеКонтактныхДанныхПриИзменении(
		ЭтаФорма, Элементы.ПолучательКонтактныеДанные.ТекущиеДанные, РолиЮрЛиц);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактныеДанныеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Получатель");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЗначениеКонтактныхДанныхНачалоВыбора(
		ЭтаФорма, Элементы.ПолучательКонтактныеДанные.ТекущиеДанные, РолиЮрЛиц, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыПолучательКонтактныеДанные

#Область ОбработчикиСобытийЭлементовТаблицыФормыПеревозчикКонтактныеДанные

&НаКлиенте
Процедура ПеревозчикКонтактныеДанныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	СтруктураДанныхЮрЛица = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ДанныеЮрЛицаПоРоли(
		ИсточникДанныхКлиент().ДанныеЮрЛиц, "Перевозчик");
	
	Если Не СтруктураДанныхЮрЛица.Заполнена Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан Перевозчик!";
		Сообщение.Поле  = "ПеревозчикСтрокой";
		Сообщение.Сообщить();
		Отказ = Истина;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикКонтактныеДанныеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока Тогда
		ОбработатьНовуюСтрокуКонтактнойИнформации(Элемент.Имя, "Перевозчик");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикКонтактныеДанныеПослеУдаления(Элемент)
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	РолиЮрЛиц = МодульТНОбщегоНазначенияКлиентСервер.РолиЮрЛиц(ЭтаФорма, "Перевозчик");
	
	ДопПараметрыОбновленияКИ = МодульТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(РолиЮрЛиц);
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(ЭтаФорма, ДопПараметрыОбновленияКИ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикКонтактныеДанныеЗначениеПриИзменении(Элемент)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Перевозчик");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЗначениеКонтактныхДанныхПриИзменении(
		ЭтаФорма, Элементы.ПеревозчикКонтактныеДанные.ТекущиеДанные, РолиЮрЛиц);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикКонтактныеДанныеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Перевозчик");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЗначениеКонтактныхДанныхНачалоВыбора(
		ЭтаФорма, Элементы.ПеревозчикКонтактныеДанные.ТекущиеДанные, РолиЮрЛиц, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыПеревозчикКонтактныеДанные

#Область ОбработчикиСобытийЭлементовТаблицыФормыВладелецОбъектаКонтактныеДанные

&НаКлиенте
Процедура ВладелецОбъектаКонтактныеДанныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	СтруктураДанныхЮрЛица = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ДанныеЮрЛицаПоРоли(
		ИсточникДанныхКлиент().ДанныеЮрЛиц, "ВладелецОбъекта");
	
	Если Не СтруктураДанныхЮрЛица.Заполнена Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан владелец объекта отгрузки!";
		Сообщение.Поле  = "ВладелецОбъектаСтрокой";
		Сообщение.Сообщить();
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецОбъектаКонтактныеДанныеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока Тогда
		ОбработатьНовуюСтрокуКонтактнойИнформации(Элемент.Имя, "ВладелецОбъекта");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецОбъектаКонтактныеДанныеПослеУдаления(Элемент)
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	РолиЮрЛиц = МодульТНОбщегоНазначенияКлиентСервер.РолиЮрЛиц(ЭтаФорма, "ВладелецОбъекта");
	
	ДопПараметрыОбновленияКИ = МодульТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(РолиЮрЛиц);
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(ЭтаФорма, ДопПараметрыОбновленияКИ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецОбъектаКонтактныеДанныеЗначениеПриИзменении(Элемент)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "ВладелецОбъекта");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЗначениеКонтактныхДанныхПриИзменении(
		ЭтаФорма, Элементы.КонтактныеДанные_Владелец.ТекущиеДанные, РолиЮрЛиц);
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецОбъектаКонтактныеДанныеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "ВладелецОбъекта");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЗначениеКонтактныхДанныхНачалоВыбора(
		ЭтаФорма, Элементы.КонтактныеДанные_Владелец.ТекущиеДанные, РолиЮрЛиц, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыВладелецОбъектаКонтактныеДанные

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтгрузчикКонтактныеДанные

&НаКлиенте
Процедура ОтгрузчикКонтактныеДанныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	СтруктураДанныхЮрЛица = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ДанныеЮрЛицаПоРоли(
		ИсточникДанныхКлиент().ДанныеЮрЛиц, "Отгрузчик");
	
	Если Не СтруктураДанныхЮрЛица.Заполнена Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указано лицо осуществляющее отгрузку!";
		Сообщение.Поле  = "ОтгрузчикСтрокой";
		Сообщение.Сообщить();
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтгрузчикКонтактныеДанныеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока Тогда
		ОбработатьНовуюСтрокуКонтактнойИнформации(Элемент.Имя, "Отгрузчик");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтгрузчикКонтактныеДанныеПослеУдаления(Элемент)
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	РолиЮрЛиц = МодульТНОбщегоНазначенияКлиентСервер.РолиЮрЛиц(ЭтаФорма, "Отгрузчик");
	
	ДопПараметрыОбновленияКИ = МодульТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(РолиЮрЛиц);
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(ЭтаФорма, ДопПараметрыОбновленияКИ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтгрузчикКонтактныеДанныеЗначениеПриИзменении(Элемент)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Отгрузчик");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЗначениеКонтактныхДанныхПриИзменении(
		ЭтаФорма, Элементы.КонтактныеДанные_Отгрузка.ТекущиеДанные, РолиЮрЛиц);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтгрузчикКонтактныеДанныеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РолиЮрЛиц = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").РолиЮрЛиц(ЭтаФорма, "Отгрузчик");
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЗначениеКонтактныхДанныхНачалоВыбора(
		ЭтаФорма, Элементы.КонтактныеДанные_Отгрузка.ТекущиеДанные, РолиЮрЛиц, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыОтгрузчикКонтактныеДанные

#Область ОбработчикиСобытийЭлементовДополнительныхСтраниц

#Область Грузы

&НаКлиенте
Процедура ГрузыНаименованиеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Грузы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Наименование = "Контейнер" И СокрЛП(ТекущиеДанные.Тип) = "Груз" Тогда
		
		ИндексСтроки = Формат(ТекущиеДанные.НомерСтроки - 1, "ЧН=0; ЧГ=0");
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "У груза с типом 'Груз' не может быть указано название 'Контейнер'!";
		Сообщение.Поле = "Объект.Грузы[" + ИндексСтроки + "].Наименование";
		Сообщение.Сообщить();
		
		ТекущиеДанные.Наименование = "Груз";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МаркировкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ПроверкаВозможностиВводаСтроки("Грузы", Отказ);
КонецПроцедуры

&НаКлиенте
Процедура МаркировкиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не Копирование Тогда
		
		ПараметрыСвязи = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПараметрыКлючаСвязиСтрок(
			Элемент.Имя, "Грузы", Элемент.ТекущаяСтрока, НоваяСтрока);
		
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ДобавитьКлючСвязиСтроки(ЭтаФорма, ПараметрыСвязи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпасныеГрузыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ПроверкаВозможностиВводаСтроки("Грузы", Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ОпасныеГрузыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не Копирование Тогда
		
		ПараметрыСвязи = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПараметрыКлючаСвязиСтрок(
			Элемент.Имя, "Грузы", Элемент.ТекущаяСтрока, НоваяСтрока);
		
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ДобавитьКлючСвязиСтроки(ЭтаФорма, ПараметрыСвязи);
		
	КонецЕсли;
	
	Если ЭтаФорма.ЭтоВО Тогда
		МодульКодаКлиент("ЭтаФорма").ОткрытьОпасныйГруз();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпасныеГрузыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО И Элементы.Страница_Груз.ТолькоПросмотр Тогда
		МодульКодаКлиент("ЭтаФорма").ОткрытьОпасныйГруз();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СведенияГосСистемПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ПроверкаВозможностиВводаСтроки("Грузы", Отказ);
КонецПроцедуры

&НаКлиенте
Процедура СведенияГосСистемПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не Копирование Тогда
		
		ПараметрыСвязи = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПараметрыКлючаСвязиСтрок(
			Элемент.Имя, "Грузы", Элемент.ТекущаяСтрока, НоваяСтрока);
		
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ДобавитьКлючСвязиСтроки(ЭтаФорма, ПараметрыСвязи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтейнерыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ПроверкаВозможностиВводаСтроки("Грузы", Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КонтейнерыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не Копирование Тогда
		
		ПараметрыСвязи = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПараметрыКлючаСвязиСтрок(
			Элемент.Имя, "Грузы", Элемент.ТекущаяСтрока, НоваяСтрока);
		
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ДобавитьКлючСвязиСтроки(ЭтаФорма, ПараметрыСвязи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВозможностиВводаСтроки(ИмяТЧ, Отказ)
	
	ТекущиеДанные = Элементы[ИмяТЧ].ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбрана строка в основной таблице " + ИмяТЧ;
		Сообщение.Сообщить();
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ГрузыЗаказчикСтрока" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		СтандартныеПараметрыСобытия = Новый Структура;
		СтандартныеПараметрыСобытия.Вставить("Элемент",         Элемент);
		СтандартныеПараметрыСобытия.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
		СтандартныеПараметрыСобытия.Вставить("Поле",            Поле);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Роль",           "Заказчик");
		ДополнительныеПараметры.Вставить("ТолькоПросмотр", Элементы.Страница_Груз.ТолькоПросмотр);
		
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЮрЛицоВТаблицеВыбор(
			ЭтаФорма, СтандартныеПараметрыСобытия, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузыПриАктивизацииСтроки(Элемент)
	
	Грузы_УстановитьОтборПоВсемСвязаннымТаблицам();
	
КонецПроцедуры

&НаКлиенте
Процедура Грузы_УстановитьОтборПоВсемСвязаннымТаблицам()
	
	ТекущиеДанные = Элементы.Грузы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("КлючСтроки_Грузы", ТекущиеДанные.КлючСтроки);
		
		Отбор = Новый ФиксированнаяСтруктура(СтруктураПоиска);
		
		Элементы.Маркировки.ОтборСтрок        = Отбор;
		Элементы.ОпасныеГрузы.ОтборСтрок      = Отбор;
		Элементы.СведенияГосСистем.ОтборСтрок = Отбор;
		Элементы.Контейнеры.ОтборСтрок        = Отбор;
		
		ЭтаФорма.ГосСистема_Наименование   = ТекущиеДанные.НаименованиеГосСистемы;
		ЭтаФорма.ГосСистема_УчетнаяЕдиница = ТекущиеДанные.УчетнаяЕдиницаГосСистемы;
		
	Иначе
		
		ЭтаФорма.ГосСистема_Наименование   = "";
		ЭтаФорма.ГосСистема_УчетнаяЕдиница = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		ТекДанные = Элементы.Грузы.ТекущиеДанные;
		ТекДанные.КлючСтроки = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").НовыйКлючСтроки(ЭтаФорма, "Грузы");
		Если Не Копирование Тогда
			ТекДанные.Тип = ЗначениеМетаданныхКлиент("Saby_ТипГруза.Груз");
		КонецЕсли;
		
		// Переустановим отбор
		Грузы_УстановитьОтборПоВсемСвязаннымТаблицам();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузыПередУдалением(Элемент, Отказ)
	
	ЗапускУдаленияСвязанныхСтрок("Грузы", "Грузы", Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузыТипПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Грузы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если СокрЛП(ТекущиеДанные.Тип) = "Контейнер" Тогда
			ТекущиеДанные.Наименование = "Контейнер";
		Иначе
			ТекущиеДанные.Наименование = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГосСистема_НаименованиеПриИзменении(Элемент)
	ИзменениеИнформацииГосСистем();
КонецПроцедуры

&НаКлиенте
Процедура ГосСистема_УчетнаяЕдиницаПриИзменении(Элемент)
	ИзменениеИнформацииГосСистем();
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеИнформацииГосСистем()
	
	ТекущиеДанные = Элементы.Грузы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные.НаименованиеГосСистемы   = ГосСистема_Наименование;
		ТекущиеДанные.УчетнаяЕдиницаГосСистемы = ГосСистема_УчетнаяЕдиница;
		
		ЭтаФорма.Модифицированность = Истина;
		
	Иначе
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбрана строка груза!";
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузыПриИзменении(Элемент)
	
	Если Не ЭтаФорма.ЭтоВО Тогда
		Возврат;
	КонецЕсли;
	
	// Пересчитаем номера строк
	Для Каждого Строка Из ЭтаФорма.Грузы Цикл
		Строка.НомерСтроки = ЭтаФорма.Грузы.Индекс(Строка) + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Грузоотправитель_СопроводительнаяВедомостьОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		ПараметрыДокумента = МодульКодаКлиент("ЭтаФорма").ПараметрыДокументаЭПД(
			"СопроводительнаяВедомость",
			"Сопроводительная ведомость",
			"Отправитель_СопроводительнаяВедомость",
			"ДокументЭПД",
			Элементы.ГруппаПогрузкаШапка.ТолькоПросмотр);
		
		МодульКодаКлиент("ЭтаФорма").ОткрытьДокументЭПД(ПараметрыДокумента, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Грузы

#Область Отметки

&НаКлиенте
Процедура ТипОтметок_ПогрузкаПриИзменении(Элемент)
	
	ОтборОтметокПоТипу();
	
	ТипОтметок_ПогрузкаТолькоПросмотр();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОтметок_ВыгрузкаПриИзменении(Элемент)
	
	ОтборОтметокПоТипу();
	
	ТипОтметок_ВыгрузкаТолькоПросмотр();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтметокПоТипу(ИдентификаторСтрокиОтметок = Неопределено) Экспорт
	
	Если Элементы.Группа_Страницы.ТекущаяСтраница = Элементы.Страница_Погрузка Тогда
		ИмяТЧ = "Отметки";
		УстановитьТипОтметок(ИдентификаторСтрокиОтметок, "ТипОтметок_Погрузка");
		Значение = ТипОтметок_Погрузка;
	ИначеЕсли Элементы.Группа_Страницы.ТекущаяСтраница = Элементы.Страница_Выгрузка Тогда
		ИмяТЧ = "Отметки_Выгрузка";
		УстановитьТипОтметок(ИдентификаторСтрокиОтметок, "ТипОтметок_Выгрузка");
		Значение = ТипОтметок_Выгрузка;
	Иначе
		Возврат;
	КонецЕсли;
	
	ЗаполнитьКритерийОтбораОтметок(ИмяТЧ, Значение);
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("КритерийОтбора", Истина);
	
	Элементы[ИмяТЧ].ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураПоиска);
	
	// Если таблица пустая Событие при активации строки не наступит
	// проверим что есть в таблице
	НайденныеСтроки = ИсточникДанныхКлиент().Отметки.НайтиСтроки(СтруктураПоиска);
	Если Не НайденныеСтроки.Количество() Тогда
		Отметки_УстановитьОтборПоВсемСвязаннымТаблицам(ИмяТЧ, Истина);
	ИначеЕсли ИдентификаторСтрокиОтметок <> Неопределено Тогда
		Отметки_УстановитьОтборПоВсемСвязаннымТаблицам(ИмяТЧ);
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКритерийОтбораОтметок(ИмяТЧ, Значение)
	
	МассивЭтапов = Новый Массив;
	
	Если ИмяТЧ = "Отметки" Тогда
		
		Если СокрЛП(Значение) = "Отправитель" Тогда
			МассивЭтапов.Добавить(ТипТитула("Погрузка"));
		Иначе
			МассивЭтапов.Добавить(ТипТитула("ПолучениеГруза"));
		КонецЕсли;
		
	Иначе
		
		// Отметки_Выгрузка
		Если СокрЛП(Значение) = "Получатель" Тогда
			МассивЭтапов.Добавить(ТипТитула("ПриемкаГруза"));
		Иначе
			МассивЭтапов.Добавить(ТипТитула("ВыдачаГруза"));
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого СтрокаОтметок Из ИсточникДанныхКлиент().Отметки Цикл
		СтрокаОтметок.КритерийОтбора = СтрокаОтметок.Роль = Значение
			И МассивЭтапов.Найти(СтрокаОтметок.Этап) <> Неопределено;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТипОтметок(ИдентификаторСтрокиОтметок, ТипОтметок)
	
	Если ИдентификаторСтрокиОтметок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаОтметок = ИсточникДанныхКлиент().Отметки.НайтиПоИдентификатору(ИдентификаторСтрокиОтметок);
	Если СтрокаОтметок.Роль <> ЭтаФорма[ТипОтметок] Тогда
		ЭтаФорма[ТипОтметок] = СтрокаОтметок.Роль;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ТипТитула(Титул)
	
	Возврат ЗначениеМетаданныхКлиент("Saby_ТипТитулаЭтрН." + Титул);
	
КонецФункции

&НаКлиенте
Процедура ДобавитьДинамическиеТитулыСОтметками(МассивЭтапов)
	
	МассивЭтапов.Добавить(ТипТитула("Переадресовка"));
	МассивЭтапов.Добавить(ТипТитула("ЗаменаВодителяТС"));
	МассивЭтапов.Добавить(ТипТитула("ИзменениеСтоимости"));
	МассивЭтапов.Добавить(ТипТитула("СогласованиеСтоимости"));
	
КонецПроцедуры

&НаКлиенте
Процедура Отметки_УстановитьОтборПоВсемСвязаннымТаблицам(ИмяТаблицы, ПустойОтбор = Ложь)
	
	ТекущиеДанные = Элементы[ИмяТаблицы].ТекущиеДанные;
	
	СтруктураПоиска = Новый Структура;
	Если ПустойОтбор Или ТекущиеДанные = Неопределено Тогда
		СтруктураПоиска.Вставить("КлючСтроки_Отметки", 0);
	Иначе
		СтруктураПоиска.Вставить("КлючСтроки_Отметки", ТекущиеДанные.КлючСтроки);
	КонецЕсли;
	
	Отбор = Новый ФиксированнаяСтруктура(СтруктураПоиска);
	
	Если ИмяТаблицы = "Отметки" Тогда
		Элементы.Штрафы.ОтборСтрок = Отбор;
		Элементы.Акты.ОтборСтрок   = Отбор;
	Иначе
		// Отметки_Выгрузка
		Элементы.Штрафы_Выгрузка.ОтборСтрок = Отбор;
		Элементы.Акты_Выгрузка.ОтборСтрок   = Отбор;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметкиПриАктивизацииСтроки(Элемент)
	
	Отметки_УстановитьОтборПоВсемСвязаннымТаблицам("Отметки");
	
КонецПроцедуры

&НаКлиенте
Процедура Отметки_ВыгрузкаПриАктивизацииСтроки(Элемент)
	
	Отметки_УстановитьОтборПоВсемСвязаннымТаблицам("Отметки_Выгрузка");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметкиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		ТекДанные = Элементы.Отметки.ТекущиеДанные;
		
		МодульКлиент = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент");
		
		ТекДанные.КлючСтроки = МодульКлиент.НовыйКлючСтроки(ЭтаФорма, "Отметки");
		ТекДанные.ДатаВремя  = МодульКлиент.ДатаСеанса();
		ТекДанные.Роль       = ТипОтметок_Погрузка;
		
		// Этап
		Если СокрЛП(ТипОтметок_Погрузка) = "Отправитель" Тогда
			ТекДанные.Этап = ТипТитула("Погрузка");
		Иначе
			ТекДанные.Этап = ТипТитула("ПолучениеГруза");
		КонецЕсли;
		
		ТекДанные.КритерийОтбора = Истина;
		
		// Переустановим отбор
		Отметки_УстановитьОтборПоВсемСвязаннымТаблицам(Элемент.Имя);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отметки_ВыгрузкаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		ТекДанные = Элементы.Отметки_Выгрузка.ТекущиеДанные;
		
		МодульКлиент = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент");
		
		ТекДанные.КлючСтроки = МодульКлиент.НовыйКлючСтроки(ЭтаФорма, "Отметки");
		ТекДанные.ДатаВремя  = МодульКлиент.ДатаСеанса();
		ТекДанные.Роль       = ТипОтметок_Выгрузка;
		
		// Этап
		Если СокрЛП(ТипОтметок_Выгрузка) = "Получатель" Тогда
			ТекДанные.Этап = ТипТитула("ПриемкаГруза");
		Иначе
			ТекДанные.Этап = ТипТитула("ВыдачаГруза");
		КонецЕсли;
		
		ТекДанные.КритерийОтбора = Истина;
		
		// Переустановим отбор
		Отметки_УстановитьОтборПоВсемСвязаннымТаблицам(Элемент.Имя);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШтрафыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ПроверкаВозможностиВводаСтроки("Отметки", Отказ);
КонецПроцедуры

&НаКлиенте
Процедура Штрафы_ВыгрузкаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ПроверкаВозможностиВводаСтроки("Отметки_Выгрузка", Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ШтрафыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не Копирование Тогда
		
		ПараметрыСвязи = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПараметрыКлючаСвязиСтрок(
			Элемент.Имя, "Отметки", Элемент.ТекущаяСтрока, НоваяСтрока);
		
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ДобавитьКлючСвязиСтроки(ЭтаФорма, ПараметрыСвязи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Штрафы_ВыгрузкаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не Копирование Тогда
		
		ПараметрыСвязи = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПараметрыКлючаСвязиСтрок(
			"Штрафы", "Отметки_Выгрузка", Элемент.ТекущаяСтрока, НоваяСтрока, "КлючСтроки_Отметки");
		
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ДобавитьКлючСвязиСтроки(ЭтаФорма, ПараметрыСвязи);
		
	КонецЕсли;
	
КонецПроцедуры

#Область АктыОтметок

&НаКлиенте
Процедура АктыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ПроверкаВозможностиВводаСтроки("Отметки", Отказ);
КонецПроцедуры

&НаКлиенте
Процедура Акты_ВыгрузкаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ПроверкаВозможностиВводаСтроки("Отметки_Выгрузка", Отказ);
КонецПроцедуры

&НаКлиенте
Процедура АктыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не Копирование Тогда
		
		ПараметрыСвязи = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПараметрыКлючаСвязиСтрок(
			Элемент.Имя, "Отметки", Элемент.ТекущаяСтрока, НоваяСтрока);
		
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ДобавитьКлючСвязиСтроки(ЭтаФорма, ПараметрыСвязи);
		
	КонецЕсли;
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		МодульКодаКлиент("ЭтаФорма").СкопироватьСтрокуДокументаЭПД(Элемент, Копирование);
		
		МодульКодаКлиент("ЭтаФорма").АктыОткрыть(Элемент.ТекущийЭлемент.Имя, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО
		И Элементы.Группа_ОтметкиПогрузка.ТолькоПросмотр Тогда
		МодульКодаКлиент("ЭтаФорма").АктыОткрыть(Элемент.ТекущийЭлемент.Имя, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Акты_ВыгрузкаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не Копирование Тогда
		
		ПараметрыСвязи = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ПараметрыКлючаСвязиСтрок(
			"Акты", "Отметки_Выгрузка", Элемент.ТекущаяСтрока, НоваяСтрока, "КлючСтроки_Отметки");
		
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ДобавитьКлючСвязиСтроки(ЭтаФорма, ПараметрыСвязи);
		
	КонецЕсли;
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		МодульКодаКлиент("ЭтаФорма").СкопироватьСтрокуДокументаЭПД(Элемент, Копирование);
		
		МодульКодаКлиент("ЭтаФорма").АктыОткрыть(Элемент.ТекущийЭлемент.Имя, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Акты_ВыгрузкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО И Элементы.Группа_ОтметкиВыгрузкаТЧ.ТолькоПросмотр Тогда
		МодульКодаКлиент("ЭтаФорма").АктыОткрыть(Элемент.ТекущийЭлемент.Имя, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктыДокументНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО Тогда
		МодульКодаКлиент("ЭтаФорма").АктыОткрыть(Элемент.Имя, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Акты_ВыгрузкаДокументНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО Тогда
		МодульКодаКлиент("ЭтаФорма").АктыОткрыть(Элемент.Имя, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктыПередУдалением(Элемент, Отказ)
	УдалитьДокументыЭПДПоАктам("Акты", Отказ);
КонецПроцедуры

&НаКлиенте
Процедура Акты_ВыгрузкаПередУдалением(Элемент, Отказ)
	УдалитьДокументыЭПДПоАктам("Акты_Выгрузка", Отказ);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьДокументыЭПДПоАктам(ТаблицаФормы, Отказ)
	
	Если Не ЭтаФорма.ЭтоВО Тогда
		Возврат;
	КонецЕсли;
	
	ВыделенныеСтроки = Элементы[ТаблицаФормы].ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для Каждого ИндексСтроки Из ВыделенныеСтроки Цикл
		Строка = ЭтаФорма.Акты.НайтиПоИдентификатору(ИндексСтроки);
		ЭтаФорма.УдаляемыеКлючи_ДокументыЭПД.Добавить(Строка.КлючСтроки_ДокументыЭПД);
	КонецЦикла;
	
	ПодключитьОбработчикОжидания("УдалитьСвязанныеСтроки", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти // АктыОтметок

&НаКлиенте
Процедура ОтметкиПередУдалением(Элемент, Отказ)
	
	ОтметкиУдалитьСтроки("Отметки", Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура Отметки_ВыгрузкаПередУдалением(Элемент, Отказ)
	
	ОтметкиУдалитьСтроки("Отметки_Выгрузка", Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметкиУдалитьСтроки(ТаблицаФормы, Отказ)
	
	ЗапускУдаленияСвязанныхСтрок(ТаблицаФормы, "Отметки", Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ОчисткаОтметок(Этап)
	
	Титул = ЗначениеМетаданных("Saby_ТипТитулаЭтрН." + Этап);
	Роль  = ЗначениеМетаданных("Saby_РолиКонтрагентов.ПустаяСсылка");
	
	МодульКода("Saby_ТНОбщегоНазначенияСервер").ЗачисткаОтметокШтрафовАктов(Объект, Титул, Роль);
	
КонецПроцедуры

#КонецОбласти // Отметки

&НаКлиенте
Процедура Грузоотправитель_ИнициаторПереадресацииПриИзменении(Элемент)
	
	РольОтправитель = ЗначениеМетаданныхКлиент("Saby_РолиКонтрагентов.Отправитель");
	
	Если ИсточникДанныхКлиент().ИнициаторПереадресации = РольОтправитель Тогда
		ИсточникДанныхКлиент().ИнициаторПереадресацииПодтверждение = "";
	КонецЕсли;
	
	ОбновитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициаторПереадресацииПодтверждениеОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		ПараметрыДокумента = МодульКодаКлиент("ЭтаФорма").ПараметрыДокументаЭПД(
			"ПравоПереадресации",
			"Право переадресации",
			"ИнициаторПереадресацииПодтверждение",
			"ДокументЭПД",
			Элементы.ГруппаПогрузкаШапка.ТолькоПросмотр);
		
		МодульКодаКлиент("ЭтаФорма").ОткрытьДокументЭПД(ПараметрыДокумента, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

#Область СопроводительныеДокументы

&НаКлиенте
Процедура СопроводительныеДокументыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		ТекущиеДанные = Элементы.СопроводительныеДокументы.ТекущиеДанные;
		
		ТекущиеДанные.ЭтоСопроводительныйДокумент = Истина;
		ТекущиеДанные.КлючСтроки = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").НовыйКлючСтроки(ЭтаФорма, "ДокументыЭПД");
		
	КонецЕсли;
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		МодульКодаКлиент("ЭтаФорма").ДокументыЭПДОткрыть(
			Элемент.ТекущийЭлемент.Имя,
			"Сопроводительный документ",
			"СопроводительныйДокумент",
			Элементы.Страница_Груз.ТолькоПросмотр);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительныеДокументыНаименованиеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		МодульКодаКлиент("ЭтаФорма").ДокументыЭПДОткрыть(
			Элемент.Имя,
			"Сопроводительный документ",
			"СопроводительныйДокумент",
			Элементы.Страница_Груз.ТолькоПросмотр);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительныеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО И Элементы.Страница_Груз.ТолькоПросмотр Тогда
		
		МодульКодаКлиент("ЭтаФорма").ДокументыЭПДОткрыть(
			"СопроводительныеДокументыНаименование",
			"Сопроводительный документ",
			"СопроводительныйДокумент",
			Элементы.Страница_Груз.ТолькоПросмотр);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительныеДокументыПередУдалением(Элемент, Отказ)
	
	Если ЭтаФорма.ЭтоВО Тогда
		ЗапускУдаленияСвязанныхСтрок("СопроводительныеДокументы", "ДокументыЭПД", Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительныеДокументыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	КопированиеСвязанныхДанныхДокументов(Копирование, Элемент.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура КопированиеСвязанныхДанныхДокументов(Копирование, ТекущиеДанные)
	
	Если Не Копирование
		Или Не ЭтаФорма.ЭтоВО
		Или ТекущиеДанные = Неопределено Тогда
		Возврат;
    КонецЕсли;
	
	МодульКодаКлиент("ЭтаФорма").СкопироватьСвязанныеДанныеДокументаЭПД(ЭтаФорма, ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти // СопроводительныеДокументы

&НаКлиенте
Процедура Оформитель_ОснованиеОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		ПараметрыДокумента = МодульКодаКлиент("ЭтаФорма").ПараметрыДокументаЭПД(
			"СоставительНаОсновании",
			"Основание оформления ЭТрН",
			"Оформитель_Основание",
			"ДокументЭПД",
			Элементы.Группа_Шапка.ТолькоПросмотр);
		
		МодульКодаКлиент("ЭтаФорма").ОткрытьДокументЭПД(ПараметрыДокумента, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Заказчик_ДоговорОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		ПараметрыДокумента = МодульКодаКлиент("ЭтаФорма").ПараметрыДокументаЭПД(
			"ДоговорПеревозки",
			"Договор перевозки",
			"Заказчик_Договор",
			"ДокументЭПД",
			Элементы.Группа_Шапка.ТолькоПросмотр);
		
		МодульКодаКлиент("ЭтаФорма").ОткрытьДокументЭПД(ПараметрыДокумента, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

#Область ОтгрузкаЗаполнение

&НаКлиенте
Процедура Отгрузка_НаОснованииДокументОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО Тогда
		
		ПараметрыДокумента = МодульКодаКлиент("ЭтаФорма").ПараметрыДокументаЭПД(
			"ПередачиЦенностей",
			"Основание отгрузки",
			"Отгрузка_НаОснованииДокумент",
			"ДокументЭПД",
			Элементы.ГруппаПогрузкаШапка.ТолькоПросмотр);
		
		МодульКодаКлиент("ЭтаФорма").ОткрытьДокументЭПД(ПараметрыДокумента, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отгрузка_ОтветственныйНаОснованииПриИзменении(Элемент)
	
	ПустаяСсылкаНаДокумент = ЗначениеМетаданныхКлиент("Saby_ДокументыЭПД.ПустаяСсылка", "Справочник");
	
	Если ИсточникДанныхКлиент().Отгрузка_ОтветственныйНаОсновании = ПустаяСсылкаНаДокумент Тогда
		
		Если ЭтаФорма.ЭтоВО Тогда
			ОткрытьДокументОтветственногоНаОсновании(Ложь);
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор",              Новый Структура);
		ПараметрыФормы.Вставить("РежимВыбора",        Истина);
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
		
		ТипДляОтбора = ЗначениеМетаданныхКлиент("Saby_ТипыДокумента.ОтветственныйНаОсновании");
		
		ПараметрыФормы.Отбор.Вставить("Тип", ТипДляОтбора);
		
		ПараметрыОткрытияФормыОбработки = ПараметрыОткрытияФормыОбработки(
			"Справочник.Saby_ДокументыЭПД.ФормаВыбора", ПараметрыФормы, Элемент);
		
		ПараметрыОткрытияФормыОбработки.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФормуОбработки(ПараметрыОткрытияФормыОбработки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отгрузка_ОтветственныйНаОснованииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		
		СтандартнаяОбработка = Ложь;
		ИсточникДанныхКлиент().Отгрузка_ОтветственныйНаОсновании = ВыбранноеЗначение;
		ЭтаФорма.Модифицированность = Истина;
		
		Если ЭтаФорма.ЭтоВО И ВыбранноеЗначение = "Должностные обязанности" Тогда
			
			Отбор = Новый Структура("Тип", "ОтветственныйНаОсновании");
			МодульКодаКлиент("ЭтаФорма").УдалитьДокументыЭПДПоОтбору(ЭтаФорма, Отбор);
			
			ЗаполнитьСписокВыбораОтветственногоНаОсновании();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отгрузка_ОтветственныйНаОснованииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИсточникДанныхКлиент().Отгрузка_ОтветственныйНаОсновании = Неопределено;
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Отгрузка_ОтветственныйНаОснованииОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ЭтаФорма.ЭтоВО И ЭтаФорма.Отгрузка_ОтветственныйНаОсновании <> "Должностные обязанности" Тогда
		
		ОткрытьДокументОтветственногоНаОсновании(СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументОтветственногоНаОсновании(СтандартнаяОбработка)
	
	ПараметрыДокумента = МодульКодаКлиент("ЭтаФорма").ПараметрыДокументаЭПД(
		"ОтветственныйНаОсновании",
		"Ответственный на основании",
		"Отгрузка_ОтветственныйНаОсновании",
		"ДокументЭПД",
		Элементы.ГруппаПогрузкаШапка.ТолькоПросмотр);
	
	МодульКодаКлиент("ЭтаФорма").ОткрытьДокументЭПД(ПараметрыДокумента, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Отгрузка_ОтветственныйНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураОтветственного = МодульКодаКлиент("Saby_ТНОбщегоНазначенияСервер").СтруктураОтветственного(
		ИсточникДанныхКлиент().ОтветственныеЛица, "Ответственный");
	СтруктураОтветственного.Вставить("ТолькоПросмотр", Элементы.ГруппаПогрузкаШапка.ТолькоПросмотр);
	СтруктураОтветственного.Вставить(
		"ИмяМетаданных",
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ИмяМетаданныхПоФорме(ЭтаФорма));
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").НачатьРедактированиеОтветственногоЛица(
		ЭтаФорма, СтруктураОтветственного, "Ответственный");
	
КонецПроцедуры

#КонецОбласти // ОтгрузкаЗаполнение

#КонецОбласти // ОбработчикиСобытийЭлементовДополнительныхСтраниц

#Область СлужебныеПроцедурыИФункции

Процедура ПриЧтенииСозданииПослеЗаписиНаСервере(ПрочитатьИзСтруктуры = Ложь)
	
	Если ЭтаФорма.ЭтоВО И ПрочитатьИзСтруктуры Тогда
		ПрочитатьИзСтруктуры(ЭтаФорма.Ссылка);
	КонецЕсли;
	
	УстановкаВидимостиИРасчетныхРеквизитов();
	
	ЗаполнитьСписокВыбораОтветственногоНаОсновании();
	
КонецПроцедуры

&НаСервере
Процедура УстановкаВидимостиИРасчетныхРеквизитов(Знач ОбновитьДанныеПоОшибкам = Ложь)
	
	ЗаполнитьРасчетныеРеквизиты();
	
	МодульТНОбщегоНазначенияСервер = МодульКода("Saby_ТНОбщегоНазначенияСервер");
	
	МодульТНОбщегоНазначенияСервер.ОбновитьЛенту(ЭтаФорма, Истина);
	
	ОбновитьДоступностьПолейВвода();
	ОбновитьВидимостьИДоступностьЭлементов();
	
	МодульТНОбщегоНазначенияСервер.ОбновитьВнешнийВидЭлементовОшибок(ЭтаФорма);
	
	МодульТНОбщегоНазначенияСервер.УстановитьОтборТаблицыОтветственных(Элементы.Водители, "Водитель");
	
	МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ОбновитьЭлементыИныхПолучателей(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасчетныеРеквизиты()
	
	ИсточникДанных = ИсточникДанных();
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	СтруктураОтправителя = МодульТНОбщегоНазначенияКлиентСервер.ДанныеЮрЛицаПоРоли(
		ИсточникДанных.ДанныеЮрЛиц, "Отправитель");
	
	СтруктураПолучателя  = МодульТНОбщегоНазначенияКлиентСервер.ДанныеЮрЛицаПоРоли(
		ИсточникДанных.ДанныеЮрЛиц, "Получатель");
	
	СтруктураОтгрузчика  = МодульТНОбщегоНазначенияКлиентСервер.ДанныеЮрЛицаПоРоли(
		ИсточникДанных.ДанныеЮрЛиц, "Отгрузчик");
	
	СтруктураОформителя  = МодульТНОбщегоНазначенияКлиентСервер.ДанныеЮрЛицаПоРоли(
		ИсточникДанных.ДанныеЮрЛиц, "Оформитель");
	
	СтруктураОформителя.Основание = ИсточникДанных.Составитель_НаОснованииДокумент;
	
	Если СтруктураОтправителя.Заполнена Тогда 
		ЭтаФорма.Грузоотправитель_Грузополучатель = МодульТНОбщегоНазначенияКлиентСервер.ЮрЛицаСовпадают(
			СтруктураПолучателя, СтруктураОтправителя);
	КонецЕсли;
	
	Если СтруктураОформителя.Заполнена Тогда
		ЭтаФорма.Грузоотправитель_НеЯвляетсяОтправителем = Не МодульТНОбщегоНазначенияКлиентСервер.ЮрЛицаСовпадают(
			СтруктураОтправителя, СтруктураОформителя);
	КонецЕсли;
	
	ЭтаФорма.Грузоотправитель_ОсуществляетОтгрузку = МодульТНОбщегоНазначенияКлиентСервер.ЮрЛицаСовпадают(
			СтруктураОтправителя, СтруктураОтгрузчика);
	
	Если Не ЗначениеЗаполнено(ЭтаФорма.ТипОтметок_Погрузка) Тогда
		ЭтаФорма.ТипОтметок_Погрузка = ЗначениеМетаданных("Saby_РолиКонтрагентов.Отправитель");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ЭтаФорма.ТипОтметок_Выгрузка) Тогда
		ЭтаФорма.ТипОтметок_Выгрузка = ЗначениеМетаданных("Saby_РолиКонтрагентов.Получатель");
	КонецЕсли;
	
	ЭтаФорма.ОтправительСтрокой = МодульТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли(
		СтруктураОтправителя.НаименованиеОрганизации);
		
	ЭтаФорма.ОформительСтрокой = МодульТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли(
		СтруктураОформителя.НаименованиеОрганизации);
	МодульТНОбщегоНазначенияКлиентСервер.ЗаполнитьДополнительныеПоляЮрЛица(
		ЭтаФорма, СтруктураОформителя, "Основание", "Оформитель");
		
	ЭтаФорма.ПолучательСтрокой = МодульТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли(
		СтруктураПолучателя.НаименованиеОрганизации);
		
	ЭтаФорма.ПеревозчикСтрокой = МодульТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли(
		ИсточникДанных.ДанныеЮрЛиц, "Перевозчик");
		
	ЭтаФорма.ЗаказчикСтрокой = МодульТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли(
		ИсточникДанных.ДанныеЮрЛиц, "Заказчик");
		
	ЭтаФорма.ВладелецОбъектаСтрокой = МодульТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли(
		ИсточникДанных.ДанныеЮрЛиц, "ВладелецОбъекта");
		
	ЭтаФорма.ОтгрузчикСтрокой = МодульТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли(
		СтруктураОтгрузчика.НаименованиеОрганизации);
	
	// Телефоны
	РолиЮрЛиц = МодульТНОбщегоНазначенияКлиентСервер.РолиЮрЛиц(
		ЭтаФорма, "Отправитель,Получатель,Перевозчик,Отгрузчик,ВладелецОбъекта");
	ДопПараметры = МодульТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(РолиЮрЛиц);
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(ЭтаФорма, ДопПараметры);
	
	// Документ основание
	Если ЗначениеЗаполнено(ИсточникДанных.ДокументОснование_Идентификатор) Тогда
		ДокументОснование = Документы[ИсточникДанных.ДокументОснование_ОбъектМетаданных].ПолучитьСсылку(
			Новый УникальныйИдентификатор(ИсточникДанных.ДокументОснование_Идентификатор));
		Элементы.ДекорацияДокументОснование.Заголовок = СокрЛП(ДокументОснование);
	КонецЕсли;
	
	МодульТНОбщегоНазначенияСервер = МодульКода("Saby_ТНОбщегоНазначенияСервер");
	
	// Активный этап и индикация состояния
	МодульТНОбщегоНазначенияСервер.ОтобразитьНаФормеСбисЭтапИСостояние(ЭтаФорма);
	
	АктивныеЭтапы = ПараметрыАктивныхЭтапов();
	
	// Обязательно после процедуры СбисЭтапИСостояние(), так как устанавливается реквизит АктивныйЭтапСтрокой
	ОграничитьТипыАктов(АктивныеЭтапы);
	
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьВодителейНаФорме(ЭтаФорма);
	
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьТранспортныеСредстваНаФорме(ЭтаФорма);
	
	СтруктураОтветственного = МодульТНОбщегоНазначенияСервер.СтруктураОтветственного(
		ИсточникДанных.ОтветственныеЛица, "Ответственный");
		
	МодульТНОбщегоНазначенияСервер.ОбновитьОтветственногоНаФорме(
		ЭтаФорма, "ОтветственныйОтгрузка", СтруктураОтветственного);
	
	ОбновитьКодФилиалаНаФорме();
	
	МаксимальныеКлючиСтрок();
	
	ЗначенияПоУмолчаниюВО(ИсточникДанных);
	
КонецПроцедуры

&НаСервере
Процедура ЗначенияПоУмолчаниюВО(ИсточникДанных)
		
	// Заполнение значений по умолчанию
	Если Не ЭтаФорма.ЭтоВО Тогда
		Возврат;
	КонецЕсли;
	
	НовыйДокВО = Не ЗначениеЗаполнено(ИсточникДанных.Ссылка.Идентификатор);
	
	Если НовыйДокВО Тогда
		
		ВозможностьПерегрузкиНеВыбрана = -1;
		
		// в ВО нет заполнения значений на реквизитах, поэтому делаем это кодом
		ИсточникДанных.Владелец_Тип                    = "Отправитель";
		ИсточникДанных.Погрузка_МассаМетодРасчета      = "ВзвешиваниеПоОбщейМассе";
		ИсточникДанных.Выгрузка_МассаМетодРасчета      = "ВзвешиваниеПоОбщейМассе";
		ИсточникДанных.Направление                     = "Исходящий";
		ИсточникДанных.Отправитель_ПерегрузкаЗапрещена = ВозможностьПерегрузкиНеВыбрана;
		ИсточникДанных.ИнициаторПереадресации          = "Отправитель";
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыАктивныхЭтапов()
	
	Завершен = ДОЗавершен();
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Завершен",              Завершен);
	РезультатФункции.Вставить("Черновик",              Ложь);
	РезультатФункции.Вставить("ПолучениеГруза",        Ложь);
	РезультатФункции.Вставить("ПриемкаГруза",          Ложь);
	РезультатФункции.Вставить("ВыдачаГруза",           Ложь);
	РезультатФункции.Вставить("СогласованиеСтоимости", Ложь);
	РезультатФункции.Вставить("ЭтоДинамическийТитул",  Ложь);
	
	ТекущийЭтап =  МодульКода("Saby_ТНОбщегоНазначенияСервер").РезультатФункцииТитулов(
		"Saby_ТранспортнаяНакладная", "ЗначениеПоПредставлению", АктивныйЭтапСтрокой);
	
	РезультатФункции.ЭтоДинамическийТитул = МодульКода("Saby_ТНОбщегоНазначенияСервер").РезультатФункцииТитулов(
		"Saby_ТранспортнаяНакладная", "ЭтоДинамическийТитул", ТекущийЭтап);
	
	Если Не РезультатФункции.Завершен Тогда
		
		Погрузка = (ТекущийЭтап = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.Погрузка"));
		ЭтоСогласованиеСтоимости = ТекущийЭтап = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.СогласованиеСтоимости");
		
		РезультатФункции.ПолучениеГруза = (ТекущийЭтап = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.ПолучениеГруза"));
		РезультатФункции.ПриемкаГруза   = (ТекущийЭтап = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.ПриемкаГруза"));
		РезультатФункции.ВыдачаГруза    = (ТекущийЭтап = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.ВыдачаГруза"));
		
		РезультатФункции.Черновик              = Не ЗначениеЗаполнено(ТекущийЭтап) Или Погрузка;
		РезультатФункции.СогласованиеСтоимости = ЭтоСогласованиеСтоимости;
		
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

&НаСервере
Процедура ОграничитьТипыАктов(АктивныеЭтапы = Неопределено)
	
	Если АктивныеЭтапы = Неопределено Тогда
		АктивныеЭтапы = ПараметрыАктивныхЭтапов();
	КонецЕсли;
	
	ЗначениеАктВзвешивания  = ЗначениеМетаданных("Saby_ТипыДокумента.АктВзвешивания");
	ЗначениеАктКоммерческий = ЗначениеМетаданных("Saby_ТипыДокумента.АктКоммерческий");
	
	Если АктивныеЭтапы.ПолучениеГруза Или АктивныеЭтапы.ВыдачаГруза Тогда
		ЗначениеПараметра = ЗначениеАктКоммерческий;
		Если ЭтаФорма.ЭтоВО Тогда
			УдалитьИзСпискаВыбора(Элементы.АктыТип.СписокВыбора,          ЗначениеАктВзвешивания);
			УдалитьИзСпискаВыбора(Элементы.Акты_ВыгрузкаТип.СписокВыбора, ЗначениеАктВзвешивания);
		КонецЕсли;
	Иначе
		ЗначениеПараметра = Новый Массив;
		ЗначениеПараметра.Добавить(ЗначениеАктВзвешивания);
		ЗначениеПараметра.Добавить(ЗначениеАктКоммерческий);
	КонецЕсли;
	
	МассивПараметровОтбора = Новый Массив;
	Параметр = Новый ПараметрВыбора("Отбор.Тип", ЗначениеПараметра);
	МассивПараметровОтбора.Добавить(Параметр);
	
	ПараметрыВыбораАктов = Новый ФиксированныйМассив(МассивПараметровОтбора);
	
	Элементы.АктыДокумент.ПараметрыВыбора          = ПараметрыВыбораАктов;
	Элементы.Акты_ВыгрузкаДокумент.ПараметрыВыбора = ПараметрыВыбораАктов;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьИзСпискаВыбора(СписокВыбора, ЗначениеДляУдаления)
	
	ЭлементДляУдаления = СписокВыбора.НайтиПоЗначению(ЗначениеДляУдаления);
	Если ЭлементДляУдаления <> Неопределено Тогда
		СписокВыбора.Удалить(ЭлементДляУдаления);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступностьПолейВвода()
	
	ПараметрыЭтапов = ПараметрыАктивныхЭтапов();
	ДоступныеРоли   = МодульКода("Документы.Saby_ТранспортнаяНакладная").РолиОрганизации(ИсточникДанных().ДанныеЮрЛиц);
	
	ЭтоПриемкаИлиВыдача = ПараметрыЭтапов.ПриемкаГруза Или ПараметрыЭтапов.ВыдачаГруза;
	
	ЭтоНеЧерновик = ПараметрыЭтапов.ПолучениеГруза Или ЭтоПриемкаИлиВыдача 
		Или ПараметрыЭтапов.Завершен Или ПараметрыЭтапов.ЭтоДинамическийТитул;
	
	// Видимость
	Элементы.ТипОтметок_Погрузка.Видимость        = ЭтоНеЧерновик;
	Элементы.Группа_ПогрузкаОговорки.Видимость    = ЭтоНеЧерновик;
	Элементы.Страница_Выгрузка.Видимость          = ЭтоПриемкаИлиВыдача Или БылаПриемкаИлиВыдача();
	Элементы.ТипОтметок_Выгрузка.Видимость        = ЭтоНеЧерновик;
	Элементы.Группа_ОговоркиПеревозчика.Видимость = ПараметрыЭтапов.ВыдачаГруза Или ПараметрыЭтапов.Завершен;
	
	// Только просмотр
	Элементы.Группа_СтандартныеРеквизиты.ТолькоПросмотр = ЭтоНеЧерновик;
	Элементы.Группа_Шапка.ТолькоПросмотр                = ЭтоНеЧерновик;
	Элементы.Страница_Основное.ТолькоПросмотр           = ЭтоНеЧерновик;
	Элементы.Страница_Груз.ТолькоПросмотр               = ЭтоНеЧерновик;
	Элементы.ГруппаПогрузкаШапка.ТолькоПросмотр         = ЭтоНеЧерновик;
	Элементы.Погрузка_Адрес.Доступность                 = Не ЭтоНеЧерновик;
	Элементы.АдресДоставки.Доступность                  = Не ЭтоНеЧерновик;
	
	Элементы.ГруппаДокументОснование.ТолькоПросмотр     = ЭтоНеЧерновик;
	
	Элементы.ГруппаВыгрузкаШапка.ТолькоПросмотр         = Не ПараметрыЭтапов.ПриемкаГруза Или Не ДоступныеРоли.Получатель;
	
	Элементы.ЗаполнитьВыгрузкуПоДаннымГрузоотправителя.Доступность = Не Элементы.ГруппаВыгрузкаШапка.ТолькоПросмотр;
	
	Элементы.Группа_ОтметкиВыгрузкаТЧ.ТолькоПросмотр    = ПараметрыЭтапов.Завершен;
	
	Элементы.Группа_ОтметкиПогрузка.ТолькоПросмотр      = ПараметрыЭтапов.Завершен Или ЭтоПриемкаИлиВыдача;
	Элементы.Группа_ПогрузкаОговорки.ТолькоПросмотр     = ПараметрыЭтапов.Завершен Или ЭтоПриемкаИлиВыдача
		Или ПараметрыЭтапов.ЭтоДинамическийТитул Или Не ДоступныеРоли.Перевозчик;
	
	Элементы.Группа_ОговоркиПеревозчика.ТолькоПросмотр  = ПараметрыЭтапов.Завершен
		Или Не ПараметрыЭтапов.ВыдачаГруза Или Не ДоступныеРоли.Перевозчик;
	
	Если ПараметрыЭтапов.ПолучениеГруза Тогда
		ТипОтметок_ПогрузкаТолькоПросмотр(ПараметрыЭтапов, ДоступныеРоли);
	ИначеЕсли ПараметрыЭтапов.ПриемкаГруза Тогда
		ЭтаФорма.ТипОтметок_Выгрузка = ЗначениеМетаданных("Saby_РолиКонтрагентов.Получатель");
		ТипОтметок_ВыгрузкаТолькоПросмотр(ПараметрыЭтапов, ДоступныеРоли);
	ИначеЕсли ПараметрыЭтапов.ВыдачаГруза Тогда
		ТипОтметок_ВыгрузкаТолькоПросмотр(ПараметрыЭтапов, ДоступныеРоли);
	Иначе
		ЭтаФорма.ТипОтметок_Погрузка = ЗначениеМетаданных("Saby_РолиКонтрагентов.Отправитель");
	КонецЕсли;
	
	// до завершен или это ДТ
	ЭтаФорма.ТолькоПросмотр = ПараметрыЭтапов.Завершен Или ПараметрыЭтапов.ЭтоДинамическийТитул;
	
	// Доступность
	Элементы.НастройкаДокументовОснований.Доступность = Не ЭтоНеЧерновик;
	
КонецПроцедуры

#Область ОбработкаОповещений

&НаКлиенте
Процедура ОбработатьОповещениеОбновитьДокумент(Параметр) Экспорт
	
	ЭтаФорма.ТаблицаОшибок.Очистить();
	ЭтаФорма.КоличествоОшибок = 0;
	
	Если ТипЗнч(Параметр) = Тип("Структура") Тогда
		ЭтаФорма.Ссылка = Параметр;
		ЭтаФорма.Ссылка.КоличествоОшибок = 0;
		ЭтаФорма.Ссылка.ТаблицаОшибок.Очистить();
	КонецЕсли;
	
	ЭтаФорма.Прочитать();
	
	УстановитьОтборыПоТаблицам();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОповещениеВыполнитьПереходНаФорме() Экспорт
	
	ОбменДаннымиСБИС("ОбновитьАктивныйЭтап");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОповещениеЗавершениеВыполнитьДействиеНаФорме(Параметр) Экспорт
	
	Если Параметр.status = "error" Тогда
		МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ОбработатьОшибкиИзФормыВыполнитьДействие(
			ЭтаФорма, ИсточникДанныхКлиент().Ссылка, Параметр);
		ЭтаФорма.Прочитать();
	Иначе
		Если ТипЗнч(ИсточникДанныхКлиент().Ссылка) = Тип("Структура") Тогда
			ЭтаФорма.АктивныйЭтапСтрокой = ИсточникДанныхКлиент().Ссылка.АктивныйЭтапСтрокой;
		КонецЕсли;
		ОбменДаннымиСБИС("ОбновитьАктивныйЭтап");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработкаОповещений

#Область КлючиСтрок

&НаКлиенте
Процедура ЗапускУдаленияСвязанныхСтрок(ТаблицаФормы, ТаблицаДанных, Отказ)

	ВыделенныеСтроки = Элементы[ТаблицаФормы].ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СписокКлючей = "УдаляемыеКлючи_" + ТаблицаДанных;
	
	Для Каждого ИдСтроки Из ВыделенныеСтроки Цикл
		ТекДанные = ИсточникДанныхКлиент()[ТаблицаДанных].НайтиПоИдентификатору(ИдСтроки);
		Если ТекДанные <> Неопределено Тогда
			// Необходимо очистить дополнительные сведения
			ЭтаФорма[СписокКлючей].Добавить(ТекДанные.КлючСтроки);
		КонецЕсли;
	КонецЦикла;
	
	ПодключитьОбработчикОжидания("УдалитьСвязанныеСтроки", 0.1, Истина);
	
КонецПроцедуры

&НаСервере
Процедура МаксимальныеКлючиСтрок()
	
	ИмяКолонки = "КлючСтроки";
	
	МассивТаблиц = Новый Массив;
	МассивТаблиц.Добавить("Грузы");
	МассивТаблиц.Добавить("Отметки");
	МассивТаблиц.Добавить("ОтветственныеЛица");
	
	Для Каждого ИмяТЧ Из МассивТаблиц Цикл
		
		ИмяКлюча = "МаксимальныйКлючСтроки_" + ИмяТЧ;
		
		Если ИсточникДанных()[ИмяТЧ].Количество() = 0 Тогда
			ЭтаФорма[ИмяКлюча] = 0;
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из ИсточникДанных()[ИмяТЧ] Цикл
			ЭтаФорма[ИмяКлюча] = Макс(ЭтаФорма[ИмяКлюча], СтрокаТаблицы[ИмяКолонки]);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// ПодключаемыеОбработчики
&НаКлиенте
Процедура УдалитьСвязанныеСтроки()
	
	УдалитьСвязанныеЗаписиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСвязанныеЗаписиНаСервере()
	
	ИсточникДанных = ИсточникДанных();
	
	ВидыУдаляемыхСтрок = Новый Структура;
	
	// Отметки
	КлючиОтметки = СтруктураСвязанныхДанныхДляУдаления(УдаляемыеКлючи_Отметки, "Отметки");
	
	ВидыУдаляемыхСтрок.Вставить("Акты",   КлючиОтметки);
	ВидыУдаляемыхСтрок.Вставить("Штрафы", КлючиОтметки);
	
	// Водители
	КлючиВодители = СтруктураСвязанныхДанныхДляУдаления(УдаляемыеКлючи_ОтветственныеЛица, "ОтветственныеЛица");
	Если ЭтаФорма.ЭтоВО Тогда
		ИмяТаблицыДокументов = "ДокументыЭПД";
	Иначе	
		ИмяТаблицыДокументов = "ПутевыеЛисты";
	КонецЕсли;
	
	ВидыУдаляемыхСтрок.Вставить(ИмяТаблицыДокументов, КлючиВодители);
	
	// Грузы
	КлючиГруз = СтруктураСвязанныхДанныхДляУдаления(УдаляемыеКлючи_Грузы, "Грузы");
	
	ВидыУдаляемыхСтрок.Вставить("Маркировки",        КлючиГруз);
	ВидыУдаляемыхСтрок.Вставить("ОпасныеГрузы",      КлючиГруз);
	ВидыУдаляемыхСтрок.Вставить("СведенияГосСистем", КлючиГруз);
	ВидыУдаляемыхСтрок.Вставить("Контейнеры",        КлючиГруз);
	
	Если ЭтаФорма.ЭтоВО Тогда
		МодульКода("ЭтаФорма").УдалитьСвязанныеЗаписиВО(ИсточникДанных);
	КонецЕсли;
	
	УдалитьСтроки(ИсточникДанных, ВидыУдаляемыхСтрок);
	
	// сбросим списки в начальное состояние
	ЭтаФорма.УдаляемыеКлючи_Грузы.Очистить();
	ЭтаФорма.УдаляемыеКлючи_Отметки.Очистить();
	ЭтаФорма.УдаляемыеКлючи_ОтветственныеЛица.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтроки(ИсточникДанных, ВидыУдаляемыхСтрок)
	
	Для Каждого ВидУдаляемыхСтрок Из ВидыУдаляемыхСтрок Цикл
		
		ИмяТабличнойЧасти       = ВидУдаляемыхСтрок.Ключ;
		СписокУдаленныхКлючей   = ВидУдаляемыхСтрок.Значение.Список;
		КлючСтроки              = "КлючСтроки_" + ВидУдаляемыхСтрок.Значение.ИмяКолонки;
		
		Для Каждого СтрокаКлюча Из СписокУдаленныхКлючей Цикл
			
			КлючПоиска = Новый Структура(КлючСтроки, СтрокаКлюча.Значение);
			
			Если Не ИсточникДанных[ИмяТабличнойЧасти].Количество() Тогда
				Продолжить;
			КонецЕсли;
			
			МассивСтрок = ИсточникДанных[ИмяТабличнойЧасти].НайтиСтроки(КлючПоиска);
			
			Для Каждого СтрокаТЧ Из МассивСтрок Цикл
				Если ЭтаФорма.ЭтоВО Тогда
					ЭтаФорма[ИмяТабличнойЧасти].Удалить(СтрокаТЧ);
				Иначе
					Объект[ИмяТабличнойЧасти].Удалить(СтрокаТЧ);
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СтруктураСвязанныхДанныхДляУдаления(Список, ИмяКолонки)
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Список",     Список);
	РезультатФункции.Вставить("ИмяКолонки", ИмяКолонки);
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // КлючиСтрок

&НаКлиенте
Функция ЗаполнениеНаОснованииINI() Экспорт
	
	Если ЭтаФорма.ЭтоВО Тогда
		Результат = ЗаполнитьНаОснованииINI(ЭтаФорма, ДокументОснование, "Saby_ТранспортнаяНакладная");
	Иначе
		Результат = ЗаполнениеНаОснованииINIНаСервере();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ЭтаФорма.Прочитать();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗаполнениеНаОснованииINIНаСервере()
	
	Возврат МодульКода("Saby_ТНОбщегоНазначенияСервер").ЗаполнитьНаОснованииINI(
		ЭтаФорма, ДокументОснование, "Saby_ТранспортнаяНакладная");
	
КонецФункции

&НаКлиенте
Процедура УстановитьОтборыПоТаблицам()
	
	// Контактная информация
	ОтборИнформацииПоКонтактнымДанным("ОтправительКонтактныеДанные",     "Отправитель");
	ОтборИнформацииПоКонтактнымДанным("ПолучательКонтактныеДанные",      "Получатель");
	ОтборИнформацииПоКонтактнымДанным("ПеревозчикКонтактныеДанные",      "Перевозчик");
	ОтборИнформацииПоКонтактнымДанным("ВладелецОбъектаКонтактныеДанные", "ВладелецОбъекта");
	ОтборИнформацииПоКонтактнымДанным("ОтгрузчикКонтактныеДанные",       "Отгрузчик");
	
	// Грузы. Связанные таблицы
	Грузы_УстановитьОтборПоВсемСвязаннымТаблицам();
	
	// Отметки
	ОтборОтметокПоТипу();
	
	// ДокументыЭПД
	УстановитьОтборДокументовЭПД();
	
КонецПроцедуры

// Заполнение реквизитов формы при изменение, связанной информации
// или первоначальной инициализации формы

&НаСервере
Процедура ОграничитьТипыДокументовОснованийНаСервере() Экспорт
	
	МодульКода("Saby_ТНОбщегоНазначенияСервер").ОграничитьТипыДокументовОснований(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьИДоступностьЭлементовНаКлиенте() Экспорт
	ОбновитьВидимостьИДоступностьЭлементов();
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьИДоступностьЭлементов()
	
	ИсточникДанных = ИсточникДанных();
	
	ОбновитьВидимостьЭлементов(ИсточникДанных);
	
	Элементы.Группа_Грузополучатель.ТолькоПросмотр = ЭтаФорма.Грузоотправитель_Грузополучатель;
	
	РольПолучатель = ЗначениеМетаданных("Saby_РолиКонтрагентов.Получатель");
	Элементы.ИнициаторПереадресацииПодтверждение.Доступность = ИсточникДанных.ИнициаторПереадресации = РольПолучатель;
	
	// Доступность доп команд по данным сервиса
	ОбновитьДоступностьДополнительныхКоманд();
	
	НеЯвляетсяОтправителем = ЭтаФорма.Грузоотправитель_НеЯвляетсяОтправителем;
	Элементы.Грузоотправитель_ОсуществляетОтгрузку.Доступность = Не НеЯвляетсяОтправителем;
	Элементы.Группа_ОтгрузкаДругоеЮрЛицоДанные.Доступность     = Не НеЯвляетсяОтправителем;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьЭлементов(ИсточникДанных)
	
	Неизвестен = ЗначениеМетаданных("Saby_ВладелецОбъектаОтгрузки.Неизвестен");
	
	Элементы.Владелец_НеизвестенПричина.Видимость  = (ИсточникДанных.Владелец_Тип = Неизвестен);
	Элементы.Группа_Заказчик.Видимость             = ИсточникДанных.Отправитель_Экспедитор;
	Элементы.Группа_Составитель.Видимость          = ЭтаФорма.Грузоотправитель_НеЯвляетсяОтправителем;
	Элементы.Группа_ОтгрузкаДругоеЮрЛицо.Видимость = Не ЭтаФорма.Грузоотправитель_ОсуществляетОтгрузку
		Или ЭтаФорма.Грузоотправитель_НеЯвляетсяОтправителем;
	
	// Владелец
	Если ИсточникДанных.Владелец_Тип = ЗначениеМетаданных("Saby_ВладелецОбъектаОтгрузки.Отправитель") Тогда
		
		Элементы.Группа_Владелец.Видимость            = Ложь;
		Элементы.Владелец_НеизвестенПричина.Видимость = Ложь;
		
	ИначеЕсли ИсточникДанных.Владелец_Тип = ЗначениеМетаданных("Saby_ВладелецОбъектаОтгрузки.ДругоеЮрЛицо") Тогда
		
		Элементы.Группа_Владелец.Видимость            = Истина;
		Элементы.Владелец_НеизвестенПричина.Видимость = Ложь;
		
	Иначе
		// Неизвестен
		Элементы.Группа_Владелец.Видимость            = Ложь;
		Элементы.Владелец_НеизвестенПричина.Видимость = Истина;
		
	КонецЕсли;
	
	ЭтоИсходящий = (ИсточникДанных.Направление = ЗначениеМетаданных("Saby_Направление.Исходящий"));
	Завершен     = ДОЗавершен();
	ЭтоЧерновик  = Не ЗначениеЗаполнено(АктивныйЭтапСтрокой) И Не Завершен;
	
	Если Не ЭтоИсходящий Тогда
		Элементы.ГруппаДокументОснование.Видимость = Ложь;
	Иначе
		Элементы.ГруппаДокументОснование.Видимость = ЭтоЧерновик Или ЗначениеЗаполнено(ДокументОснование);
	КонецЕсли;
	
	Элементы.НомерСбис.Видимость = ЭтаФорма.ЭтоВО Или Не ЭтоИсходящий Или ЗначениеЗаполнено(ИсточникДанных.НомерСбис);
	
	АдресПогрузкиЗаполнен = ЗначениеЗаполнено(ИсточникДанных.Погрузка_Адрес);
	Элементы.Погрузка_Адрес.Видимость = АдресПогрузкиЗаполнен;
	Элементы.ДекорацияЗаполнитьАдресПогрузки.Видимость = Не АдресПогрузкиЗаполнен;
	
	АдресДоставкиЗаполнен = ЗначениеЗаполнено(ИсточникДанных.Отправитель_АдресДоставки);
	Элементы.АдресДоставки.Видимость = АдресДоставкиЗаполнен;
	Элементы.ДекорацияЗаполнитьАдресДоставки.Видимость = Не АдресДоставкиЗаполнен;
	
КонецПроцедуры

&НаСервере
Функция ДОЗавершен()
	
	ДОЗавершенОтрицательно = 5;
	ДОЗавершенПоложительно = 6;
	
	Завершен = ИндексКартинки = ДОЗавершенОтрицательно
		Или ИндексКартинки = ДОЗавершенПоложительно;
	
	Возврат Завершен;
	
КонецФункции

&НаСервере
Функция БылаПриемкаИлиВыдача()
	
	РезультатФункции = Ложь;
	
	ОтборСтрок = Новый Структура("ТипТитула", ЗначениеМетаданных("Saby_ТипТитулаЭтрН.ПриемкаГруза"));
	НайденныеСтроки = Лента.НайтиСтроки(ОтборСтрок);
	Для Каждого СтрокаЛенты Из НайденныеСтроки Цикл
		РезультатФункции = РезультатФункции Или СтрокаЛенты.Период <> Дата(1, 1, 1, 0, 0, 0);
	КонецЦикла;
	
	ОтборСтрок = Новый Структура("ТипТитула", ЗначениеМетаданных("Saby_ТипТитулаЭтрН.ВыдачаГруза"));
	НайденныеСтроки = Лента.НайтиСтроки(ОтборСтрок);
	Для Каждого СтрокаЛенты Из НайденныеСтроки Цикл
		РезультатФункции = РезультатФункции Или СтрокаЛенты.Период <> Дата(1, 1, 1, 0, 0, 0);
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

&НаСервере
Процедура ТипОтметок_ПогрузкаТолькоПросмотр(ПараметрыЭтапов = Неопределено, ДоступныеРоли = Неопределено)
	
	Если ПараметрыЭтапов = Неопределено Тогда
		ПараметрыЭтапов = ПараметрыАктивныхЭтапов();
	КонецЕсли;
	Если ДоступныеРоли = Неопределено Тогда
		ДоступныеРоли = МодульКода("Документы.Saby_ТранспортнаяНакладная").РолиОрганизации(ИсточникДанных().ДанныеЮрЛиц);
	КонецЕсли;
	
	Если ПараметрыЭтапов.ПриемкаГруза Или ПараметрыЭтапов.ВыдачаГруза Тогда
		Возврат;
	КонецЕсли;
	
	РольОтправитель = ЗначениеМетаданных("Saby_РолиКонтрагентов.Отправитель");
	
	Если ТипОтметок_Погрузка = РольОтправитель Тогда
		Элементы.Группа_ОтметкиПогрузка.ТолькоПросмотр = Истина;
	Иначе
		Элементы.Группа_ОтметкиПогрузка.ТолькоПросмотр = ПараметрыЭтапов.Завершен Или Не ДоступныеРоли.Перевозчик;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ТипОтметок_ВыгрузкаТолькоПросмотр(ПараметрыЭтапов = Неопределено, ДоступныеРоли = Неопределено)
	
	Если ПараметрыЭтапов = Неопределено Тогда
		ПараметрыЭтапов = ПараметрыАктивныхЭтапов();
	КонецЕсли;
	Если ДоступныеРоли = Неопределено Тогда
		ДоступныеРоли = МодульКода("Документы.Saby_ТранспортнаяНакладная").РолиОрганизации(ИсточникДанных().ДанныеЮрЛиц);
	КонецЕсли;
	
	Если ПараметрыЭтапов.Черновик Или ПараметрыЭтапов.ПолучениеГруза Тогда
		Возврат;
	КонецЕсли;
	
	РольПолучатель = ЗначениеМетаданных("Saby_РолиКонтрагентов.Получатель");
	
	Если ПараметрыЭтапов.Завершен Тогда
		ПросмотрОтметок = Истина;
	ИначеЕсли ТипОтметок_Выгрузка = РольПолучатель Тогда
		ПросмотрОтметок = Не ДоступныеРоли.Получатель Или ПараметрыЭтапов.ВыдачаГруза;
	Иначе
		ПросмотрОтметок = Не ДоступныеРоли.Перевозчик Или ПараметрыЭтапов.ПриемкаГруза;
	КонецЕсли;
	
	Элементы.Группа_ОтметкиВыгрузкаТЧ.ТолькоПросмотр = ПросмотрОтметок;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступностьДополнительныхКоманд()
	
	ИсточникДанных = ИсточникДанных();
	
	Если Не ЗначениеЗаполнено(ИсточникДанных.Ссылка) Тогда
		// Кнопки по умолчанию отключены в доступности
		Возврат;
	КонецЕсли;
	
	ИзменитьСтоимостьДоступно         = Ложь;
	СогласоватьСтоимостьДоступно      = Ложь;
	ПереадресовкаДоступно             = Ложь;
	ЗаменаВодителяДоступно            = Ложь;
	УведомлениеОПереадресовкеДоступно = Ложь;
	
	ДопДействия = Новый Соответствие;
	Для Каждого Строка Из ИсточникДанных.ДоступныеДействия Цикл
		ДопДействия.Вставить(Строка.Действие, Истина);
	КонецЦикла;
	
	Если ДопДействия.Количество() Тогда
		
		ПереадресовкаДоступно = ДействиеДоступноНаТекущемЭтапе(
			ДопДействия, ЗначениеМетаданных("Saby_ДоступныеДействия.Переадресовка"));
		
		ЗаменаВодителяДоступно = ДействиеДоступноНаТекущемЭтапе(
			ДопДействия, ЗначениеМетаданных("Saby_ДоступныеДействия.ЗаменаВодителяТС"));
		
		УведомлениеОПереадресовкеДоступно = ДействиеДоступноНаТекущемЭтапе(
			ДопДействия, ЗначениеМетаданных("Saby_ДоступныеДействия.УведомлениеОПереадресовке"));
		
		ИзменитьСтоимостьДоступно = ДействиеДоступноНаТекущемЭтапе(
			ДопДействия, ЗначениеМетаданных("Saby_ДоступныеДействия.ИзменитьСтоимость")) И НЕ БылоИзменениеСтоимости();
		
	КонецЕсли;
	
	СтруктураОтправителя = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ДанныеЮрЛицаПоРоли(
		ИсточникДанных.ДанныеЮрЛиц, "Отправитель");
	
	// исключение из правил, это отдельный этап
	// должен приходить только на участника процесса - ТК и ГО, поэтому нужна отсечка по ГО
	ЭтоСогласованиеСтоимости = (ТекущийЭтап = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.СогласованиеСтоимости"));
	ЭтоОтправитель           = СтруктураОтправителя.Заполнена
		И МодульКода("Saby_ТНОбщегоНазначенияСервер").ОрганизацияПрисутствуетВБазе(
			СтруктураОтправителя.ИНН, СтруктураОтправителя.КПП);
		
	СогласоватьСтоимостьДоступно = ЭтоСогласованиеСтоимости И ЭтоОтправитель;
	
	// Доступность кнопок
	Элементы.ФормаПереадресовка.Доступность            = ПереадресовкаДоступно;
	Элементы.ФормаЗаменаВодителя.Доступность           = ЗаменаВодителяДоступно;
	Элементы.ФормаИзменитьСтоимость.Доступность        = ИзменитьСтоимостьДоступно;
	Элементы.ФормаСогласоватьСтоимость.Доступность     = СогласоватьСтоимостьДоступно;
	Элементы.ФормаПереадресовкаУведомление.Доступность = УведомлениеОПереадресовкеДоступно;
	
КонецПроцедуры

Функция БылоИзменениеСтоимости()
	
	ОтборСтрок = Новый Структура("ТипТитула", ЗначениеМетаданных("Saby_ТипТитулаЭтрН.ИзменениеСтоимости"));
	НайденныеСтроки = Лента.НайтиСтроки(ОтборСтрок);
	РезультатФункции = НайденныеСтроки.Количество() > 0;
	
	Возврат РезультатФункции;
	
КонецФункции

&НаСервере
Функция ДействиеДоступноНаТекущемЭтапе(ДопДействия, Действие)
	
	ЕстьДействие = ДопДействия.Получить(Действие);
	Если ЕстьДействие = Неопределено Тогда
		ДействиеДоступно = Ложь;
	Иначе
		ДействиеДоступно = Истина;
	КонецЕсли;
	
	Возврат ДействиеДоступно;
	
КонецФункции

&НаКлиенте
Процедура ОбменДаннымиСБИС(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ИмяКоманды = Результат;
	Иначе
		ИмяКоманды = ДополнительныеПараметры.ИмяКоманды;
	КонецЕсли;
	
	ИсточникДанных = ИсточникДанныхКлиент();
	
	Если ИмяКоманды = "ВыгрузитьВСбис" Тогда
		
		ЗаполнитьСтруктуруДокумента();
		ДанныеДляОбработки = Новый Массив;
		ПараметрыВыгрузки = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыВыгрузки(ИсточникДанных.Ссылка);
		ПараметрыВыгрузки.ПроверятьПоляСОХ = Грузоотправитель_НеЯвляетсяОтправителем;
		ПараметрыВыгрузки.ПроверятьПоляОтгрузчика = Не Грузоотправитель_ОсуществляетОтгрузку;
		Если ЭтаФорма.ЭтоВО Тогда
			ПараметрыВыгрузки.ВыгрузкаНаКлиенте = ЭтаФорма.ЭтоВО;
			ПараметрыВыгрузки.ДанныеДокумента = ИсточникДанных.Ссылка;
		КонецЕсли;
		ДанныеДляОбработки.Добавить(ПараметрыВыгрузки);
		
	Иначе
		
		ДанныеДляОбработки = Новый Массив;
		ПараметрыЗагрузки = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыЗагрузки(ИсточникДанных.Ссылка);
		Если ИмяКоманды = "ОбновитьАктивныйЭтап" Тогда
			ПараметрыЗагрузки.ТекущийТитул = ЭтаФорма.АктивныйЭтапСтрокой;
			ПараметрыЗагрузки.ТолькоАктивныйЭтап = Истина;
		КонецЕсли;
		ПараметрыЗагрузки.ИзДокумента = Истина;
		ПараметрыЗагрузки.ИмяМетаданных = "Saby_ТранспортнаяНакладная";
		ПараметрыЗагрузки.ЗагрузкаНаКлиенте = ЭтаФорма.ЭтоВО;
		ДанныеДляОбработки.Добавить(ПараметрыЗагрузки);
		
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ДвоичныеДанныеОбработки", ЭтаФорма.ДвоичныеДанныеОбработки);
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ВыполнитьКомандуСбис(
		ЭтаФорма, ИмяКоманды, ДанныеДляОбработки, ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТекстПодсказкиГрузоотправитель_ОсуществляетОтгрузку()
	
	Если Грузоотправитель_ОсуществляетОтгрузку Тогда
		ТекстПодсказки = "Отгрузку осуществляет грузоотправитель";
	Иначе
		ТекстПодсказки = "Отгрузку осуществляет лицо, отличное от грузоотправителя";
	КонецЕсли;
	
	Элементы.Грузоотправитель_ОсуществляетОтгрузку.Подсказка = ТекстПодсказки;
	
КонецПроцедуры

&НаСервере
Процедура УстановкаСпискаДоступныхТиповКИНаФорме()
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ОтправительКонтактныеДанныеТип");
	МассивЭлементов.Добавить("ПолучательКонтактныеДанныеТип");
	МассивЭлементов.Добавить("ПеревозчикКонтактныеДанныеТип");
	МассивЭлементов.Добавить("ВладелецОбъектаКонтактныеДанныеТип");
	МассивЭлементов.Добавить("ОтгрузчикКонтактныеДанныеТип");
	
	МодульКода("Saby_ТНОбщегоНазначенияСервер").УстановкаСпискаДоступныхТиповКИ(ЭтаФорма, МассивЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоОшибкамНаКлиенте(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьДанныеПоОшибкамНаСервере();
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").РасшифровкаЗаписиТаблицыОшибок(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПоОшибкамНаСервере(Знач Ошибки = Неопределено) Экспорт
	
	ЗаполнитьСтруктуруДокумента();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Ссылка",                  ИсточникДанных().Ссылка);
	ДополнительныеПараметры.Вставить("ПроверятьПоляСОХ",        Грузоотправитель_НеЯвляетсяОтправителем);
	ДополнительныеПараметры.Вставить("ПроверятьПоляОтгрузчика", Не Грузоотправитель_ОсуществляетОтгрузку);
	
	МодульКода("Saby_ТНОбщегоНазначенияСервер").ОбновитьОшибки(
		ЭтаФорма,
		ДополнительныеПараметры,
		Ошибки);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВнешнийВидЭлементовОшибокНаСервере()
	
	МодульКода("Saby_ТНОбщегоНазначенияСервер").ОбновитьВнешнийВидЭлементовОшибок(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДекорациюНаСервере(Знач ПараметрыВыделенияДекорации) Экспорт
	
	МодульКода("Saby_ТНОбщегоНазначенияСервер").ОбновитьДекорацию(ЭтаФорма, ПараметрыВыделенияДекорации);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораОтветственногоНаОсновании(Знач НовоеЗначение = Неопределено) Экспорт
	
	ПараметрыЭтапов = ПараметрыАктивныхЭтапов();
	Если ЭтаФорма.ЭтоВО И Не ПараметрыЭтапов.Черновик Тогда
		ЗаполненноеЗначение = ЭтаФорма.Отгрузка_ОтветственныйНаОсновании;
	Иначе
		ЗаполненноеЗначение = НовоеЗначение;
	КонецЕсли;
	
	Элементы.Отгрузка_ОтветственныйНаОсновании.СписокВыбора.Очистить();
	
	Элементы.Отгрузка_ОтветственныйНаОсновании.СписокВыбора.Добавить("Должностные обязанности");
	Если ЗаполненноеЗначение = Неопределено Тогда
		Элементы.Отгрузка_ОтветственныйНаОсновании.СписокВыбора.Добавить(
			ЗначениеМетаданных("Saby_ДокументыЭПД.ПустаяСсылка", "Справочник"), "Документ");
	Иначе
		Элементы.Отгрузка_ОтветственныйНаОсновании.СписокВыбора.Добавить(ЗаполненноеЗначение, "Документ");
	КонецЕсли;
	
	// дефолтное значение
	Если ЭтаФорма.ЭтоВО Тогда
		Если ЗаполненноеЗначение <> Неопределено Тогда
			ЭтаФорма.Отгрузка_ОтветственныйНаОсновании = ЗаполненноеЗначение;
		Иначе
			ЭтаФорма.Отгрузка_ОтветственныйНаОсновании = "Должностные обязанности";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьВодителейДляВебКлиента()
	
	#Если ВебКлиент Тогда
		
		Элементы.Группа_Водители.Поведение                = ПоведениеОбычнойГруппы.Авто;
		Элементы.Группа_Водители.РастягиватьПоГоризонтали = Истина;
		Элементы.Группа_Водители.ОтображатьЗаголовок      = Ложь;
		Элементы.ВодителиСтрокой.Видимость                = Ложь;
		
	#КонецЕсли
	
КонецПроцедуры

#Область ИсторияСобытий

&НаКлиенте
Функция ПараметрыФормыРасшифровки(ИдентификаторТитула, НаименованиеТитула)
	
	ИсточникДанных = ИсточникДанныхКлиент();
	
	РезультатФункции = ШаблонРезультата();
	РезультатФункции.ТипТитула = НаименованиеТитула;
	
	РезультатФункции.ИмяФормы            = "ФормаДинамическихТитулов";
	РезультатФункции.СсылкаНаДокумент    = ИсточникДанных.Ссылка;
	РезультатФункции.ИдентификаторТитула = ИдентификаторТитула;
	
	РезультатФункции.ДоступныеДействия.Вставить(
		"УведомлениеОПереадресовкеДоступно", Элементы.ФормаПереадресовкаУведомление.Доступность);
	РезультатФункции.ДоступныеДействия.Вставить(
		"ПереадресовкаДоступна",             Элементы.ФормаПереадресовка.Доступность);
	РезультатФункции.ДоступныеДействия.Вставить(
		"ЗаменаВодителяДоступна",            Элементы.ФормаЗаменаВодителя.Доступность);
	РезультатФункции.ДоступныеДействия.Вставить(
		"ИзменениеСтоимостиДоступно",        Элементы.ФормаИзменитьСтоимость.Доступность);
	РезультатФункции.ДоступныеДействия.Вставить(
		"СогласованиеСтоимостиДоступно",     Элементы.ФормаСогласоватьСтоимость.Доступность);
	
	ЗаполнитьДанныеТитулов(РезультатФункции);
	
	Если НаименованиеТитула = "ИзменениеСтоимости" Или НаименованиеТитула = "СогласованиеСтоимости" Тогда
		
		РезультатФункции.Вставить(
			"СтруктураОтправителя",
			МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ДанныеЮрЛицаПоРоли(ИсточникДанных.ДанныеЮрЛиц, "Отправитель"));
			
		РезультатФункции.Вставить(
			"СтруктураПеревозчика",
			МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ДанныеЮрЛицаПоРоли(ИсточникДанных.ДанныеЮрЛиц, "Перевозчик"));
		
	ИначеЕсли НаименованиеТитула = "УведомлениеОУточнении" Тогда
		
		РезультатФункции.ИмяФормы = "СтандартнаяФормаВвода";
		
	Иначе
		Возврат РезультатФункции;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

&НаКлиенте
Функция ШаблонРезультата()
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ИмяФормы",                "");
	РезультатФункции.Вставить("СсылкаНаДокумент",        "");
	РезультатФункции.Вставить("ИдентификаторТитула",     "");
	РезультатФункции.Вставить("ТипТитула",               "");
	РезультатФункции.Вставить("ДоступныеДействия",       Новый Структура);
	РезультатФункции.Вставить("ДанныеТитулов",           Неопределено);
	РезультатФункции.Вставить("ДвоичныеДанныеОбработки", ЭтаФорма.ДвоичныеДанныеОбработки);
	
	Возврат РезультатФункции;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуТитула(НаименованиеТитула, ИдентификаторТитула = "", Описание = "") Экспорт
	
	ПараметрыФормы = ПараметрыФормыРасшифровки(ИдентификаторТитула, НаименованиеТитула);
	
	Если НаименованиеТитула = "УведомлениеОУточнении" Тогда		
		ПоказатьЗначение(, Описание);
	Иначе
		
		ПараметрыОткрытияФормыОбработки = ПараметрыОткрытияФормыОбработки(
			"Документ.Saby_ТранспортнаяНакладная.Форма.ФормаДинамическихТитулов", ПараметрыФормы, ЭтаФорма);
		ПараметрыОткрытияФормыОбработки.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФормуОбработки(ПараметрыОткрытияФормыОбработки);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ИсторияСобытий

#Область Лента

&НаКлиенте
Процедура ЛентаВлево(Команда)
	
	СдвинутьЛентуНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛентаВправо(Команда)
	
	СдвинутьЛентуНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияТитулОбработкаНавигационнойСсылки(Элемент,
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ОбработкаНавигационнойСсылкиЛенты(ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьРеквизитИзТитула(СтрокаЛенты) Экспорт
	
	Если ЗначениеЗаполнено(СтрокаЛенты.Описание) Тогда
		ПоказатьЗначение(, СтрокаЛенты.Описание);
	КонецЕсли;
	
	Если СтрокаЛенты.НаименованиеТитула = "Погрузка" Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.ОтправительСтрокой;
	ИначеЕсли СтрокаЛенты.НаименованиеТитула = "ПолучениеГруза" Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.Приемка_ДатаПрибыл;
	ИначеЕсли СтрокаЛенты.НаименованиеТитула = "ПриемкаГруза" Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.Выгрузка_Дата;
	ИначеЕсли СтрокаЛенты.НаименованиеТитула = "ВыдачаГруза" Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.Перевозчик_ВыгрузкаДатаПрибыл;
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТитулОтрицательноЗавершен(СтрокаЛенты) Экспорт
	
	Если СтрокаЛенты.ТипТитула = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.ПриемкаГруза")
		Или СтрокаЛенты.ТипТитула = ЗначениеМетаданных("Saby_ТипТитулаЭтрН.ВыдачаГруза") Тогда
		РезультатФункции = ЗначениеЗаполнено(СтрокаЛенты.Описание);
	Иначе
		РезультатФункции = СтрокаЛенты.НаименованиеТитула = "УведомлениеОУточнении";
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

&НаСервере
Процедура СдвинутьЛентуНаСервере(Знач Вправо)
	
	МодульКода("Saby_ТНОбщегоНазначенияСервер").СдвинутьЛенту(ЭтаФорма, Вправо);
	
КонецПроцедуры

#КонецОбласти // Лента

&НаКлиенте
Процедура ПослеОбновленияДанныхТранспортныхСредств(Результат, ДополнительныеПараметры) Экспорт
	
	ПослеОбновленияДанныхТранспортныхСредствНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеОбновленияДанныхТранспортныхСредствНаСервере()
	
	МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер").ОбновитьТранспортныеСредстваНаФорме(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКодФилиалаНаФорме(Знач КодФилиала = Неопределено)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Роль", ЗначениеМетаданных("Saby_РолиКонтрагентов.Отправитель"));
	
	НайденныеСтроки = ИсточникДанных().ДанныеЮрЛиц.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаОтправителя = НайденныеСтроки[0];
	Если КодФилиала <> Неопределено Тогда
		СтрокаОтправителя.КодФилиала = КодФилиала;
	КонецЕсли;
	
	ЭтаФорма.КодФилиала = СтрокаОтправителя.КодФилиала;
	
КонецПроцедуры

#Область ДанныеЮрЛиц

&НаКлиенте
Процедура ЗаполнитьЮрЛицоКопированием(РольСтрокойПриемник, РольСтрокойИсточник)
	
	ИсточникДанных = ИсточникДанныхКлиент();
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	СтруктураИсточник = МодульТНОбщегоНазначенияКлиентСервер.ДанныеЮрЛицаПоРоли(
		ИсточникДанных.ДанныеЮрЛиц, РольСтрокойИсточник);
		
	СтруктураПриемник = МодульТНОбщегоНазначенияКлиентСервер.ДанныеЮрЛицаПоРоли(
		ИсточникДанных.ДанныеЮрЛиц, РольСтрокойПриемник);
		
	РольОтправитель = ЗначениеМетаданныхКлиент("Saby_ВладелецОбъектаОтгрузки.Отправитель");
		
	ЗаполнениеРазрешено = Истина;
	Если РольСтрокойПриемник = "ВладелецОбъекта" Тогда
		ЗаполнениеРазрешено = СтруктураИсточник.Заполнена
			И Не СтруктураПриемник.Заполнена
			И ИсточникДанных.Владелец_Тип = РольОтправитель;
	КонецЕсли;
	
	Если Не ЗаполнениеРазрешено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьЮрЛицо(РольСтрокойПриемник);
	
	МодульТНОбщегоНазначенияКлиентСервер.СкопироватьДанныеЮрЛица(
		ИсточникДанных, СтруктураПриемник, СтруктураИсточник);
	
	ЭтаФорма[РольСтрокойПриемник + "Строкой"] = МодульТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли(
		СтруктураИсточник.НаименованиеОрганизации);
	
	РолиЮрЛиц    = МодульТНОбщегоНазначенияКлиентСервер.РолиЮрЛиц(ЭтаФорма, РольСтрокойПриемник);
	ДопПараметры = МодульТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(РолиЮрЛиц);
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(ЭтаФорма, ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЮрЛицо(РольСтрокой)
	
	ИсточникДанных = ИсточникДанныхКлиент();
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	МодульТНОбщегоНазначенияКлиентСервер.УдалитьСтрокиПоРоли(ИсточникДанных.ДанныеЮрЛиц,      РольСтрокой);
	МодульТНОбщегоНазначенияКлиентСервер.УдалитьСтрокиПоРоли(ИсточникДанных.КонтактныеДанные, РольСтрокой);
	
	ЭтаФорма[РольСтрокой + "Строкой"] = МодульТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли("");
	
	ДопПараметры = МодульТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(РольСтрокой);
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьОсновнойНомерТелефона(ЭтаФорма, ДопПараметры);
	
КонецПроцедуры

#КонецОбласти // ДанныеЮрЛиц

&НаКлиенте
Процедура ПослеРедактированияОтветственногоЛицаНаФорме(СтруктураОтветственногоЛица) Экспорт
	
	Роль = СтруктураОтветственногоЛица.Роль;
	
	Если Роль = ЗначениеМетаданныхКлиент("Saby_РолиОтветственных.Ответственный") Тогда
		ЭтаФорма.ОтветственныйОтгрузка = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПредставлениеФИО(
			СтруктураОтветственногоЛица);
		Если Не ЗначениеЗаполнено(ЭтаФорма.ОтветственныйОтгрузка) Тогда
			ЭтаФорма.ОтветственныйОтгрузка = "Заполнить";
		КонецЕсли;
	КонецЕсли;
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьАдресаПогрузкиВыгрузки(РольСтрокой)

	ИсточникДанных = ИсточникДанныхКлиент();
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	РолиЮрЛиц = МодульТНОбщегоНазначенияКлиентСервер.РолиЮрЛиц(ЭтаФорма, РольСтрокой);
	
	ДопПараметры = МодульТНОбщегоНазначенияКлиентСервер.ДопПараметрыОбновленияКИ(
		РолиЮрЛиц,
		ИсточникДанных.ДанныеЮрЛиц,
		ИсточникДанных.КонтактныеДанные);
	
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьАдресаПогрузкиДоставки(ЭтаФорма, ДопПараметры);
	
	ОбновитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Функция СоответствиеТаблицФормыИОбъекта()
	
	СоответствиеТаблицФормыИОбъекта = Новый Соответствие;
	СоответствиеТаблицФормыИОбъекта.Вставить("Водители", "ОтветственныеЛица");
	СоответствиеТаблицФормыИОбъекта.Вставить("Прицепы",  "ТранспортныеСредства");
	
	Возврат СоответствиеТаблицФормыИОбъекта;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьОшибкуЗаполнения()
	
	СоответствиеТаблицФормыИОбъекта = СоответствиеТаблицФормыИОбъекта();
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").РасшифровкаЗаписиТаблицыОшибок(
		ЭтаФорма, 
		СоответствиеТаблицФормыИОбъекта);
	
КонецПроцедуры

&НаСервере
Функция ЗначениеПоУмолчанию(ТипЗначения)
	
	Если ТипЗначения.СодержитТип(Тип("ТаблицаЗначений")) Тогда
		Значение = Новый Массив;
	ИначеЕсли ТипЗначения.СодержитТип(Тип("Число")) Тогда
		Значение = 0;
	ИначеЕсли ТипЗначения.СодержитТип(Тип("Строка")) Тогда
		Значение = "";
	ИначеЕсли ТипЗначения.СодержитТип(Тип("Булево")) Тогда
		Значение = Ложь;
	Иначе
		Значение = Неопределено;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

&НаСервере
Процедура ПрочитатьИзСтруктуры(СтруктураДанных)
	
	Исключения = Новый Соответствие;
	Исключения.Вставить("Объект",                    Истина);
	Исключения.Вставить("Ссылка",                    Истина);
	Исключения.Вставить("ИмяМетаданных",             Истина);
	Исключения.Вставить("Комментарий",               Истина);
	Исключения.Вставить("ДатаИзменения",             Истина);
	Исключения.Вставить("Валюта",                    Истина);
	Исключения.Вставить("СопроводительныеДокументы", Истина);
	Исключения.Вставить("Водители",                  Истина);
	Исключения.Вставить("СоставительСтрокой",        Истина);
	Исключения.Вставить("Номер",                     Истина);
	Исключения.Вставить("ПутевыеЛисты",              Истина);
	Исключения.Вставить("ПроверятьПоляОтгрузчика",   Истина);
	
	Для Каждого КлючЗначение Из СтруктураДанных Цикл
		
		ИмяРеквизита = КлючЗначение.Ключ;
		Значение     = КлючЗначение.Значение;
		
		Если Исключения[ИмяРеквизита] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(Значение) = Тип("Массив") Тогда
			ЭтаФорма[ИмяРеквизита].Очистить();
			Для Каждого ЭлементМассива Из Значение Цикл
				НоваяСтрокаДанных = ЭтаФорма[ИмяРеквизита].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаДанных, ЭлементМассива);
			КонецЦикла;
		Иначе
			ЭтаФорма[ИмяРеквизита] = Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	ЭтаФорма.Ссылка = СтруктураДанных;
	
	// Заполнение ключей	
	ТНКлиентСервер = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	ЭтаФорма.МаксимальныйКлючСтроки_ДокументыЭПД = ТНКлиентСервер.НовыйКлючОсновнойСтроки(ЭтаФорма, "ДокументыЭПД") - 1;
	
	ЭтаФорма.МаксимальныйКлючСтроки_Отметки = ТНКлиентСервер.НовыйКлючОсновнойСтроки(ЭтаФорма, "Отметки") - 1;
	
	ЭтаФорма.МаксимальныйКлючСтроки_ТранспортныеСредства = ТНКлиентСервер.НовыйКлючОсновнойСтроки(
		ЭтаФорма, "ТранспортныеСредства") - 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДатыПогрузки(ИсточникДанных)
	
	СтруктураДатыПогрузки = Новый Структура;
	Если Не ЗначениеЗаполнено(ИсточникДанных.Погрузка_ДатаВремя) Тогда
		СтруктураДатыПогрузки.Вставить("Погрузка_ДатаВремя", ИсточникДанных.Дата);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИсточникДанных.Погрузка_ДатаВремяПрибыл) Тогда
		СтруктураДатыПогрузки.Вставить("Погрузка_ДатаВремяПрибыл", ИсточникДанных.Дата);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ИсточникДанных, СтруктураДатыПогрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИныхПолучателей()
	
	Если ЭтаФорма.ЭтоВО Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьИныхПолучателейНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИныхПолучателейНаСервере()
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	ДанныеЮрЛиц.Роль КАК Роль,
	|	ДанныеЮрЛиц.ИНН КАК ИНН,
	|	ДанныеЮрЛиц.КПП КАК КПП
	|ПОМЕСТИТЬ ДанныеЮрЛицДокумента
	|ИЗ
	|	&ДанныеЮрЛиц КАК ДанныеЮрЛиц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Saby_ТранспортнаяНакладная.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОтправленныеДокументыЗаПериод
	|ИЗ
	|	РегистрСведений.Saby_Состояние КАК Saby_Состояние
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Saby_ТранспортнаяНакладная КАК Saby_ТранспортнаяНакладная
	|		ПО Saby_Состояние.Объект = Saby_ТранспортнаяНакладная.Ссылка
	|			И (Saby_ТранспортнаяНакладная.МоментВремени < &МоментВремениДокумента)
	|			И (Saby_ТранспортнаяНакладная.Дата >= &ДатаОграниченияПолученияДокументов)
	|			И (Saby_Состояние.Состояние <> ЗНАЧЕНИЕ(Справочник.Saby_СостоянияОбъектов.ПустаяСсылка))
	|			И (Saby_Состояние.UID <> """")
	|			И (Saby_ТранспортнаяНакладная.Направление = ЗНАЧЕНИЕ(Перечисление.Saby_Направление.Исходящий))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Ссылка.МоментВремени КАК МоментВремени,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Ссылка КАК Ссылка,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Ссылка.Дата КАК Дата
	|ПОМЕСТИТЬ ДокументыОтправителя
	|ИЗ
	|	Документ.Saby_ТранспортнаяНакладная.ДанныеЮрЛиц КАК Saby_ТранспортнаяНакладнаяДанныеЮрЛиц
	|ГДЕ
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Ссылка В
	|			(ВЫБРАТЬ
	|				ОтправленныеДокументыЗаПериод.Ссылка КАК Ссылка
	|			ИЗ
	|				ОтправленныеДокументыЗаПериод КАК ОтправленныеДокументыЗаПериод)
	|	И (Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Роль, Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.ИНН, Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.КПП) В
	|			(ВЫБРАТЬ
	|				ДанныеЮрЛицДокумента.Роль КАК Роль,
	|				ДанныеЮрЛицДокумента.ИНН КАК ИНН,
	|				ДанныеЮрЛицДокумента.КПП КАК КПП
	|			ИЗ
	|				ДанныеЮрЛицДокумента КАК ДанныеЮрЛицДокумента
	|			ГДЕ
	|				ДанныеЮрЛицДокумента.Роль = ЗНАЧЕНИЕ(Перечисление.Saby_РолиКонтрагентов.Отправитель))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Ссылка.МоментВремени КАК МоментВремени
	|ПОМЕСТИТЬ ДокументыПолучателя
	|ИЗ
	|	Документ.Saby_ТранспортнаяНакладная.ДанныеЮрЛиц КАК Saby_ТранспортнаяНакладнаяДанныеЮрЛиц
	|ГДЕ
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Ссылка В
	|			(ВЫБРАТЬ
	|				ОтправленныеДокументыЗаПериод.Ссылка КАК Ссылка
	|			ИЗ
	|				ОтправленныеДокументыЗаПериод КАК ОтправленныеДокументыЗаПериод)
	|	И (Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Роль, Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.ИНН, Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.КПП) В
	|			(ВЫБРАТЬ
	|				ДанныеЮрЛицДокумента.Роль КАК Роль,
	|				ДанныеЮрЛицДокумента.ИНН КАК ИНН,
	|				ДанныеЮрЛицДокумента.КПП КАК КПП
	|			ИЗ
	|				ДанныеЮрЛицДокумента КАК ДанныеЮрЛицДокумента
	|			ГДЕ
	|				ДанныеЮрЛицДокумента.Роль = ЗНАЧЕНИЕ(Перечисление.Saby_РолиКонтрагентов.Получатель))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ДокументыОтправителя.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ ПоследнийДокументОтправителяИПолучателя
	|ИЗ
	|	ДокументыОтправителя КАК ДокументыОтправителя
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПолучателя КАК ДокументыПолучателя
	|		ПО ДокументыОтправителя.МоментВремени = ДокументыПолучателя.МоментВремени
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Роль КАК Роль,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.ИНН КАК ИНН,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.КПП КАК КПП,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.ОГРН КАК ОГРН,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.ЮрФизЛицо КАК ЮрФизЛицо,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.СтруктураФИО КАК СтруктураФИО,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Адрес КАК Адрес,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.АдресСтруктура КАК АдресСтруктура,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.СтранаРегистрации КАК СтранаРегистрации,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.КодФилиала КАК КодФилиала,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.НаименованиеОрганизации КАК НаименованиеОрганизации,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Основание КАК Основание,
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.ИдентификаторЭДО КАК ИдентификаторЭДО
	|ИЗ
	|	Документ.Saby_ТранспортнаяНакладная.ДанныеЮрЛиц КАК Saby_ТранспортнаяНакладнаяДанныеЮрЛиц
	|ГДЕ
	|	Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Ссылка В
	|			(ВЫБРАТЬ
	|				ПоследнийДокументОтправителяИПолучателя.Ссылка КАК Ссылка
	|			ИЗ
	|				ПоследнийДокументОтправителяИПолучателя КАК ПоследнийДокументОтправителяИПолучателя)
	|	И Saby_ТранспортнаяНакладнаяДанныеЮрЛиц.Роль = ЗНАЧЕНИЕ(Перечисление.Saby_РолиКонтрагентов.ИнойПолучатель)";
	
	СсылкаНаДокумент = Неопределено;
	Если ЗначениеЗаполнено(ЭтаФорма.Объект.Ссылка) Тогда
		СсылкаНаДокумент = ЭтаФорма.Объект.Ссылка;
	КонецЕсли;
	
	ДатаДокумента = ЭтаФорма.Объект.Дата;
	Если Не ЗначениеЗаполнено(ДатаДокумента) Или НачалоДня(ДатаДокумента) = ДатаДокумента Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ЗапросДанных.УстановитьПараметр("ДанныеЮрЛиц",                        ЭтаФорма.Объект.ДанныеЮрЛиц.Выгрузить());
	ЗапросДанных.УстановитьПараметр("МоментВремениДокумента",             Новый МоментВремени(ДатаДокумента, СсылкаНаДокумент));
	// Дата ограничения нужна для того, чтобы не росло время выполнения запроса при росте количества документов.
	ЗапросДанных.УстановитьПараметр("ДатаОграниченияПолученияДокументов", ДобавитьМесяц(ТекущаяДатаСеанса(), - 3));
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКода("Saby_ТНОбщегоНазначенияКлиентСервер");
	
	ОтборИныхПолучателей = Новый Структура("Роль", ЗначениеМетаданных("Saby_РолиКонтрагентов.ИнойПолучатель"));
	ИныеПолучатели = МодульТНОбщегоНазначенияКлиентСервер.НайтиСтрокиУниверсально(
		ЭтаФорма.Объект.ДанныеЮрЛиц, ОтборИныхПолучателей);
	
	Для Каждого СтрокаДляУдаления Из ИныеПолучатели Цикл
		ЭтаФорма.Объект.ДанныеЮрЛиц.Удалить(СтрокаДляУдаления);
	КонецЦикла;
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Пока ВыборкаДанных.Следующий() Цикл
		СтрокаЮрЛица = ЭтаФорма.Объект.ДанныеЮрЛиц.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЮрЛица, ВыборкаДанных);
		СтрокаЮрЛица.КлючСтроки = МодульТНОбщегоНазначенияКлиентСервер.НовыйКлючОсновнойСтроки(ЭтаФорма.Объект, "ДанныеЮрЛиц");
	КонецЦикла;
	
	МодульТНОбщегоНазначенияКлиентСервер.ОбновитьЭлементыИныхПолучателей(ЭтаФорма);
	
КонецПроцедуры

#Область include_etrn_base_Document_ТранспортнаяНакладная_DocumentForm_ФормаДокумента_СлужебныеПроцедурыИФункции
#КонецОбласти // include_etrn_base_Document_ТранспортнаяНакладная_DocumentForm_ФормаДокумента_СлужебныеПроцедурыИФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
