
#Область ПрограммныйИнтерфейс

Функция ЗагрузкаДокументовПоИзменениям(СтруктураПараметров) Экспорт
		
	КоличествоЗагружено = 0;
	ЗагрузкаПрошлаУспешно = Истина;
	
	// Изменения для последующей обработки
	СписокИзменений = СписокИзмененийДокументов(СтруктураПараметров);
	Если СписокИзменений <> Неопределено Тогда 
		
		// Создание новых или изменение существующих документов
		ОбработатьИзмененияДокументов(СписокИзменений, КоличествоЗагружено);
		
	КонецЕсли;
	
	Если КоличествоЗагружено <> СписокИзменений.Количество() Тогда
		ЗагрузкаПрошлаУспешно = Ложь;
	КонецЕсли;	
			
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Количество",        КоличествоЗагружено);
	СтруктураДанных.Вставить("Успешно",           ЗагрузкаПрошлаУспешно);
	СтруктураДанных.Вставить("ВсегоДокументов",   СписокИзменений.Количество());
	СтруктураДанных.Вставить("УспешныеДокументы", Новый Массив);
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция ЗаписатьДокументВСбис(МассивДокументы, context_params, ДопПараметры = Неопределено) Экспорт
	
	КоличествоЗагружено       = 0;
	УспешныеДокументы         = Новый Массив;
	СписокДокументовСОшибками = Новый Соответствие;
	Этап                      = Неопределено;
	ТекущийЭтап               = Неопределено;
	ВыполнитьПереход          = Ложь; 
	
	Если ДопПараметры <> Неопределено Тогда 
		ЭтоОтрицательныйПереход = ДопПараметры.Свойство("ДанныеТитула")
									И ДопПараметры.Свойство("ОтрицательныйПереход")
								  	И ДопПараметры.ОтрицательныйПереход;
	Иначе 
		ЭтоОтрицательныйПереход = Ложь;						
	КонецЕсли;
								
	Для Каждого Ссылка Из МассивДокументы Цикл
		
		// ФЛК
		Ошибки = ОшибкиФЛК(Ссылка, ДопПараметры);
		Если ЗначениеЗаполнено(Ошибки) Тогда
			СписокДокументовСОшибками.Вставить(Ссылка, Ошибки);
			Продолжить;
		КонецЕсли;
		
		Ошибки = Неопределено;
				
		/////////// НУЖЕН ЭТАП
		//////////////////////
				
		ОбъектОбработки = Обработки.SABY.Создать();
		
		Если Не ЭтоОтрицательныйПереход Тогда
			
			Попытка
				
				// 1. Выгрузим наш документ в сбис формата json и получим xml вложения
				//    в результате получим данные для создания документа в сбис
				ОтветXML = РезультатПолученияXMLИзСбис(Ссылка, ДопПараметры, context_params, ОбъектОбработки);
				ТекущийЭтап = ОтветXML.Этап;
				Если Не ЗначениеЗаполнено(ОтветXML) Тогда  
					
					ТекстОшибки = "Не сформирован файл json для генерации вложения!";
					ДобавитьОшибкуПользователю(Ошибки, Неопределено, ТекстОшибки);
					
					СписокДокументовСОшибками.Вставить(Ссылка, Ошибки); 			
					Продолжить;
					
				КонецЕсли;	
				
				ИдСбис = ОтветXML.ИдСбис;
				
				УстановитьИД   = Ложь;
				УстановитьЭтап = Ложь;
				
				// 2. Отправим в сбис полученное вложение документа
				//    передадим полученный ранее xml в формате двоичных данных		       
			
				
				// на 1 этапе записываем документ, далее прикрепляем вложения
				Если ТекущийЭтап = "1110339" Тогда  
					
					ДокСбис      = ЗаписатьНовыйДокументВСбис(ОтветXML, context_params, ОбъектОбработки);
					ИДСбис       = ДокСбис["Идентификатор"];
					УстановитьИД = Истина;
					
					Этап = ДокСбис["Этап"];
					
				Иначе 	
					// для существующего документа пишем только вложение
					РезультатВложения = ЗаписатьВложениеДляДокументаСбис(ОтветXML.Файл, ИДСбис, context_params, ОбъектОбработки); 
					Этап = РезультатВложения["Этап"];
					
				КонецЕсли;
				
				// Динамические титулы не возвращают переходов
				// нужно сделать переход самостоятельно
				Если ТекущийЭтап = "1110343" 
					Или ТекущийЭтап = "1110344" 
					Или ТекущийЭтап = "1167009"
					Или ТекущийЭтап = "1110345" 
					Или ТекущийЭтап = "1110346" Тогда
					
					ВыполнитьПереход = Истина;  
					
				Иначе 
					
					// если титул не динамический
					// исключаем не нужные переходы
					УдалитьЛишниеДействия(Этап);
					
					СтруктураОтвет = ДанныеУспешнойЗагрузкиДокумента(Ссылка, Этап, Ложь, КоличествоЗагружено);
					УспешныеДокументы.Добавить(СтруктураОтвет);				
					
					УстановитьЭтап = Истина;	
					
				КонецЕсли;
				
			Исключение
				
				ИнфоОшибки = ИнформацияОбОшибке();
				ТекстОшибки = ПричинаОшибки(ИнфоОшибки.Описание);					
				ДобавитьОшибкуПользователю(Ошибки, Неопределено, ТекстОшибки);     
				
				СписокДокументовСОшибками.Вставить(Ссылка, Ошибки);				
				Продолжить;
				
			КонецПопытки; 
			
		Иначе 	
			
			ВыполнитьПереход = Истина;
			
			Если ДопПараметры.Этап = "Согласовать стоимость" Тогда
				ТекущийЭтап = "1110346";
			КонецЕсли;
			
			УстановитьИД   = Ложь;
			УстановитьЭтап = Ложь;
			
			ДанныеСостояния = РегистрыСведений.Saby_Состояние.ПрочитатьПоОбъекту(Ссылка);
			ИДСбис          = ДанныеСостояния.UID;
			
		КонецЕсли;
				
		// 3. По динамическим титулам 
		//    идем в обход общей формы выполнить действие
		Если ВыполнитьПереход Тогда 
			
			/// ПРИ ОТРИЦАТЕЛЬНОМ ПЕРЕХОДЕ ЭТАП НАДО ПОЛУЧИТЬ СРАЗУ.........
						
			ДинамическийТитул = Saby_ТНОбщегоНазначенияСервер.ЗначениеТитула(ТекущийЭтап, Истина);
			
			ПараметрыВыполнения = Новый Структура;
			ПараметрыВыполнения.Вставить("ДокументыСОшибками",   СписокДокументовСОшибками);
			ПараметрыВыполнения.Вставить("context_params",       context_params);
			ПараметрыВыполнения.Вставить("ОбъектОбработки",      ОбъектОбработки);
			ПараметрыВыполнения.Вставить("Ссылка",               Ссылка);
			ПараметрыВыполнения.Вставить("ИД",                   ИДСбис);
			ПараметрыВыполнения.Вставить("Ошибки",               Ошибки);
			ПараметрыВыполнения.Вставить("Этап",                 Этап);
			ПараметрыВыполнения.Вставить("ТекущийЭтап",          ТекущийЭтап);
			ПараметрыВыполнения.Вставить("ДинамическийТитул",    СокрЛП(ДинамическийТитул));
			ПараметрыВыполнения.Вставить("ОтрицательныйПереход", ЭтоОтрицательныйПереход);
			
			Если ДопПараметры.Свойство("ПричинаОтрицательногоПерехода") Тогда
				ПараметрыВыполнения.Вставить("ПричинаОтрицательногоПерехода", ДопПараметры.ПричинаОтрицательногоПерехода);
			Иначе
				ПараметрыВыполнения.Вставить("ПричинаОтрицательногоПерехода", "");
			КонецЕсли;
			
			ВыполнитьПереходПоДинамическомуТитулу(ПараметрыВыполнения);
			
			// Успешно выполнили переход по динамическому списку 
			Если Не ЗначениеЗаполнено(Ошибки) Тогда   
								
				Этап = ПараметрыВыполнения.Этап;
				СтруктураОтвет = ДанныеУспешнойЗагрузкиДокумента(Ссылка, Этап, Истина, КоличествоЗагружено);
				УспешныеДокументы.Добавить(СтруктураОтвет);
				
			КонецЕсли;
			
		КонецЕсли;	
		
		// Запишем данные в регистр по итогам выполнения команд
		Если УстановитьЭтап Или УстановитьИД Тогда
			
			Параметры = Новый Структура;
			Параметры.Вставить("ИД",           ИдСбис);
			Параметры.Вставить("АктивныйЭтап", ТекущийЭтап);
			Параметры.Вставить("Ссылка",       Ссылка);
			Параметры.Вставить("Загрузка",     Истина);
			Параметры.Вставить("Состояние",    Неопределено);
			Параметры.Вставить("СбисИд",       Неопределено);
			
			УстановитьИДИСтатус(Параметры);
						
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаписьОшибокВЖурналРегистрации(СписокДокументовСОшибками);
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Количество",           КоличествоЗагружено);
	СтруктураДанных.Вставить("Успешно",              Не ЗначениеЗаполнено(СписокДокументовСОшибками));
	СтруктураДанных.Вставить("ВсегоДокументов",      МассивДокументы.Количество());
	СтруктураДанных.Вставить("СписокДокументов",     СписокДокументовСОшибками);
	СтруктураДанных.Вставить("УспешныеДокументы",    УспешныеДокументы);
	СтруктураДанных.Вставить("ЭтоДинамическийТитул", ДопПараметры <> Неопределено И ДопПараметры.Свойство("ДанныеТитула"));
	
	Возврат СтруктураДанных;
		
КонецФункции

Функция ПрочитатьДокументИзСбис(МассивДокументы, context_params, ДополнительныеПараметры) Экспорт
	
	КоличествоЗагружено       = 0;
	УспешныеДокументы         = Новый Массив;
	СписокДокументовСОшибками = Новый Соответствие;
	
	ЗагружатьПечатнуюФорму = Константы.Saby_ХранитьПечатныеФормыВБД.Получить();
	
	Для Каждого Ссылка Из МассивДокументы Цикл
		
		Ошибки = Неопределено;
			
		Данные = РегистрыСведений.Saby_Состояние.ПрочитатьПоОбъекту(Ссылка);
		Если Данные <> Неопределено Тогда 
			ИДСбис = Данные.UID;
		Иначе 
			ИДСбис = "";
		КонецЕсли;
		
		// документа нет в сбис. данные не загрузить
		Если Не ЗначениеЗаполнено(ИДСбис) Тогда 
			ТекстОшибки = "У документа нет идентфикатора сбис, обновление данных не возможно!";					
			ДобавитьОшибкуПользователю(Ошибки, Неопределено, ТекстОшибки);			
			СписокДокументовСОшибками.Вставить(Ссылка, Ошибки); 
			Продолжить;                             
		КонецЕсли;
			
	    ОбъектОбработки = Обработки.SABY.Создать();
		
		Параметры = Новый Структура;
		Параметры.Вставить("Идентификатор", ИДСбис);
		Параметры.Вставить("ДопПоля",       "ДопДействия,ТекущиеЭтапы,Подстановки,Расширение");
		
		Попытка		
			Результат = ОбъектОбработки.local_helper_read_document(context_params, Параметры); 		
		Исключение
			
			ИнфоОшибки = ИнформацияОбОшибке();
			ТекстОшибки = ПричинаОшибки(ИнфоОшибки.Описание);					
			ДобавитьОшибкуПользователю(Ошибки, Неопределено, ТекстОшибки);
			
			СписокДокументовСОшибками.Вставить(Ссылка, Ошибки); 
			Продолжить;
			
		КонецПопытки;
		
		// Загрузим что пришло....... 
		Попытка                 
			
			ТекущиеЭтапы = Результат["ТекущиеЭтапы"];
			КодСостояния = Saby_ТНОбщегоНазначенияСервер.ЧислоИзСтроки(Результат["Состояние"]["Код"]);
			Состояние    = Справочники.Saby_СостоянияОбъектов.ПолучитьСтатусДокументаСБИС(КодСостояния);
							
			ПараметрыЗагрузки = Новый Структура;
			ПараметрыЗагрузки.Вставить("ИДСбис",             ИДСбис);
			ПараметрыЗагрузки.Вставить("Данные",             Результат);
			ПараметрыЗагрузки.Вставить("Ссылка",             Ссылка);
			ПараметрыЗагрузки.Вставить("ИзДокумента",        ДополнительныеПараметры.ИзДокумента);
			ПараметрыЗагрузки.Вставить("ТолькоАктивныйЭтап", ДополнительныеПараметры.ТолькоАктивныйЭтап);
			Если ДополнительныеПараметры.ТекущийТитул <> Неопределено Тогда
				ПараметрыЗагрузки.Вставить("ТекущийТитул",   ДополнительныеПараметры.ТекущийТитул);
			КонецЕсли;
			
			Saby_ТНОбщегоНазначенияСервер.ЗагрузкаДанныхДокумента(ПараметрыЗагрузки);  
			
			УспешныеДокументы.Добавить(Ссылка);
			КоличествоЗагружено = КоличествоЗагружено + 1;
			
			Если ЗагружатьПечатнуюФорму Тогда
				ПараметрыЗагрузкиПечатнойФормы = ПараметрыЗагрузкиПечатнойФормыИзСбис(Ссылка, context_params);
				ЗагрузитьПечатнуюФормуДокументаИзСбис(ПараметрыЗагрузкиПечатнойФормы, Результат["СсылкаНаPDF"]);
			КонецЕсли;
			
		Исключение 
			
			ИнфоОшибки = ИнформацияОбОшибке();
			ТекстОшибки = ПричинаОшибки(ИнфоОшибки.Описание);					
			ДобавитьОшибкуПользователю(Ошибки, Неопределено, ТекстОшибки);
			
			СписокДокументовСОшибками.Вставить(Ссылка, Ошибки); 
			Продолжить;
			
		КонецПопытки;		
			
	КонецЦикла;
	
	Справочники.Saby_ОпасныеГрузы.ДозаполнитьНовыеЭлементыДаннымиСОнлайна();
	Справочники.Saby_ВидыУпаковки.ДозаполнитьНовыеЭлементыДаннымиСОнлайна();
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Количество",           КоличествоЗагружено);
	СтруктураДанных.Вставить("Успешно",              Не ЗначениеЗаполнено(СписокДокументовСОшибками));
	СтруктураДанных.Вставить("ВсегоДокументов",      МассивДокументы.Количество());
	СтруктураДанных.Вставить("СписокДокументов",     СписокДокументовСОшибками);
	СтруктураДанных.Вставить("УспешныеДокументы",    УспешныеДокументы);
	СтруктураДанных.Вставить("ЭтоДинамическийТитул", Ложь);
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция ОбновитьАктивныйЭтапИзСбис(МассивДокументы, context_params, ДополнительныеПараметры) Экспорт
	
	ДопПараметры = Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыЧтенияДокументаИзСБИС(ДополнительныеПараметры);
	ДопПараметры.ТолькоАктивныйЭтап = Истина;
	ДопПараметры.ТекущийТитул = ДополнительныеПараметры.ТекущийТитул;
	
	Возврат ПрочитатьДокументИзСбис(МассивДокументы, context_params, ДопПараметры);
		
КонецФункции

Процедура УстановитьИДИСтатус(Параметры) Экспорт
	
	АктивныйЭтап = Параметры.АктивныйЭтап;
	
	Если ЗначениеЗаполнено(Параметры.ИД) Тогда
		
		Если АктивныйЭтап <> Неопределено 
			И Параметры.Загрузка Тогда
			АктивныйЭтап = Saby_ТНОбщегоНазначенияСервер.ЗначениеТитула(АктивныйЭтап, Истина);
		КонецЕсли;	
		
		// возможно где то используется... 
		//АктивныйЭтап = Saby_ТНОбщегоНазначенияСервер.ТитулПоПредставлению(АктивныйЭтап);
		
		РегистрыСведений.Saby_Состояние.ОбновитьПоUID(
			Параметры.ИД, 
			АктивныйЭтап, 
			Параметры.Состояние, 
			Параметры.Ссылка,
			,,
			Параметры.СбисИД);	
		
	КонецЕсли;

КонецПроцедуры

Функция ОшибкиФЛК(Ссылка, ДопПараметры = Неопределено) Экспорт
	
	Ошибки = Неопределено;
	
	// Для динамических титулов данные существуют только на форме, поэтому проверка происходит на форме
	Если ДопПараметры <> Неопределено И ДопПараметры.Свойство("ДанныеТитула") Тогда
		Возврат Ошибки;
	КонецЕсли;
	
	ДопУсловияПроверок = ДопУсловияПроверок(ДопПараметры);
	
	ТипТитула = ТекущийТипТитула(Ссылка, ДопПараметры);
	
	ДанныеДокумента = Saby_ТНОбщегоНазначенияСервер.ЗначенияРеквизитовТранспортнойНакладной(
		ТипТитула, Ссылка, ДополнительныеДанныеПоТитулам(ТипТитула)
	);
	Если ДанныеДокумента = Неопределено Тогда
		Возврат Ошибки;
	КонецЕсли;
	
	ДанныеДокумента.Вставить("ТекущийЭтап", ТипТитула);
	
	ПроверяемыеРеквизиты = ПроверяемыеРеквизитыЭтапа(ДанныеДокумента, ДопУсловияПроверок);
	
	СоответствиеРеквизитовОшибок = СоответствиеРеквизитовОшибок();
	
	ПроверитьЗаполненНеЗаполнен(ДанныеДокумента, ПроверяемыеРеквизиты, Ошибки, СоответствиеРеквизитовОшибок);
	
	Если Не ЗначениеЗаполнено(ТипТитула) Тогда
		
		ПроверитьКонтактныеДанные(ДанныеДокумента, "Отправитель", "Телефон", Ошибки, СоответствиеРеквизитовОшибок);
		ПроверитьКонтактныеДанные(ДанныеДокумента, "Получатель", "Телефон", Ошибки, СоответствиеРеквизитовОшибок);
		ПроверитьКонтактныеДанные(ДанныеДокумента, "Перевозчик", "Телефон", Ошибки, СоответствиеРеквизитовОшибок);
		
		Если ДанныеДокумента.Реквизиты.Владелец_Тип = Перечисления.Saby_ВладелецОбъектаОтгрузки.ДругоеЮрЛицо Тогда
			ПроверитьКонтактныеДанные(ДанныеДокумента, "ВладелецОбъекта", "Телефон", Ошибки, СоответствиеРеквизитовОшибок);
		КонецЕсли;
		
		Если ДопУсловияПроверок.ПроверятьПоляОтгрузчика И Не ДопУсловияПроверок.ПроверятьПоляСОХ Тогда
			ПроверитьКонтактныеДанные(ДанныеДокумента, "Отгрузчик", "Телефон", Ошибки, СоответствиеРеквизитовОшибок);
		КонецЕсли;
		
		ПроверитьЗаполнениеТипаВладения(ДанныеДокумента, Ошибки, СоответствиеРеквизитовОшибок);
		
	КонецЕсли;
	
	Возврат Ошибки;
	
КонецФункции

Функция ТипыДляЗаполненияНаОсновании(ЗначенияПоУмолчанию = Ложь) Экспорт
	
	Если Не ЗначенияПоУмолчанию Тогда
		РезультатФункции = ПользовательскиеТипыДляЗаполненияНаОсновании();
	Иначе
		РезультатФункции = Неопределено;
	КонецЕсли;
	
	Если РезультатФункции = Неопределено Тогда
		РезультатФункции = ТипыПоУмолчаниюДляЗаполненияНаОсновании();
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура СохранитьВыбранныеТипыДляЗаполненияНаОсновании(МассивТипов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ХранилищеОбщихНастроек.Сохранить("Saby", "ТипыДляЗаполненияНаОснованииДокументаТРН", МассивТипов);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#Область Команды

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

#КонецОбласти // Команды

Функция ПоследнееУспешноЗагруженноеИзменение() Экспорт
	
	ДатаЗагрузки = Неопределено;
	
	Запрос = Новый Запрос;	
	Запрос.Текст = 
	"ВЫБРАТЬ
	 |	МАКСИМУМ(Saby_ТранспортнаяНакладная.ДатаИзменения) КАК ДатаИзменения
	 |ИЗ
	 |	Документ.Saby_ТранспортнаяНакладная КАК Saby_ТранспортнаяНакладная";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.ДатаИзменения) Тогда 
			ДатаЗагрузки = Выборка.ДатаИзменения;              
		КонецЕсли;
	КонецЕсли;	
			
	Возврат ДатаЗагрузки;
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция СписокИзмененийДокументов(СтруктураПараметров, Изменения = Неопределено, ИДСобытия = "", Всего = 0)
	
	Если Изменения = Неопределено Тогда
		Изменения = Новый Соответствие;
	КонецЕсли;
	
	РазмерСтраницы = 30;
	
	Навигация = Новый Структура;
	Навигация.Вставить("РазмерСтраницы", Формат(РазмерСтраницы,"ЧГ=0"));
	
	filter = Новый Структура;
	filter.Вставить("Навигация", Навигация);
	
	Если ЗначениеЗаполнено(ИДСобытия) Тогда
		filter.Вставить("ИдентификаторСобытия", ИДСобытия);
	КонецЕсли;
	
	ДопПараметры = СтруктураПараметров.ДопПараметры;
	
	Если ДопПараметры.Свойство("Начало")
		И ЗначениеЗаполнено(ДопПараметры.Начало) Тогда
		
		ДатаС = ДопПараметры.Начало;
		ПараметрДатаС = Формат(ДатаС, "ДФ='dd.MM.yyyy ЧЧ.мм.сс'");
		filter.Вставить("ДатаВремяС", ПараметрДатаС);
		
	КонецЕсли;
	
	Если ДопПараметры.Свойство("Окончание")
		И ЗначениеЗаполнено(ДопПараметры.Окончание) Тогда
		
		ДатаПо = ДопПараметры.Окончание;
		ПараметрДатаПо = Формат(ДатаПо, "ДФ='dd.MM.yyyy ЧЧ.мм.сс'");
		filter.Вставить("ДатаВремяПо", ПараметрДатаПо);
		
	КонецЕсли;
	
	filter.Вставить("Тип",                "ConsignmentNote"); 	
	filter.Вставить("ДопПоля",            "ДопДействия,ТекущиеЭтапы,Подстановки"); //ПодстановкиСобытие");
    filter.Вставить("ПолныйСертификатЭП", "Нет"); // из описание метода, ускорение отдачи результата
	
	ОбъектОбработки = Обработки.SABY.Создать();
	
	СписокИзменений = Неопределено;
	ЕстьЕщеСтраницы = Ложь;
	
	Попытка                                                                                      
		
		СоответствиеИзменений = ОбъектОбработки.local_helper_read_changes(СтруктураПараметров.context_params, filter);		
		СписокИзменений       = СоответствиеИзменений["Документ"];
		ЕстьЕщеСтраницы       = Булево(СоответствиеИзменений["Навигация"]["ЕстьЕще"]);
		
	Исключение
		ИнфОбОшибке = ИнформацияОбОшибке();
		//Если Найти(ИнфОбОшибке.Описание, "Неверный фильтр. Не найдено событие с идентификатором") Тогда
		//	
		//	/filter.Удалить("ИдентификаторСобытия");
		//	//СписокИзмененийДокументов(context_params, АктуальныеИзменения, ПоследнийИдентификатор);

		//КонецЕсли;
	КонецПопытки;
			
	// Обработаем полученные изменения
	Если СписокИзменений <> Неопределено Тогда
		
		Номер = 0;
		Для Каждого Изменение Из СписокИзменений Цикл 
			
			Если ЗначениеЗаполнено(Изменение["Подстановки"]) Тогда
				ДобавитьИзменение(Изменения, Изменение);			
			КонецЕсли;
										
			Номер = Номер + 1;
			Если Номер = СписокИзменений.Количество() Тогда  
				
				Событие = Изменение["Событие"];
				Если Событие.Количество() Тогда 
					ПоследнийИдентификатор = Событие[0]["Идентификатор"]
				КонецЕсли;
				
			КонецЕсли;
			
			// ТЕСТ. Собираем сколько всего изменений получили
			Всего = Всего + 1;
			
		КонецЦикла;
				
	КонецЕсли;
	
	Если ЕстьЕщеСтраницы Тогда	
		// получим еще изменения
		СписокИзмененийДокументов(СтруктураПараметров, Изменения, ПоследнийИдентификатор, Всего);		
	КонецЕсли;	
			
	Возврат Изменения;
	
КонецФункции

Процедура ДобавитьИзменение(Изменения, ИзменениеСОнлайна)
	
	ИдентификаторДокумента = ИзменениеСОнлайна["Идентификатор"];
	
	// Изменения с онлайна приходят не сгруппированные по документу.
	// Поэтому группируем их по идентификатору документа
	ИзмененияПоДокументу = Изменения.Получить(ИдентификаторДокумента);
	Если ИзмененияПоДокументу = Неопределено Тогда
		
		ИзмененияПоДокументу = Новый Соответствие;
		ИзмененияПоДокументу.Вставить("Подстановки",  Новый Соответствие);
		ИзмененияПоДокументу.Вставить("ИдТитулов",    Новый Соответствие);
		ИзмененияПоДокументу.Вставить("Номер",        ИзменениеСОнлайна["Номер"]);
		ИзмененияПоДокументу.Вставить("Название",     ИзменениеСОнлайна["Название"]);
		ИзмененияПоДокументу.Вставить("Направление",  ИзменениеСОнлайна["Направление"]);
		ИзмененияПоДокументу.Вставить("Состояние",    ИзменениеСОнлайна["Состояние"]);
		ИзмененияПоДокументу.Вставить("Событие",      Новый Массив);
		ИзмененияПоДокументу.Вставить("ТекущиеЭтапы", ИзменениеСОнлайна["ТекущиеЭтапы"]);
		ИзмененияПоДокументу.Вставить("ДопДействия",  ИзменениеСОнлайна["ДопДействия"]);
		
		Изменения.Вставить(ИдентификаторДокумента, ИзмененияПоДокументу);
		
	КонецЕсли;
	
	Если ИзменениеСОнлайна["Подстановки"] <> Неопределено Тогда
		Для Каждого Подстановки Из ИзменениеСОнлайна["Подстановки"] Цикл
			ДобавитьПодстановкуТитула(ИзмененияПоДокументу, Подстановки.Ключ, Подстановки.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Если ИзменениеСОнлайна["Событие"] <> Неопределено Тогда
		Для Каждого Событие Из ИзменениеСОнлайна["Событие"] Цикл
			ИзмененияПоДокументу["Событие"].Добавить(Событие);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПодстановкуТитула(ИзмененияПоДокументу, КодТитулаОнлайна, ЗначенияПодстановок)
	
	КодТитула = Saby_ТНОбщегоНазначенияСервер.КодТитула(КодТитулаОнлайна);
	
	Для Каждого ДанныеТитула Из ЗначенияПодстановок Цикл
		
		ИдентификаторТитула = Saby_ТНОбщегоНазначенияСервер.ИдентификаторВложения(ДанныеТитула, КодТитула);
		
		МассивТитулов = ИзмененияПоДокументу["Подстановки"][КодТитулаОнлайна];
		Если МассивТитулов = Неопределено Тогда
			МассивТитулов = Новый Массив;
			ИзмененияПоДокументу["Подстановки"].Вставить(КодТитулаОнлайна, МассивТитулов)
		КонецЕсли;
		
		ИндексТитула = ИзмененияПоДокументу["ИдТитулов"][ИдентификаторТитула];
		Если ИндексТитула = Неопределено Тогда
			МассивТитулов.Добавить(ДанныеТитула);
			ИндексТитула = МассивТитулов.ВГраница();
			ИзмененияПоДокументу["ИдТитулов"].Вставить(ИдентификаторТитула, ИндексТитула);
		Иначе
			МассивТитулов.Вставить(ИндексТитула, ДанныеТитула);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьИзмененияДокументов(СписокИзменений, Количество)
	
	// ToDo: Загружаем все что пришло
	// если документ уже есть можно загружать данные по титулам
	// учесть что могли быть выполнены доп операции
	
	ЗагружатьПечатнуюФорму = Константы.Saby_ХранитьПечатныеФормыВБД.Получить();
	ПараметрыЗагрузкиПечатнойФормы = ПараметрыЗагрузкиПечатнойФормыИзСбис();
	
	Для Каждого Изменение Из СписокИзменений Цикл 
				
		Попытка 
			
			ПараметрыЗагрузки = Новый Структура;
			ПараметрыЗагрузки.Вставить("ИДСбис",      Изменение.Ключ);
			ПараметрыЗагрузки.Вставить("Данные",      Изменение.Значение);
			
			СсылкаДокумент = Saby_ТНОбщегоНазначенияСервер.ЗагрузкаДанныхДокумента(ПараметрыЗагрузки);  
			Если ЗначениеЗаполнено(СсылкаДокумент) Тогда 
				Количество = Количество + 1;
				Если ЗагружатьПечатнуюФорму Тогда
					ПараметрыЗагрузкиПечатнойФормы.СсылкаНаДокумент = СсылкаДокумент;
					ЗаполнитьПараметрыЗагрузкиПечатнойФормыПоСсылке(ПараметрыЗагрузкиПечатнойФормы);
					ЗагрузитьПечатнуюФормуДокументаИзСбис(ПараметрыЗагрузкиПечатнойФормы, Изменение.Значение["СсылкаНаPDF"]);
				КонецЕсли;
			КонецЕсли;
					
		Исключение
			
			Описание = "Ошибка загрузки из сбис";
			Текст    = "Документ " + Изменение.Значение["Название"] + " (id:" + Изменение.Ключ + ")";
			
			Saby_ТНОбщегоНазначенияСервер.ЗаписатьОшибкуВЖурналРегистрации(Неопределено, Описание, Текст);
			Продолжить;
			
		КонецПопытки;
				
	КонецЦикла;
	
	Справочники.Saby_ОпасныеГрузы.ДозаполнитьНовыеЭлементыДаннымиСОнлайна();
	Справочники.Saby_ВидыУпаковки.ДозаполнитьНовыеЭлементыДаннымиСОнлайна();
	
КонецПроцедуры

#Область ФЛК

Функция ТекущийТипТитула(СсылкаНаДокумент, ДопПараметры = Неопределено)
	
	РезультатФункции = Неопределено;
	
	ДанныеСостояния = РегистрыСведений.Saby_Состояние.ПрочитатьПоОбъекту(СсылкаНаДокумент);
	Если ДопПараметры <> Неопределено И ДопПараметры.Свойство("Этап") Тогда
		РезультатФункции = Saby_ТНОбщегоНазначенияСервер.ТитулПоПредставлению(ДопПараметры.Этап);
	ИначеЕсли ДанныеСостояния <> Неопределено Тогда 	
		РезультатФункции = Saby_ТНОбщегоНазначенияСервер.ТитулПоПредставлению(ДанныеСостояния.АктивныйЭтап);
	КонецЕсли;
	
	Если РезультатФункции = Неопределено Тогда
		РезультатФункции = Перечисления.Saby_ТипТитулаЭтрН.ПустаяСсылка();
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

#Область ПроверяемыеРеквизиты

Функция ДопУсловияПроверок(ДопПараметры = Неопределено)
	
	ПроверятьПоляСОХ = ДопПараметры <> Неопределено
		И ДопПараметры.Свойство("ПроверятьСОХ")
		И ДопПараметры.ПроверятьСОХ;
		
	ПроверятьПоляОтгрузчика = ДопПараметры <> Неопределено
		И ДопПараметры.Свойство("ПроверятьПоляОтгрузчика")
		И ДопПараметры.ПроверятьПоляОтгрузчика;
		
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ПроверятьПоляСОХ",        ПроверятьПоляСОХ);
	РезультатФункции.Вставить("ПроверятьПоляОтгрузчика", ПроверятьПоляОтгрузчика);
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ПроверяемыеРеквизитыЭтапа(ДанныеДокумента, ДопУсловияПроверок)
	
	ПроверяемыеРеквизиты = Новый Массив;
	
	Если Не ЗначениеЗаполнено(ДанныеДокумента.ТекущийЭтап) Тогда
		ДополнитьПроверяемыеРеквизитыЭтапаПогрузка(ПроверяемыеРеквизиты, ДанныеДокумента, ДопУсловияПроверок);
	ИначеЕсли ДанныеДокумента.ТекущийЭтап = Перечисления.Saby_ТипТитулаЭтрН.ПриемкаГруза Тогда
		ДополнитьПроверяемыеРеквизитыЭтапаПриемкаГруза(ПроверяемыеРеквизиты);
	КонецЕсли;
	
	ПроверяемыеРеквизиты.Добавить("Отметки.ДатаВремя");
	ПроверяемыеРеквизиты.Добавить("Отметки.Описание");
	ПроверяемыеРеквизиты.Добавить("Акты.Документ");
	ПроверяемыеРеквизиты.Добавить("Штрафы.Расчет");
	
	Возврат ПроверяемыеРеквизиты;
	
КонецФункции

Процедура ДополнитьПроверяемыеРеквизитыЭтапаПогрузка(ПроверяемыеРеквизиты, ДанныеДокумента, ДопУсловияПроверок)
	
	РеквизитыОбъекта = ДанныеДокумента.Реквизиты;
	
	ПроверяемыеРеквизиты.Добавить("Направление");
	ПроверяемыеРеквизиты.Добавить("Отправитель");
	
	Если ДопУсловияПроверок.ПроверятьПоляСОХ Тогда
		ПроверяемыеРеквизиты.Добавить("Составитель");
		ПроверяемыеРеквизиты.Добавить("Составитель_НаОснованииДокумент");
	КонецЕсли;
	
	ПроверяемыеРеквизиты.Добавить("Получатель");
	ПроверяемыеРеквизиты.Добавить("Перевозчик");
	ПроверяемыеРеквизиты.Добавить("Погрузка_Адрес");
	ПроверяемыеРеквизиты.Добавить("Отправитель_АдресДоставки");
	ПроверяемыеРеквизиты.Добавить("Отправитель_ЗаявкаНомер");
	ПроверяемыеРеквизиты.Добавить("Отправитель_ЗаявкаДата");
	ПроверяемыеРеквизиты.Добавить("Отправитель_ТранспортноеСредство");
	
	ПроверяемыеРеквизиты.Добавить("Водители");
	ПроверяемыеРеквизиты.Добавить("Водители.Водитель");
	
	Если ЗначениеЗаполнено(РеквизитыОбъекта.Отправитель_Экспедитор) И РеквизитыОбъекта.Отправитель_Экспедитор Тогда
		ПроверяемыеРеквизиты.Добавить("Заказчик");
		ПроверяемыеРеквизиты.Добавить("Заказчик_Договор");
	КонецЕсли;
	
	Если РеквизитыОбъекта.Владелец_Тип = Перечисления.Saby_ВладелецОбъектаОтгрузки.Неизвестен Тогда
		ПроверяемыеРеквизиты.Добавить("Владелец_НеизвестенПричина");
	КонецЕсли;
	
	Если ДанныеДокумента.ТекущийЭтап = Перечисления.Saby_ТипТитулаЭтрН.ПустаяСсылка()
		И РеквизитыОбъекта.Владелец_Тип = Перечисления.Saby_ВладелецОбъектаОтгрузки.ДругоеЮрЛицо Тогда
		ПроверяемыеРеквизиты.Добавить("ВладелецОбъекта");
	КонецЕсли;
	
	ПроверяемыеРеквизиты.Добавить("Грузы");
	ПроверяемыеРеквизиты.Добавить("Грузы.Тип");
	ПроверяемыеРеквизиты.Добавить("Грузы.Наименование");
	ПроверяемыеРеквизиты.Добавить("Грузы.Состояние");
	ПроверяемыеРеквизиты.Добавить("Грузы.Количество");
	ПроверяемыеРеквизиты.Добавить("Грузы.МассаБрутто");
	ПроверяемыеРеквизиты.Добавить("Грузы.СпособУпаковки");
	ПроверяемыеРеквизиты.Добавить("Грузы.ВидТары");
	
	Если ЗначениеЗаполнено(ДанныеДокумента.Грузы) Тогда		
		Отбор = Новый Структура("Тип", Перечисления.Saby_ТипГруза.Контейнер);
		ЕстьТипГрузаКонтейнер = ДанныеДокумента.Грузы.НайтиСтроки(Отбор).Количество() > 0;
		Если ЕстьТипГрузаКонтейнер Тогда
			ПроверяемыеРеквизиты.Добавить("Контейнеры");
			ПроверяемыеРеквизиты.Добавить("Контейнеры.Номер");
			ПроверяемыеРеквизиты.Добавить("Контейнеры.Количество");
			ПроверяемыеРеквизиты.Добавить("Отправитель_СопроводительнаяВедомость");
		КонецЕсли;
	КонецЕсли;
	
	ПроверяемыеРеквизиты.Добавить("Погрузка_КоличествоМест");
	ПроверяемыеРеквизиты.Добавить("Погрузка_Масса");
	ПроверяемыеРеквизиты.Добавить("Погрузка_МассаМетодРасчета");
	
	Если ДопУсловияПроверок.ПроверятьПоляОтгрузчика Тогда
		Если Не ДопУсловияПроверок.ПроверятьПоляСОХ Тогда
			ПроверяемыеРеквизиты.Добавить("Отгрузчик");
		КонецЕсли;
		ПроверяемыеРеквизиты.Добавить("Отгрузка_НаОснованииДокумент");
		ПроверяемыеРеквизиты.Добавить("Отгрузка_Ответственный");
		ПроверяемыеРеквизиты.Добавить("Отгрузка_ОтветственныйДолжность");
		ПроверяемыеРеквизиты.Добавить("Отгрузка_ОтветственныйНаОсновании");
	КонецЕсли;
	
	РольПолучатель = Перечисления.Saby_РолиКонтрагентов.Получатель;
	Если РеквизитыОбъекта.ИнициаторПереадресации = РольПолучатель Тогда
		ПроверяемыеРеквизиты.Добавить("ИнициаторПереадресацииПодтверждение");
	КонецЕсли;
	
	ПроверяемыеРеквизиты.Добавить("Отправитель_ДоставитьДо");
	ПроверяемыеРеквизиты.Добавить("Погрузка_ДатаВремя");
	ПроверяемыеРеквизиты.Добавить("Погрузка_ДатаВремяПрибыл");
	ПроверяемыеРеквизиты.Добавить("Погрузка_ДатаВремяУбыл");
	
	ПроверяемыеРеквизиты.Добавить("Маркировки.Значение");
	ПроверяемыеРеквизиты.Добавить("ОпасныеГрузы.ОпасныйГруз");
	ПроверяемыеРеквизиты.Добавить("Прицепы.Прицеп");
	
	ПроверяемыеРеквизиты.Добавить("СпецРазрешения.Номер");
	ПроверяемыеРеквизиты.Добавить("СпецРазрешения.Дата");
	ПроверяемыеРеквизиты.Добавить("СпецРазрешения.Срок");
	
	ПроверяемыеРеквизиты.Добавить("КонтактныеДанные.Тип");
	ПроверяемыеРеквизиты.Добавить("КонтактныеДанные.Значение");
	
	ПроверяемыеРеквизиты.Добавить("ПутевыеЛисты.Документ");
	
КонецПроцедуры

Процедура ДополнитьПроверяемыеРеквизитыЭтапаПриемкаГруза(ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("Выгрузка_КоличествоМест");
	ПроверяемыеРеквизиты.Добавить("Выгрузка_МассаБрутто");
	ПроверяемыеРеквизиты.Добавить("Выгрузка_МассаМетодРасчета");
	ПроверяемыеРеквизиты.Добавить("Выгрузка_Состояние");
	
	ПроверяемыеРеквизиты.Добавить("Выгрузка_ДатаВремяПрибыл");
	ПроверяемыеРеквизиты.Добавить("Выгрузка_ДатаВремяУбыл");
	
КонецПроцедуры 

Функция ДополнительныеДанныеПоТитулам(ТипТитула)
	
	РезультатФункции = Новый Соответствие;
	
	ДопДанные = Новый Структура;
	ДопДанные.Вставить("Реквизиты", Новый Массив);
	ДопДанные.Вставить("Таблицы",   Новый Массив);
	
	Если Не ЗначениеЗаполнено(ТипТитула) Тогда
		
		ДопДанные.Реквизиты.Добавить("Направление");
		ДопДанные.Реквизиты.Добавить("ТипВладенияТС");
		
		ДопДанные.Таблицы.Добавить("ДокументыПодтверждающиеВладение");
		
	КонецЕсли;
	
	РезультатФункции.Вставить(ТипТитула, ДопДанные);
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // ПроверяемыеРеквизиты

Процедура ПроверитьЗаполненНеЗаполнен(ДанныеДокумента, МассивПроверяемыхРеквизитов, Ошибки, СоответствиеРеквизитовОшибок) Экспорт
	
	Перем ПроверяемыеРеквизитыТЧ;
	
	ТабличныеЧасти = Новый Структура;
	
	// Проверка реквизитов объекта и заполнение структуры по реквизитам табличных частей.
	Для Каждого Реквизит Из МассивПроверяемыхРеквизитов Цикл
		
		ПозицияТочки = СтрНайти(Реквизит,".");
		
		Если ПозицияТочки > 0 Тогда // В случае если указан реквизит табличной части
			
			ДлинаСтроки       = СтрДлина(Реквизит);
			ИмяТабличнойЧасти = Лев(Реквизит, ПозицияТочки - 1);
			ИмяРеквизита      = Прав(Реквизит, ДлинаСтроки - ПозицияТочки);
			
			// Сохранение проверяемого реквизита табличной части в структуру
			Если НЕ ТабличныеЧасти.Свойство(ИмяТабличнойЧасти, ПроверяемыеРеквизитыТЧ) Тогда
				ПроверяемыеРеквизитыТЧ = Новый Массив;
				ТабличныеЧасти.Вставить(ИмяТабличнойЧасти, ПроверяемыеРеквизитыТЧ);
			КонецЕсли;
			ПроверяемыеРеквизитыТЧ.Добавить(ИмяРеквизита);
			
		Иначе // В случае если указан реквизит объекта
			
			Если ДанныеДокумента.Реквизиты.Свойство(Реквизит) Тогда
				ЗначениеРеквизитаЗаполнено = ЗначениеЗаполнено(ДанныеДокумента.Реквизиты[Реквизит]);
			Иначе
				ЗначениеРеквизитаЗаполнено = ЗначениеЗаполнено(ДанныеДокумента[Реквизит]);
			КонецЕсли;
			
			Если Не ЗначениеРеквизитаЗаполнено Тогда
				
				СтруктураДанныхОшибки = СоответствиеРеквизитовОшибок.Получить(Реквизит);
				
				ДобавитьОшибкуПользователю(
					Ошибки,
					СтрШаблон("Объект.%1", Реквизит),
					СтруктураДанныхОшибки.Ошибка,
					СтруктураДанныхОшибки.РеквизитОбъекта,
					СтруктураДанныхОшибки.ЭлементФормы
				);
				Отказ = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Проверка реквизитов в табличных частях
	Для Каждого ТабличнаяЧасть Из ТабличныеЧасти Цикл
		
		ИмяТабличнойЧасти = ТабличнаяЧасть.Ключ;
		ТЧ = ДанныеДокумента[ТабличнаяЧасть.Ключ];
		МассивРеквизитов = ТабличнаяЧасть.Значение;
		
		// Цикл по всем строкам табличной части.
		Для ИндексСтроки = 0 По ТЧ.Количество() - 1 Цикл
			
			// Цикл по всем проверяемым реквизитам для текущей табличной части.
			Для НомерРеквизита = 0 По МассивРеквизитов.Количество() - 1 Цикл
				
				ИмяРеквизита = МассивРеквизитов[НомерРеквизита];
				
				ИмяТЧ = ПолучитьИмяТЧОтметок(ИмяТабличнойЧасти, ТЧ[ИндексСтроки], ДанныеДокумента);
				
				СтруктураДанныхОшибки = СоответствиеРеквизитовОшибок.Получить(ИмяТЧ+"."+ИмяРеквизита);
				
				Если Не ЗначениеЗаполнено(ТЧ[ИндексСтроки][ИмяРеквизита]) Тогда
					
					ПолеОшибки = СтрШаблон(
						"Объект.%1",
						ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТабличнойЧасти, ИндексСтроки + 1, ИмяРеквизита)
					);
					ДобавитьОшибкуПользователю(
						Ошибки,
						ПолеОшибки,
						СтруктураДанныхОшибки.Ошибка,
						СтруктураДанныхОшибки.РеквизитОбъекта,
						СтруктураДанныхОшибки.ЭлементФормы,
						ИндексСтроки
					);
					Отказ = Истина;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	МассивПроверяемыхРеквизитов.Очистить();
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеТипаВладения(ДанныеДокумента, Ошибки, СоответствиеРеквизитовОшибок)
	
	ДокументыВладенияНеНайдены = Не ДокументыВладенияНайдены(
		ДанныеДокумента.ДокументыПодтверждающиеВладение,
		ДанныеДокумента.Реквизиты.ТипВладенияТС,
		ДанныеДокумента.Реквизиты.Отправитель_ТранспортноеСредство
	);
	
	Если ДокументыВладенияНеНайдены Тогда
		
		СтруктураДанныхОшибки = СоответствиеРеквизитовОшибок.Получить("ТранспортноеСредствоДокумент");
		
		ДобавитьОшибкуПользователю(
			Ошибки,
			"Объект.Отправитель_ТранспортноеСредство",
			СтруктураДанныхОшибки.Ошибка,
			СтруктураДанныхОшибки.РеквизитОбъекта,
			СтруктураДанныхОшибки.ЭлементФормы
		);
		
	КонецЕсли;
	
	Для Каждого СтрокаПрицепа Из ДанныеДокумента.Прицепы Цикл
		
		ДокументыВладенияНеНайдены = Не ДокументыВладенияНайдены(
			ДанныеДокумента.ДокументыПодтверждающиеВладение,
			СтрокаПрицепа.ТипВладенияТС,
			СтрокаПрицепа.Прицеп
		);
		
		Если ДокументыВладенияНеНайдены Тогда
			
			СтруктураДанныхОшибки = СоответствиеРеквизитовОшибок.Получить("Прицепы.ПрицепДокумент");
			
			ДобавитьОшибкуПользователю(
				Ошибки,
				СтрШаблон("Объект.Прицепы[%1]", СтрокаПрицепа.НомерСтроки),
				СтруктураДанныхОшибки.Ошибка,
				СтруктураДанныхОшибки.РеквизитОбъекта,
				СтруктураДанныхОшибки.ЭлементФормы,
				СтрокаПрицепа.НомерСтроки - 1
			);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДокументыВладенияНайдены(ДокументыПодтверждающиеВладение, ТипВладенияТС, ТранспортноеСредство)
	
	РезультатФункции = Истина;
	
	ТипыВладенияТребующиеДокумент = Новый Массив;
	ТипыВладенияТребующиеДокумент.Добавить(Перечисления.Saby_ТипыВладенияТС.Аренда);
	ТипыВладенияТребующиеДокумент.Добавить(Перечисления.Saby_ТипыВладенияТС.Лизинг);
	ТипыВладенияТребующиеДокумент.Добавить(Перечисления.Saby_ТипыВладенияТС.БезвозмездноеПользование);
	
	Если ТипыВладенияТребующиеДокумент.Найти(ТипВладенияТС) = Неопределено Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ТранспортноеСредство", ТранспортноеСредство);
	
	НайденныеДокументы = ДокументыПодтверждающиеВладение.НайтиСтроки(СтруктураПоиска);
	
	Для Каждого СтрокаДокументов Из НайденныеДокументы Цикл
		РезультатФункции = РезультатФункции И ЗначениеЗаполнено(СтрокаДокументов.ДокументВладения);
	КонецЦикла;
	
	РезультатФункции = РезультатФункции И НайденныеДокументы.Количество() > 0;
		
	Возврат РезультатФункции;
	
КонецФункции

Процедура ПроверитьКонтактныеДанные(ДанныеДокумента, Роль, Тип, Ошибки, СоответствиеРеквизитовОшибок)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Роль", Перечисления.Saby_РолиКонтрагентов[Роль]);
	Отбор.Вставить("Тип",  Перечисления.ТипыКонтактнойИнформации[Тип]);
	
	ИмяРеквизита = Роль + "_" + Тип;
	
	СтруктураДанныхОшибки = СоответствиеРеквизитовОшибок.Получить(ИмяРеквизита);
	
	НайденныеСтроки = ДанныеДокумента.КонтактныеДанные.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() = 0 Тогда
		ДобавитьОшибкуПользователю(
			Ошибки, 
			ИмяРеквизита, 
			СтруктураДанныхОшибки.Ошибка,
			СтруктураДанныхОшибки.РеквизитОбъекта,
			СтруктураДанныхОшибки.ЭлементФормы
		);
	КонецЕсли;
	
КонецПроцедуры

Функция СоответствиеРеквизитовОшибок() Экспорт
	
	РезультатФункции = Новый Соответствие;
	
	ТабДокОшибок = ПолучитьМакет("ФЛК");
	
	НомерСтроки = 2;
	Пока ЗначениеЗаполнено(ТабДокОшибок.Область(НомерСтроки, 1).Текст) Цикл
		
		РеквизитОбъекта = СокрЛП(ТабДокОшибок.Область(НомерСтроки, 1, НомерСтроки, 1).Текст);
		ЭлементФормы    = СокрЛП(ТабДокОшибок.Область(НомерСтроки, 2, НомерСтроки, 2).Текст);
		Ошибка          = СокрЛП(ТабДокОшибок.Область(НомерСтроки, 3, НомерСтроки, 3).Текст);
		
		СтруктураДанныхОшибки = Новый Структура;
		СтруктураДанныхОшибки.Вставить("РеквизитОбъекта", РеквизитОбъекта);
		СтруктураДанныхОшибки.Вставить("ЭлементФормы",    ЭлементФормы);
		СтруктураДанныхОшибки.Вставить("Ошибка",          Ошибка);
		
		РезультатФункции.Вставить(РеквизитОбъекта, СтруктураДанныхОшибки);
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ДобавитьОшибкуПользователю(Ошибки, ПолеОшибки, ТекстДляОднойОшибки, РеквизитОбъекта = "", ЭлементФормы = "", ИндексСтроки = 0) Экспорт
	
	Если Ошибки = Неопределено Тогда
		Ошибки = Новый Структура;
		Ошибки.Вставить("СписокОшибок", Новый Массив);
		Ошибки.Вставить("ГруппыОшибок", Новый Соответствие);
	КонецЕсли;
	
	Ошибка = Новый Структура;
	Ошибка.Вставить("ПолеОшибки",          ПолеОшибки);
	Ошибка.Вставить("ТекстДляОднойОшибки", ТекстДляОднойОшибки);
	Ошибка.Вставить("РеквизитОбъекта",     РеквизитОбъекта);
	Ошибка.Вставить("ЭлементФормы",        ЭлементФормы);
	Ошибка.Вставить("ИндексСтроки",        ИндексСтроки);
	
	Ошибки.СписокОшибок.Добавить(Ошибка);
	
КонецПроцедуры

Функция ПолучитьИмяТЧОтметок(ИмяТабличнойЧасти, СтрокаТабличнойЧасти, ДанныеДокумента)
	
	РезультатФункции = ИмяТабличнойЧасти;
	
	Если ИмяТабличнойЧасти = "Отметки" Тогда
		
		Если ЭтоОтметкиВыгрузки(СтрокаТабличнойЧасти) Тогда
			РезультатФункции = "Отметки_Выгрузка";
		КонецЕсли;
		
	ИначеЕсли ИмяТабличнойЧасти = "Акты" Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("КлючСтроки", СтрокаТабличнойЧасти.КлючСтроки_Отметки);
		
		СтрокиОтметок = ДанныеДокумента.Отметки.НайтиСтроки(Отбор);
		Если СтрокиОтметок.Количество() > 0 Тогда
			Если ЭтоОтметкиВыгрузки(СтрокиОтметок[0]) Тогда
				РезультатФункции = "Акты_Выгрузка";
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяТабличнойЧасти = "Штрафы" Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("КлючСтроки", СтрокаТабличнойЧасти.КлючСтроки_Отметки);
		
		СтрокиОтметок = ДанныеДокумента.Отметки.НайтиСтроки(Отбор);
		Если СтрокиОтметок.Количество() > 0 Тогда
			Если ЭтоОтметкиВыгрузки(СтрокиОтметок[0]) Тогда
				РезультатФункции = "Штрафы_Выгрузка";
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ЭтоОтметкиВыгрузки(СтрокаТабличнойЧасти)
	
	Если СтрокаТабличнойЧасти.Роль = Перечисления.Saby_РолиКонтрагентов.Получатель Тогда
		Возврат Истина;
	ИначеЕсли СтрокаТабличнойЧасти.Роль = Перечисления.Saby_РолиКонтрагентов.Перевозчик Тогда
		Возврат СтрокаТабличнойЧасти.Этап = Перечисления.Saby_ТипТитулаЭтрН.ПриемкаГруза
			Или СтрокаТабличнойЧасти.Этап = Перечисления.Saby_ТипТитулаЭтрН.ВыдачаГруза;
	КонецЕсли;
		
	Возврат Ложь;
	
КонецФункции

#КонецОбласти // ФЛК

Функция РезультатПолученияXMLИзСбис(Ссылка, ДопПараметры, context_params, ОбъектОбработки)
	
	ДанныеДокумента = Saby_ТНОбщегоНазначенияСервер.ДанныеДляВыгрузки(Ссылка, ДопПараметры);
	Если Не ЗначениеЗаполнено(ДанныеДокумента) Тогда 
		Возврат Неопределено;
	КонецЕсли;	
		
	ПараметрыМетода  = Новый Структура;
	ПараметрыМетода.Вставить("Документ", ДанныеДокумента); 
	
	ПараметрыЗапроса = Новый Структура;                  	
	ПараметрыЗапроса.Вставить("method",    "СБИС.СгенерироватьВложение");
	//ПараметрыЗапроса.Вставить("data",      ДанныеДляОтправки()); 
	ПараметрыЗапроса.Вставить("params",    ПараметрыМетода);
	ПараметрыЗапроса.Вставить("auto_auth", Истина);
	
	Ответ        = ОбъектОбработки.local_helper_exec_method(context_params, ПараметрыЗапроса);
	ДанныеОтвета = Ответ["result"];
	
	Вложение = ДанныеОтвета["Вложение"][0];
	Файл     = Вложение["Файл"];
	
	//Проверка = ПолучитьСтрокуИзДвоичныхДанных(Base64Значение(Файл["ДвоичныеДанные"]), КодировкаТекста.ANSI);
	
	Если ДанныеДокумента.Свойство("Идентификатор") Тогда 
		ИдСбис = ДанныеДокумента.Идентификатор;
	Иначе 	 
		ИдСбис = "";
	КонецЕсли;
		
	ОтветСтруктура = Новый Структура;
	ОтветСтруктура.Вставить("Файл",   Файл);
	ОтветСтруктура.Вставить("ИДСбис", ИдСбис);
	ОтветСтруктура.Вставить("Этап",   ДанныеДокумента.Вложение[0].Подтип);
	
	Возврат ОтветСтруктура;
	
КонецФункции

Функция ЗаписатьНовыйДокументВСбис(Данные, context_params, ОбъектОбработки)
	
	Файл   = Данные.Файл;
	ИдСбис = Данные.ИдСбис;
		
	ФайлСтруктура = Новый Структура;
	ФайлСтруктура.Вставить("Имя",            Файл["Имя"]);
	ФайлСтруктура.Вставить("ДвоичныеДанные", Файл["ДвоичныеДанные"]);
	
	СтруктураВложения = Новый Структура;
	//СтруктураВложения.Вставить("Идентификатор", ""); // ИдСбис? 
	СтруктураВложения.Вставить("Файл", ФайлСтруктура);
	
	МассивВложений = Новый Массив;
	МассивВложений.Добавить(СтруктураВложения);
	
	Параметры = Новый Структура;	
	Параметры.Вставить("Тип",      "ConsignmentNote");	
	Параметры.Вставить("Вложение", МассивВложений); 
	
	Если ЗначениеЗаполнено(Данные.ИдСбис) Тогда 
		Параметры.Вставить("Идентификатор", Данные.ИдСбис);
	КонецЕсли;
		
	ДокСбис = ОбъектОбработки.local_helper_write_document(context_params, Параметры); 
	
	Возврат ДокСбис;
	
КонецФункции

Функция ЗаписатьВложениеДляДокументаСбис(Файл, ИдСбис, context, ОбъектОбработки)
	
	ФайлСтруктура = Новый Структура;
	ФайлСтруктура.Вставить("Имя",            Файл["Имя"]);
	ФайлСтруктура.Вставить("ДвоичныеДанные", Файл["ДвоичныеДанные"]);
	
	СтруктураВложения = Новый Структура;
	//СтруктураВложения.Вставить("Идентификатор", ИдСбис); // ИдСбис? 
	СтруктураВложения.Вставить("Файл", ФайлСтруктура);
	
	МассивВложений = Новый Массив;
	МассивВложений.Добавить(СтруктураВложения);
			
	Параметры = Новый Структура;	
	//Параметры.Вставить("Тип",      "ConsignmentNote");	
	Параметры.Вставить("Идентификатор", ИдСбис);
	Параметры.Вставить("Вложение",      МассивВложений);
	
    Результат = ОбъектОбработки.local_helper_write_attachment(context, Параметры);
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьДействиеКПереходуСбис(Параметры, ДанныеДляПодписания)
	
	context_params  = Параметры.context_params;
    ОбъектОбработки = Параметры.ОбъектОбработки;
	
	СтруктураЭтапа = ЗначениеСледущегоЭтапа(
		Параметры.ТекущийЭтап,
		Параметры.ОтрицательныйПереход,
		Параметры.ПричинаОтрицательногоПерехода,
		ДанныеДляПодписания);
		
	ПараметрыДействия = Новый Структура;
	ПараметрыДействия.Вставить("Идентификатор", Параметры.ИД); 
	ПараметрыДействия.Вставить("Этап",          СтруктураЭтапа);
	
	
	Результат = ОбъектОбработки.local_helper_prepare_action(context_params, ПараметрыДействия);
	
	Структура = Новый Структура;
	Структура.Вставить("Результат",         Результат);
	Структура.Вставить("ПараметрыДействия", ПараметрыДействия);
	
	Возврат Структура;
	
КонецФункции

Функция ВыполнитьДействиеВСбис(Параметры)
	
	ОбъектОбработки = Параметры.ОбъектОбработки;
	
	РезультатВыполнить = ОбъектОбработки.local_helper_execute_action(Параметры.context_params, Параметры.ПараметрыДействия);		

	Массив = Новый Массив();
	Массив.Добавить(Параметры.Ссылка); 
	
	ДопПараметры = Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыОбновленияАктивногоЭтапа();
	ДопПараметры.ТекущийТитул = Параметры.ДинамическийТитул;
	
	РезультатПрочитать = ОбновитьАктивныйЭтапИзСбис(Массив, Параметры.context_params, ДопПараметры);
			
	Возврат РезультатПрочитать;
	
КонецФункции

Функция ПричинаОшибки(Описание) 
	
	ПодробноеОписание = "";
	
	Попытка
		Структура = ЗначениеИзСтрокиВнутр(Описание);
	Исключение
		// может прийти не десеарилизуемая строка
		Возврат Описание;
	КонецПопытки;
    	
	Если ТипЗнч(Структура.detail) = Тип("Строка") Тогда 
		ПодробноеОписание = Структура.detail;		
	ИначеЕсли ЗначениеЗаполнено(Структура.detail) Тогда  
		ПодробноеОписание = Структура.detail["error"]["details"];		
	КонецЕсли;	
	
	ТекстОшибки = Структура.message + ?(ПустаяСтрока(ПодробноеОписание), "", " - " + ПодробноеОписание);
	
	Возврат ТекстОшибки; 
	
КонецФункции	

Функция ЗначениеСледущегоЭтапа(ТекущийЭтап, ОтрицательныйПереход, ПричинаОтрицательногоПерехода, ДанныеДляПодписания)
	
	Комментарий = "";
	
	Если ТекущийЭтап = "1110343" Тогда
		
		Этап       = "Переадресовка";
		//ИдЭтап     = "327a0052-6b11-49eb-ba7a-c74c90938902";
		
		Действие   = "Переадресован";
		ИдДействие = "7b9b3f53-6549-4e37-8c49-650a7e41296c";
		
	ИначеЕсли ТекущийЭтап = "1110344" Тогда
		
		Этап       = "Замена водителей/ТС";
		//ИдЭтап     = "7b4c733f-3639-41a5-9e7c-bdac7e9d9a93";
		
		Действие   = "Замена водителей/ТС";
		ИдДействие = "8a458ae4-5a38-497d-b4ec-424e14cc64ed";
		
	ИначеЕсли ТекущийЭтап = "1167009" Тогда
		
		Этап       = "Уведомление о переадресовке";
		//ИдЭтап     = "bc2bb0b5-194f-40be-afd2-e43e01ed4baf";
		
		Действие   = "Уведомить";
		ИдДействие = "3f9da9bb-6cff-4d9f-aa97-99891079e4dd";
		
	ИначеЕсли ТекущийЭтап = "1110345" Тогда
		
		Этап       = "Изменить стоимость";
		//ИдЭтап     = "a03b3162-615f-49b4-916e-aa49a559e404";
		
		Действие   = "Согласовать";
		ИдДействие = "c1c702d6-f686-4baf-aa0f-52f4baee2e66";
		
	ИначеЕсли ТекущийЭтап = "1110346" Тогда
		
		Этап       = "Согласовать стоимость";
		//ИдЭтап     = "49c91f1d-1860-46ab-8279-d42ea215b0ce";
		
		Если ОтрицательныйПереход Тогда			
			Действие    = "Отклонено";
			ИдДействие  = "cc83d6a2-1c02-4246-8dc0-280f5ef9c9aa";
			Комментарий = ПричинаОтрицательногоПерехода;
		Иначе			
			Действие   = "Согласовано";
			ИдДействие = "9c4737f5-24a9-40e8-9d21-6b1122293550";			
		КонецЕсли;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураДействия = Новый Структура;
    СтруктураДействия.Вставить("Название",      Действие);
	СтруктураДействия.Вставить("Идентификатор", ИдДействие);
	СтруктураДействия.Вставить("Комментарий",   Комментарий);
	
	Если ДанныеДляПодписания <> Неопределено
		И ДанныеДляПодписания.Свойство("СертификатДок")
		И ДанныеДляПодписания.СертификатДок <> Неопределено Тогда
		СтруктураДействия.Вставить("Сертификат", ДанныеДляПодписания.СертификатДок);
	КонецЕсли;
	
	СтруктураЭтапа = Новый Структура;
	СтруктураЭтапа.Вставить("Действие", СтруктураДействия);
	
	//Если ЗначениеЗаполнено(Этап) Тогда
	СтруктураЭтапа.Вставить("Название", Этап);
	//СтруктураЭтапа.Вставить("Идентификатор", ИдЭтап);
	
	Возврат СтруктураЭтапа;
	
КонецФункции

Процедура УдалитьЛишниеДействия(Этап)
	
	Если Не ЗначениеЗаполнено(Этап) Тогда 
		Возврат;
	КонецЕсли;
		
	Для Каждого Элемент Из Этап Цикл 
		
		Действия = Элемент["Действие"];
		Если Не ЗначениеЗаполнено(Действия) Тогда 
			Продолжить;
		КонецЕсли;
		
		НовыеДействия = Новый Массив;
		
		Для Каждого Действие Из Действия Цикл 
			Если Действие["Название"] <> "Переназначить" Тогда 
				НовыеДействия.Добавить(Действие);
			КонецЕсли;
		КонецЦикла;
		
		Элемент.Вставить("Действие", НовыеДействия);
		
	КонецЦикла;
		
КонецПроцедуры

// Выполним подготовку и переход "хардкодом" по динамическому титулу
// из-за не возможности использовать общий механизм делаем прямой вызов нужных методов
//
Процедура ВыполнитьПереходПоДинамическомуТитулу(Параметры)
	
	Ссылка             = Параметры.Ссылка;	
	ДокументыСОшибками = Параметры.ДокументыСОшибками;
	Ошибки             = Параметры.Ошибки;
	ЕстьОшибки         = Ложь;
	
	Упал = Ложь;
	
	// 1. Подготовить действие. получим данные для осуществления перехода на след. этап 
	Попытка
		РезультатПодготовкиССертификатами = ПодготовитьДействиеССертификатами(Параметры);
		ДанныеДляПодписания = РезультатПодготовкиССертификатами.ДанныеДляПодписания;
		РезультатПодготовки = РезультатПодготовкиССертификатами.РезультатПодготовки;
	Исключение
		Упал = Истина;
	КонецПопытки;
	
	Если Упал Тогда
		
		Попытка
			РезультатПодготовкиССертификатами = ПодготовитьДействиеССертификатами(Параметры);
			ДанныеДляПодписания = РезультатПодготовкиССертификатами.ДанныеДляПодписания;
			РезультатПодготовки = РезультатПодготовкиССертификатами.РезультатПодготовки;
		Исключение
			ИнфоОшибки = ИнформацияОбОшибке();
			ТекстОшибки = ПричинаОшибки(ИнфоОшибки.Описание);						
			ДобавитьОшибкуПользователю(Ошибки, Неопределено, ТекстОшибки);
			
			ДокументыСОшибками.Вставить(Ссылка, Ошибки);			
			ЕстьОшибки = Истина;
		КонецПопытки;
		
	КонецЕсли;
	
	// при ошибках не выполняем переход 
	Если Не ЕстьОшибки Тогда 
		
		// 2. Выполнить действие. Перейдем на след. этап 
		Попытка
			
			ДобавитьЛокальнуюПодписьЕслиОнаЕсть(Параметры, РезультатПодготовки, ДанныеДляПодписания);
			
			// дополним исходные параметры
			Параметры.Вставить("ПараметрыДействия", РезультатПодготовки.ПараметрыДействия);			
			Итог = ВыполнитьДействиеВСбис(Параметры);
									
		Исключение
			
			ИнфоОшибки = ИнформацияОбОшибке();
			ТекстОшибки = ПричинаОшибки(ИнфоОшибки.Описание);					
			ДобавитьОшибкуПользователю(Ошибки, Неопределено, ТекстОшибки);
			
			ДокументыСОшибками.Вставить(Ссылка, Ошибки);
			
			СостояниеКод = 6; // Ошибка
			
			Состояние = Справочники.Saby_СостоянияОбъектов.ПолучитьСтатусДокументаСБИС(СостояниеКод);
			
			ПараметрыСтатус = Новый Структура;
			ПараметрыСтатус.Вставить("ИД",           Параметры.Ид);
			ПараметрыСтатус.Вставить("АктивныйЭтап", Параметры.Этап);
			ПараметрыСтатус.Вставить("Ссылка",       Ссылка);
			ПараметрыСтатус.Вставить("Загрузка",     Истина);
			ПараметрыСтатус.Вставить("Состояние",    Состояние);
			ПараметрыСтатус.Вставить("СбисИд",       "");
			
			УстановитьИДИСтатус(ПараметрыСтатус);
						
		КонецПопытки;
											
	КонецЕсли;
		
КонецПроцедуры

Функция ПодготовитьДействиеССертификатами(Параметры)
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ДанныеДляПодписания", ДанныеДляПодписания(Параметры));
	
	РезультатФункции.Вставить(
		"РезультатПодготовки",
		ПодготовитьДействиеКПереходуСбис(Параметры, РезультатФункции.ДанныеДляПодписания));
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ДанныеДляПодписания(Параметры)
	
	ДоступныеСертификаты = ДоступныеЛокальныеСертификаты(Параметры);
	
	Возврат Параметры.ОбъектОбработки.сбисОпределитьДанныеДляПодписания(
		Неопределено, Неопределено, ДоступныеСертификаты
	);
	
КонецФункции

Процедура ДобавитьЛокальнуюПодписьЕслиОнаЕсть(Параметры, РезультатПодготовки, ДанныеДляПодписания)
	
	Если Не ЗначениеЗаполнено(ДанныеДляПодписания) Тогда
		Возврат;
	КонецЕсли;
	
	ВложенияДляДобавленияПодписи = РезультатПодготовки["Результат"]["Этап"][0]["Вложение"];
	
	Контекст = Новый Структура("params", Параметры.context_params);
	
	Если ДанныеДляПодписания["Тип"] = "Дистанционное" Тогда
		Параметры.ОбъектОбработки.ПодписатьДистанционно(
			Контекст,
			ВложенияДляДобавленияПодписи,
			ДанныеДляПодписания["СертификатДляПодписания"]);
	ИначеЕсли ДанныеДляПодписания["Тип"] = "ЛокальноеНаСервере" Тогда
		Параметры.ОбъектОбработки.ПодписатьНаСервере(
			Контекст,
			ВложенияДляДобавленияПодписи,
			ДанныеДляПодписания["СертификатДляПодписания"]);
	ИначеЕсли ДанныеДляПодписания["Тип"] = "ЛокальноеНаКлиенте" Тогда
		Параметры.ОбъектОбработки.ПодписатьНаКлиенте(
			Контекст,
			ВложенияДляДобавленияПодписи,
			ДанныеДляПодписания,
			Неопределено);
	КонецЕсли;
	
	ДанныеПодписи = ДанныеПодписи(ВложенияДляДобавленияПодписи);
	
	СтруктураПодписи = Новый Структура;
	СтруктураПодписи.Вставить("Файл", Новый Структура);
	СтруктураПодписи.Файл.Вставить("ДвоичныеДанные", ДанныеПодписи.ДвоичныеДанные);
	
	МассивПодписей = Новый Массив;
	МассивПодписей.Добавить(СтруктураПодписи);
	
	СтруктураВложения = Новый Структура;
	СтруктураВложения.Вставить("Идентификатор", ДанныеПодписи.Идентификатор);
	СтруктураВложения.Вставить("Подпись", МассивПодписей);
	
	МассивВложений = Новый Массив;
	МассивВложений.Добавить(СтруктураВложения);
	
	РезультатПодготовки.ПараметрыДействия.Этап.Вставить("Вложение", МассивВложений);
	
КонецПроцедуры

Функция ДоступныеЛокальныеСертификаты(Параметры)
	
	РезультатФункции = Новый Соответствие;
	
	РезультатФункции.Вставить("ТребуетПодписания", "Да");
	РезультатФункции.Вставить("Сертификат", Новый Массив);
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("Идентификатор", Параметры.Ид);
	ПараметрыМетода.Вставить("ДопПоля",       "ЭтапВернутьВсеСертификаты");
	
	Результат = Параметры.ОбъектОбработки.local_helper_read_document(Параметры.context_params, ПараметрыМетода);
	
	Если Результат["Этап"] = Неопределено Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	Для Каждого Этап Из Результат["Этап"] Цикл
		Для Каждого Сертификат Из Этап["Действие"][0]["Сертификат"] Цикл
			Если Сертификат["Ключ"]["Тип"] = "Дистанционный" Или Сертификат["Ключ"]["Тип"] = "Клиентский" Тогда
				РезультатФункции["Сертификат"].Добавить(Сертификат);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ДанныеПодписи(ВложенияДляДобавленияПодписи)
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ДвоичныеДанные", ВложенияДляДобавленияПодписи[0]["Подпись"][0].Файл.ДвоичныеДанные);
	РезультатФункции.Вставить("Идентификатор",  ВложенияДляДобавленияПодписи[0]["Идентификатор"]);
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ДанныеУспешнойЗагрузкиДокумента(Ссылка, Этап, ДинамическийТитул, КоличествоЗагружено)
	
	КоличествоЗагружено = КоличествоЗагружено + 1;
	
	СтруктураОтвет = Новый Структура;
	СтруктураОтвет.Вставить("Ключ",              Ссылка);
	СтруктураОтвет.Вставить("Этап",              Этап);
	СтруктураОтвет.Вставить("ДинамическийТитул", ДинамическийТитул);
	
	Возврат СтруктураОтвет;
			
КонецФункции

#Область ЗаполнениеНаОснованииINI

Функция ЗаполнитьНаОснованииINI(Объект, ДокументОснование) Экспорт
	
	ПараметрыЗаполнения = ПараметрыЗаполненияНаОснованииINI();
	
	ПараметрыЗаполнения.Объект = Объект;
	
	ПерваяИтерация = Истина;
	
	ОснованияДляЗаполнения = ДанныеОснованийДляЗаполненияНаОснованииINI(ДокументОснование);
	Для Каждого СтруктураОснования Из ОснованияДляЗаполнения Цикл
		
		ПараметрыЗаполнения.ini_name             = СтруктураОснования.ini_name;
		ПараметрыЗаполнения.Основание            = СтруктураОснования.Основание;
		ПараметрыЗаполнения.ЭтоВыбранныйДокумент = СтруктураОснования.Основание = ДокументОснование;
		ПараметрыЗаполнения.ЭтоПервоеОснование   = ПерваяИтерация;
		ПараметрыЗаполнения.РольКонтрагента      = СтруктураОснования.РольКонтрагента;
		
		РезультатФункции = РезультатЗаполненияНаОснованииINI(ПараметрыЗаполнения);
		
		Если РезультатФункции.Ошибка Тогда
			Прервать;
		КонецЕсли;
		
		ДобавитьОснованияДляЗаполнения(ОснованияДляЗаполнения, РезультатФункции);
		
		ПерваяИтерация = Ложь;
		
	КонецЦикла;
	
	Если РезультатФункции.Ошибка Тогда 					
		РезультатФункции.Вставить("ТекстЗаголовка", "Ошибка");		
		РезультатФункции.Вставить("Картинка",       БиблиотекаКартинок["Saby_Ошибка32"]); 		
	Иначе                                                        		
		РезультатФункции.Вставить("ТекстЗаголовка", "Выполнено");
		РезультатФункции.Вставить("Картинка",       БиблиотекаКартинок["Saby_Успешно32"]);
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции	

Функция ДанныеОснованийДляЗаполненияНаОснованииINI(ДокументОснование)
	
	ОписаниеТипаОснования = Новый ОписаниеТипов;
	ОписаниеТипаОснования = Новый ОписаниеТипов(ОписаниеТипаОснования, Справочники.ТипВсеСсылки().Типы());
	ОписаниеТипаОснования = Новый ОписаниеТипов(ОписаниеТипаОснования, Документы.ТипВсеСсылки().Типы());
	
	ОписаниеТипаИмениИНИ = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(128,ДопустимаяДлина.Переменная));
	ОписаниеТипаРолейКонтрагентов = Новый ОписаниеТипов("ПеречислениеСсылка.Saby_РолиКонтрагентов");
	
	РезультатФункции = Новый ТаблицаЗначений;
	РезультатФункции.Колонки.Добавить("Основание",       ОписаниеТипаОснования);
	РезультатФункции.Колонки.Добавить("ini_name",        ОписаниеТипаИмениИНИ);
	РезультатФункции.Колонки.Добавить("РольКонтрагента", ОписаниеТипаРолейКонтрагентов);
	
	ШаблонИмениINI = "Blockly_%1_ЭТрН_read";
	ИмяМетаданных = Saby_ТНОбщегоНазначенияСервер.ИмяМетаданныхДокумента(ДокументОснование);
	
	СтруктураОснования = РезультатФункции.Добавить();
	СтруктураОснования.Основание = ДокументОснование;
	СтруктураОснования.ini_name  = СтрШаблон(ШаблонИмениINI, ИмяМетаданных);
	
	ЕстьТранспортнаяНакладнаяВКонфигурации = Метаданные.Документы.Найти("ТранспортнаяНакладная") <> Неопределено;
	Если ЕстьТранспортнаяНакладнаяВКонфигурации Тогда
		
		ЭтоТранспортнаяНакладная = ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ТранспортнаяНакладная");
		
		Если ЭтоТранспортнаяНакладная Тогда
			ЗапросДанных = Новый Запрос;
			ЗапросДанных.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТранспортнаяНакладнаяДокументыОснования.ДокументОснование КАК Ссылка
			|ИЗ
			|	Документ.ТранспортнаяНакладная.ДокументыОснования КАК ТранспортнаяНакладнаяДокументыОснования
			|ГДЕ
			|	ТранспортнаяНакладнаяДокументыОснования.Ссылка = &ДокументОснование
			|	И НЕ ТранспортнаяНакладнаяДокументыОснования.Ссылка.ПометкаУдаления";
			
			ЗапросДанных.УстановитьПараметр("ДокументОснование", ДокументОснование);
			
			ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
			Если ВыборкаДанных.Следующий() Тогда
				
				ИмяМетаданных = Saby_ТНОбщегоНазначенияСервер.ИмяМетаданныхДокумента(ВыборкаДанных.Ссылка);
				
				СтруктураОснования = РезультатФункции.Вставить(0);
				СтруктураОснования.Основание = ВыборкаДанных.Ссылка;
				СтруктураОснования.ini_name  = СтрШаблон(ШаблонИмениINI, ИмяМетаданных);
				
			КонецЕсли;
		Иначе
			ЗапросДанных = Новый Запрос;
			ЗапросДанных.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТранспортнаяНакладнаяДокументыОснования.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.ТранспортнаяНакладная.ДокументыОснования КАК ТранспортнаяНакладнаяДокументыОснования
			|ГДЕ
			|	ТранспортнаяНакладнаяДокументыОснования.ДокументОснование = &ДокументОснование
			|	И НЕ ТранспортнаяНакладнаяДокументыОснования.Ссылка.ПометкаУдаления";
			
			ЗапросДанных.УстановитьПараметр("ДокументОснование", ДокументОснование);
			
			ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
			Если ВыборкаДанных.Следующий() Тогда
				
				СтруктураОснования = РезультатФункции.Добавить();
				СтруктураОснования.Основание = ВыборкаДанных.Ссылка;
				СтруктураОснования.ini_name  = "Blockly_ТранспортнаяНакладная_ЭТрН_read";
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ДобавитьОснованияДляЗаполнения(ОснованияДляЗаполнения, РезультатФункции)
	
	Если Не РезультатФункции.Свойство("НовыеОснования") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("РольКонтрагента");
	
	Для Каждого НовоеОснование Из РезультатФункции.НовыеОснования Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, НовоеОснование);
		НайденныеСтроки = ОснованияДляЗаполнения.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтруктураОснования = НайденныеСтроки[0];
		Иначе
			СтруктураОснования = ОснованияДляЗаполнения.Добавить();
		КонецЕсли;
		
		СтруктураОснования.Основание       = НовоеОснование.Основание;
		СтруктураОснования.ini_name        = "Blockly_ДанныеКонтрагентаОрганизации_ЭТрН_read";
		СтруктураОснования.РольКонтрагента = НовоеОснование.РольКонтрагента;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьНовыеОснования(СтруктураРезультата, ДанныеИзИНИ)
	
	ГрузоотправительСсылка = ДанныеИзИНИ["ГрузоотправительСсылка"];
	Если ЗначениеЗаполнено(ГрузоотправительСсылка) Тогда
		
		СтруктураОснования = Новый Структура;
		СтруктураОснования.Вставить("Основание",       ГрузоотправительСсылка);
		СтруктураОснования.Вставить("РольКонтрагента", Перечисления.Saby_РолиКонтрагентов.Отправитель);
		СтруктураРезультата.НовыеОснования.Добавить(СтруктураОснования);
		
	КонецЕсли;
	
	ГрузополучательСсылка = ДанныеИзИНИ["ГрузополучательСсылка"];
	Если ЗначениеЗаполнено(ГрузополучательСсылка) Тогда
		СтруктураОснования = Новый Структура;
		СтруктураОснования.Вставить("Основание",       ГрузополучательСсылка);
		СтруктураОснования.Вставить("РольКонтрагента", Перечисления.Saby_РолиКонтрагентов.Получатель);
		СтруктураРезультата.НовыеОснования.Добавить(СтруктураОснования);
	КонецЕсли;
	
	ГрузоперевозчикСсылка = ДанныеИзИНИ["ГрузоперевозчикСсылка"];
	Если ЗначениеЗаполнено(ГрузоперевозчикСсылка) Тогда
		СтруктураОснования = Новый Структура;
		СтруктураОснования.Вставить("Основание",       ГрузоперевозчикСсылка);
		СтруктураОснования.Вставить("РольКонтрагента", Перечисления.Saby_РолиКонтрагентов.Перевозчик);
		СтруктураРезультата.НовыеОснования.Добавить(СтруктураОснования);
	КонецЕсли;
	
	ЗаказчикСсылка = ДанныеИзИНИ["ЗаказчикСсылка"];
	Если ЗначениеЗаполнено(ЗаказчикСсылка) Тогда
		СтруктураОснования = Новый Структура;
		СтруктураОснования.Вставить("Основание",       ЗаказчикСсылка);
		СтруктураОснования.Вставить("РольКонтрагента", Перечисления.Saby_РолиКонтрагентов.Заказчик);
		СтруктураРезультата.НовыеОснования.Добавить(СтруктураОснования);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПодстановки(ДанныеИзИНИ, ПараметрыЗаполнения)
	
	Если ПараметрыЗаполнения.РольКонтрагента = Перечисления.Saby_РолиКонтрагентов.Отправитель Тогда
		ДополнитьПодстановкиГрузоотправителя(ДанныеИзИНИ, ПараметрыЗаполнения);
	ИначеЕсли ПараметрыЗаполнения.РольКонтрагента = Перечисления.Saby_РолиКонтрагентов.Получатель Тогда
		ДополнитьПодстановкиГрузополучателя(ДанныеИзИНИ, ПараметрыЗаполнения);
	ИначеЕсли ПараметрыЗаполнения.РольКонтрагента = Перечисления.Saby_РолиКонтрагентов.Перевозчик Тогда
		ДополнитьПодстановкиГрузоперевозчика(ДанныеИзИНИ, ПараметрыЗаполнения);
	ИначеЕсли ПараметрыЗаполнения.РольКонтрагента = Перечисления.Saby_РолиКонтрагентов.Заказчик Тогда
		ДополнитьПодстановкиЗаказчика(ДанныеИзИНИ, ПараметрыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПодстановкиГрузоотправителя(ДанныеИзИНИ, ПараметрыЗаполнения)
	
	Если Не ЗапрещеноЗаполнять("ПунктПогрузки", ПараметрыЗаполнения) Тогда
		ПунктПогрузкиВыгрузки = ДанныеИзИНИ["ПунктПогрузкиВыгрузки"];
		Если ПунктПогрузкиВыгрузки <> Неопределено Тогда
			ДанныеИзИНИ.Вставить("ПунктПогрузки", ПунктПогрузкиВыгрузки);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗапрещеноЗаполнять("Грузоотправитель", ПараметрыЗаполнения) Тогда
		ДанныеИзИНИ.Вставить("Грузоотправитель", ДанныеЮрЛица(ДанныеИзИНИ));
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПодстановкиГрузополучателя(ДанныеИзИНИ, ПараметрыЗаполнения)
	
	Если Не ЗапрещеноЗаполнять("ПунктВыгрузки", ПараметрыЗаполнения) Тогда
		ПунктПогрузкиВыгрузки = ДанныеИзИНИ["ПунктПогрузкиВыгрузки"];
		Если ПунктПогрузкиВыгрузки <> Неопределено Тогда
			ДанныеИзИНИ.Вставить("ПунктВыгрузки", ПунктПогрузкиВыгрузки);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗапрещеноЗаполнять("Грузополучатель", ПараметрыЗаполнения) Тогда
		ДанныеИзИНИ.Вставить("Грузополучатель", ДанныеЮрЛица(ДанныеИзИНИ));
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПодстановкиГрузоперевозчика(ДанныеИзИНИ, ПараметрыЗаполнения)
	
	Если Не ЗапрещеноЗаполнять("Грузоперевозчик", ПараметрыЗаполнения) Тогда
		ДанныеИзИНИ.Вставить("Грузоперевозчик", ДанныеЮрЛица(ДанныеИзИНИ));
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПодстановкиЗаказчика(ДанныеИзИНИ, ПараметрыЗаполнения)
	
	Если Не ЗапрещеноЗаполнять("Заказчик", ПараметрыЗаполнения) Тогда
		ДанныеИзИНИ.Вставить("Заказчик", ДанныеЮрЛица(ДанныеИзИНИ));
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеЮрЛица(ДанныеИзИНИ)
	
	РезультатФункции = Новый Соответствие;
	РезультатФункции.Вставить("Контакты",  ДанныеИзИНИ["Контакты"]);
	РезультатФункции.Вставить("Адрес",     ДанныеИзИНИ["Адрес"]);
	РезультатФункции.Вставить("Реквизиты", ДанныеИзИНИ["Реквизиты"]);
	РезультатФункции.Вставить("Название",  ДанныеИзИНИ["Название"]);
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура УстановитьЗапретЗаполнения(ПараметрыЗаполнения)
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Saby_ТранспортнаяНакладная.Погрузка_Адрес КАК Погрузка_Адрес,
	|	Saby_ТранспортнаяНакладная.Отправитель_АдресДоставки КАК Отправитель_АдресДоставки
	|ИЗ
	|	Документ.Saby_ТранспортнаяНакладная КАК Saby_ТранспортнаяНакладная
	|ГДЕ
	|	Saby_ТранспортнаяНакладная.Ссылка = &Ссылка";
	
	ЗапросДанных.УстановитьПараметр("Ссылка", ПараметрыЗаполнения.Объект.Ссылка);
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда
		
		Если ЗначениеЗаполнено(ВыборкаДанных.Погрузка_Адрес) Тогда
			ПараметрыЗаполнения.ЗапретЗаполнения.Вставить("ПунктПогрузки", Истина);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаДанных.Отправитель_АдресДоставки) Тогда
			ПараметрыЗаполнения.ЗапретЗаполнения.Вставить("ПунктВыгрузки", Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЗапрещеноЗаполнять(ИмяПоля, ПараметрыЗаполнения)
	
	Возврат ПараметрыЗаполнения.ЗапретЗаполнения[ИмяПоля] <> Неопределено;
	
КонецФункции

Функция ПараметрыЗаполненияНаОснованииINI()
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ini_name",             "");
	РезультатФункции.Вставить("Объект",               Неопределено);
	РезультатФункции.Вставить("Основание",            Неопределено);
	РезультатФункции.Вставить("ЭтоВыбранныйДокумент", Ложь);
	РезультатФункции.Вставить("ЭтоПервоеОснование",   Ложь);
	РезультатФункции.Вставить("ТранспортБлокли",      Обработки.SABY.Создать());
	РезультатФункции.Вставить("context_params",       Saby_Core.ПроверитьНаличиеПараметровПодключения());
	РезультатФункции.Вставить("РольКонтрагента",      Перечисления.Saby_РолиКонтрагентов.ПустаяСсылка());
	РезультатФункции.Вставить("ЗапретЗаполнения",     Новый Соответствие);
	
	Возврат РезультатФункции;
	
КонецФункции

Функция РезультатЗаполненияНаОснованииINI(ПараметрыЗаполнения)
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ТекстСообщения", "");
	РезультатФункции.Вставить("Ошибка",         Ложь);
	РезультатФункции.Вставить("НовыеОснования", Новый Массив);
	
	ConnectionId = Неопределено; 
	Если ПараметрыЗаполнения.context_params = Неопределено Тогда 
		
		РезультатФункции.ТекстСообщения = "Нет входа в учетную запись!";
		РезультатФункции.Ошибка = Истина;
		Возврат РезультатФункции;
		
	КонецЕсли;
	
	ПараметрыЗаполнения.context_params.Свойство("ConnectionId", ConnectionId);
	
	ПараметрыВызова	= Новый Соответствие();
	ПараметрыВызова.Вставить("params",          ПараметрыЗаполнения.context_params);
	ПараметрыВызова.Вставить("commands_result", Новый Массив);
	ПараметрыВызова.Вставить("endpoint",        "");
	ПараметрыВызова.Вставить("operation_uuid",  СокрЛП(Новый УникальныйИдентификатор));
	ПараметрыВызова.Вставить("ini_name",        ПараметрыЗаполнения.ini_name);
	ПараметрыВызова.Вставить("connection_uuid", ConnectionId);
	ПараметрыВызова.Вставить("ref",             ПараметрыЗаполнения.Основание);
	
	Попытка
		XMLИниФайл = ПараметрыЗаполнения.ТранспортБлокли.Load_ini(ПараметрыЗаполнения.ini_name, ПараметрыВызова);		
	Исключение
		XMLИниФайл = Неопределено;
	КонецПопытки;
	 	
	Если Не (ТипЗнч(XMLИниФайл) = Тип("Строка") И Врег(Лев(XMLИниФайл,4)) = "<XML") Тогда
		РезультатФункции.ТекстСообщения = "Не найдена INI для данного типа документа";
		РезультатФункции.Ошибка = Истина; 
		Возврат РезультатФункции;
	КонецЕсли;	
	
	Результат = ПараметрыЗаполнения.ТранспортБлокли.API_BLOCKLY_RUN(ПараметрыВызова);
	
	Если Результат.status <> "complete" Тогда
		РезультатФункции.ТекстСообщения = "Ошибка расчета INI. Данные не получены";
		РезультатФункции.Ошибка = Истина;
		Возврат РезультатФункции;
	КонецЕсли;
	
	ДанныеИзИНИ = Результат.data;
	
	СформироватьНовыеОснования(РезультатФункции, ДанныеИзИНИ);
	ДополнитьПодстановки(ДанныеИзИНИ, ПараметрыЗаполнения);
	
	Подстановки = Новый Соответствие;
	ЭТрН_Погрузка = Новый Соответствие;
	ЭТрН_Погрузка.Вставить("ЭТрН_1110339", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеИзИНИ));
	Подстановки.Вставить("Подстановки", ЭТрН_Погрузка);
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("Данные",                   Подстановки);
	ПараметрыЗагрузки.Вставить("Ссылка",                   ПараметрыЗаполнения.Объект.Ссылка);
	ПараметрыЗагрузки.Вставить("ИдСбис",                   Неопределено);
	ПараметрыЗагрузки.Вставить("ИзДокумента",              Истина);
	ПараметрыЗагрузки.Вставить("ЭтоЗаполнениеСПомощьюINI", Истина);
	ПараметрыЗагрузки.Вставить("ЭтоПервоеОснование",       ПараметрыЗаполнения.ЭтоПервоеОснование);
	
	Если ПараметрыЗаполнения.ЭтоВыбранныйДокумент Тогда
		ПараметрыЗагрузки.Вставить("ДокументОснование", ПараметрыЗаполнения.Основание); 
	КонецЕсли;
	
	Saby_ТНОбщегоНазначенияСервер.ЗагрузкаДанныхДокумента(ПараметрыЗагрузки);
	
	УстановитьЗапретЗаполнения(ПараметрыЗаполнения);
	
	РезультатФункции.ТекстСообщения = "Заполнение на основании завершено";
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // ЗаполнениеНаОснованииINI

Функция ТипыПоУмолчаниюДляЗаполненияНаОсновании()
	
	РезультатФункции = Новый Массив;
	
	Если Метаданные.Синоним = "Бухгалтерия предприятия, редакция 3.0" Тогда 
		
		РезультатФункции.Добавить(Тип("ДокументСсылка.ВозвратТоваровПоставщику"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
		
	ИначеЕсли Метаданные.Синоним = "1С:ERP Управление предприятием 2" Тогда 
		
		РезультатФункции.Добавить(Тип("ДокументСсылка.ВозвратТоваровПоставщику"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ВозвратСырьяДавальцу"));		
		РезультатФункции.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ОтгрузкаТоваровСХранения"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПередачаДавальцу"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПередачаСырьяПереработчику"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПередачаТоваровХранителю"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));		
		РезультатФункции.Добавить(Тип("ДокументСсылка.ТранспортнаяНакладная"));
		
	ИначеЕсли Метаданные.Синоним = "1С:Комплексная автоматизация 2" Тогда
		
		РезультатФункции.Добавить(Тип("ДокументСсылка.ВозвратТоваровПоставщику"));	
		РезультатФункции.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ОтгрузкаТоваровСХранения"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПередачаСырьяПереработчику"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПередачаТоваровХранителю"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));		
		РезультатФункции.Добавить(Тип("ДокументСсылка.ТранспортнаяНакладная"));    
		
	ИначеЕсли Метаданные.Синоним = "Управление торговлей, редакция 11" Тогда   	
		
		РезультатФункции.Добавить(Тип("ДокументСсылка.ВозвратТоваровПоставщику"));	
		РезультатФункции.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ОтгрузкаТоваровСХранения"));  
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПередачаТоваровХранителю"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));		
		РезультатФункции.Добавить(Тип("ДокументСсылка.ТранспортнаяНакладная")); 
		
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ПользовательскиеТипыДляЗаполненияНаОсновании()
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатФункции = ХранилищеОбщихНастроек.Загрузить("Saby", "ТипыДляЗаполненияНаОснованииДокументаТРН");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ЗаписьОшибокВЖурналРегистрации(СписокДокументов) 
	
	Если ТипЗнч(СписокДокументов) <> Тип("Соответствие") Тогда
		Возврат; 
	КонецЕсли;
		
	// Запись ошибок в ЖР
	Если СписокДокументов.Количество() Тогда
		
		СписокЗаписейЖР = Новый СписокЗначений;
		Для Каждого ОшибкиДокумента Из СписокДокументов Цикл   
			
			МассивОшибок = ОписанияОшибок(ОшибкиДокумента.Значение.СписокОшибок);
			
			Описание = "Не выгружен документ " + СокрЛП(ОшибкиДокумента.Ключ);
			Текст    = СтрСоединить(МассивОшибок, Символы.ПС);
									
			Saby_ТНОбщегоНазначенияСервер.ЗаписатьОшибкуВЖурналРегистрации(ОшибкиДокумента.Ключ, Описание, Текст);
						
		КонецЦикла;	
				
	КонецЕсли;
	
КонецПроцедуры

Функция ОписанияОшибок(СписокОшибок)

	РезультатФункции = Новый Массив;

	Для Каждого СтруктураОшибки Из СписокОшибок Цикл

		РезультатФункции.Добавить(СтруктураОшибки.ТекстДляОднойОшибки);

	КонецЦикла;

	Возврат РезультатФункции;

КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

#Область ПечатнаяФормаPDFИзСБИС

Функция ЗагрузитьПечатнуюФормуДокументаИзСбис(ПараметрыЗагрузки, URLФайлаВСбис = Неопределено, ВзятьИзБазыЕслиЕсть = Ложь) Экспорт
	
	СтруктураПрисоединенногоФайла = СтруктураПрисоединенногоФайла(ПараметрыЗагрузки);
	
	Если ВзятьИзБазыЕслиЕсть И ЗначениеЗаполнено(СтруктураПрисоединенногоФайла.СсылкаНаПрисоединенныйФайл) Тогда
		Возврат СтруктураПрисоединенногоФайла.СсылкаНаПрисоединенныйФайл;
	КонецЕсли;
	
	ВременноеХранилище = АдресДвоичныхДанныхФайлаЭТрН(ПараметрыЗагрузки, URLФайлаВСбис);
	
	Если Не ЗначениеЗаполнено(ВременноеХранилище) Тогда
		Возврат Справочники.Saby_ТранспортнаяНакладнаяПрисоединенныеФайлы.ПустаяСсылка();
	КонецЕсли;
	
	ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
	ПараметрыФайла.Автор              = Пользователи.АвторизованныйПользователь();
	ПараметрыФайла.ВладелецФайлов     = ПараметрыЗагрузки.СсылкаНаДокумент;
	ПараметрыФайла.ИмяБезРасширения   = СтруктураПрисоединенногоФайла.ИмяБезРасширения;
	ПараметрыФайла.РасширениеБезТочки = СтруктураПрисоединенногоФайла.РасширениеБезТочки;
	
	Возврат РаботаСФайлами.ДобавитьФайл(
		ПараметрыФайла,
		ВременноеХранилище,
		,,
		СтруктураПрисоединенногоФайла.СсылкаНаПрисоединенныйФайл
	);
	
КонецФункции

Функция АдресДвоичныхДанныхФайлаЭТрН(ПараметрыЗагрузки, URLФайлаВСбис = Неопределено) Экспорт
	
	РезультатФункции = Неопределено;
	
	Если URLФайлаВСбис = Неопределено Тогда
		URLФайлаВСбис = СсылкаНаPDF(ПараметрыЗагрузки);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(URLФайлаВСбис) Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	ДвоичныеДанныеФайла = ПараметрыЗагрузки.ОбъектОбработки.local_helper_download_from_link(
		ПараметрыЗагрузки.context_params,
		URLФайлаВСбис
	);
	
	РезультатФункции = ПоместитьВоВременноеХранилище(
		ДвоичныеДанныеФайла,
		ПараметрыЗагрузки.УникальныйИдентификаторФормы
	);
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ПараметрыЗагрузкиПечатнойФормыИзСбис(Знач СсылкаНаДокумент = Неопределено,
		context_params = Неопределено, УникальныйИдентификаторФормы = Неопределено) Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("СсылкаНаДокумент",             СсылкаНаДокумент);
	РезультатФункции.Вставить("УИДСбис",                      "");
	РезультатФункции.Вставить("Номер",                        "");
	РезультатФункции.Вставить("Дата",                         Дата(1, 1, 1, 0, 0, 0));
	РезультатФункции.Вставить("ОбъектОбработки",              Обработки.SABY.Создать());
	РезультатФункции.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификаторФормы);
	РезультатФункции.Вставить("СписокДокументов",             Неопределено);
	
	Если context_params = Неопределено Тогда
		РезультатФункции.Вставить("context_params", Saby_Core.ПроверитьНаличиеПараметровПодключения());
	Иначе
		РезультатФункции.Вставить("context_params", context_params);
	КонецЕсли;
	
	Если СсылкаНаДокумент <> Неопределено Тогда
		ЗаполнитьПараметрыЗагрузкиПечатнойФормыПоСсылке(РезультатФункции);
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ЗаполнитьПараметрыЗагрузкиПечатнойФормыПоСсылке(Параметры, ПовторноеПолучение = Ложь)
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Saby_Состояние.UID, """") КАК UID,
	|	Saby_ТранспортнаяНакладная.Номер КАК Номер,
	|	Saby_ТранспортнаяНакладная.Дата КАК Дата
	|ИЗ
	|	Документ.Saby_ТранспортнаяНакладная КАК Saby_ТранспортнаяНакладная
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Saby_Состояние КАК Saby_Состояние
	|		ПО (Saby_Состояние.Объект = Saby_ТранспортнаяНакладная.Ссылка)
	|ГДЕ
	|	Saby_ТранспортнаяНакладная.Ссылка = &СсылкаНаДокумент";
	
	ЗапросДанных.УстановитьПараметр("СсылкаНаДокумент", Параметры.СсылкаНаДокумент);
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда;
		
		Параметры.УИДСбис = ВыборкаДанных.UID;
		Параметры.Номер   = ВыборкаДанных.Номер;
		Параметры.Дата    = ВыборкаДанных.Дата;
		
	КонецЕсли;
	
	Если Не ПовторноеПолучение И Параметры.УИДСбис = "" Тогда
		МассивДокументов = Новый Массив;
		МассивДокументов.Добавить(Параметры.СсылкаНаДокумент);
		РезультатЗаписи = ЗаписатьДокументВСбис(МассивДокументов, Параметры.context_params);
		Если РезультатЗаписи.Успешно Тогда
			ЗаполнитьПараметрыЗагрузкиПечатнойФормыПоСсылке(Параметры, Истина);
		Иначе
			Параметры.СписокДокументов = РезультатЗаписи.СписокДокументов;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СсылкаНаPDF(ПараметрыЗагрузки)
	
	РезультатФункции = Неопределено;
	
	Если Не ЗначениеЗаполнено(ПараметрыЗагрузки.УИДСбис) Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Идентификатор", ПараметрыЗагрузки.УИДСбис);
	Параметры.Вставить("ДопПоля",       "");
	
	Попытка		
		
		Результат = ПараметрыЗагрузки.ОбъектОбработки.local_helper_read_document(
			ПараметрыЗагрузки.context_params,
			Параметры
		);
		РезультатФункции = Результат["СсылкаНаPDF"];
		
	Исключение
		
		ИнфоОшибки = ИнформацияОбОшибке();
		
		Saby_ТНОбщегоНазначенияСервер.ЗаписатьОшибкуВЖурналРегистрации(
			ПараметрыЗагрузки.СсылкаНаДокумент,
			"Не удалось получить печатную форму",
			ИнфоОшибки.Описание
		);
		
	КонецПопытки;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция СтруктураПрисоединенногоФайла(ПараметрыЗагрузки)
	
	ШаблонИмениФайла = "ЭТрН №%1 от №%2 (%3)";
	
	ИмяБезРасширения = СтрШаблон(
		ШаблонИмениФайла,
		ПараметрыЗагрузки.Номер,
		Формат(ПараметрыЗагрузки.Дата, "ДФ=dd.MM.yyyy"),
		ПараметрыЗагрузки.УИДСбис
	);
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ИмяБезРасширения",           ИмяБезРасширения);
	РезультатФункции.Вставить("РасширениеБезТочки",         "pdf");
	РезультатФункции.Вставить("СсылкаНаПрисоединенныйФайл", Неопределено);
	
	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Saby_ТранспортнаяНакладнаяПрисоединенныеФайлы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Saby_ТранспортнаяНакладнаяПрисоединенныеФайлы КАК Saby_ТранспортнаяНакладнаяПрисоединенныеФайлы
	|ГДЕ
	|	НЕ Saby_ТранспортнаяНакладнаяПрисоединенныеФайлы.ПометкаУдаления
	|	И Saby_ТранспортнаяНакладнаяПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
	|	И Saby_ТранспортнаяНакладнаяПрисоединенныеФайлы.Наименование ПОДОБНО ""%&Наименование%""";
	
	ЗапросДанных.Текст = СтрЗаменить(ЗапросДанных.Текст, "&Наименование", ПараметрыЗагрузки.УИДСбис);
	ЗапросДанных.УстановитьПараметр("ВладелецФайла", ПараметрыЗагрузки.СсылкаНаДокумент);
	
	ВыборкаДанных = ЗапросДанных.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда
		НайденнаяСсылка = ВыборкаДанных.Ссылка;
	Иначе
		НайденнаяСсылка = Справочники.Saby_ТранспортнаяНакладнаяПрисоединенныеФайлы.ПустаяСсылка();
	КонецЕсли;
	
	Если Не НайденнаяСсылка.Пустая() Тогда
		РезультатФункции.СсылкаНаПрисоединенныйФайл = НайденнаяСсылка;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // ПечатнаяФормаPDFИзСБИС
