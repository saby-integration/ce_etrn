 
#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнениеРеквизитовФормы(Параметры);	
	Элементы.ГруппаСсылка.Видимость                 = Не Параметры.ТолькоПросмотр;
	Элементы.ФормаУстановитьНовыеДанные.Доступность = Не Параметры.ТолькоПросмотр;
	
	ЭтоОтветственный    = Параметры.Свойство("Ответственный");
	ЭтоОформилДокументы = Параметры.Свойство("Оформил");
	
	НеВодитель = ЭтоОтветственный Или ЭтоОформилДокументы;
	
	Если НеВодитель Тогда 
		
		ЭтаФорма.Заголовок = Параметры.Наименование;
		
		// Видимость		
		Элементы.ИНН.Видимость            = Ложь;
		Элементы.ГруппаВУ.Видимость       = Ложь;
		
		Если ЭтоОтветственный Тогда 
			Элементы.ГруппаТелефоны.Видимость = Ложь;
		КонецЕсли;
			
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьИзСправочника(Команда)
	
	// в дальнейшем настройка на произвольный справочник 
	Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораВодителя", ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.ФизическиеЛица.ФормаВыбора", ,
		ЭтаФорма, , , ,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоСсылке(Команда)
	
	ОбновитьДанныеПоСсылке();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНовыеДанные(Команда)
	
	ВУСтрокой = Saby_ТНОбщегоНазначенияКлиентСервер.ПредставлениеВУ(ЭтаФорма);
	ФИО       = Saby_ТНОбщегоНазначенияКлиентСервер.ПредставлениеФИО(ЭтаФорма);
	
	ТелефоныЕмейлы = ТелефоныЕмейлыСтрокой();
		
	Структура = Новый Структура;
	Структура.Вставить("Фамилия",      Фамилия);
	Структура.Вставить("Имя",          Имя);
	Структура.Вставить("Отчество",     Отчество);
	Структура.Вставить("ИНН",          ИНН);
	Структура.Вставить("СерияВУ",      СерияВУ);
	Структура.Вставить("НомерВУ",      НомерВУ);
	Структура.Вставить("ДатаВыдачиВУ", ДатаВыдачиВУ);
	Структура.Вставить("Телефоны",     ТелефоныЕмейлы.Телефоны);
	Структура.Вставить("Емейлы",       ТелефоныЕмейлы.Емейлы);
	Структура.Вставить("ФИО",          ФИО);
	Структура.Вставить("ВУСтрокой",    ВУСтрокой);
	
	ЭтаФорма.Закрыть(Структура);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсеПоля(Команда)
	
	ОчиститьРеквизитыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьРеквизитыФормы()
	
	Saby_ТНОбщегоНазначенияСервер.ОчиститьРеквизитыФормы(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнениеРеквизитовФормы(Знач ДанныеЗаполнения)

	РеквизитыФормы = ЭтаФорма.ПолучитьРеквизиты();
	ТипТаблицаЗначений = Новый ОписаниеТипов("ТаблицаЗначений");	
	
	ТолькоПросмотр = ДанныеЗаполнения.Свойство("ТолькоПросмотр")
		И ДанныеЗаполнения.ТолькоПросмотр = Истина;
		
	ЭтаФорма.ТолькоПросмотр                   = ТолькоПросмотр;	
	Элементы.ГруппаФИОИНН.ТолькоПросмотр      = ТолькоПросмотр;
	Элементы.Телефоны.ТолькоПросмотр          = ТолькоПросмотр;
	Элементы.ФормаОчиститьВсеПоля.Доступность = Не ТолькоПросмотр;
	Элементы.ГруппаВУ.ТолькоПросмотр          = ТолькоПросмотр;
	
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл 
		
		Если ДанныеЗаполнения.Свойство(РеквизитФормы.Имя) Тогда
			
			ЗначениеИсточник = ДанныеЗаполнения[РеквизитФормы.Имя];			
			Если РеквизитФормы.ТипЗначения = ТипТаблицаЗначений Тогда 
				ЗаполнитьТаблицуЗначений(ДанныеЗаполнения, РеквизитФормы, ЗначениеИсточник);								
			Иначе
				ЭтаФорма[РеквизитФормы.Имя] = ЗначениеИсточник;
			КонецЕсли;
												
		КонецЕсли;
		
	КонецЦикла;
	
	СформироватьНадписьТелефоныСтрокой();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЗначений(ДанныеЗаполнения, РеквизитФормы, ЗначениеИсточник)
	
	ЭтаФорма[РеквизитФормы.Имя].Очистить();
	
	Если ЗначениеЗаполнено(ЗначениеИсточник) Тогда 
		
		ИмяКолонки = Лев(РеквизитФормы.Имя, СтрДлина(РеквизитФормы.Имя) - 1);
		
		Если ТипЗнч(ЗначениеИсточник) = Тип("Массив") Тогда
			Массив = ЗначениеИсточник;
		Иначе
			Массив = ЗначениеИзСтрокиВнутр(ЗначениеИсточник);
		КонецЕсли;
		
		Для Каждого Значение Из Массив Цикл 
			НС = ЭтаФорма[РеквизитФормы.Имя].Добавить();
			НС[ИмяКолонки] = Значение;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#Область Телефоны

&НаКлиенте
Процедура ТелефоныПослеУдаления(Элемент)
	СформироватьНадписьТелефоныСтрокой();
КонецПроцедуры

&НаСервере
Процедура СформироватьНадписьТелефоныСтрокой()
		
	Массив = Новый Массив;
	Для Каждого Строка Из Телефоны Цикл 
		
		Если ЗначениеЗаполнено(Строка.Телефон) Тогда
			Массив.Добавить(СокрЛП(Строка.Телефон));
		КонецЕсли;
				
	КонецЦикла;
	
	Если Массив.Количество() Тогда 
		ЭтаФорма.ТелефоныСтрокой = СтрСоединить(Массив, ", ");
	Иначе 
		ЭтаФорма.ТелефоныСтрокой = "";
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныТелефонПриИзменении(Элемент)
	СформироватьНадписьТелефоныСтрокой();
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭтаФорма.ТекущийЭлемент = Элементы.Телефоны; 
	
КонецПроцедуры

#КонецОбласти // Телефоны

&НаКлиенте
Процедура ОбработкаВыбораВодителя(ВыбранныйЭлемент, ДопПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ЭтаФорма.Водитель = ВыбранныйЭлемент;				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаПриИзменении(Элемент)
	
	ОбновитьДанныеПоСсылке();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоСсылке()
	
	Если ЗначениеЗаполнено(Водитель) Тогда 
		ДанныеВодителя = Saby_ТНОбщегоНазначенияСервер.ДанныеВодителяПоСсылке(Водитель);		
		ЗаполнениеРеквизитовФормы(ДанныеВодителя);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ТелефоныЕмейлыСтрокой()
	
	Если Телефоны.Количество() Тогда 
		
		МассивТелефонов = Новый Массив;
		Для Каждого Строка Из Телефоны Цикл 
			МассивТелефонов.Добавить(Строка.Телефон);			
		КонецЦикла;
		
		ТелефоныСтрокой = ЗначениеВСтрокуВнутр(МассивТелефонов);
		
	Иначе 	
		ТелефоныСтрокой = "";
	КонецЕсли;	
	
	Если Емейлы.Количество() Тогда 
		
		МассивЕмейлы = Новый Массив;
		Для Каждого Строка Из Емейлы Цикл 
			МассивЕмейлы.Добавить(Строка.Емейл);			
		КонецЦикла;
		
		ЕмейлыСтрокой = ЗначениеВСтрокуВнутр(МассивЕмейлы);
		
	Иначе 	
		ЕмейлыСтрокой = "";
	КонецЕсли;	
	
	Структура = Новый Структура;
	Структура.Вставить("Телефоны", ТелефоныСтрокой);
	Структура.Вставить("Емейлы",   ЕмейлыСтрокой);
		
	Возврат Структура;
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
