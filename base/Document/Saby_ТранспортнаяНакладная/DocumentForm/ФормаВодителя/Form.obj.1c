 
#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнениеРеквизитовФормы(Параметры);	
	Элементы.ГруппаСсылка.Видимость                 = Не Параметры.ТолькоПросмотр;
	Элементы.ФормаУстановитьНовыеДанные.Доступность = Не Параметры.ТолькоПросмотр;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьИзСправочника(Команда)
	
	// в дальнейшем настройка на произвольный справочник 
	Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораВодителя", ЭтотОбъект);
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаВыбора",,ЭтаФорма,,,,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоСсылке(Команда)
	ОбновитьДанныеПоСсылке();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНовыеДанные(Команда)
	
	ВУСтрокой = Saby_ТНОбщегоНазначенияКлиентСервер.ПредставлениеВУ(ЭтаФорма);
	ФИО       = Saby_ТНОбщегоНазначенияКлиентСервер.ПредставлениеФИО(ЭтаФорма);
	
	Структура = Новый Структура;
	Структура.Вставить("Фамилия",      Фамилия);
	Структура.Вставить("Имя",          Имя);
	Структура.Вставить("Отчество",     Отчество);
	Структура.Вставить("ИНН",          ИНН);
	Структура.Вставить("СерияВУ",      СерияВУ);
	Структура.Вставить("НомерВУ",      НомерВУ);
	Структура.Вставить("ДатаВыдачиВУ", ДатаВыдачиВУ);
	Структура.Вставить("Телефоны",     ТелефоныСтрокой());
	Структура.Вставить("ФИО",          ФИО);
	Структура.Вставить("ВУСтрокой",    ВУСтрокой);
	
	ЭтаФорма.Закрыть(Структура);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнениеРеквизитовФормы(ДанныеЗаполнения)

	РеквизитыФормы = ЭтаФорма.ПолучитьРеквизиты();
	ТипТаблицаЗначений = Новый ОписаниеТипов("ТаблицаЗначений");	
	
	ТолькоПросмотр = Ложь;
	Если ДанныеЗаполнения.Свойство("ТолькоПросмотр") Тогда		
		ТолькоПросмотр = ДанныеЗаполнения.ТолькоПросмотр;
	КонецЕсли;
	
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл 
		
		Если ДанныеЗаполнения.Свойство(РеквизитФормы.Имя) Тогда
			
			ЗначениеИсточник = ДанныеЗаполнения[РеквизитФормы.Имя];			
			Если РеквизитФормы.ТипЗначения = ТипТаблицаЗначений Тогда 
				
				ЭтаФорма[РеквизитФормы.Имя].Очистить();
				
				Если ЗначениеЗаполнено(ЗначениеИсточник) Тогда 
					
					ИмяКолонки = Лев(РеквизитФормы.Имя, СтрДлина(РеквизитФормы.Имя)-1);
					
					Массив = ЗначениеИзСтрокиВнутр(ЗначениеИсточник);
					Для Каждого Значение Из Массив Цикл 
						НС = ЭтаФорма[РеквизитФормы.Имя].Добавить();
						НС[ИмяКолонки] = Значение;
					КонецЦикла;
					
				КонецЕсли;
				
			Иначе
				ЭтаФорма[РеквизитФормы.Имя] = ЗначениеИсточник;
			КонецЕсли;
			
			// Отключим возможность вносить изменения
			Если ТолькоПросмотр Тогда
				Элементы[РеквизитФормы.Имя].ТолькоПросмотр = Истина;
			КонецЕсли;
									
		КонецЕсли;
		
	КонецЦикла;
	
	СформироватьНадписьТелефоныСтрокой();
	
КонецПроцедуры

#Область Телефоны

&НаКлиенте
Процедура ТелефоныПослеУдаления(Элемент)
	СформироватьНадписьТелефоныСтрокой();
КонецПроцедуры

&НаСервере
Процедура СформироватьНадписьТелефоныСтрокой()
		
	Массив = Новый Массив;
	Для Каждого Строка Из Телефоны Цикл 
		
		Если ЗначениеЗаполнено(Строка.Телефон) Тогда
			Массив.Добавить(СокрЛП(Строка.Телефон));
		КонецЕсли;
				
	КонецЦикла;
	
	Если Массив.Количество() Тогда 
		ТелефоныСтрокой = СтрСоединить(Массив, ", ");
	Иначе 
		ТелефоныСтрокой = "";
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныТелефонПриИзменении(Элемент)
	СформироватьНадписьТелефоныСтрокой();
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭтаФорма.ТекущийЭлемент = Элементы.Телефоны; 
	
КонецПроцедуры

#КонецОбласти // Телефоны

&НаКлиенте
Процедура ОбработкаВыбораВодителя(ВыбранныйЭлемент, ДопПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		Ссылка = ВыбранныйЭлемент;				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаПриИзменении(Элемент)
	
	ОбновитьДанныеПоСсылке();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоСсылке()
	
	Если ЗначениеЗаполнено(Ссылка) Тогда 
		ДанныеВодителя = Saby_ТНОбщегоНазначенияСервер.ДанныеВодителяПоСсылке(Ссылка);		
		ЗаполнениеРеквизитовФормы(ДанныеВодителя);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ТелефоныСтрокой()
	
	Если Телефоны.Количество() Тогда 
		
		МассивТелефонов = Новый Массив;
		Для Каждого Строка Из Телефоны Цикл 
			МассивТелефонов.Добавить(Строка.Телефон);			
		КонецЦикла;
		
		ТелефоныСтрокой = ЗначениеВСтрокуВнутр(МассивТелефонов);
		
	Иначе 	
		ТелефоныСтрокой = "";
	КонецЕсли;	
	
	Возврат ТелефоныСтрокой;
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
