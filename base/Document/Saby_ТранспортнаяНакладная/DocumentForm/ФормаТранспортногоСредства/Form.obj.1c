
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьДоступностьЭлементовФормы(Параметры.ТолькоПросмотр);
	
	ЗаполнитьРеквизитыИзПараметров(Параметры);
	
	Если Параметры.Свойство("ОснованияВладения") Тогда
		ЗаполнитьОснованияВладенияИзСтроки(Параметры.ОснованияВладения);
	КонецЕсли;
	
	ОбновитьОтображениеОснованийВладения();
	
	УстановитьОграничениеТипаСсылки();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	
	ОчиститьСообщения();
	
	ЗаполнитьРеквизитыНаОснованииСправочника();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипВладенияПриИзменении(Элемент)
	
	ОбновитьОтображениеОснованийВладения();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	СтруктураТранспортногоСредства = Новый Структура();
	СтруктураТранспортногоСредства.Вставить("Вид",                  ЭтотОбъект.Вид);
	СтруктураТранспортногоСредства.Вставить("РегистрационныйНомер", ЭтотОбъект.РегистрационныйНомер);
	СтруктураТранспортногоСредства.Вставить("Тип",                  ЭтотОбъект.Тип);
	СтруктураТранспортногоСредства.Вставить("Марка",                ЭтотОбъект.Марка);
	СтруктураТранспортногоСредства.Вставить("ВИН",                  ЭтотОбъект.ВИН);
	СтруктураТранспортногоСредства.Вставить("НомерСТС",             ЭтотОбъект.НомерСТС);
	СтруктураТранспортногоСредства.Вставить("Грузоподъемность",     ЭтотОбъект.Грузоподъемность);
	СтруктураТранспортногоСредства.Вставить("Вместимость",          ЭтотОбъект.Вместимость);
	СтруктураТранспортногоСредства.Вставить("ТипВладения",          ЭтотОбъект.ТипВладения);
	
	СтруктураТранспортногоСредства.Вставить(
		"ОснованияВладения", ОснованияВладенияВСтроку(ЭтотОбъект.ОснованияВладения));
	
	Закрыть(СтруктураТранспортногоСредства);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаОсновании(Команда)
	
	ЗаполнитьРеквизитыНаОснованииСправочника();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсеПоля(Команда)
	
	ОчиститьРеквизитыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьРеквизитыФормы() 
	
	Saby_ТНОбщегоНазначенияСервер.ОчиститьРеквизитыФормы(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеНаОсновании

&НаСервере
Процедура ЗаполнитьРеквизитыНаОснованииСправочника()
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.ТранспортноеСредство) Тогда
		Возврат;
	КонецЕсли;
	
	МассивТС = Новый Массив;
	МассивТС.Добавить(ЭтотОбъект.ТранспортноеСредство);
	РеквизитыТС = Saby_ТНОбщегоНазначенияСервер.РеквизитыТранспортныхСредств(МассивТС);
	РеквизитыТС = РеквизитыТС[ЭтотОбъект.ТранспортноеСредство];
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыТС, , "ОснованияВладения,Вид");
	ЗаполнитьОснованияВладенияИзСтроки(РеквизитыТС.ОснованияВладения);
	
	ОбновитьОтображениеОснованийВладения();
	
КонецПроцедуры

#КонецОбласти // ЗаполнениеНаОсновании

&НаСервере
Процедура ЗаполнитьРеквизитыИзПараметров(СтруктураПараметров)
	
	РеквизитыИсключения = Новый Массив;
	РеквизитыИсключения.Добавить("ОснованияВладения");
	
	Для Каждого РеквизитФормы Из ЭтаФорма.ПолучитьРеквизиты() Цикл
		Если Не Параметры.Свойство(РеквизитФормы.Имя) Тогда
			Продолжить;
		КонецЕсли;
		Если РеквизитыИсключения.Найти(РеквизитФормы.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, РеквизитФормы.Имя);
	КонецЦикла;
	
	Если ЭтотОбъект.Вид.Пустая() Тогда
		Если СтруктураПараметров.ЭтоПрицеп Тогда
			ЭтотОбъект.Вид = Перечисления.Saby_ВидыТС.Прицеп;
		Иначе
			ЭтотОбъект.Вид = Перечисления.Saby_ВидыТС.Автомобиль;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеОснованийВладения()
	
	Элементы.ОснованияВладения.Видимость = Перечисления.Saby_ТипыВладенияТС.ТребуетПодтвержденияВладения(
		ЭтотОбъект.ТипВладения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОснованияВладенияВСтроку(Знач ОснованияВладения)
	
	МассивОснований = Новый Массив;
	Для Каждого СтрокаОснованияВладения Из ОснованияВладения Цикл
		МассивОснований.Добавить(СтрокаОснованияВладения.ДокументВладения);
	КонецЦикла;
	
	Если МассивОснований.Количество() > 0 Тогда
		Возврат ЗначениеВСтрокуВнутр(МассивОснований);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОснованияВладенияИзСтроки(ОснованияВладенияСтрокой)
	
	ОснованияВладения.Очистить();
	
	Если Не ЗначениеЗаполнено(ОснованияВладенияСтрокой) Тогда
		Возврат;
	КонецЕсли;
	
	МассивОснований = ЗначениеИзСтрокиВнутр(ОснованияВладенияСтрокой);
	
	Для Каждого Основание Из МассивОснований Цикл
		НоваяСтрока = ОснованияВладения.Добавить();
		НоваяСтрока.ДокументВладения = Основание;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступностьЭлементовФормы(ТолькоПросмотр)
	
	Элементы.ГруппаЗаполнениеНаОсновании.Видимость = Не ТолькоПросмотр;
	
	Элементы.ФормаЗаписатьИЗакрыть.Доступность = Не ТолькоПросмотр;
	Элементы.ФормаОчиститьВсеПоля.Доступность = Не ТолькоПросмотр;
	Элементы.ЗаполнитьНаОсновании.Доступность = Не ТолькоПросмотр;
	
	Для Каждого РеквизитФормы Из ЭтаФорма.ПолучитьРеквизиты() Цикл
		Элементы[РеквизитФормы.Имя].ТолькоПросмотр = ТолькоПросмотр;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОграничениеТипаСсылки()
	
	ИмяКонфигурации = Saby_ТНОбщегоНазначенияСервер.ИмяКонфигурации();
	ДоступныеКонфигурации = Saby_ТНОбщегоНазначенияСервер.ДоступныеКонфигурации();
	
	Если ИмяКонфигурации = ДоступныеКонфигурации.УАТ Тогда
		Элементы.ТранспортноеСредство.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.уатТС");
	Иначе
		Элементы.ТранспортноеСредство.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ТранспортныеСредства");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
