  
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, 
		"Список.Дата", "Дата");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Saby_ТНОбщегоНазначенияКлиент.ОбработатьОповещенияДляФормы(ЭтаФорма, ИмяСобытия, Параметр, Источник); 
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтказВПриемкеГруза(Команда)
		
	Подсказка    = "Укажите причину отказа от приемки груза"; 
	ДопПараметры = Новый Структура;  	
	Оповещение   = Новый ОписаниеОповещения("ПослеВводаПричиныОтказа", ЭтаФорма, ДопПараметры);
	
	ПоказатьВводСтроки(Оповещение, "", Подсказка, 0, Истина);
					
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВСбис(Команда)
	
	Saby_ТНОбщегоНазначенияКлиент.ОбменДаннымиСБИСИзФормыСписка(
		ЭтаФорма, 
		Команда.Имя, 
		Новый Структура);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьДокумент(Команда)
	
	Saby_ТНОбщегоНазначенияКлиент.ОбменДаннымиСБИСИзФормыСписка(ЭтаФорма, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВСбис(Команда)
	
	Saby_ТНОбщегоНазначенияКлиент.ОбменДаннымиСБИСИзФормыСписка(ЭтаФорма, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДокументИзСбис(Команда)
			
	Saby_ТНОбщегоНазначенияКлиент.ОбновитьДокументыСБИС(ЭтаФорма, Команда.Имя, "Saby_ТранспортнаяНакладная");
			
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДокументИзСбисЗаПериод(Команда)
	
	Saby_ТНОбщегоНазначенияКлиент.ОбновитьДокументыЗаПериод(ЭтаФорма, "ОбновитьДокументИзСбис", Истина); 

КонецПроцедуры

&НаКлиенте
Процедура Просмотрено(Команда)
	
	МассивДокументов = Saby_ТНОбщегоНазначенияКлиент.ДокументыДляОбработки(ЭтаФорма);
	Saby_ТНОбщегоНазначенияСервер.СбросСтатусаНеПросмотрено(МассивДокументов);
	ОповеститьОбИзменении(Тип("ДокументСсылка.Saby_ТранспортнаяНакладная"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИзмененныхДокументов(Команда)
	
	УстановленОтборИзмененныхДокументов = Не УстановленОтборИзмененныхДокументов;
	Если УстановленОтборИзмененныхДокументов Тогда		
		Значение = Ложь;		
	Иначе		
		Значение = Неопределено;		
	КонецЕсли;
		
	Saby_ТНОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		Значение, 
		"Просмотрен");
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	СсылкаНаДокумент = Элементы.Список.ТекущаяСтрока;
	Если Не ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	Saby_ТНОбщегоНазначенияКлиент.СоздатьИОткрытьПечатнуюФорму(СсылкаНаДокумент, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ДатаПоследнегоОбновления()
	Возврат Документы.Saby_ТранспортнаяНакладная.ПоследнееУспешноЗагруженноеИзменение();
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьДокументВСбис(Ссылка, context_params, Ошибки)
	
	Документы.Saby_ТранспортнаяНакладная.ЗаписатьДокументВСбис(
		Ссылка, 
		context_params,
		Ошибки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаПричиныОтказа(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	// ToDo: обработать документы
	
	Если Не ЗначениеЗаполнено(РезультатЗакрытия) Тогда 
						
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Для отказа от приемки груза укажите причину";
		Сообщение.Поле  = "ФормаОтказВПриемкеГруза";
		Сообщение.Сообщить();
		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
		
	Saby_ТНОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, 
		Тип, 
		"Направление"); 
	
КонецПроцедуры

#Область ОбработкаОповещений

&НаКлиенте
Процедура ОбработатьОповещениеВыполнитьПереход() Экспорт
	
	Saby_ТНОбщегоНазначенияКлиент.ОбменДаннымиСБИСИзФормыСписка(ЭтаФорма, "ОбновитьАктивныйЭтап");
	
КонецПроцедуры

#КонецОбласти // ОбработкаОповещений

#КонецОбласти // СлужебныеПроцедурыИФункции  
