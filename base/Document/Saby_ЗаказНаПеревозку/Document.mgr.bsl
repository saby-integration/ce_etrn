                                 
#Область ПрограммныйИнтерфейс

// Возвращает массив типов доступных для заполнения на основании.
//
// Параметры:
//  ЗначенияПоУмолчанию - Булево - признак получения типов по умолчанию
//
// Возвращаемое значение:
//   Массив - массив типов выбранных для заполнения на основании.
//
Функция ТипыДляЗаполненияНаОсновании(ЗначенияПоУмолчанию = Ложь) Экспорт
	
	Если Не ЗначенияПоУмолчанию Тогда
		РезультатФункции = ПользовательскиеТипыДляЗаполненияНаОсновании();
	Иначе
		РезультатФункции = Неопределено;
	КонецЕсли;
	
	Если РезультатФункции = Неопределено Тогда
		РезультатФункции = ТипыПоУмолчаниюДляЗаполненияНаОсновании();
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

// Сохраняет выбранные типы документов для заполнения на основании выбранные пользователем.
//
// Параметры:
//  МассивТипов - Массив - массив типов
//
Процедура СохранитьВыбранныеТипыДляЗаполненияНаОсновании(МассивТипов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ХранилищеОбщихНастроек.Сохранить("Saby", "ТипыДляЗаполненияНаОснованииДокументаЗнП", МассивТипов);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Возвращает структуру реквизитов документа, относящиеся титулу
//
// Параметры:
//  СсылкаНаДокумент - ДокументСсылка.Saby_ЗаказНаПеревозку - ссылка на документ
//  ТипТитула - ПеречислениеСсылка.Saby_ТипТитулаЗнП - ссылка на тип титула
//
// Возвращаемое значение:
//   Структура - структура полученных данных документа
//
Функция ДанныеДокумента(СсылкаНаДокумент, ТипТитула) Экспорт
	
	Если Перечисления.Saby_ТипТитулаЗнП.ЭтоПервыйТитул(ТипТитула) Тогда
		
		РезультатФункции = ВыгрузкаДанныеОформление(СсылкаНаДокумент);
		
	ИначеЕсли ТипТитула = Перечисления.Saby_ТипТитулаЗнП.Утверждение Тогда
		
		РезультатФункции = ВыгрузкаДанныеУтверждение(СсылкаНаДокумент);
		
	Иначе
		
		РезультатФункции = Новый Структура;
		РезультатФункции.Вставить("Реквизиты", Новый Структура);
		
	КонецЕсли;
	
	РезультатФункции.Реквизиты.Вставить("ТипТитула", ТипТитула);
	
	Возврат РезультатФункции;
	
КонецФункции

// Добавляет данные титулов для выгрузки на онлайн
//
// Параметры:
//  ТекущийЭтап - ПеречислениеСсылка.Saby_ТипДокументаЗнП - тип текущего титула
//  ДанныеТитула - Структура - структура реквизитов и таблиц документа для выгрузки
//
// Возвращаемое значение:
//   Структура - структура титула для отправки
//
Функция ВложениеНаВыгрузку(ТекущийЭтап, ДанныеТитула) Экспорт
	
	Если Перечисления.Saby_ТипТитулаЗнП.ЭтоПервыйТитул(ТекущийЭтап) Тогда
		
		Возврат ТитулОформление(ДанныеТитула);
		
	ИначеЕсли ТекущийЭтап = Перечисления.Saby_ТипТитулаЗнП.Утверждение Тогда
		
		СтруктураТитула = ТитулУтверждение(ДанныеТитула);
		
		СтруктураТитула.Вставить("СторонняяОрганизация", Истина);
		
		Возврат СтруктураТитула;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции
 
#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

#Область Команды

// Заполняет список команд печати.
//
// Параметры:
//  КомандыПечати - ТаблицаЗначений - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти // Команды

Функция МенеджерТитулов() Экспорт
	
	Возврат Перечисления.Saby_ТипТитулаЗнП;
	
КонецФункции

#Область ИнтерфейсФЛК

// Возвращает параметры для выполнения ФЛК
//
// Параметры:
//  ДанныеДокумента - Структура - структура реквизитов и табличных частей документа
//  ПараметрыВыгрузки - Структура - см. Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыВыгрузки
//
// Возвращаемое значение:
//   Структура - структура данных для дальнейшей проверки реквизитов
//
Функция ПараметрыПроверкиРеквизитов(ДанныеДокумента, ПараметрыВыгрузки) Экспорт
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ПроверяемыеРеквизиты", Новый Массив);
	
	ОшибкиПоРеквизитам = Saby_ТНОбщегоНазначенияСервер.СоответствиеРеквизитовОшибок(ПараметрыВыгрузки);
	РезультатФункции.Вставить("ОшибкиПоРеквизитам", ОшибкиПоРеквизитам);
	
	Если Перечисления.Saby_ТипТитулаЗнП.ЭтоПервыйТитул(ПараметрыВыгрузки.ТипТитула) Тогда
		
		ДополнитьПроверяемыеРеквизитыЭтапаОформление(ДанныеДокумента, РезультатФункции.ПроверяемыеРеквизиты);
		
	ИначеЕсли ПараметрыВыгрузки.ТипТитула = Перечисления.Saby_ТипТитулаЗнП.Утверждение Тогда
		
		ДополнитьПроверяемыеРеквизитыЭтапаУтверждение(ДанныеДокумента, РезультатФункции.ПроверяемыеРеквизиты);
		
	Иначе
		РезультатФункции.ПроверяемыеРеквизиты = Новый Массив;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

// Возвращает имя таблицы документа с учетом специфики
//
// Параметры:
//  ИмяТабличнойЧасти - Строка - наименование табличной части из конфигуратора
//  СтрокаТабличнойЧасти - СтрокаТабличнойЧасти - строка табличной части документа
//  ДанныеДокумента - Структура - структура реквизитов и табличных частей документа
//
// Возвращаемое значение:
//   Строка - наименование таблицы на форме документа
//
Функция ИмяТЧДокумента(ИмяТабличнойЧасти, СтрокаТабличнойЧасти, ДанныеДокумента) Экспорт
	
	РезультатФункции = ИмяТабличнойЧасти;
	
	Если ИмяТабличнойЧасти = "ДанныеЮрЛиц"
		И СтрокаТабличнойЧасти.Роль = Перечисления.Saby_РолиКонтрагентов.УполномоченноеЛицо Тогда
		РезультатФункции = "УполномоченныеЛицаЗаказчика";
	ИначеЕсли ИмяТабличнойЧасти = "ОтветственныеЛица"
		И СтрокаТабличнойЧасти.Роль = Перечисления.Saby_РолиОтветственных.Водитель Тогда
		РезультатФункции = "Водители";
	Иначе 
		РезультатФункции = Неопределено;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ПроверятьРеквизитДокумента(ИмяРеквизита, ДанныеДокумента) Экспорт
	
	Если ИмяРеквизита = "ПредставительЗаказчикаНаименование" Или ИмяРеквизита = "ПредставительЗаказчика_Основание" Тогда
		Возврат ДанныеДокумента.Реквизиты.ПредставительЗаказчика;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает признак необходимости ФЛК реквизитов табличной части документа
//
// Параметры:
//  СтрокаТЧ - СтрокаТаблицыЗначений - строка табличной части документа
//  ИмяТЧ - Строка - имя табличной части документа
//  ИмяРеквизита - Строка - имя реквизита табличной части документа
//  ДанныеДокумента - Структура - структура реквизитов и табличных частей документа
//
// Возвращаемое значение:
//   Булево - Истина, если проверять необходимо
//
Функция ПроверятьРеквизитТЧДокумента(СтрокаТЧ, ИмяТЧ, ИмяРеквизита, ДанныеДокумента) Экспорт
	
	Если ИмяТЧ = "ДанныеЮрЛиц" Тогда
		Возврат СтрокаТЧ.Роль = Перечисления.Saby_РолиКонтрагентов.УполномоченноеЛицоПеревозчика
			И ДанныеДокумента.Реквизиты.ТипТитула = Перечисления.Saby_ТипТитулаЗнП.Утверждение;
	ИначеЕсли ИмяТЧ = "ОтветственныеЛица" Тогда
		Возврат ПроверятьРеквизитОтветственного(СтрокаТЧ, ИмяРеквизита);
	ИначеЕсли ИмяТЧ = "УполномоченныеЛицаЗаказчика" Тогда
		Возврат Перечисления.Saby_ТипТитулаЗнП.ЭтоПервыйТитул(ДанныеДокумента.Реквизиты.ТипТитула);		
	ИначеЕсли ИмяТЧ = "СпецРазрешения" Тогда
		Возврат ПроверятьРеквизитыСпецРазрешения(СтрокаТЧ, ИмяРеквизита);
	ИначеЕсли ИмяТЧ = "АдресаПунктовВыгрузки" Тогда
		Возврат ИмяРеквизита <> "ОрганизацияСтрока" Или СтрокаТЧ.ЭтоПогрузка;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Выполняет нестандартные проверки ФЛК с логикой сложнее, чем заполнен незаполнен.
//
// Параметры:
//  ДанныеДокумента - Структура - структура реквизитов и табличных частей документа
//  ПараметрыВыгрузки - Структура - см. Saby_ТНОбщегоНазначенияКлиентСервер.ПараметрыВыгрузки
//  ПараметрыПроверкиРеквизитов - Структура - см. функцию ПараметрыПроверкиРеквизитов
//  Ошибки - Массив,Неопределено - массив для накопления ошибок
//
Процедура ВыполнитьДополнительныеПроверкиФЛК(ДанныеДокумента, ПараметрыВыгрузки,
		ПараметрыПроверкиРеквизитов, Ошибки) Экспорт
		
	Если Перечисления.Saby_ТипТитулаЗнП.ЭтоПервыйТитул(ПараметрыВыгрузки.ТипТитула) Тогда
		
		ДопПроверкиФЛКОформление(ДанныеДокумента, ПараметрыВыгрузки, ПараметрыПроверкиРеквизитов, Ошибки);
		
	ИначеЕсли ПараметрыВыгрузки.ТипТитула = Перечисления.Saby_ТипТитулаЗнП.Утверждение Тогда
		
		ДопПроверкиФЛКУтверждение(ДанныеДокумента, ПараметрыВыгрузки, ПараметрыПроверкиРеквизитов, Ошибки);
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти // ИнтерфейсФЛК

Функция ТекущиеЭтапыДокумента(Форма) Экспорт
	
	РезультатФункции = Новый Массив;
	
	ДанныеСостояния = Saby_ТНОбщегоНазначенияСервер.ДанныеСостоянияОбъекта(Форма.Объект.Ссылка);
	Если ДанныеСостояния <> Неопределено Тогда
		ТекущийТитул = Перечисления.Saby_ТипТитулаЗнП.ЗначениеПоПредставлению(ДанныеСостояния.АктивныйЭтап);
	Иначе
		ТекущийТитул = Перечисления.Saby_ТипТитулаЗнП.ПервыйТитул();
	КонецЕсли;
	
	РезультатФункции.Добавить(ТекущийТитул);
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ВложенияЗагружены(Вложения, Титул, ОбъектДок, ДопПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Вложения) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВложенияМассив = Saby_ТНОбщегоНазначенияКлиентСервер.ЗначениеВМассив(Вложения);
	
	Если Титул = Перечисления.Saby_ТипТитулаЗнП.Оформление Тогда
		
		ЗагрузкаТитулаОформление(ОбъектДок, ВложенияМассив, Титул, ДопПараметры);
		
	ИначеЕсли Титул = Перечисления.Saby_ТипТитулаЗнП.Утверждение Тогда
		
		ЗагрузкаТитулаУтверждение(ОбъектДок, ВложенияМассив, Титул, ДопПараметры);
				
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ТитулыДляЗагрузки(ОбъектДок, ПараметрыЗагрузки) Экспорт
	
	ТекущиеЗначения = Saby_ТНЗагрузкаСервер.ТекущиеЗначенияДокумента(ОбъектДок, ПараметрыЗагрузки);
	
	Этап = ТекущиеЗначения.Титул;
	
	Если ЗначениеЗаполнено(ТекущиеЗначения.Состояние) Тогда  
	   КодСостояния = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущиеЗначения.Состояние, "ИдентификаторСБИС");
	КонецЕсли;   
	
	ТипыТитулов = Saby_ТНОбщегоНазначенияСервер.ВсеЗначенияПеречисления("Saby_ТипТитулаЗнП");
	
    МассивТитуловДляЗагрузки = Новый Массив;
	
	ДОЗавершенУспешно      = 7;
	ДОЗавершенОтрицательно = 9;
	
	Если КодСостояния = ДОЗавершенУспешно
		Или КодСостояния = ДОЗавершенОтрицательно Тогда
		
		Если ПараметрыЗагрузки.ТолькоАктивныйЭтап Тогда
			МассивТитуловДляЗагрузки.Добавить(Этап);
		Иначе
			МассивТитуловДляЗагрузки.Добавить(ТипыТитулов.Утверждение);
		КонецЕсли;
		
		Возврат МассивТитуловДляЗагрузки;	
		
	КонецЕсли;	
	
	Если ПараметрыЗагрузки.ТолькоАктивныйЭтап Тогда
		
		// В регистре состояния нет данных о текущем этапе, при этом документооборот не завершен.
		Если Этап.Пустая() Тогда
			МассивТитуловДляЗагрузки.Добавить(ТипыТитулов.Оформление);
		Иначе
			МассивТитуловДляЗагрузки.Добавить(Этап);
		КонецЕсли;
		
	Иначе
		
		МассивТитуловДляЗагрузки.Добавить(ТипыТитулов.Оформление);
		МассивТитуловДляЗагрузки.Добавить(ТипыТитулов.Утверждение);
		
	КонецЕсли;
	
	Возврат МассивТитуловДляЗагрузки;

КонецФункции

#Область ЗаполнениеНаОснованииИНИИнтерфейс

Функция ШаблонИмениINI() Экспорт
	
	Возврат "Blockly_%1_ЗнП_read";
	
КонецФункции

Процедура СформироватьНовыеОснования(СтруктураРезультата, ДанныеИзИНИ) Экспорт
	
	ГрузоотправительСсылка = ДанныеИзИНИ["ГрузоотправительСсылка"];
	
	Если ЗначениеЗаполнено(ГрузоотправительСсылка) Тогда
		
		СтруктураОснования = Saby_ТНОбщегоНазначенияСервер.СтруктураОснования(
			ГрузоотправительСсылка, Перечисления.Saby_РолиКонтрагентов.Отправитель);
		СтруктураРезультата.НовыеОснования.Добавить(СтруктураОснования);
		
	КонецЕсли;
	
	ГрузоперевозчикСсылка = ДанныеИзИНИ["ГрузоперевозчикСсылка"];
	Если ЗначениеЗаполнено(ГрузоперевозчикСсылка) Тогда
		СтруктураОснования = Saby_ТНОбщегоНазначенияСервер.СтруктураОснования(
			ГрузоперевозчикСсылка, Перечисления.Saby_РолиКонтрагентов.Перевозчик);
		СтруктураРезультата.НовыеОснования.Добавить(СтруктураОснования);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПодстановки(ДанныеИзИНИ, ПараметрыЗаполнения) Экспорт
	
	Если ПараметрыЗаполнения.РольКонтрагента = Перечисления.Saby_РолиКонтрагентов.Отправитель Тогда
		ДополнитьПодстановкиГрузоотправителя(ДанныеИзИНИ, ПараметрыЗаполнения);
	ИначеЕсли ПараметрыЗаполнения.РольКонтрагента = Перечисления.Saby_РолиКонтрагентов.Перевозчик Тогда
		ДополнитьПодстановкиГрузоперевозчика(ДанныеИзИНИ, ПараметрыЗаполнения);
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ЗаполнениеНаОснованииИНИИнтерфейс

Функция ДоступныеОрганизацииЭтапы(ОбъектДок, ПараметрыЗагрузки) Экспорт
	
	РезультатФункции = Новый Соответствие;
	
	ДанныеНашейОрганизации = Saby_ТНЗагрузкаСервер.ИННКППОрганизации(
		ПараметрыЗагрузки.ДанныеДокумента["НашаОрганизация"]);
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Роль", Перечисления.Saby_РолиКонтрагентов.Отправитель);
	СтруктураПоиска.Вставить("ИНН",  ДанныеНашейОрганизации.ИНН);
	СтруктураПоиска.Вставить("КПП",  ДанныеНашейОрганизации.КПП);
	НайденныеСтроки = ОбъектДок.ДанныеЮрЛиц.НайтиСтроки(СтруктураПоиска);
	
	ЭтоОрганизацияОтправитель = НайденныеСтроки.Количество() > 0
		И Saby_ТНОбщегоНазначенияСервер.ОрганизацияПрисутствуетВБазе(СтруктураПоиска.ИНН, СтруктураПоиска.КПП);
		
	СтруктураПоиска.Вставить("Роль", Перечисления.Saby_РолиКонтрагентов.Перевозчик);
	НайденныеСтроки = ОбъектДок.ДанныеЮрЛиц.НайтиСтроки(СтруктураПоиска);
	
	ЭтоОрганизацияПеревозчик = НайденныеСтроки.Количество() > 0
		И Saby_ТНОбщегоНазначенияСервер.ОрганизацияПрисутствуетВБазе(СтруктураПоиска.ИНН, СтруктураПоиска.КПП);
	
	Для Каждого ТекущийЭтап Из ПараметрыЗагрузки.ДанныеДокумента["ТекущиеЭтапы"] Цикл
		
		НаименованиеЭтапа = ТекущийЭтап["Наименование"];
		
		ЭтапДоступен = (НаименованиеЭтапа = "Оформление" И ЭтоОрганизацияОтправитель)
			Или (НаименованиеЭтапа = "Утверждение" И ЭтоОрганизацияПеревозчик);

		Если ЭтапДоступен Тогда
			РезультатФункции.Вставить(НаименованиеЭтапа, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПользовательскиеТипыДляЗаполненияНаОсновании()
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатФункции = ХранилищеОбщихНастроек.Загрузить("Saby", "ТипыДляЗаполненияНаОснованииДокументаЗнП");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ТипыПоУмолчаниюДляЗаполненияНаОсновании()
	
	РезультатФункции = Новый Массив;
	
	СинонимКонфигурации = Метаданные.Синоним;
	
	ДоступныеКонфигурации    = Saby_ТНОбщегоНазначенияСервер.ДоступныеКонфигурации();
	ЭтоДоступнаяКонфигурация = Saby_ТНОбщегоНазначенияСервер.ЭтоДоступнаяКонфигурация(
		СинонимКонфигурации, ДоступныеКонфигурации);
		
	Если Не ЭтоДоступнаяКонфигурация Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	Если Метаданные.Синоним <> ДоступныеКонфигурации.УАТ Тогда
		РезультатФункции.Добавить(Тип("ДокументСсылка.ВозвратТоваровПоставщику"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	КонецЕсли;
	
	Если Метаданные.Синоним = ДоступныеКонфигурации.ЕРП Тогда
		
		РезультатФункции.Добавить(Тип("ДокументСсылка.ВозвратСырьяДавальцу"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПередачаДавальцу"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПередачаСырьяПереработчику"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ОтгрузкаТоваровСХранения"));
		РезультатФункции.Добавить(Тип("ДокументСсылка.ПередачаТоваровХранителю"));
		
	КонецЕсли;
	
	Возврат РезультатФункции;
		
КонецФункции

#Область ФЛК

Процедура ДополнитьПроверяемыеРеквизитыЭтапаОформление(ДанныеДокумента, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("Заказчик");
	ПроверяемыеРеквизиты.Добавить("ПредставительЗаказчикаНаименование");
	ПроверяемыеРеквизиты.Добавить("ПредставительЗаказчика_Основание");
	ПроверяемыеРеквизиты.Добавить("Перевозчик");
	ПроверяемыеРеквизиты.Добавить("СпецУсловия");
	ПроверяемыеРеквизиты.Добавить("ТребованияКТС");
	
	ПроверяемыеРеквизиты.Добавить("ПодачаАдрес");
	ПроверяемыеРеквизиты.Добавить("ПодачаДатаВремя");
	
	ПроверяемыеРеквизиты.Добавить("АдресаПунктовВыгрузки");
	ПроверяемыеРеквизиты.Добавить("АдресаПунктовВыгрузки.Значение");
	ПроверяемыеРеквизиты.Добавить("АдресаПунктовВыгрузки.ДатаВремя");
	ПроверяемыеРеквизиты.Добавить("АдресаПунктовВыгрузки.ОрганизацияСтрока");
	
	ПроверяемыеРеквизиты.Добавить("Грузы");
	ПроверяемыеРеквизиты.Добавить("Грузы.Тип");
	ПроверяемыеРеквизиты.Добавить("Грузы.Наименование");
	ПроверяемыеРеквизиты.Добавить("Грузы.Состояние");
	ПроверяемыеРеквизиты.Добавить("Грузы.Количество");
	ПроверяемыеРеквизиты.Добавить("Грузы.МассаБрутто");
	ПроверяемыеРеквизиты.Добавить("Грузы.ВидТары");
	ПроверяемыеРеквизиты.Добавить("Грузы.МетодОпределенияМассы");
	ПроверяемыеРеквизиты.Добавить("Грузы.Объем");
	ПроверяемыеРеквизиты.Добавить("Грузы.Длина");
	ПроверяемыеРеквизиты.Добавить("Грузы.Ширина");
	ПроверяемыеРеквизиты.Добавить("Грузы.Высота");
	ПроверяемыеРеквизиты.Добавить("Грузы.РаспределениеПоДлинеПлатформы");
	ПроверяемыеРеквизиты.Добавить("Грузы.Делимость");
	
	ПроверяемыеРеквизиты.Добавить("ТипТС");
	ПроверяемыеРеквизиты.Добавить("ГрузоподъемностьТС");
	ПроверяемыеРеквизиты.Добавить("ВместимостьТС");
	
	ПроверяемыеРеквизиты.Добавить("ДанныеЮрЛиц.Полномочия");
	ПроверяемыеРеквизиты.Добавить("ДанныеЮрЛиц.Основание");
	
КонецПроцедуры

Процедура ДополнитьПроверяемыеРеквизитыЭтапаУтверждение(ДанныеДокумента, ПроверяемыеРеквизиты)
	
	// ТС
	ПроверяемыеРеквизиты.Добавить("ТранспортныеСредства.РегистрационныйНомер");
	ПроверяемыеРеквизиты.Добавить("ТранспортныеСредства.ТипВладения");
	ПроверяемыеРеквизиты.Добавить("ТранспортныеСредства.Тип");
	ПроверяемыеРеквизиты.Добавить("ТранспортныеСредства.Марка");
	ПроверяемыеРеквизиты.Добавить("ТранспортныеСредства.Грузоподъемность");
	ПроверяемыеРеквизиты.Добавить("ТранспортныеСредства.Вместимость");
	
	// Ответственные
	ПроверяемыеРеквизиты.Добавить("ОтветственныеЛица.Фамилия");
	ПроверяемыеРеквизиты.Добавить("ОтветственныеЛица.Имя");
	ПроверяемыеРеквизиты.Добавить("ОтветственныеЛица.ИНН");
	ПроверяемыеРеквизиты.Добавить("ОтветственныеЛица.Серия");
	ПроверяемыеРеквизиты.Добавить("ОтветственныеЛица.Номер");
	ПроверяемыеРеквизиты.Добавить("ОтветственныеЛица.ДатаВыдачи");
	ПроверяемыеРеквизиты.Добавить("ОтветственныеЛица.Телефоны");
	
	// Юр лица
	ПроверяемыеРеквизиты.Добавить("ДанныеЮрЛиц.Полномочия");
	ПроверяемыеРеквизиты.Добавить("ДанныеЮрЛиц.Основание");
	
	// Спец разрешения
	ПроверяемыеРеквизиты.Добавить("СпецРазрешения.Вид");
	ПроверяемыеРеквизиты.Добавить("СпецРазрешения.Дата");
	ПроверяемыеРеквизиты.Добавить("СпецРазрешения.Номер");
	ПроверяемыеРеквизиты.Добавить("СпецРазрешения.Срок");
	ПроверяемыеРеквизиты.Добавить("СпецРазрешения.НаименованиеОрганаВласти");
	ПроверяемыеРеквизиты.Добавить("СпецРазрешения.ИД");
	
КонецПроцедуры

Процедура ДопПроверкиФЛКОформление(ДанныеДокумента, ПараметрыВыгрузки, ПараметрыПроверкиРеквизитов, Ошибки)
	
	ИмяТелефон = "Телефон";
	
	Saby_ТНОбщегоНазначенияСервер.ПроверитьКонтактныеДанные(
		ДанныеДокумента, "Отправитель", ИмяТелефон, Ошибки, ПараметрыПроверкиРеквизитов.ОшибкиПоРеквизитам);
		
	Saby_ТНОбщегоНазначенияСервер.ПроверитьКонтактныеДанные(
		ДанныеДокумента, "Перевозчик", ИмяТелефон, Ошибки, ПараметрыПроверкиРеквизитов.ОшибкиПоРеквизитам);
		
КонецПроцедуры

Процедура ДопПроверкиФЛКУтверждение(ДанныеДокумента, ПараметрыВыгрузки, ПараметрыПроверкиРеквизитов, Ошибки)
	
	Saby_ТНОбщегоНазначенияСервер.ПроверитьЗаполнениеТипаВладения(
		ДанныеДокумента, Ошибки, ПараметрыПроверкиРеквизитов.ОшибкиПоРеквизитам);
	
КонецПроцедуры

Функция ПроверятьРеквизитОтветственного(СтрокаТЧ, ИмяРеквизита)
	
	Возврат СтрокаТЧ.Роль = Перечисления.Saby_РолиОтветственных.Ответственный
		И (ИмяРеквизита = "Фамилия" Или ИмяРеквизита = "Имя" Или ИмяРеквизита = "Телефоны");
	
КонецФункции

// Возвращает признак необходимости проверки реквизитов спец разрешений
//
// Параметры:
//  СтрокаТЧ - СтрокаТаблицыЗначений - строка таблицы, в которой проверяются реквизиты
//  ИмяРеквизита - Строка - имя текущего проверяемого реквизита
//
// Возвращаемое значение:
//   Булево - Истина, если реквизит необходимо проверять
//
Функция ПроверятьРеквизитыСпецРазрешения(СтрокаТЧ, ИмяРеквизита)
		
	Если ИмяРеквизита = "ИД" Тогда
		
		Возврат
			Не ЗначениеЗаполнено(СтрокаТЧ.Номер) 
			Или Не ЗначениеЗаполнено(СтрокаТЧ.Дата)
			Или Не ЗначениеЗаполнено(СтрокаТЧ.Срок)
			Или Не ЗначениеЗаполнено(СтрокаТЧ.Вид)
			Или Не ЗначениеЗаполнено(СтрокаТЧ.НаименованиеОрганаВласти);
			
	Иначе
		Возврат Не ЗначениеЗаполнено(СтрокаТЧ.ИД);
	КонецЕсли;	
		
КонецФункции

#КонецОбласти // ФЛК

#Область ЗаполнениеНаОснованииINI

Процедура ДополнитьПодстановкиГрузоотправителя(ДанныеИзИНИ, ПараметрыЗаполнения)
	
	ДанныеИзИНИ.Вставить("Грузоотправитель", Saby_ТНОбщегоНазначенияСервер.ДанныеЮрЛицаИзИНИ(ДанныеИзИНИ));
	
КонецПроцедуры

Процедура ДополнитьПодстановкиГрузоперевозчика(ДанныеИзИНИ, ПараметрыЗаполнения)
	
	ДанныеИзИНИ.Вставить("Грузоперевозчик", Saby_ТНОбщегоНазначенияСервер.ДанныеЮрЛицаИзИНИ(ДанныеИзИНИ));
	
КонецПроцедуры

#КонецОбласти // ЗаполнениеНаОснованииINI

#Область ДанныеДокумента

Функция ВыгрузкаДанныеОформление(СсылкаНаДокумент)
	
	Реквизиты = Новый Массив;
	
	Реквизиты.Добавить("Номер");
	Реквизиты.Добавить("Дата");
	Реквизиты.Добавить("ВместимостьТС");
	Реквизиты.Добавить("ГрузоподъемностьТС");
	Реквизиты.Добавить("ТипТС");
	Реквизиты.Добавить("КузовТС");
	Реквизиты.Добавить("ПогрузкаТС");
	Реквизиты.Добавить("Договор");
	Реквизиты.Добавить("ВлажностьОт");
	Реквизиты.Добавить("ВлажностьДо");
	Реквизиты.Добавить("ТемператураОт");
	Реквизиты.Добавить("ТемператураДо");
	Реквизиты.Добавить("ИныеУсловия");
	Реквизиты.Добавить("ПредельныйСрок");
	Реквизиты.Добавить("ПредставительЗаказчика");
	Реквизиты.Добавить("ПредставительЗаказчикаНаименование");
	Реквизиты.Добавить("ПредставительЗаказчика_Основание");
	Реквизиты.Добавить("ТребованияКТС");
	Реквизиты.Добавить("СпецУсловия");
	Реквизиты.Добавить("ПодачаАдрес");
	Реквизиты.Добавить("ПодачаДатаВремя");
	Реквизиты.Добавить("ПодачаПредельныйИнтервал");
	Реквизиты.Добавить("КонечнаяТочкаАдрес");
	Реквизиты.Добавить("Отправитель_Экспедитор");
	Реквизиты.Добавить("Заказчик");
	Реквизиты.Добавить("Перевозчик");
	
	Таблицы = Новый Массив;
	Таблицы.Добавить("Грузы");
	Таблицы.Добавить("ДанныеЮрЛиц");
	Таблицы.Добавить("КонтактныеДанные");
	Таблицы.Добавить("АдресаПунктовВыгрузки");
	Таблицы.Добавить("АдресаПромежуточныхПунктов");
	
	Возврат ДанныеИзБД(СсылкаНаДокумент, Реквизиты, Таблицы);
	
КонецФункции

Функция ВыгрузкаДанныеУтверждение(СсылкаНаДокумент)
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить("ВалютаКод");
	Реквизиты.Добавить("ВалютаНаименование");
	Реквизиты.Добавить("СтавкаНДС");
    Реквизиты.Добавить("Сумма");
	Реквизиты.Добавить("СуммаБезНДС");
	Реквизиты.Добавить("СуммаСНДС");
	Реквизиты.Добавить("МеханизмРасчета");
		
	Таблицы = Новый Массив;
	Таблицы.Добавить("ДанныеЮрЛиц");
	Таблицы.Добавить("КонтактныеДанные");
	Таблицы.Добавить("ТранспортныеСредства");	
	Таблицы.Добавить("ОтветственныеЛица");
	Таблицы.Добавить("СпецРазрешения");
	Таблицы.Добавить("Водители");
	
	Возврат ДанныеИзБД(СсылкаНаДокумент, Реквизиты, Таблицы);
	
КонецФункции

Функция ДанныеИзБД(СсылкаНаДокумент, Реквизиты, Таблицы)
	
	РезультатФункции = Новый Структура;
	
	// Ссылка всегда должна быть
	Реквизиты.Добавить("Ссылка");
	
	ТекстЗапросаРеквизитов = ТекстЗапросаРеквизитов();
	ШаблонЗапросаТаблиц    = ШаблонЗапросаТаблиц();
	
	Saby_ТНОбщегоНазначенияСервер.ДобавитьРеквизитыИзБД(
		РезультатФункции, СсылкаНаДокумент, Реквизиты, ТекстЗапросаРеквизитов);
	
	НестандартныеЗапросыТаблиц = НестандартныеЗапросыТаблиц(Таблицы);
	
	Saby_ТНОбщегоНазначенияСервер.ДобавитьТаблицыИзБД(
		РезультатФункции, СсылкаНаДокумент, Таблицы, ШаблонЗапросаТаблиц, НестандартныеЗапросыТаблиц);
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ТекстЗапросаРеквизитов()
	
	Возврат
	"ВЫБРАТЬ
    |	Saby_ЗаказНаПеревозку.Ссылка КАК Ссылка,
    |	Saby_ЗаказНаПеревозку.ВерсияДанных КАК ВерсияДанных,
    |	Saby_ЗаказНаПеревозку.ПометкаУдаления КАК ПометкаУдаления,
    |	Saby_ЗаказНаПеревозку.Номер КАК Номер,
    |	Saby_ЗаказНаПеревозку.Дата КАК Дата,
    |	Saby_ЗаказНаПеревозку.Проведен КАК Проведен,
    |	Saby_ЗаказНаПеревозку.ВалютаКод КАК ВалютаКод,
    |	Saby_ЗаказНаПеревозку.ВалютаНаименование КАК ВалютаНаименование,
    |	Saby_ЗаказНаПеревозку.ВлажностьДо КАК ВлажностьДо,
    |	Saby_ЗаказНаПеревозку.ВлажностьОт КАК ВлажностьОт,
    |	Saby_ЗаказНаПеревозку.ВместимостьТС КАК ВместимостьТС,
    |	Saby_ЗаказНаПеревозку.ГрузоподъемностьТС КАК ГрузоподъемностьТС,
    |	Saby_ЗаказНаПеревозку.ДатаИзменения КАК ДатаИзменения,
    |	Saby_ЗаказНаПеревозку.Договор КАК Договор,
    |	Saby_ЗаказНаПеревозку.ДокументОснование_Идентификатор КАК ДокументОснование_Идентификатор,
    |	Saby_ЗаказНаПеревозку.ДокументОснование_ОбъектМетаданных КАК ДокументОснование_ОбъектМетаданных,
    |	Saby_ЗаказНаПеревозку.Заказчик КАК Заказчик,
    |	Saby_ЗаказНаПеревозку.ИныеУсловия КАК ИныеУсловия,
    |	Saby_ЗаказНаПеревозку.Комментарий КАК Комментарий,
    |	Saby_ЗаказНаПеревозку.КонечнаяТочкаАдрес КАК КонечнаяТочкаАдрес,
    |	Saby_ЗаказНаПеревозку.КонечнаяТочкаАдресСтруктура КАК КонечнаяТочкаАдресСтруктура,
    |	Saby_ЗаказНаПеревозку.КузовТС КАК КузовТС,
    |	Saby_ЗаказНаПеревозку.МеханизмРасчета КАК МеханизмРасчета,
    |	Saby_ЗаказНаПеревозку.Направление КАК Направление,
    |	Saby_ЗаказНаПеревозку.НомерСбис КАК НомерСбис,
    |	Saby_ЗаказНаПеревозку.Перевозчик КАК Перевозчик,
    |	Saby_ЗаказНаПеревозку.ПогрузкаТС КАК ПогрузкаТС,
    |	Saby_ЗаказНаПеревозку.ПодачаАдрес КАК ПодачаАдрес,
    |	Saby_ЗаказНаПеревозку.ПодачаАдресСтруктура КАК ПодачаАдресСтруктура,
    |	Saby_ЗаказНаПеревозку.ПодачаДатаВремя КАК ПодачаДатаВремя,
	|	Saby_ЗаказНаПеревозку.ПодачаПредельныйИнтервал КАК ПодачаПредельныйИнтервал,
    |	Saby_ЗаказНаПеревозку.ПредельныйСрок КАК ПредельныйСрок,
    |	Saby_ЗаказНаПеревозку.ПредставительЗаказчика КАК ПредставительЗаказчика,
	|	Saby_ЗаказНаПеревозку.ПредставительЗаказчикаНаименование КАК ПредставительЗаказчикаНаименование,
    |	Saby_ЗаказНаПеревозку.ПредставительЗаказчика_Основание КАК ПредставительЗаказчика_Основание,
    |	Saby_ЗаказНаПеревозку.СпецУсловия КАК СпецУсловия,
    |	Saby_ЗаказНаПеревозку.СтавкаНДС КАК СтавкаНДС,
    |	Saby_ЗаказНаПеревозку.Сумма КАК Сумма,
    |	Saby_ЗаказНаПеревозку.СуммаБезНДС КАК СуммаБезНДС,
    |	Saby_ЗаказНаПеревозку.СуммаСНДС КАК СуммаСНДС,
    |	Saby_ЗаказНаПеревозку.ТемператураДо КАК ТемператураДо,
    |	Saby_ЗаказНаПеревозку.ТемператураОт КАК ТемператураОт,
    |	Saby_ЗаказНаПеревозку.ТипДокумента КАК ТипДокумента,
    |	Saby_ЗаказНаПеревозку.ТипТС КАК ТипТС,
    |	Saby_ЗаказНаПеревозку.ТранспортноеСредствоСтрокой КАК ТранспортноеСредствоСтрокой,
    |	Saby_ЗаказНаПеревозку.ТребованияКТС КАК ТребованияКТС,
    |	Saby_ЗаказНаПеревозку.Представление КАК Представление,
    |	Saby_ЗаказНаПеревозку.МоментВремени КАК МоментВремени,
	|	ЛОЖЬ КАК Отправитель_Экспедитор
    |ИЗ
    |	Документ.Saby_ЗаказНаПеревозку КАК Saby_ЗаказНаПеревозку
    |ГДЕ
    |	Saby_ЗаказНаПеревозку.Ссылка = &Ссылка";
	
КонецФункции

Функция ШаблонЗапросаТаблиц()
	
	Возврат
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	Документ.Saby_ЗаказНаПеревозку.%1 КАК ТЧЗаказаНаПеревозку
	|ГДЕ
	|	ТЧЗаказаНаПеревозку.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
КонецФункции

Функция НестандартныеЗапросыТаблиц(Таблицы)
	
	РезультатФункции = Новый Соответствие;
	
	Для Каждого ИмяТаблицы Из Таблицы Цикл
		
		ТекстЗапроса = Неопределено;
		
		Если ИмяТаблицы = "Грузы" Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	Saby_ЗаказНаПеревозкуГрузы.Тип КАК Тип,
			|	Saby_ЗаказНаПеревозкуГрузы.МассаБрутто КАК МассаБрутто,
			|	Saby_ЗаказНаПеревозкуГрузы.МассаНетто КАК МассаНетто,
			|	Saby_ЗаказНаПеревозкуГрузы.Высота КАК Высота,
			|	Saby_ЗаказНаПеревозкуГрузы.Длина КАК Длина,
			|	Saby_ЗаказНаПеревозкуГрузы.Количество КАК КоличествоМест,
			|	Saby_ЗаказНаПеревозкуГрузы.КоличествоПаллет КАК КоличествоПаллет,
			|	Saby_ЗаказНаПеревозкуГрузы.Объем КАК Объем,
			|	Saby_ЗаказНаПеревозкуГрузы.Ширина КАК Ширина,
			|	Saby_ЗаказНаПеревозкуГрузы.Количество КАК Количество,
			|	Saby_ЗаказНаПеревозкуГрузы.Количество КАК КоличествоГрузовыхМест,
			|	Saby_ЗаказНаПеревозкуГрузы.Наименование КАК Наименование,
			|	Saby_ЗаказНаПеревозкуГрузы.Состояние КАК Состояние,
			|	Saby_ЗаказНаПеревозкуГрузы.ВидТары КАК ВидТары,
			|	Saby_ЗаказНаПеревозкуГрузы.ВидТары.Код КАК ТараКод,
			|	Saby_ЗаказНаПеревозкуГрузы.Договор КАК Договор,
			|	Saby_ЗаказНаПеревозкуГрузы.КлючСтроки_ДанныеЮрЛиц КАК КлючСтроки_ДанныеЮрЛиц,
			|	Saby_ЗаказНаПеревозкуГрузы.КлючСтроки КАК КлючСтроки,
			|	Saby_ЗаказНаПеревозкуГрузы.МетодОпределенияМассы КАК МетодОпределенияМассы,
			|	Saby_ЗаказНаПеревозкуГрузы.РаспределениеПоДлинеПлатформы КАК РаспределениеПоДлинеПлатформы,
			|	Saby_ЗаказНаПеревозкуГрузы.Делимость КАК Делимость,
			|	Saby_ЗаказНаПеревозкуГрузы.ПогрузкаВодителем КАК ПогрузкаВодителем,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз КАК ОпасныйГруз,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.Наименование КАК НаименованиеОтгрузочное,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.НомерООН КАК НомерООН,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ТехническоеНаименование КАК НаименованиеТехническое,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.Комментарий КАК Комментарий,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.Класс КАК Класс,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.КлассификационныйКод КАК КлассификационныйКод,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ГруппаУпаковкиЗначение КАК ГруппаУпаковки,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ЗнакОпасности КАК ЗнакОпасности,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ОграничениеПроездаЧерезТуннели КАК КодОграниченияПроездТуннеля,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.НазваниеРадионуклида КАК НазваниеРадионуклида,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ФизИХимФорма КАК ФизХимФорма,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.МаксАктивность КАК РадиоактивныйМаксАктивность,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.КатегорияУпаковки КАК КатегорияУпаковки,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ТранспортныйИндекс КАК ТранспортныйИндекс,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ИндексБезопасности КАК ИндексБезопасности,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ОпознавательныйЗнак КАК ОпознавательныйЗнак,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ПолнаяАктивность КАК РадиоактивныйПолнаяАктивность,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.МассаНеттоВзрывчатый КАК МассаНеттоВзрывчатый,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.МассаНеттоВзрывчатыйВсего КАК МассаНеттоВзрывчатыйВсего,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ПроцентСмеси КАК ПроцентСмеси,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.ДатаИстеченияУдержания КАК ДатаИстеченияУдержания,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.КонтрольнаяТемпература КАК ТемператураКонтрольная,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.АварийнаяТемпература КАК ТемператураАварийная,
			|	Saby_ЗаказНаПеревозкуГрузы.ОпасныйГруз.Представление КАК Представление
			|ИЗ
			|	Документ.Saby_ЗаказНаПеревозку.Грузы КАК Saby_ЗаказНаПеревозкуГрузы
			|ГДЕ
			|	Saby_ЗаказНаПеревозкуГрузы.Ссылка = &Ссылка";
			
		ИначеЕсли ИмяТаблицы = "Водители" Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.Ссылка КАК Ссылка,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.НомерСтроки КАК НомерСтроки,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.КлючСтроки КАК КлючСтроки,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.ИНН КАК ИНН,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.Серия КАК Серия,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.Номер КАК Номер,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.ДатаВыдачи КАК ДатаВыдачи,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.Фамилия КАК Фамилия,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.Имя КАК Имя,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.Отчество КАК Отчество,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.Телефоны КАК Телефоны,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.Роль КАК Роль,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.ДатаОкончанияДействия КАК ДатаОкончанияДействия,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.ЭлектроннаяПочта КАК ЭлектроннаяПочта,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.Должность КАК Должность,
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.ШтатныйСотрудник КАК ШтатныйСотрудник
			|ИЗ
			|	Документ.Saby_ЗаказНаПеревозку.ОтветственныеЛица КАК Saby_ЗаказНаПеревозкуОтветственныеЛица
			|ГДЕ
			|	Saby_ЗаказНаПеревозкуОтветственныеЛица.Ссылка = &Ссылка
			|	И Saby_ЗаказНаПеревозкуОтветственныеЛица.Роль = ЗНАЧЕНИЕ(Перечисление.Saby_РолиОтветственных.Водитель)";
			
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если ТекстЗапроса <> Неопределено Тогда
			РезультатФункции.Вставить(ИмяТаблицы, ТекстЗапроса);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // ДанныеДокумента

#Область Выгрузка

#Область Выгрузка_Оформление

Функция ТитулОформление(ДанныеДокумента)
	
	СтруктураТитула = Новый Структура;
	
	РеквизитыОбъекта = ДанныеДокумента.Реквизиты;
	
	Документ = Новый Структура;
	Документ.Вставить("Номер", РеквизитыОбъекта.Номер);
	СтруктураТитула.Вставить("Документ", Документ);
	
	ПараметрыТС = Новый Структура;
	ПараметрыТС.Вставить("Вместимость",      РеквизитыОбъекта.ВместимостьТС);
	ПараметрыТС.Вставить("Грузоподъемность", РеквизитыОбъекта.ГрузоподъемностьТС);
	ПараметрыТС.Вставить("Тип",              РеквизитыОбъекта.ТипТС);
	ПараметрыТС.Вставить("ТипКузова",        РеквизитыОбъекта.КузовТС);
	ПараметрыТС.Вставить("ТипПогрузки",      РеквизитыОбъекта.ПогрузкаТС);
	
	СтруктураТитула.Вставить("ПараметрыТС", ПараметрыТС);
	
	МассивДокументов = Новый Массив();
	МассивДокументов.Добавить(РеквизитыОбъекта.Договор);
	МассивДокументов.Добавить(РеквизитыОбъекта.ПредставительЗаказчика_Основание);
	ДанныеДокументов = Saby_ТНОбщегоНазначенияСервер.РеквизитыДокументовСбис(МассивДокументов);
	
	ДанныеДоговора = ДанныеДокументов.Получить(РеквизитыОбъекта.Договор);
	Если ДанныеДоговора <> Неопределено Тогда
				
		ОбработатьСлужебныеСвойстваСтруктурыДокумента(ДанныеДоговора, ДанныеДоговора.ДатаВремя);	
		СтруктураТитула.Вставить("Договор", ДанныеДоговора);
		
	КонецЕсли;
	
	СтруктураТитула.Вставить("Маршрут", Маршрут(ДанныеДокумента));
	
	ЗаполнитьПредставителяЗаказчика(СтруктураТитула, ДанныеДокумента, ДанныеДокументов);
	
	СтруктураТитула.Вставить("УсловияПеревозки", УсловияПеревозки(РеквизитыОбъекта));
	
	СтруктураТитула.Вставить("Груз", Груз(ДанныеДокумента));
	
	СтруктураТитула.Вставить(
		"Грузоперевозчик", Saby_ТНВыгрузкаСервер.ДанныеЮрЛица("Перевозчик", ДанныеДокумента, Истина));
	
	СтруктураТитула.Вставить(
		"Грузоотправитель", Saby_ТНВыгрузкаСервер.ДанныеЮрЛица("Отправитель", ДанныеДокумента, Истина));
	
	ЗаполнитьУполномоченныхЛиц(СтруктураТитула.Грузоотправитель, ДанныеДокумента, "УполномоченноеЛицо");
	
	Возврат СтруктураТитула;
	
КонецФункции

Функция Маршрут(ДанныеДокумента)
	
	ИмяКлючаНазвание = "Название";
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("КонечныйПункт",      Новый Структура);
	РезультатФункции.Вставить("Отправление",        Новый Структура);
	РезультатФункции.Вставить("ПромежуточныйПункт", Новый Массив);
	РезультатФункции.Вставить("Пункт",              Новый Массив);
	
	РезультатФункции.КонечныйПункт.Вставить(ИмяКлючаНазвание, ДанныеДокумента.Реквизиты.КонечнаяТочкаАдрес);
	
	ПодачаДатаВремя = Saby_ТНВыгрузкаСервер.СтруктураДатаВремя(ДанныеДокумента.Реквизиты.ПодачаДатаВремя, , Истина);
	
	ПодачаПредельныйИнтервал = Дата(
		Год(ДанныеДокумента.Реквизиты.ПодачаДатаВремя),
		Месяц(ДанныеДокумента.Реквизиты.ПодачаДатаВремя),
		День(ДанныеДокумента.Реквизиты.ПодачаДатаВремя),
		Час(ДанныеДокумента.Реквизиты.ПодачаПредельныйИнтервал),
		Минута(ДанныеДокумента.Реквизиты.ПодачаПредельныйИнтервал),
		Секунда(ДанныеДокумента.Реквизиты.ПодачаПредельныйИнтервал));
		
	ПодачаПредельныйИнтервал = Saby_ТНВыгрузкаСервер.СтруктураДатаВремя(ПодачаПредельныйИнтервал, , Истина);
	
	ПодачаТС = Новый Структура;
	ПодачаТС.Вставить("ДатаВремя",                           ПодачаДатаВремя.Значение);
	ПодачаТС.Вставить("ДатаВремяНаличиеUTC",                 ПодачаДатаВремя.ЕстьЧасовойПояс);
	ПодачаТС.Вставить("ПредельныйИнтервалВремени",           ТолькоВремя(ПодачаПредельныйИнтервал.Значение));
	ПодачаТС.Вставить("ПредельныйИнтервалВремениНаличиеUTC", ПодачаПредельныйИнтервал.ЕстьЧасовойПояс);
	РезультатФункции.Отправление.Вставить("ПодачаТС", ПодачаТС);
	РезультатФункции.Отправление.Вставить("АдресТекст", ДанныеДокумента.Реквизиты.ПодачаАдрес);
	
	Для Каждого СтрокаПромежуточногоПункта Из ДанныеДокумента.АдресаПромежуточныхПунктов Цикл
		СтруктураПромежуточногоПункта = Новый Структура;
		СтруктураПромежуточногоПункта.Вставить(ИмяКлючаНазвание, СтрокаПромежуточногоПункта.Значение);
		РезультатФункции.ПромежуточныйПункт.Добавить(СтруктураПромежуточногоПункта);
	КонецЦикла;
	
	Для Каждого СтрокаПункта Из ДанныеДокумента.АдресаПунктовВыгрузки Цикл
		СтруктураПункта = Новый Структура;
		СтруктураПункта.Вставить("АдресТекст", СтрокаПункта.Значение);
		
		ДатаВремя       = Saby_ТНВыгрузкаСервер.СтруктураДатаВремя(СтрокаПункта.ДатаВремя, , Истина);
		ДопустимоеВремя = Saby_ТНВыгрузкаСервер.СтруктураДатаВремя(СтрокаПункта.ДопустимоеВремя, , Истина);
		
		ТипОперации = Формат(СтрокаПункта.ЭтоПогрузка, "БЛ=Выгрузка; БИ=Погрузка");
		
		Операция = Новый Структура;
		Операция.Вставить("ДатаВремя",                           ДатаВремя.Значение);
		Операция.Вставить("ДатаВремяНаличиеUTC",                 ДатаВремя.ЕстьЧасовойПояс);
		Операция.Вставить("ПредельныйИнтервалВремени",           ТолькоВремя(ДопустимоеВремя.Значение));
		Операция.Вставить("ПредельныйИнтервалВремениНаличиеUTC", ДопустимоеВремя.ЕстьЧасовойПояс);
		Операция.Вставить("Тип",                                 ТипОперации);
		
		СтруктураПункта.Вставить("Операция", Операция);
		
		СтрокаЮрЛица = ДанныеДокумента.ДанныеЮрЛиц.Найти(СтрокаПункта.КлючСтроки_ДанныеЮрЛиц, "КлючСтроки");
		Если СтрокаЮрЛица <> Неопределено Тогда
			СтруктураПункта.Вставить("Организация", Новый Структура);
			СтруктураПункта.Организация.Вставить("ИНН", СтрокаЮрЛица.ИНН);
			СтруктураПункта.Организация.Вставить(ИмяКлючаНазвание, СтрокаЮрЛица.НаименованиеОрганизации);
		КонецЕсли;
		
		РезультатФункции.Пункт.Добавить(СтруктураПункта);
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ЗаполнитьПредставителяЗаказчика(СтруктураТитула, ДанныеДокумента, ДанныеДокументов)
	
	Если Не ДанныеДокумента.Реквизиты.ПредставительЗаказчика Тогда
		Возврат;
	КонецЕсли;
	
	Составитель = ДанныеДокумента.Реквизиты.ПредставительЗаказчикаНаименование;
	
	Если Не ЗначениеЗаполнено(Составитель) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураТитула.Вставить("Файл", Новый Структура);		
	СтруктураТитула.Файл.Вставить("Составитель", Новый Структура);
	
	СтруктураТитула.Файл.Составитель.Вставить("Наименование", Составитель);
	
	Если ЗначениеЗаполнено(ДанныеДокумента.Реквизиты.ПредставительЗаказчика_Основание) Тогда
		ДанныеОснования = ДанныеДокументов.Получить(ДанныеДокумента.Реквизиты.ПредставительЗаказчика_Основание);
		Если ДанныеОснования <> Неопределено Тогда
							
			ОбработатьСлужебныеСвойстваСтруктурыДокумента(ДанныеОснования, ДанныеОснования.ДатаВремя);								
			Основание = Новый Структура;
			Основание.Вставить("Документ", ДанныеОснования);
			СтруктураТитула.Файл.Составитель.Вставить("Основание", Основание);
			
		КонецЕсли;
	КонецЕсли;
	
	СтруктураТитула.Документ.Вставить(
		"Оформитель",
		Saby_ТНВыгрузкаСервер.ДанныеЮрЛица("Оформитель", ДанныеДокумента, Истина));
	
КонецПроцедуры

Функция УсловияПеревозки(РеквизитыОбъекта)
	
	РезультатФункции = Новый Структура;
	
	ФорматЧисловогоЗначения = "ЧЦ=5; ЧДЦ=2; ЧРД=.";
	
	КлиматическийРежим = Новый Структура;
	КлиматическийРежим.Вставить("Влажность",   Новый Структура);
	КлиматическийРежим.Влажность.Вставить(
		"Минимальная",  Формат(РеквизитыОбъекта.ВлажностьОт, ФорматЧисловогоЗначения));
	КлиматическийРежим.Влажность.Вставить(
		"Максимальная", Формат(РеквизитыОбъекта.ВлажностьДо, ФорматЧисловогоЗначения));
	КлиматическийРежим.Вставить("Температура", Новый Структура);
	КлиматическийРежим.Температура.Вставить(
		"Минимальная",  Формат(РеквизитыОбъекта.ТемператураОт, ФорматЧисловогоЗначения));
	КлиматическийРежим.Температура.Вставить(
		"Максимальная", Формат(РеквизитыОбъекта.ТемператураДо, ФорматЧисловогоЗначения));
	
	РезультатФункции.Вставить("КлиматическийРежим",                    КлиматическийРежим);
	РезультатФункции.Вставить("Иные",                                  РеквизитыОбъекта.ИныеУсловия);
	РезультатФункции.Вставить("ПредельныйСрок",                        РеквизитыОбъекта.ПредельныйСрок);
	РезультатФункции.Вставить("ТребованияПоПеревозкеПищевыхПродуктов", РеквизитыОбъекта.ТребованияКТС);
	РезультатФункции.Вставить("УказанияНорм",                          РеквизитыОбъекта.СпецУсловия);
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ТолькоВремя(ДатаВремяСтрокой)
	
	РазделительВремени = "T";
	Если СтрНайти(ДатаВремяСтрокой, РазделительВремени) = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	МассивЗначений = СтрРазделить(ДатаВремяСтрокой, РазделительВремени);
	
	Возврат МассивЗначений[МассивЗначений.ВГраница()];
	
КонецФункции

#Область Груз

Функция Груз(ДанныеДокумента)
	
	МассивПозиций = Новый Массив;
	
	МассивДокументов = ДанныеДокумента.Грузы.ВыгрузитьКолонку("Договор");
	ДанныеДокументов = Saby_ТНОбщегоНазначенияСервер.РеквизитыДокументовСбис(МассивДокументов);
	
	Для Каждого СтрокаГруза Из ДанныеДокумента.Грузы Цикл
		
		СтруктураГруза = ШаблонГруза();
		
		ЗаполнитьЗначенияСвойств(СтруктураГруза, СтрокаГруза, , "ОпасныйГруз");
		
		МетодОпределенияМассы = Перечисления.Saby_МетодОпределенияМассы.КодПоЗначению(
			СтрокаГруза.МетодОпределенияМассы, Истина);		
			
		РаспределениеПоДлинеПлатформы = Формат(СтрокаГруза.РаспределениеПоДлинеПлатформы, "БЛ=1; БИ=0");
		Делимость                     = Формат(СтрокаГруза.Делимость,                     "БЛ=0; БИ=1");
		ПогрузкаВодителем             = Формат(СтрокаГруза.ПогрузкаВодителем,             "БЛ=1; БИ=0");
		
		СтруктураГруза.МетодОпределенияМассы         = МетодОпределенияМассы;
		СтруктураГруза.РаспределениеПоДлинеПлатформы = РаспределениеПоДлинеПлатформы;
		СтруктураГруза.Делимость                     = Делимость;
		СтруктураГруза.ПогрузкаВодителем             = ПогрузкаВодителем;
		
		ДанныеДоговора = ДанныеДокументов.Получить(СтрокаГруза.Договор);
		Если ДанныеДоговора <> Неопределено Тогда
			ОбработатьСлужебныеСвойстваСтруктурыДокумента(ДанныеДоговора, ДанныеДоговора.ДатаВремя);
			СтруктураГруза.Вставить("Договор", ДанныеДоговора);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаГруза.КлючСтроки_ДанныеЮрЛиц) Тогда
			
			ДанныеОрганизации = ДанныеОрганизации(ДанныеДокумента, СтрокаГруза.КлючСтроки_ДанныеЮрЛиц);			
			ДанныеОрганизации.Реквизиты.Вставить("Ссылка", ДанныеДокумента.Реквизиты.Ссылка);
			
			СтруктураГруза.Вставить(
				"Заказчик",
				Saby_ТНВыгрузкаСервер.ДанныеЮрЛица(ДанныеОрганизации.РольСтрокой, ДанныеОрганизации, Истина));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаГруза.ОпасныйГруз) Тогда
			
			ОпасныеГрузыИзДокумента = Новый Массив;
			ОпасныйГруз             = Новый Массив;
			
			ОпасныеГрузыИзДокумента.Добавить(СтрокаГруза);
						
			Saby_ТНВыгрузкаСервер.ЗаполнитьОпасныйГруз(ОпасныеГрузыИзДокумента, ОпасныйГруз);						
			СтруктураГруза.ОпасныйГруз = ОпасныйГруз[0];
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураГруза.Параметры, СтрокаГруза);
		СтруктураГруза.Параметры.Масса.Брутто = Формат(СтрокаГруза.МассаБрутто, "ЧДЦ=3; ЧРД=.; ЧГ=");
		СтруктураГруза.Параметры.Масса.Нетто  = Формат(СтрокаГруза.МассаНетто,  "ЧДЦ=3; ЧРД=.; ЧГ=");
		
		ЗаполнитьЗначенияСвойств(СтруктураГруза.ПунктыПогрузкиВыгрузки, СтрокаГруза);
		
		МассивПозиций.Добавить(СтруктураГруза);
		
	КонецЦикла;
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Позиция", МассивПозиций);
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ШаблонГруза()
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Договор",                       "");
	РезультатФункции.Вставить("Заказчик",                      "");
	РезультатФункции.Вставить("ОпасныйГруз",                   Новый Массив);
	РезультатФункции.Вставить("Параметры",                     Saby_ТНВыгрузкаСервер.ШаблонПараметровГруза());
	РезультатФункции.Вставить("ПунктыПогрузкиВыгрузки",        Новый Структура);
	РезультатФункции.Вставить("ГруженностьКонтейнера",         "");
	РезультатФункции.Вставить("Делимость",                     "");
	РезультатФункции.Вставить("ДопИнформация",                 "");
	РезультатФункции.Вставить("МетодОпределенияМассы",         "");
	РезультатФункции.Вставить("Наименование",                  "");
	РезультатФункции.Вставить("ПогрузкаВодителем",             "");
	РезультатФункции.Вставить("РаспределениеПоДлинеПлатформы", "");
	РезультатФункции.Вставить("Состояние",                     "");
	РезультатФункции.Вставить("ТараКод",                       "");
	РезультатФункции.Вставить("ТараМасса",                     "");
	
	РезультатФункции.ПунктыПогрузкиВыгрузки.Вставить("КоличествоГрузовыхМест", "");
	
	РезультатФункции.Параметры.Вставить("Масса", Новый Структура);
	РезультатФункции.Параметры.Масса.Вставить("Брутто", 0);
	РезультатФункции.Параметры.Масса.Вставить("Нетто",  0);
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти // Груз

Функция ДанныеОрганизации(ДанныеДокумента, КлючСтроки_ДанныеЮрЛиц)
	
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("Реквизиты",   Новый Структура);
	РезультатФункции.Вставить("ДанныеЮрЛиц", Новый Массив);
	РезультатФункции.Вставить("РольСтрокой", "");
	
	ОтборЮрЛиц = Новый Структура();
	ОтборЮрЛиц.Вставить("КлючСтроки", КлючСтроки_ДанныеЮрЛиц);
	
	РезультатФункции.ДанныеЮрЛиц = ДанныеДокумента.ДанныеЮрЛиц.НайтиСтроки(ОтборЮрЛиц);
	
	Если РезультатФункции.ДанныеЮрЛиц.Количество() > 0 Тогда
		Роль = РезультатФункции.ДанныеЮрЛиц[0].Роль;
		МетаданныеПеречисления = Роль.Метаданные();
		ИндексРоли = Перечисления[МетаданныеПеречисления.Имя].Индекс(Роль);
		МетаданныеПеречисления = МетаданныеПеречисления.ЗначенияПеречисления[ИндексРоли];
		РезультатФункции.РольСтрокой = МетаданныеПеречисления.Имя;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ОбработатьСлужебныеСвойстваСтруктурыДокумента(ДанныеДокумента, ДатаДокумента)
	
	ДанныеДокумента.Вставить("Дата", Формат(ДатаДокумента, "ДФ=dd.MM.yyyy"));
	
	УдаляемыеСвойства = Новый Массив;
	УдаляемыеСвойства.Добавить("ДатаВремя");
	УдаляемыеСвойства.Добавить("Тип");
	
	Saby_ТНОбщегоНазначенияСервер.УдалитьСвойстваИзСтруктуры(ДанныеДокумента, УдаляемыеСвойства);
				
 КонецПроцедуры
 
#КонецОбласти // Выгрузка_Оформление 
 
#Область Выгрузка_Утверждение 

Функция ТитулУтверждение(ДанныеДокумента)
	
	СтруктураТитула = Новый Структура;
	
	РеквизитыОбъекта = ДанныеДокумента.Реквизиты;
	
	СтруктураТитула.Вставить("СтоимостьПеревозки", СтоимостьПеревозки(РеквизитыОбъекта));
	
	// ТС и прицеп
	СтруктураТитула.Вставить(
		"ТранспортноеСредство",
		Saby_ТНВыгрузкаСервер.ТранспортНаВыгрузку(ДанныеДокумента.ТранспортныеСредства));
	
	СтруктураТитула.ТранспортноеСредство.Вставить(
		"Прицеп", Saby_ТНВыгрузкаСервер.ПрицепыНаВыгрузку(ДанныеДокумента.ТранспортныеСредства));
	
	СтруктураТитула.ТранспортноеСредство.Вставить("СпецУсловия", СпецУсловия(ДанныеДокумента.СпецРазрешения));
	
	// Грузоперевозчик
	Грузоперевозчик = Новый Структура;
	
	// Уполномоченные лица
	ЗаполнитьУполномоченныхЛиц(Грузоперевозчик, ДанныеДокумента, "УполномоченноеЛицоПеревозчика");
	
	ДанныеОтветственного = Saby_ТНОбщегоНазначенияСервер.СтруктураОтветственного(
		ДанныеДокумента.ОтветственныеЛица, "Ответственный");
		
	Грузоперевозчик.Вставить("Ответственный", ОтветственноеЛицо(ДанныеОтветственного));
	
	СтруктураТитула.Вставить("Грузоперевозчик", Грузоперевозчик);
	
	// Водители
	СтруктураТитула.Вставить("Водители", ВодителиДляВыгрузки(ДанныеДокумента));
	
	Возврат СтруктураТитула;
	
КонецФункции

Функция СтоимостьПеревозки(Реквизиты)
	
	СтоимостьПеревозки = Новый Структура;
	СтоимостьПеревозки.Вставить("Валюта", Новый Структура);
	СтоимостьПеревозки.Вставить("Налог",  Новый Структура);
	
	// Валюта
	Если ЗначениеЗаполнено(Реквизиты.ВалютаКод) Тогда
		КодВалюта          = Реквизиты.ВалютаКод;
		ВалютаНаименование = Реквизиты.ВалютаНаименование;
	Иначе
		КодВалюта          = "643";
		ВалютаНаименование = "Российский рубль";
	КонецЕсли;
	
	СтоимостьПеревозки.Валюта.Вставить("Код",          КодВалюта);
	СтоимостьПеревозки.Валюта.Вставить("Наименование", ВалютаНаименование);
	
	// Ставка НДС
	СтоимостьПеревозки.Налог.Вставить("Ставка", Реквизиты.СтавкаНДС);
	
	СтоимостьПеревозки.Вставить("БезНДС",     Реквизиты.СуммаБезНДС);
	СтоимостьПеревозки.Вставить("ВключаяНДС", Реквизиты.СуммаСНДС);
	СтоимостьПеревозки.Вставить("Расчет",     Реквизиты.МеханизмРасчета);
	
    Возврат СтоимостьПеревозки;
	
КонецФункции

Функция СпецУсловия(СпецРазрешения)
	
	СпецУсловия = Новый Массив;
	
	Для Каждого Строка Из СпецРазрешения Цикл
		
		СпецРазрешение      = Saby_ТНОбщегоНазначенияСервер.СтрокаТаблицыЗначенийВСтруктуру(Строка);				
		СпецРазрешение.Дата = Формат(Строка.Дата, "ДФ=dd.MM.yyyy");
		
		СпецУсловие = Новый Структура("СпецРазрешение", СпецРазрешение);
		
		СпецУсловия.Добавить(СпецУсловие);
		
	КонецЦикла;
	
	Возврат СпецУсловия;
	
КонецФункции

Функция ОтветственноеЛицо(ДанныеОтветственного)
	
	Ответственный = Новый Структура;
	Ответственный.Вставить("Имя",      "");
	Ответственный.Вставить("Отчество", "");
	Ответственный.Вставить("Фамилия",  "");
	Ответственный.Вставить("Телефон",  "");
	
	ЗаполнитьЗначенияСвойств(Ответственный, ДанныеОтветственного);
	
	Saby_ТНВыгрузкаСервер.ЗаполнитьТелефоныОтветственного(ДанныеОтветственного, Ответственный);
	
	Возврат Ответственный;
	
КонецФункции

Функция ВодителиДляВыгрузки(ДанныеДокумента)
	
	Водители = Новый Массив;
	
	Отбор = Новый Структура("Роль", Перечисления.Saby_РолиОтветственных.Водитель);
	СтрокиВодителей = ДанныеДокумента.ОтветственныеЛица.НайтиСтроки(Отбор);
	
	Для Каждого СтрокаВодитель Из СтрокиВодителей Цикл
		
		ИндексСтроки = СтрокаВодитель.НомерСтроки - 1;
		
		Водитель = Saby_ТНОбщегоНазначенияСервер.СтруктураОтветственного(
			ДанныеДокумента.ОтветственныеЛица, 
			"Водитель", 
			ИндексСтроки);		
		
		Водители.Добавить(Водитель);		
		
	КонецЦикла;
	
	Исключения = Новый Массив;
	Исключения.Добавить("СведенияПЭП");
	Исключения.Добавить("ПутевойЛист");
		
	ДанныеВодителей = Saby_ТНВыгрузкаСервер.ВодителиНаВыгрузку(Водители, Новый Массив, Исключения);
	
	Возврат ДанныеВодителей;
	
КонецФункции

#КонецОбласти // Выгрузка_Утверждение

Процедура ЗаполнитьУполномоченныхЛиц(СтруктураДляДобавленияЛиц, ДанныеДокумента, РольСтрокой)
	
	Роль = Перечисления.Saby_РолиКонтрагентов[РольСтрокой];
	
	ОтборУполномоченных = Новый Структура;
	ОтборУполномоченных.Вставить("Роль", Роль);
	
	НайденныеУполномоченные = ДанныеДокумента.ДанныеЮрЛиц.НайтиСтроки(ОтборУполномоченных);
	
	Если НайденныеУполномоченные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = Новый Структура("Ссылка", ДанныеДокумента.Реквизиты.Ссылка);
	
	ДанныеОрганизации = Новый Структура;
	ДанныеОрганизации.Вставить("Реквизиты",        Реквизиты);	
	ДанныеОрганизации.Вставить("ДанныеЮрЛиц",      Новый Массив);
	ДанныеОрганизации.Вставить("КонтактныеДанные", ДанныеДокумента.КонтактныеДанные);
	
	МассивДокументов = ДанныеДокумента.ДанныеЮрЛиц.ВыгрузитьКолонку("Основание");
	ДанныеДокументов = Saby_ТНОбщегоНазначенияСервер.РеквизитыДокументовСбис(МассивДокументов);
	
	МассивУполномоченныхЛиц = Новый Массив;
	
	Для Каждого СтрокаЮрЛица Из НайденныеУполномоченные Цикл
		
		ДанныеОрганизации.ДанныеЮрЛиц.Очистить();
		ДанныеОрганизации.ДанныеЮрЛиц.Добавить(СтрокаЮрЛица);
		
		СтруктураОрганизации = Saby_ТНВыгрузкаСервер.ДанныеЮрЛица(РольСтрокой, ДанныеОрганизации, Истина);
		
		ДанныеОснования = ДанныеДокументов.Получить(СтрокаЮрЛица.Основание);
		Если ДанныеОснования <> Неопределено Тогда
			ОбработатьСлужебныеСвойстваСтруктурыДокумента(ДанныеОснования, ДанныеОснования.Дата);	
			СтруктураОрганизации.Вставить("Документ",   ДанныеОснования);
			СтруктураОрганизации.Вставить("Полномочия", СтрокаЮрЛица.Полномочия);
		КонецЕсли;
		
		МассивУполномоченныхЛиц.Добавить(СтруктураОрганизации);
		
	КонецЦикла;
	
	Если Роль = Перечисления.Saby_РолиКонтрагентов.УполномоченноеЛицоПеревозчика Тогда
		СтруктураДляДобавленияЛиц.Вставить("УполномоченноеЛицо", МассивУполномоченныхЛиц[0]);
	Иначе
		СтруктураДляДобавленияЛиц.Вставить("УполномоченныеЛица", МассивУполномоченныхЛиц);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Выгрузка

#Область Загрузка

Процедура ЗагрузкаТитулаОформление(ОбъектДок, Вложения, Титул, ДопПараметры)
	
	Если Не Вложения.Количество() Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеОформление = ДанныеОформление(ОбъектДок, Вложения, Титул, ДопПараметры);
	
	Если Не ДопПараметры.ТолькоАктивныйЭтап Тогда
		
		ДанныеТитула = ДанныеОформление[ДанныеОформление.ВГраница()];
		
		ЗаполнитьЗначенияСвойств(ОбъектДок, ДанныеТитула.ДляДокумента);
				
		Saby_ТНЗагрузкаСервер.ЗаполнитьДанныеТСИПрицеповДокумента(ОбъектДок, ДанныеТитула.ДляДокумента);
		
		ЗаполнитьСтрокиТаблицы(ОбъектДок, ДанныеТитула.ДляДокумента, "АдресаПромежуточныхПунктов");
		
		ЗаполнитьСтрокиТаблицы(ОбъектДок, ДанныеТитула.ДляДокумента, "АдресаПунктовВыгрузки");
		
		// Груз 
	    Saby_ТНЗагрузкаСервер.ЗаполнитьДанныеПоГрузам(
			ОбъектДок, 
			ДанныеТитула.ДляДокумента["Груз"], 
			ДопПараметры.ЗагрузкаСОнлайна);
			
		РолиДляЗаполнения = Новый Массив;
		РолиДляЗаполнения.Добавить(Перечисления.Saby_РолиКонтрагентов.Отправитель);
		РолиДляЗаполнения.Добавить(Перечисления.Saby_РолиКонтрагентов.Перевозчик);
		РолиДляЗаполнения.Добавить(Перечисления.Saby_РолиКонтрагентов.Отгрузчик);
		РолиДляЗаполнения.Добавить(Перечисления.Saby_РолиКонтрагентов.Заказчик);
		РолиДляЗаполнения.Добавить(Перечисления.Saby_РолиКонтрагентов.УполномоченноеЛицо);
		Для Каждого РольДляЗаполнения Из РолиДляЗаполнения Цикл
			Если НеобходимоЗаполнитьДанныеЮрЛиц(ДопПараметры, РольДляЗаполнения) Тогда
				Saby_ТНЗагрузкаСервер.ЗаполнитьДанныеЮрЛицИКонтактнуюИнформацию(
					ОбъектДок, ДанныеТитула.ДляДокумента, РольДляЗаполнения);
			КонецЕсли;
		КонецЦикла;
				
	КонецЕсли;
	
	Если ДопПараметры.ЗагрузкаСОнлайна Тогда
		РегистрыСведений.Saby_ДанныеТитулов.ЗаписатьДанные(ДанныеОформление, ОбъектДок, ДопПараметры.ИмяМетаданных);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузкаТитулаУтверждение(ОбъектДок, Вложения, Титул, ДопПараметры)
	
	Если Не Вложения.Количество() Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеУтверждение = ДанныеУтверждение(ОбъектДок, Вложения, Титул, ДопПараметры);
	
	ДанныеТитула = ДанныеУтверждение[ДанныеУтверждение.ВГраница()];
	ЗаполнитьЗначенияСвойств(ОбъектДок, ДанныеТитула.ДляДокумента);
	
	Если Не ДопПараметры.ТолькоАктивныйЭтап Тогда
		
		Saby_ТНЗагрузкаСервер.ЗаполнитьДанныеЮрЛицИКонтактнуюИнформацию(
			ОбъектДок, ДанныеТитула.ДляДокумента, Перечисления.Saby_РолиКонтрагентов.УполномоченноеЛицоПеревозчика);
		
		Saby_ТНЗагрузкаСервер.ЗаполнитьОтветственныхЛиц(
			ОбъектДок,
			ДанныеТитула.ДляДокумента.ОтветственныеЗаПеревозку,
			Перечисления.Saby_РолиОтветственных.Ответственный);
		
		Saby_ТНЗагрузкаСервер.ЗаполнитьДанныеТСИПрицеповДокумента(ОбъектДок, ДанныеТитула.ДляДокумента);
		
		Saby_ТНЗагрузкаСервер.ЗаполнитьОтветственныхЛиц(
			ОбъектДок,
			ДанныеТитула.ДляДокумента.Водители,
			Перечисления.Saby_РолиОтветственных.Водитель);
		
	КонецЕсли;
		
	РегистрыСведений.Saby_ДанныеТитулов.ЗаписатьДанные(
		ДанныеУтверждение, ОбъектДок, ДопПараметры.ИмяМетаданных);
	
КонецПроцедуры

#Область ДанныеТитуловДляЗагрузки

Функция ДанныеОформление(ОбъектДок, Вложения, Титул, ДопПараметры)
	
	РезультатФункции = Новый Массив;
	
	ВложенияДляЗагрузки = РегистрыСведений.Saby_ДанныеТитулов.ВложенияДляЗагрузки(
		ОбъектДок.Ссылка, Титул, Вложения, ДопПараметры);
	
	Для Каждого Вложение Из ВложенияДляЗагрузки Цикл
		
		ДляДокумента = Новый Структура;		
		ДляДокумента.Вставить("ДанныеЮрЛиц",      Новый Массив);
		ДляДокумента.Вставить("КонтактныеДанные", Новый Массив);
						
		// Документ
		ЗагрузитьДанныеДокумента(ДляДокумента, Вложение["Документ"], ДопПараметры);
		
		// Документы ЭПД
		ЗагрузитьДанныеДокументовЭПД(ДляДокумента, Вложение);
		
        // Маршрут
	    ЗагрузитьМаршрут(ДляДокумента, Вложение["Маршрут"]);
		
	    // Условия перевозки
	    ЗагрузитьУсловияПеревозки(ДляДокумента, Вложение["УсловияПеревозки"]);
		
		// Параметры ТС
		ЗагрузитьПараметрыТС(ДляДокумента, Вложение["ПараметрыТС"]);
		
		// Груз
		ДляДокумента.Вставить("Груз", Вложение["Груз"]);
		
		// ЮрЛица
		ЗаполнитьДанныеЮрЛиц(ДляДокумента, Вложение);
				
		ДляРегистра = СтруктураДанныхДляРегистра(ОбъектДок, Вложение, Титул, ДопПараметры.ИмяМетаданных);
		
		РезультатФункции.Добавить(Saby_ТНЗагрузкаСервер.СтруктураТитула(Титул, ДляДокумента, ДляРегистра));
		
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ДанныеУтверждение(ОбъектДок, Вложения, Титул, ДопПараметры)
	
	РезультатФункции = Новый Массив;
	
	ВложенияДляЗагрузки = РегистрыСведений.Saby_ДанныеТитулов.ВложенияДляЗагрузки(
		ОбъектДок.Ссылка, Титул, Вложения, ДопПараметры);
		
	Для Каждого Вложение Из ВложенияДляЗагрузки Цикл
		
		ДляДокумента = Новый Структура;		
		ДляДокумента.Вставить("ДанныеЮрЛиц",      Новый Массив);
		ДляДокумента.Вставить("КонтактныеДанные", Новый Массив);
		
		ЗагрузитьДанныеПоСтоимости(ДляДокумента, Вложение["СтоимостьПеревозки"]);
		
		СтруктураОтветственного = Новый Структура;
		Saby_ТНЗагрузкаСервер.ЗагрузитьОтветственного(
			СтруктураОтветственного,
			Вложение["Грузоперевозчик"]["Ответственный"]);
		СтруктураОтветственного.Вставить("Роль", Перечисления.Saby_РолиОтветственных.Ответственный);
		
		ДляДокумента.Вставить("ОтветственныеЗаПеревозку", Новый Массив);
		ДляДокумента.ОтветственныеЗаПеревозку.Добавить(СтруктураОтветственного);
		
		РольУполномоченноеЛицоПеревозчика = Перечисления.Saby_РолиКонтрагентов.УполномоченноеЛицоПеревозчика;
		УполномоченноеЛицоПеревозчика     = Вложение["Грузоперевозчик"]["УполномоченноеЛицо"];
		Saby_ТНЗагрузкаСервер.ЗагрузитьДанныеОрганизации(
			ДляДокумента,
			УполномоченноеЛицоПеревозчика,
			РольУполномоченноеЛицоПеревозчика);
		КИУполномоченноеЛицоПеревозчика = Saby_ТНЗагрузкаСервер.КонтактнаяИнформация(
			УполномоченноеЛицоПеревозчика,
			РольУполномоченноеЛицоПеревозчика);
		ДополнитьКонтактнуюИнформацию(ДляДокумента.КонтактныеДанные, КИУполномоченноеЛицоПеревозчика);
		
		ЗагрузитьОснованиеУполномоченногоЛица(
			ДляДокумента, УполномоченноеЛицоПеревозчика, РольУполномоченноеЛицоПеревозчика);
		
		Saby_ТНЗагрузкаСервер.ДанныеТСПрицеповСпецРазрешений(ДляДокумента, Вложение["ТранспортноеСредство"]);
		
		ДляДокумента.Вставить("Водители", Saby_ТНЗагрузкаСервер.ДанныеВодителей(Вложение["Водители"], ДопПараметры));
		
		ДляРегистра = СтруктураДанныхДляРегистра(ОбъектДок, Вложение, Титул, ДопПараметры.ИмяМетаданных);
		
		РезультатФункции.Добавить(Saby_ТНЗагрузкаСервер.СтруктураТитула(Титул, ДляДокумента, ДляРегистра));
		
	КонецЦикла;
			
	Возврат РезультатФункции;
	
КонецФункции

Функция СтруктураДанныхДляРегистра(ОбъектДок, Вложение, Титул, ИмяМетаданных, СохранитьДанные = Ложь)
	
	Основной = Не Перечисления.Saby_ТипТитулаЗнП.ЭтоДинамическийТитул(Титул);
	
	СтруктураДанныхДляРегистра = Saby_ТНЗагрузкаСервер.СтруктураДанныхДляРегистра(ОбъектДок.Ссылка, Титул, Основной);
	СтруктураДанныхДляРегистра.Просмотрено     = Истина;
	СтруктураДанныхДляРегистра.СохранитьДанные = СохранитьДанные;
	
	Вложение.Вставить("ЗагруженВРегистр", Вложение["ЗагруженВРегистр"] Или Вложение["ИдентификаторВложенияТитула"] = "");
	
	Возврат Saby_ТНЗагрузкаСервер.ДанныеДляРегистраТитулов(Вложение, ИмяМетаданных, СтруктураДанныхДляРегистра);
		
КонецФункции

#КонецОбласти // ДанныеТитуловДляЗагрузки

Процедура ЗагрузитьДанныеДокумента(ДляДокумента, ДокументСОнлайна, ДопПараметры)
	
	Если Не ЗначениеЗаполнено(ДокументСОнлайна) Тогда
		Возврат;
	КонецЕсли;
		
	ДляДокумента.Вставить("НомерСБИС", ДокументСОнлайна["Номер"]);
	
	ДляДокумента.Вставить(
	    "ТипДокумента",
		Перечисления.Saby_ТипДокументаЗнП.ЗначениеПоКоду(ДокументСОнлайна["Тип"]));
	
	Если Не ДопПараметры.ЗагрузкаСОнлайна Тогда
		ДляДокумента = Saby_ТНОбщегоНазначенияСервер.СтруктураБезПустыхЗначений(ДляДокумента);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьМаршрут(ДляДокумента, Маршрут)
	
	Если Не ЗначениеЗаполнено(Маршрут) Тогда
		Возврат;
	КонецЕсли;
	
	// Конечный пункт
	ДляДокумента.Вставить(
		"КонечнаяТочкаАдрес",
		Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Маршрут, "КонечныйПункт.Название"));
	
	// Отправление
	Отправление = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Маршрут, "Отправление");
	
	ДляДокумента.Вставить(
		"ПодачаАдрес",
		Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Отправление, "АдресТекст"));
	
	// Подача ТС
	ПодачаДата = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Отправление, "ПодачаТС.ДатаВремя");
	ПодачаДата = Saby_ТНОбщегоНазначенияСервер.ПреобразоватьСтрокуВДату(ПодачаДата);
	
	ДляДокумента.Вставить("ПодачаДатаВремя", ПодачаДата);
	
	Интервал = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Отправление, "ПодачаТС.ПредельныйИнтервалВремени");
	Интервал = Saby_ТНОбщегоНазначенияСервер.ПреобразоватьСтрокуВоВремя(Интервал);
	
	ДляДокумента.Вставить("ПодачаПредельныйИнтервал", Интервал);
	
	ЗагрузитьПромежуточныеПункты(ДляДокумента, Маршрут);
	
	ЗагрузитьПунктыВыгрузки(ДляДокумента, Маршрут);
	
КонецПроцедуры

Процедура ЗагрузитьПромежуточныеПункты(ДляДокумента, Маршрут)
	
	ПромежуточныеПункты = Маршрут["ПромежуточныйПункт"];
	
	Если Не ЗначениеЗаполнено(ПромежуточныеПункты) Тогда
		Возврат;
	КонецЕсли;
	
	АдресаПромежуточныхПунктов = Новый Массив;
	Для Каждого Пункт Из ПромежуточныеПункты Цикл
		
		НазваниеПункта = Пункт["Название"];
		
		Если Не ЗначениеЗаполнено(НазваниеПункта) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПункта = Новый Структура;
		СтруктураПункта.Вставить("Значение", НазваниеПункта);
		
		АдресаПромежуточныхПунктов.Добавить(СтруктураПункта);
		
	КонецЦикла;
	
	ДляДокумента.Вставить("АдресаПромежуточныхПунктов", АдресаПромежуточныхПунктов);
	
КонецПроцедуры

Процедура ЗагрузитьПунктыВыгрузки(ДляДокумента, Маршрут)
	
	Пункты = Маршрут["Пункт"];
	Если Не ЗначениеЗаполнено(Пункты) Тогда
		Возврат;
	КонецЕсли;
	
	АдресаПунктовВыгрузки = Новый Массив;	
	Для Каждого Пункт Из Пункты Цикл
		
		АдресТекст = Пункт["АдресТекст"];
		
		Если Не ЗначениеЗаполнено(АдресТекст) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПункта = Новый Структура;
		СтруктураПункта.Вставить("Значение", АдресТекст);
		
		// Операция
		ДатаВремяСтрокой = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Пункт, "Операция.ДатаВремя");		
		СтруктураПункта.Вставить(
			"ДатаВремя",
			Saby_ТНОбщегоНазначенияСервер.ПреобразоватьСтрокуВДату(ДатаВремяСтрокой));
		
		ИнтервалСтрокой = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Пункт, "Операция.ПредельныйИнтервалВремени");		
		СтруктураПункта.Вставить(
			"ДопустимоеВремя", 
			Saby_ТНОбщегоНазначенияСервер.ПреобразоватьСтрокуВоВремя(ИнтервалСтрокой));
		 			
		Тип = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Пункт, "Операция.Тип");
		СтруктураПункта.Вставить("ЭтоПогрузка", Тип = "Погрузка");
		
		// Организация
		СтруктураПункта.Вставить(
			"ОрганизацияСтрока", 
			Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Пункт, "Организация.Название"));
		
		СтруктураПункта.Вставить(
			"ОрганизацияИНН",
			Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Пункт, "Организация.ИНН"));
			
		ДобавитьДанныеЮрЛицПункта(ДляДокумента, СтруктураПункта);
			
		АдресаПунктовВыгрузки.Добавить(СтруктураПункта);
		
	КонецЦикла;
	
	ДляДокумента.Вставить("АдресаПунктовВыгрузки", АдресаПунктовВыгрузки);
	
КонецПроцедуры

Процедура ДобавитьДанныеЮрЛицПункта(ДляДокумента, СтруктураПункта)
	
	Если Не ЗначениеЗаполнено(СтруктураПункта.ОрганизацияИНН) Тогда
		Возврат;
	КонецЕсли;
	
	КлючСтроки = Saby_ТНОбщегоНазначенияКлиентСервер.НовыйКлючОсновнойСтроки(ДляДокумента, "ДанныеЮрЛиц");
	
	СтруктураУполномоченногоЛица = Новый Структура;
	СтруктураУполномоченногоЛица.Вставить("Роль",                    Перечисления.Saby_РолиКонтрагентов.Отгрузчик);
	СтруктураУполномоченногоЛица.Вставить("ИНН",                     СтруктураПункта.ОрганизацияИНН);
	СтруктураУполномоченногоЛица.Вставить("НаименованиеОрганизации", СтруктураПункта.ОрганизацияСтрока);
	СтруктураУполномоченногоЛица.Вставить("КлючСтроки",              КлючСтроки);
	
	ДляДокумента.ДанныеЮрЛиц.Добавить(СтруктураУполномоченногоЛица);
	
	СтруктураПункта.Вставить("КлючСтроки_ДанныеЮрЛиц", КлючСтроки);
	
КонецПроцедуры

Процедура ЗагрузитьУсловияПеревозки(ДляДокумента, УсловияПеревозки)
	
	Если Не ЗначениеЗаполнено(УсловияПеревозки) Тогда
		Возврат;
	КонецЕсли;
	
	// УсловияПеревозки
	СпецУсловия    = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(УсловияПеревозки, "УказанияНорм");
	ПредельныйСрок = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(УсловияПеревозки, "ПредельныйСрок");
	ИныеУсловия    = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(УсловияПеревозки, "Иные");
	ТребованияКТС  = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(УсловияПеревозки, 
						"ТребованияПоПеревозкеПищевыхПродуктов");
	
	ДляДокумента.Вставить("СпецУсловия",    СпецУсловия);		
	ДляДокумента.Вставить("ПредельныйСрок", ПредельныйСрок);		
	ДляДокумента.Вставить("ИныеУсловия",    ИныеУсловия);		
	ДляДокумента.Вставить("ТребованияКТС",  ТребованияКТС);
		
	// Климатический режим
	КлиматическийРежим = УсловияПеревозки["КлиматическийРежим"];
	
	ВлажностьОт   = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(КлиматическийРежим, "Влажность.Минимальная");
	ВлажностьДо   = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(КлиматическийРежим, "Влажность.Максимальная");
	ТемператураОт = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(КлиматическийРежим, "Температура.Минимальная");
	ТемператураДо = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(КлиматическийРежим, "Температура.Максимальная");	
				
	ДляДокумента.Вставить("ВлажностьОт",   ВлажностьОт);		
	ДляДокумента.Вставить("ВлажностьДо",   ВлажностьДо);		
	ДляДокумента.Вставить("ТемператураОт", ТемператураОт);		
	ДляДокумента.Вставить("ТемператураДо", ТемператураДо);
	
КонецПроцедуры

Процедура ЗагрузитьПараметрыТС(ДляДокумента, ПараметрыТС)
	
	Если Не ЗначениеЗаполнено(ПараметрыТС) Тогда
		Возврат;
	КонецЕсли;                            
	
	ТипТС      = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(
					ПараметрыТС, "Тип");
				
	КузовТС    = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(
					ПараметрыТС, "ТипКузова");
				
	ПогрузкаТС = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(
					ПараметрыТС, "ТипПогрузки");
					
	ДляДокумента.Вставить("ТипТС",      ТипТС);	
	ДляДокумента.Вставить("КузовТС",    КузовТС);		
	ДляДокумента.Вставить("ПогрузкаТС", ПогрузкаТС);
									
	ГрузоподъемностьТС = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(
							ПараметрыТС, "Грузоподъемность");
	ВместимостьТС      = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(
							ПараметрыТС, "Вместимость");
			
	ДляДокумента.Вставить("ГрузоподъемностьТС", ГрузоподъемностьТС);		
	ДляДокумента.Вставить("ВместимостьТС",      ВместимостьТС);	

КонецПроцедуры

Процедура ЗагрузитьДанныеДокументовЭПД(ДляДокумента, ДанныеДокументов)
		
	Если Не ЗначениеЗаполнено(ДанныеДокументов) Тогда 
		Возврат;
	КонецЕсли;
		
	ТЗ = Saby_ТНЗагрузкаСервер.ТаблицаДокументовЭПД();
		
	Договор              = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(ДанныеДокументов, "Договор");
	СоставительОснование = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(
		ДанныеДокументов, "Файл.Составитель.Основание");
			
	НомерСтроки = 0;
		
	// Договор
	Если ЗначениеЗаполнено(Договор) И ЗначениеЗаполнено(Договор["Дата"]) Тогда
		НС = ТЗ.Добавить();
		Тип = Перечисления.Saby_ТипыДокумента.ДоговорПеревозки;
		Saby_ТНЗагрузкаСервер.ЗаполнитьДанныеСтрокиТаблицыДокументовЭПД(НС, Договор, Тип, НомерСтроки);
	КонецЕсли;
	
	// Составитель на основание
	Если ЗначениеЗаполнено(СоставительОснование) Тогда 
		НС = ТЗ.Добавить();	
		Тип = Перечисления.Saby_ТипыДокумента.СоставительНаОсновании;
		Saby_ТНЗагрузкаСервер.ЗаполнитьДанныеСтрокиТаблицыДокументовЭПД(НС, СоставительОснование, Тип, НомерСтроки);
	КонецЕсли;
	
	Если Не ТЗ.Количество() Тогда
		Возврат;
	КонецЕсли;
		
	// Получим ссылки на существующие и новые документы
	СоответствиеДокументов = Saby_ТНЗагрузкаСервер.НайтиСоздатьДокументЭПД(ТЗ);
		
	// Заполняем данные по документам
	Для Каждого СтрокаДок Из ТЗ Цикл
		
		СсылкаДок = СоответствиеДокументов.Получить(СтрокаДок.НомерСтроки);
		
		Если СтрокаДок.ТипДок = Перечисления.Saby_ТипыДокумента.ДоговорПеревозки Тогда			
			ДляДокумента.Вставить("Договор", СсылкаДок);			
		Иначе			
			ДляДокумента.Вставить("Составитель_НаОснованииДокумент", СсылкаДок);			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокиТаблицы(ОбъектДок, ДляДокумента, НазваниеТаблицы)
	
	Если Не ДляДокумента.Свойство(НазваниеТаблицы) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектДок[НазваниеТаблицы].Очистить();
	
	Для Каждого СтрокаТаблицы Из ДляДокумента[НазваниеТаблицы] Цикл
		НоваяСтрока = ОбъектДок[НазваниеТаблицы].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеЮрЛиц(ДляДокумента, Вложение)
	
	РольОтправитель  = Перечисления.Saby_РолиКонтрагентов.Отправитель;
	РольПеревозчик   = Перечисления.Saby_РолиКонтрагентов.Перевозчик;

	// Отправитель
	Грузоотправитель = Вложение["Грузоотправитель"];
	Saby_ТНЗагрузкаСервер.ЗагрузитьДанныеОрганизации(ДляДокумента, Грузоотправитель,  РольОтправитель);
	КИГрузоотправитель = Saby_ТНЗагрузкаСервер.КонтактнаяИнформация(Грузоотправитель, РольОтправитель);
	ДополнитьКонтактнуюИнформацию(ДляДокумента.КонтактныеДанные, КИГрузоотправитель);
	
	Если ЗначениеЗаполнено(Грузоотправитель["УполномоченныеЛица"]) Тогда
		РольУполномоченноеЛицоЗаказчика = Перечисления.Saby_РолиКонтрагентов.УполномоченноеЛицо;
		Для Каждого УполномоченноеЛицоЗаказчика Из Грузоотправитель["УполномоченныеЛица"] Цикл
			КлючСтроки = Saby_ТНОбщегоНазначенияКлиентСервер.НовыйКлючОсновнойСтроки(ДляДокумента, "ДанныеЮрЛиц");
			Saby_ТНЗагрузкаСервер.ЗагрузитьДанныеОрганизации(
				ДляДокумента,
				УполномоченноеЛицоЗаказчика,
				РольУполномоченноеЛицоЗаказчика,
				КлючСтроки);
			КИУполномоченноеЛицоЗаказчика = Saby_ТНЗагрузкаСервер.КонтактнаяИнформация(
				УполномоченноеЛицоЗаказчика,
				РольУполномоченноеЛицоЗаказчика);
			ДополнитьКонтактнуюИнформацию(ДляДокумента.КонтактныеДанные, КИУполномоченноеЛицоЗаказчика, КлючСтроки);
			ЗагрузитьОснованиеУполномоченногоЛица(
				ДляДокумента, УполномоченноеЛицоЗаказчика, РольУполномоченноеЛицоЗаказчика);
		КонецЦикла;
	КонецЕсли;
	
	// Перевозчик
	Грузоперевозчик = Вложение["Грузоперевозчик"];
	Saby_ТНЗагрузкаСервер.ЗагрузитьДанныеОрганизации(ДляДокумента, Грузоперевозчик, РольПеревозчик);
	КИГрузоперевозчик = Saby_ТНЗагрузкаСервер.КонтактнаяИнформация(Грузоперевозчик, РольПеревозчик);	
	ДополнитьКонтактнуюИнформацию(ДляДокумента.КонтактныеДанные, КИГрузоперевозчик);
		
	ГрузоотправительСтрокой = Saby_ТНОбщегоНазначенияКлиентСервер.ПредставлениеЮрЛицаПоРоли(
		ДляДокумента.ДанныеЮрЛиц, "Отправитель");
		
	// Составитель	
	Составитель = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(Вложение, "Файл.Составитель.Наименование");		
	ДляДокумента.Вставить("ПредставительЗаказчикаНаименование", Составитель);			
	
	ЭтоПредставитель = (ЗначениеЗаполнено(Составитель) И ГрузоотправительСтрокой <> Составитель);			
	ДляДокумента.Вставить("ПредставительЗаказчика", ЭтоПредставитель);
	
КонецПроцедуры

Процедура ДополнитьКонтактнуюИнформацию(КонтактныеДанные, МассивКИ, КлючСтроки_ДанныеЮрЛиц = 0)
	
	Если Не ЗначениеЗаполнено(МассивКИ) Тогда
		Возврат;
	КонецЕсли;
		
	Для Каждого ЭлементКИ Из МассивКИ Цикл
		ЭлементКИ.Вставить("КлючСтроки_ДанныеЮрЛиц", КлючСтроки_ДанныеЮрЛиц);
		КонтактныеДанные.Добавить(ЭлементКИ);	
	КонецЦикла;
		
КонецПроцедуры

Функция НеобходимоЗаполнитьДанныеЮрЛиц(ДопПараметры, Роль)
	
	Если Не ДопПараметры.ПараметрыОснования.Свойство("РольКонтрагента")
		Или Не ЗначениеЗаполнено(ДопПараметры.ПараметрыОснования.РольКонтрагента) Тогда
		Возврат Истина;
	Иначе
		Возврат ДопПараметры.ПараметрыОснования.РольКонтрагента = Роль;
	КонецЕсли;
	
КонецФункции

Процедура ЗагрузитьДанныеПоСтоимости(ДляДокумента, СтоимостьПеревозки)
	
	Если СтоимостьПеревозки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДляДокумента.Вставить(
		"ВалютаКод",          Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(СтоимостьПеревозки, "Валюта.Код"));
	ДляДокумента.Вставить(
		"ВалютаНаименование", Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(СтоимостьПеревозки, "Валюта.Наименование"));
	
	СтавкаНДС = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(СтоимостьПеревозки, "Налог.Ставка");
	Если Не ЗначениеЗаполнено(СтавкаНДС) Тогда 	
		СтавкаНДС = "без НДС";
	КонецЕсли;
	ДляДокумента.Вставить("СтавкаНДС",       СтавкаНДС);
	ДляДокумента.Вставить("СуммаБезНДС",     СтоимостьПеревозки["БезНДС"]);
	ДляДокумента.Вставить("СуммаСНДС",       СтоимостьПеревозки["ВключаяНДС"]);
	ДляДокумента.Вставить("Сумма",           СтоимостьПеревозки["ВключаяНДС"]);
	ДляДокумента.Вставить("МеханизмРасчета", СтоимостьПеревозки["Расчет"]);
	
КонецПроцедуры

Процедура ЗагрузитьОснованиеУполномоченногоЛица(ДляДокумента, ДанныеЮрЛица, Роль)
	
	Если ДанныеЮрЛица = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТЗ = Saby_ТНЗагрузкаСервер.ТаблицаДокументовЭПД();
	НомерСтроки = 0;
	
	Основание = Saby_ТНОбщегоНазначенияСервер.ДанныеСоответствия(ДанныеЮрЛица, "Документ");
	Если ЗначениеЗаполнено(Основание) И ЗначениеЗаполнено(Основание["Дата"]) Тогда
		НС = ТЗ.Добавить();
		Тип = Перечисления.Saby_ТипыДокумента.УполномоченныйНаОсновании;
		Saby_ТНЗагрузкаСервер.ЗаполнитьДанныеСтрокиТаблицыДокументовЭПД(НС, Основание, Тип, НомерСтроки);
	КонецЕсли;
	
	Если ТЗ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОтборПоРоли = Новый Структура;
	ОтборПоРоли.Вставить("Роль", Роль);
	
	СоответствиеОснований = Saby_ТНЗагрузкаСервер.НайтиСоздатьДокументЭПД(ТЗ);
	Для Каждого СтрокаОснования Из ТЗ Цикл
		
		СтруктурыЮрЛица = Saby_ТНОбщегоНазначенияКлиентСервер.НайтиСтрокиУниверсально(ДляДокумента.ДанныеЮрЛиц, ОтборПоРоли);
		Если СтруктурыЮрЛица.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураЮрЛица = СтруктурыЮрЛица[СтруктурыЮрЛица.ВГраница()];
		
		СсылкаОснование = СоответствиеОснований.Получить(СтрокаОснования.НомерСтроки);
		СтруктураЮрЛица.Вставить("Основание",  СсылкаОснование);
		СтруктураЮрЛица.Вставить("Полномочия", ДанныеЮрЛица["Полномочия"]);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // Загрузка

#КонецОбласти // СлужебныеПроцедурыИФункции
