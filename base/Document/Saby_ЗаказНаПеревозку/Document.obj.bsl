                     
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.ТранспортноеСредствоСтрокой = Saby_ТНОбщегоНазначенияСервер.ТранспортныеСредстваСтрокой(ЭтотОбъект);
	
	ЭтотОбъект.Заказчик = Saby_ТНОбщегоНазначенияКлиентСервер.ДанныеЮрЛицаПоРоли(
		ЭтотОбъект.ДанныеЮрЛиц, "Отправитель").НаименованиеОрганизации;
		
	ЭтотОбъект.Перевозчик = Saby_ТНОбщегоНазначенияКлиентСервер.ДанныеЮрЛицаПоРоли(
		ЭтотОбъект.ДанныеЮрЛиц, "Перевозчик").НаименованиеОрганизации;
		
	Если ЭтотОбъект.ПредставительЗаказчика Тогда
		ЭтотОбъект.ПредставительЗаказчикаНаименование = Saby_ТНОбщегоНазначенияКлиентСервер.ДанныеЮрЛицаПоРоли(
			ЭтотОбъект.ДанныеЮрЛиц, "Оформитель").НаименованиеОрганизации;
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ТитулыДляПривязки") Тогда
		РегистрыСведений.Saby_ДанныеТитулов.ПривязатьТитулыКДокументу(
			Ссылка,
			ДополнительныеСвойства.ТитулыДляПривязки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	РеквизитыДляОчистки = Новый Массив;
	
	// Служебные 
	РеквизитыДляОчистки.Добавить("ДатаИзменения");
	РеквизитыДляОчистки.Добавить("НомерСбис");
	РеквизитыДляОчистки.Добавить("Комментарий");
	РеквизитыДляОчистки.Добавить("КомментарийУведомление");
	РеквизитыДляОчистки.Добавить("ТекущиеЭтапы");
	РеквизитыДляОчистки.Добавить("ДокументОснование_Идентификатор");
	РеквизитыДляОчистки.Добавить("ДокументОснование_ОбъектМетаданных");
	
	// Титул 2
	РеквизитыДляОчистки.Добавить("ТранспортныеСредства");
	РеквизитыДляОчистки.Добавить("ОтветственныеЛица");
	РеквизитыДляОчистки.Добавить("СпецРазрешения");
	РеквизитыДляОчистки.Добавить("СтавкаНДС");
    РеквизитыДляОчистки.Добавить("СуммаБезНДС");
    РеквизитыДляОчистки.Добавить("СуммаСНДС");
    РеквизитыДляОчистки.Добавить("МеханизмРасчета");
	
	Saby_ТНОбщегоНазначенияСервер.ОчисткаРеквизитовОбъекта(ЭтотОбъект, РеквизитыДляОчистки);
	
	Saby_ТНОбщегоНазначенияКлиентСервер.УдалитьСтрокиПоРоли(ЭтотОбъект.ДанныеЮрЛиц, "УполномоченноеЛицоПеревозчика");
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Префикс = "00";
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий 
