
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачальноеЗаполнение(Параметры);
	
	УстановитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеЮрЛиц

&НаКлиенте
Процедура ДанныеЮрЛицВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыРеквизитаФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыРеквизитаФормы(
		"НаименованиеОрганизации",
		"ДанныеЮрЛицНаименованиеОрганизации",
		ВыбраннаяСтрока);
		
	ПараметрыРеквизитаФормы.ИмяИсходнойТаблицы          = "ДанныеЮрЛиц";
	ПараметрыРеквизитаФормы.ИдентификаторИсходнойСтроки = ВыбраннаяСтрока;
	ПараметрыРеквизитаФормы.ЭтоВО                       = ЭтаФорма.ЭтоВО;
	ПараметрыРеквизитаФормы.ТолькоПросмотр              = ЭтаФорма.ТолькоПросмотр;
	
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЮрЛицоНажатие(ЭтаФорма, "ИнойПолучатель", ПараметрыРеквизитаФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеЮрЛицПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Роль = ЗначениеМетаданных("Saby_РолиКонтрагентов.ИнойПолучатель");
		Элемент.ТекущиеДанные.КлючСтроки = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").НовыйКлючОсновнойСтроки(
			ЭтаФорма, "ДанныеЮрЛиц");
	КонецЕсли;
	
	ПараметрыРеквизитаФормы = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ПараметрыРеквизитаФормы(
		"НаименованиеОрганизации",
		"ДанныеЮрЛицНаименованиеОрганизации",
		Элементы.ДанныеЮрЛиц.ТекущаяСтрока);
			
	ПараметрыРеквизитаФормы.ИмяИсходнойТаблицы          = "ДанныеЮрЛиц";
	ПараметрыРеквизитаФормы.ИдентификаторИсходнойСтроки = Элементы.ДанныеЮрЛиц.ТекущаяСтрока;
	ПараметрыРеквизитаФормы.ЭтоВО                       = ЭтаФорма.ЭтоВО;
	ПараметрыРеквизитаФормы.ТолькоПросмотр              = ЭтаФорма.ТолькоПросмотр;
			
	МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиент").ЮрЛицоНажатие(ЭтаФорма, "ИнойПолучатель", ПараметрыРеквизитаФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИныхПолучателей(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("ИныеПолучатели", Новый Массив);
	
	Для Каждого СтрокаЮрЛица Из ЭтаФорма.ДанныеЮрЛиц Цикл
		СтруктураЮрЛица = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер").ШаблонДанныхЮрЛица();
		ЗаполнитьЗначенияСвойств(СтруктураЮрЛица, СтрокаЮрЛица);
		Результат.ИныеПолучатели.Добавить(СтруктураЮрЛица);
	КонецЦикла;
	
	ЭтаФорма.Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НачальноеЗаполнение(ПараметрыФормы)
	
	ЭтаФорма.ЭтоВО = ПараметрыФормы.ЭтоВО;
	
	ЭтаФорма.ДанныеЮрЛиц.Очистить();
	
	Если ЗначениеЗаполнено(ПараметрыФормы.ИныеПолучатели) Тогда
		Для Каждого СтруктураЮрЛица Из ПараметрыФормы.ИныеПолучатели Цикл
			СтрокаЮрЛица = ЭтаФорма.ДанныеЮрЛиц.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаЮрЛица, СтруктураЮрЛица);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	ТолькоПросмотрФормы = ЭтаФорма.ТолькоПросмотр;
	
	Элементы.ДанныеЮрЛиц.ТолькоПросмотр = ТолькоПросмотрФормы;
	Элементы.ФормаСохранитьИныхПолучателей.Доступность = Не ТолькоПросмотрФормы;
	
КонецПроцедуры

#Область include_etrn_base_CommonForm_ИныеПолучателиДокументаЭПД_СлужебныеПроцедурыИФункции
#КонецОбласти // include_etrn_base_CommonForm_ИныеПолучателиДокументаЭПД_СлужебныеПроцедурыИФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

