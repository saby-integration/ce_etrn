
#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.Роль = Параметры.Роль;
	
	Если Параметры.Свойство("ТолькоПросмотр") Тогда
		ЭтаФорма.ТолькоПросмотрФормы = Параметры.ТолькоПросмотр;
	КонецЕсли;
		
	Если Параметры.Свойство("Наименование") Тогда
		ЭтаФорма.Заголовок = Параметры.Наименование;
	КонецЕсли;
	
	ЗаполнениеРеквизитовФормы(Параметры);	
		    		
	ОбновитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьПоСсылке(Команда)
	
	ОбновитьДанныеПоСсылке();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНовыеДанные(Команда)
	
	//ТелефоныЕмейлы = ТелефоныЕмейлыСтрокой();
	
	Если ЭтаФорма.ЭтоИП Тогда 
		
        ФИО = Saby_ТНОбщегоНазначенияКлиентСервер.ПредставлениеФИО(ЭтаФорма);		
		ЭтаФорма.НаименованиеОрганизации = "ИП " + ФИО;
		
		СтруктураФИО = ФИОСтрокой(ЭтаФорма.Фамилия, ЭтаФорма.Имя, ЭтаФорма.Отчество);
				
	Иначе 			
		СтруктураФИО = "";		
	КонецЕсли;
		
	Структура = Новый Структура;
	Структура.Вставить("Роль",                    ЭтаФорма.Роль);
	Структура.Вставить("ИНН",                     ЭтаФорма.ИНН);
	Структура.Вставить("КПП",                     ЭтаФорма.КПП);
	Структура.Вставить("ОГРН",                    ЭтаФорма.ОГРН);
	Структура.Вставить("ЮрФизЛицо",               ЭтаФорма.ЮрФизЛицо);
	Структура.Вставить("СтруктураФИО",            СтруктураФИО);
	Структура.Вставить("Адрес",                   ЭтаФорма.Адрес);
	Структура.Вставить("АдресСтруктура",          ЭтаФорма.АдресСтруктура);
	Структура.Вставить("СтранаРегистрации",       ЭтаФорма.СтранаРегистрации);
	Структура.Вставить("КодФилиала",              ЭтаФорма.КодФилиала);	
	Структура.Вставить("НаименованиеОрганизации", ЭтаФорма.НаименованиеОрганизации);
	Структура.Вставить("ЭлементФормы",            ЭтаФорма.ЭлементФормы);
	
	ЭтаФорма.Закрыть(Структура);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсеПоля(Команда)
	
	ОчиститьРеквизитыФормы();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентОрганизацияПриИзменении(Элемент)
	ОбновитьДанныеПоСсылке();	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОчиститьРеквизитыФормы()
	
	РеквизитыИсключения = Новый Массив;
	РеквизитыИсключения.Добавить("Роль");
	РеквизитыИсключения.Добавить("ЭлементФормы");

	Saby_ТНОбщегоНазначенияСервер.ОчиститьРеквизитыФормы(ЭтаФорма, РеквизитыИсключения);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьИДоступностьЭлементов()
	
	МассивСкрыть   = Новый Массив;
	МассивПоказать = Новый Массив;
	
	ЭтаФорма.ЭтоИП = (ЭтаФорма.ЮрФизЛицо = Перечисления.Saby_ЮрФизЛицо.ИндивидуальныйПредприниматель);
	
	Если ЭтаФорма.ЭтоИП Тогда
				
		МассивСкрыть.Добавить("НаименованиеОрганизации");
		МассивСкрыть.Добавить("КодФилиала");
		МассивСкрыть.Добавить("КПП");
		
		МассивПоказать.Добавить("ГруппаФИО");
		
	Иначе 	
			
		МассивСкрыть.Добавить("ГруппаФИО");
		
		МассивПоказать.Добавить("НаименованиеОрганизации");
		МассивПоказать.Добавить("КодФилиала");
		МассивПоказать.Добавить("КПП");
		
	КонецЕсли;
	
	МассивСкрыть.Добавить("СтранаРегистрации");
	
	Если ЗначениеЗаполнено(ЭтаФорма.Адрес) Тогда
		МассивСкрыть.Добавить("ДекорацияЗаполнитьАдресДоставки");
	Иначе 
		МассивПоказать.Добавить("ДекорацияЗаполнитьАдресДоставки");		
	КонецЕсли;
	
	Если ТипЗнч(ЭтаФорма.ТипДокументаИсточника) = Тип("ДокументСсылка.Saby_ПутевойЛист") Тогда
		МассивСкрыть.Добавить("ГруппаКонтакты");
	КонецЕсли;
	
	Saby_ТНОбщегоНазначенияКлиентСервер.ИзменитьВидимостьДоступностьЭлементовФормы(
		ЭтаФорма, "Видимость", Ложь, МассивСкрыть);
		
	Saby_ТНОбщегоНазначенияКлиентСервер.ИзменитьВидимостьДоступностьЭлементовФормы(
		ЭтаФорма, "Видимость", Истина, МассивПоказать);
				
КонецПроцедуры	

&НаСервере
Процедура ЗаполнениеРеквизитовФормы(Знач ДанныеЗаполнения)
	
	Saby_ТНОбщегоНазначенияСервер.ЗаполнитьРеквизитыФормы(ЭтаФорма, ДанныеЗаполнения);
	
	Если ДанныеЗаполнения.Свойство("ЮрФизЛицо") Тогда		
		ЭтаФорма.ЮрФизЛицо = ДанныеЗаполнения.ЮрФизЛицо;	
	Иначе 
		ЭтаФорма.ЮрФизЛицо = Перечисления.Saby_ЮрФизЛицо.ЮрЛицо;
	КонецЕсли;
			
	ЭтаФорма.ЭтоИП = (ЭтаФорма.ЮрФизЛицо = Перечисления.Saby_ЮрФизЛицо.ИндивидуальныйПредприниматель);
	Если ЭтоИП И ДанныеЗаполнения.Свойство("СтруктураФИО") Тогда
		Если ЗначениеЗаполнено(ДанныеЗаполнения.СтруктураФИО) Тогда 
			СтруктураФИО = ЗначениеИзСтрокиВнутр(ДанныеЗаполнения.СтруктураФИО);
			ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураФИО);
		КонецЕсли;		
	КонецЕсли;		
	
	Если ДанныеЗаполнения.Свойство("ЭлементФормы") Тогда 
		ЭтаФорма.ЭлементФормы = ДанныеЗаполнения.ЭлементФормы;
	КонецЕсли;
	
КонецПроцедуры

#Область Телефоны

&НаСервере
Процедура СформироватьНадписьТелефоныСтрокой()
		
	//Массив = Новый Массив;
	//Для Каждого Строка Из Телефоны Цикл 
	//	
	//	Если ЗначениеЗаполнено(Строка.Телефон) Тогда
	//		Массив.Добавить(СокрЛП(Строка.Телефон));
	//	КонецЕсли;
	//			
	//КонецЦикла;
	//
	//Если Массив.Количество() Тогда 
	//	ЭтаФорма.ТелефоныСтрокой = СтрСоединить(Массив, ", ");
	//Иначе 
	//	ЭтаФорма.ТелефоныСтрокой = "";
	//КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныТелефонПриИзменении(Элемент)
	СформироватьНадписьТелефоныСтрокой();
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭтаФорма.ТекущийЭлемент = Элементы.Телефоны; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныПослеУдаления(Элемент)
	СформироватьНадписьТелефоныСтрокой();
КонецПроцедуры

#КонецОбласти // Телефоны

&НаКлиенте
Процедура ОбработкаВыбораВодителя(ВыбранныйЭлемент, ДопПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ЭтаФорма.Водитель = ВыбранныйЭлемент;				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаПриИзменении(Элемент)
	
	ОбновитьДанныеПоСсылке();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоСсылке()
	
	Если ЗначениеЗаполнено(ЭтаФорма.КонтрагентОрганизация) Тогда 
		ОбновитьДанныеЮрЛицНаСервере(); 
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ТелефоныЕмейлыСтрокой()
	
	//Если Телефоны.Количество() Тогда 
	//	
	//	МассивТелефонов = Новый Массив;
	//	Для Каждого Строка Из Телефоны Цикл 
	//		МассивТелефонов.Добавить(Строка.Телефон);			
	//	КонецЦикла;
	//	
	//	ТелефоныСтрокой = ЗначениеВСтрокуВнутр(МассивТелефонов);
	//	
	//Иначе 	
	//	ТелефоныСтрокой = "";
	//КонецЕсли;	
	//
	//Если Емейлы.Количество() Тогда 
	//	
	//	МассивЕмейлы = Новый Массив;
	//	Для Каждого Строка Из Емейлы Цикл 
	//		МассивЕмейлы.Добавить(Строка.Емейл);			
	//	КонецЦикла;
	//	
	//	ЕмейлыСтрокой = ЗначениеВСтрокуВнутр(МассивЕмейлы);
	//	
	//Иначе 	
	//	ЕмейлыСтрокой = "";
	//КонецЕсли;	
	//
	//Структура = Новый Структура;
	//Структура.Вставить("Телефоны", ТелефоныСтрокой);
	//Структура.Вставить("Емейлы",   ЕмейлыСтрокой);
	//	
	//Возврат Структура;
	
	Возврат "";
	
КонецФункции

&НаКлиенте
Процедура ЮрФизЛицоПриИзменении(Элемент)
	
	ОбновитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

#Область КонтактныеДанные

&НаКлиенте
Процедура КонтактныеДанныеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Saby_ТНОбщегоНазначенияКлиент.ЗначениеКонтактныхДанныхНачалоВыбора(
		ЭтаФорма, Элементы.КонтактныеДанные.ТекущиеДанные, ЭтаФорма.Роль, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеДанныеЗначениеПриИзменении(Элемент)
	
	Saby_ТНОбщегоНазначенияКлиент.ЗначениеКонтактныхДанныхПриИзменении(
		ЭтаФорма, Элементы.КонтактныеДанные.ТекущиеДанные, ЭтаФорма.Роль);
	
КонецПроцедуры

#КонецОбласти // КонтактныеДанные

&НаКлиенте
Процедура АдресДоставкиНажатие(Элемент, СтандартнаяОбработка)

	ПараметрыВыбораАдреса = Новый Структура;
	ПараметрыВыбораАдреса.Вставить("Роль",     Роль);
	ПараметрыВыбораАдреса.Вставить("Значение", ЭтаФорма.Адрес);
	
	Saby_ТНОбщегоНазначенияКлиент.АдресНачалоВыбора(ЭтаФорма, ПараметрыВыбораАдреса, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьИДоступностьЭлементовНаКлиенте() Экспорт
	ОбновитьВидимостьИДоступностьЭлементов();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФИОСтрокой(Знач Фамилия, Знач Имя, Знач Отчество)

	СтруктураФИО = Новый Структура;
	СтруктураФИО.Вставить("Фамилия",  Фамилия);
	СтруктураФИО.Вставить("Имя",      Имя);
	СтруктураФИО.Вставить("Отчество", Отчество);
	
	Возврат ЗначениеВСтрокуВнутр(СтруктураФИО);
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеЮрЛицНаСервере()
	
	ДанныеЮрЛиц.Очистить();
	
	Saby_ТНОбщегоНазначенияСервер.ОбновитьДанныеЮрЛиц(ЭтаФорма, "Оформил", "КонтрагентОрганизация");
	
	Если ЭтаФорма.ДанныеЮрЛиц.Количество() Тогда
		
		Строка    = ЭтаФорма.ДанныеЮрЛиц[0];	
		Структура = Saby_ТНОбщегоНазначенияСервер.СтруктураДанныхЮрЛица();
				
		Если Строка.ЮрФизЛицо = Перечисления.Saby_ЮрФизЛицо.ЮрЛицо Тогда
		 	Строка.НаименованиеОрганизации = СокрЛП(ЭтаФорма.КонтрагентОрганизация);
		КонецЕсли;
		
		Строка.Роль = Роль;  
		
		ЗаполнитьЗначенияСвойств(Структура, Строка);		
		ЗаполнениеРеквизитовФормы(Структура);
		
	Иначе 
		ОчиститьРеквизитыФормы();
	КонецЕсли;	
	
	ОбновитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции