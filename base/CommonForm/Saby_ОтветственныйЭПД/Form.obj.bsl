 
#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнениеРеквизитовФормы(Параметры);	
				
	УстановитьВидимостьДоступностьРеквизитов(Параметры);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьПоСсылке(Команда)
	
	ОбновитьДанныеПоСсылке();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНовыеДанные(Команда)
	
	ВУСтрокой = Saby_ТНОбщегоНазначенияКлиентСервер.ПредставлениеВУ(ЭтаФорма);
	ФИО       = Saby_ТНОбщегоНазначенияКлиентСервер.ПредставлениеФИО(ЭтаФорма);
	
	ТелефоныЕмейлы = ТелефоныЕмейлыСериализовать();
		
	Структура = Новый Структура;
	Структура.Вставить("Фамилия",   ЭтаФорма.Фамилия);
	Структура.Вставить("Имя",       ЭтаФорма.Имя);
	Структура.Вставить("Отчество",  ЭтаФорма.Отчество);
	Структура.Вставить("ИНН",       ЭтаФорма.ИНН);
	Структура.Вставить("Телефоны",  ТелефоныЕмейлы.Телефоны);
	Структура.Вставить("Емейлы",    ТелефоныЕмейлы.Емейлы);
	Структура.Вставить("ФИО",       ФИО);
	Структура.Вставить("ВУСтрокой", ВУСтрокой);
	Структура.Вставить("Роль",      ЭтаФорма.Роль);	
	
	Если Элементы.ГруппаВУ.Видимость Тогда		
		Структура.Вставить("Серия",      ЭтаФорма.Серия);
		Структура.Вставить("Номер",      ЭтаФорма.Номер);		
		Структура.Вставить("ДатаВыдачи", ЭтаФорма.ДатаВыдачи);
	КонецЕсли;
		
	Если Элементы.ГруппаЛицензия.Видимость Тогда 		
		Структура.Вставить("Серия",      ЭтаФорма.ЛицензияСерия);
		Структура.Вставить("Номер",      ЭтаФорма.ЛицензияНомер);		
	    Структура.Вставить("ДатаВыдачи", ЭтаФорма.ЛицензияДатаС);
		Структура.Вставить("ДатаОкончанияДействия", ЭтаФорма.ЛицензияДатаПо);		
	КонецЕсли;
	
	ЭтаФорма.Закрыть(Структура);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсеПоля(Команда)
	
	ОчиститьРеквизитыФормы();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОчиститьРеквизитыФормы()
	
	РеквизитыИсключения = Новый Массив;
	РеквизитыИсключения.Добавить("Роль");
	
	Saby_ТНОбщегоНазначенияСервер.ОчиститьРеквизитыФормы(ЭтаФорма, РеквизитыИсключения);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьРеквизитов(Знач ДанныеЗаполнения)
	
	ЭтаФорма.ТолькоПросмотр = ТолькоПросмотр;	
	
	Элементы.ГруппаСсылка.Видимость                 = Не ЭтаФорма.ТолькоПросмотрФормы;
	Элементы.ФормаУстановитьНовыеДанные.Доступность = Не ЭтаФорма.ТолькоПросмотр;	
	Элементы.ФормаОчиститьВсеПоля.Доступность       = Не ЭтаФорма.ТолькоПросмотр;
	
	Роли = ПризнакиРолей(Роль);
					
	// Видимость		
	Элементы.ИНН.Видимость            = Роли.ЭтоВодитель;
	Элементы.ГруппаВУ.Видимость       = Роли.ЭтоВодитель;
	Элементы.ГруппаТелефоны.Видимость = Роли.ЭтоВодитель Или Роли.ЭтоОформитель;
	Элементы.ГруппаЛицензия.Видимость = Роли.ЭтоМедик; 
			
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ГруппаФИОИНН");
	МассивЭлементов.Добавить("Телефоны");
	МассивЭлементов.Добавить("ГруппаВУ");
	МассивЭлементов.Добавить("ГруппаЛицензия");
	
	Saby_ТНОбщегоНазначенияКлиентСервер.ИзменитьВидимостьДоступностьЭлементовФормы(
		ЭтаФорма, "ТолькоПросмотр", ЭтаФорма.ТолькоПросмотр, МассивЭлементов);
		
	Элементы.ГруппаСсылка.Видимость = Не ЭтаФорма.ТолькоПросмотр;	
	
	// дополнительные условия
	Если Параметры.Свойство("СкрываемыеЭлементы") Тогда
				
		Saby_ТНОбщегоНазначенияКлиентСервер.ИзменитьВидимостьДоступностьЭлементовФормы(
			ЭтаФорма, "Видимость", Ложь, Параметры.СкрываемыеЭлементы);
			
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервереБезКонтекста
Функция ПризнакиРолей(Роль)
	
	СтруктураРолей = Новый Структура; 
	СтруктураРолей.Вставить("ЭтоВодитель",      Ложь);
	СтруктураРолей.Вставить("ЭтоОтветственный", Ложь); 
	СтруктураРолей.Вставить("ЭтоМедик",         Ложь);
	СтруктураРолей.Вставить("ЭтоМеханик",       Ложь);
	СтруктураРолей.Вставить("ЭтоОформитель",    Ложь);
	
	Если Роль = Перечисления.Saby_РолиОтветственных.Водитель Тогда
		СтруктураРолей.ЭтоВодитель = Истина;
	ИначеЕсли Роль = Перечисления.Saby_РолиОтветственных.Техосмотр Тогда 	
	    СтруктураРолей.ЭтоМеханик = Истина;
	ИначеЕсли Роль = Перечисления.Saby_РолиОтветственных.ОформительДокумента Тогда
		СтруктураРолей.ЭтоОформитель = Истина;	
	ИначеЕсли Роль = Перечисления.Saby_РолиОтветственных.МедосмотрВыезд 
		Или Роль = Перечисления.Saby_РолиОтветственных.МедосмотрЗаезд Тогда 
		
		СтруктураРолей.ЭтоМедик = Истина;		
	Иначе
		СтруктураРолей.ЭтоОтветственный = Истина;
	КонецЕсли;
	
	Возврат СтруктураРолей;
	
КонецФункции

&НаСервере
Процедура ЗаполнениеРеквизитовФормы(Знач ДанныеЗаполнения)
	
	Saby_ТНОбщегоНазначенияСервер.ЗаполнитьРеквизитыФормы(ЭтаФорма, ДанныеЗаполнения);
	
	ЭтаФорма.ТолькоПросмотр = ДанныеЗаполнения.Свойство("ТолькоПросмотр")
		И ДанныеЗаполнения.ТолькоПросмотр = Истина;
		
	РольМедик = (Роль = Перечисления.Saby_РолиОтветственных.МедосмотрВыезд
        Или Роль = Перечисления.Saby_РолиОтветственных.МедосмотрЗаезд);
			
	ЗаполнениеПоСсылке = ДанныеЗаполнения.Свойство("ЗаполнениеПоСсылке");	
		
	Если Не ЗаполнениеПоСсылке И РольМедик 
		И ДанныеЗаполнения.Свойство("Серия") Тогда
        
        ЭтаФорма.ЛицензияСерия  = ДанныеЗаполнения.Серия;
        ЭтаФорма.ЛицензияНомер  = ДанныеЗаполнения.Номер;
        ЭтаФорма.ЛицензияДатаС  = ДанныеЗаполнения.ДатаВыдачи;
        
        Если ДанныеЗаполнения.Свойство("ДатаОкончанияДействия") Тогда            
            ЭтаФорма.ЛицензияДатаПо = ДанныеЗаполнения.ДатаОкончанияДействия;
        КонецЕсли;
        
    КонецЕсли;
            	
	Если Параметры.Свойство("Наименование") Тогда
		ЭтаФорма.Заголовок = Параметры.Наименование;
	КонецЕсли;
						
	СформироватьНадписьТелефоныСтрокой();
	
КонецПроцедуры

#Область Телефоны

&НаКлиенте
Процедура ТелефоныПослеУдаления(Элемент)
	СформироватьНадписьТелефоныСтрокой();
КонецПроцедуры

&НаСервере
Процедура СформироватьНадписьТелефоныСтрокой()
		
	Массив = Новый Массив;
	Для Каждого Строка Из ЭтаФорма.Телефоны Цикл 
		
		Если ЗначениеЗаполнено(Строка.Телефон) Тогда
			Массив.Добавить(СокрЛП(Строка.Телефон));
		КонецЕсли;
				
	КонецЦикла;
	
	Если Массив.Количество() Тогда 
		ЭтаФорма.ТелефоныСтрокой = СтрСоединить(Массив, ", ");
	Иначе 
		ЭтаФорма.ТелефоныСтрокой = "";
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныТелефонПриИзменении(Элемент)
	СформироватьНадписьТелефоныСтрокой();
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭтаФорма.ТекущийЭлемент = Элементы.Телефоны; 
	
КонецПроцедуры

#КонецОбласти // Телефоны

&НаКлиенте
Процедура ОбработкаВыбораВодителя(ВыбранныйЭлемент, ДопПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ЭтаФорма.Водитель = ВыбранныйЭлемент;				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаПриИзменении(Элемент)	
	ОбновитьДанныеПоСсылке();	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоСсылке()
	
	Если ЗначениеЗаполнено(ЭтаФорма.ФизЛицо) Тогда 
		
		ДанныеВодителя = Saby_ТНОбщегоНазначенияСервер.ДанныеВодителяПоСсылке(ЭтаФорма.ФизЛицо);
		ДанныеВодителя.Вставить("ЗаполнениеПоСсылке", Истина);
		
		ЗаполнениеРеквизитовФормы(ДанныеВодителя);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ТелефоныЕмейлыСериализовать()
	
	Если ЭтаФорма.Телефоны.Количество() Тогда 
		
		МассивТелефонов = Новый Массив;
		Для Каждого Строка Из ЭтаФорма.Телефоны Цикл 
			МассивТелефонов.Добавить(Строка.Телефон);			
		КонецЦикла;
		
		ТелефоныСтрокой = ЗначениеВСтрокуВнутр(МассивТелефонов);
		
	Иначе 	
		ТелефоныСтрокой = "";
	КонецЕсли;	
	
	Если ЭтаФорма.Емейлы.Количество() Тогда 
		
		МассивЕмейлы = Новый Массив;
		Для Каждого Строка Из ЭтаФорма.Емейлы Цикл 
			МассивЕмейлы.Добавить(Строка.Емейл);			
		КонецЦикла;
		
		ЕмейлыСтрокой = ЗначениеВСтрокуВнутр(МассивЕмейлы);
		
	Иначе 	
		ЕмейлыСтрокой = "";
	КонецЕсли;	
	
	Структура = Новый Структура;
	Структура.Вставить("Телефоны", ТелефоныСтрокой);
	Структура.Вставить("Емейлы",   ЕмейлыСтрокой);
		
	Возврат Структура;
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
