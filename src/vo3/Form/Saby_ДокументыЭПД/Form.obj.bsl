
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.Заголовок = Параметры.Заголовок;	
	ЭтаФорма.Тип       = Параметры.Тип;
	ЭтаФорма.ЭтоВО     = Параметры.ЭтоВО;
	
	Если Не ЗначениеЗаполнено(Параметры.Наименование) Тогда
		ЭтаФорма.Наименование = Параметры.Заголовок;
	Иначе 	
		ЭтаФорма.Наименование = Параметры.Наименование;
	КонецЕсли;
	
	ЗаполнитьДанныеПоОткрытомуДокументу(Параметры);
		
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьНовыеДанные(Команда)

	Если КонтрольЗаполнения() Тогда
		Возврат;
	КонецЕсли;	
	
	ЮрЛица = ДанныеЮрЛиц();
	
	Структура = Новый Структура;
	Структура.Вставить("Номер",        ЭтаФорма.Номер);
	Структура.Вставить("Дата",         ЭтаФорма.Дата);
	Структура.Вставить("Наименование", ЭтаФорма.Наименование);
	Структура.Вставить("Тип",          ЭтаФорма.Тип);
	Структура.Вставить("ДанныеЮрЛиц",  ЮрЛица);
	
 	ЭтаФорма.Закрыть(Структура);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область ОбработчикиСобытийЭлементовФормы

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеЮрЛиц

&НаКлиенте
Процедура ДанныеЮрЛицВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыРеквизитаФормы = ПараметрыРеквизитаФормы(
		"НаименованиеОрганизации",
		"ДанныеЮрЛицНаименованиеОрганизации",
		ВыбраннаяСтрока,
		ЭтаФорма.ЭтоВО);
	
	ПараметрыРеквизитаФормы.ИмяИсходнойТаблицы          = "ДанныеЮрЛиц";
	ПараметрыРеквизитаФормы.ИдентификаторИсходнойСтроки = ВыбраннаяСтрока;
	ПараметрыРеквизитаФормы.ТолькоПросмотр              = Элементы.Группа_СодержимоеДокумента.ТолькоПросмотр;
	
	ЮрЛицоНажатие(ЭтотОбъект, "СторонаДокумента", ПараметрыРеквизитаФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеЮрЛицПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	МодульТНОбщегоНазначенияКлиентСервер = МодульКодаКлиент("Saby_ТНОбщегоНазначенияКлиентСервер");	
	Форма = МодульТНОбщегоНазначенияКлиентСервер.ФормаОбъект(ЭтаФорма);  
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Роль = "СторонаДокумента";
		Элемент.ТекущиеДанные.КлючСтроки = НовыйКлючОсновнойСтроки(Форма, "ДанныеЮрЛиц");
	КонецЕсли;
	
	ПараметрыРеквизитаФормы = ПараметрыРеквизитаФормы(
		"НаименованиеОрганизации",
		"ДанныеЮрЛицНаименованиеОрганизации",
		Элементы.ДанныеЮрЛиц.ТекущаяСтрока,
		ЭтаФорма.ЭтоВО);
			
	ПараметрыРеквизитаФормы.ИмяИсходнойТаблицы          = "ДанныеЮрЛиц";
	ПараметрыРеквизитаФормы.ИдентификаторИсходнойСтроки = Элементы.ДанныеЮрЛиц.ТекущаяСтрока;
			
	ЮрЛицоНажатие(ЭтаФорма, "СторонаДокумента", ПараметрыРеквизитаФормы);

КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыДанныеЮрЛиц

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьНаименование()
	
	Текст = СтрШаблон("%1 №%2 от %3",
				ЭтаФорма.Заголовок,
				ЭтаФорма.Номер,
				Формат(ЭтаФорма.Дата, "ДФ=dd.MM.yyyy"));
	
	ЭтаФорма.Наименование = Текст;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПоОткрытомуДокументу(Параметры)
	
	Если Параметры.Свойство("Номер") Тогда 
		ЭтаФорма.Номер = Параметры.Номер;
	КонецЕсли;
	
	Если Параметры.Свойство("Дата") Тогда 
		ЭтаФорма.Дата = Параметры.Дата;
	КонецЕсли;
	
	// + данные юр лиц
	Если Параметры.Свойство("ДанныеЮрЛиц") Тогда
		
		Для Каждого ЮрЛицо Из Параметры.ДанныеЮрЛиц Цикл
						
			НоваяСтрока = ЭтаФорма.ДанныеЮрЛиц.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЮрЛицо);
			
		КонецЦикла;
						
	КонецЕсли;	
	
	// если документ открыт на просмотр, отключим возможноть редактирования
	Если Параметры.ТолькоПросмотр Тогда
		Элементы.ФормаЗаписатьИЗакрыть.Видимость           = Ложь;
		Элементы.Группа_СодержимоеДокумента.ТолькоПросмотр = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ДанныеЮрЛиц() 
	
	ЮрЛица = Новый Массив;
	Для Каждого СтрокаТаблицы Из ЭтаФорма.ДанныеЮрЛиц Цикл
		Структура = СтрокаТаблицыФормыВСтруктуру(ЭтаФорма, "ДанныеЮрЛиц", СтрокаТаблицы);
		ЮрЛица.Добавить(Структура); 
	КонецЦикла;
	
	Возврат ЮрЛица;
	
КонецФункции

&НаКлиенте
Функция КонтрольЗаполнения()
	
	МассивОшибок = Новый Массив;
	
	Если Не ЗначениеЗаполнено(ЭтаФорма.Номер) Тогда		
		ДобавитьОписаниеОшибки(МассивОшибок, "Не заполнен номер документа", "Номер");			
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ЭтаФорма.Дата) Тогда		
		ДобавитьОписаниеОшибки(МассивОшибок, "Не заполнена дата документа", "Дата"); 		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтаФорма.Наименование) Тогда
		ДобавитьОписаниеОшибки(МассивОшибок, "Не заполнено наименование документа", "Наименование"); 
	КонецЕсли;
	
	Если Не ЭтаФорма.ДанныеЮрЛиц.Количество() Тогда 		
		ДобавитьОписаниеОшибки(МассивОшибок, "Не заполнены стороны документа", "ДанныеЮрЛиц");		
	КонецЕсли; 
	
	Для Каждого Ошибка Из МассивОшибок Цикл
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Ошибка.Описание;
		Сообщение.Поле  = Ошибка.Поле;
		Сообщение.Сообщить();	
		
	КонецЦикла;
		
	Возврат МассивОшибок.Количество();
	
КонецФункции

Процедура ДобавитьОписаниеОшибки(МассивОшибок, Описание, ЭлементФормы)
	
	Структура = Новый Структура;
	Структура.Вставить("Описание", Описание);
	Структура.Вставить("Поле",     ЭлементФормы);
	
	МассивОшибок.Добавить(Структура);
	
КонецПроцедуры

#Область include_etrn_src_vo3_CommonModule_ОпределениеМодуляКода
#КонецОбласти // include_etrn_src_vo3_CommonModule_ОпределениеМодуляКода

#Область include_etrn_src_vo3_CommonModule_ОпределениеМодуляКодаКлиент
#КонецОбласти // include_etrn_src_vo3_CommonModule_ОпределениеМодуляКодаКлиент

#Область include_etrn_src_vo3_CommonModule_ВыполнениеФункцииНаСервере //&НаСервере
#КонецОбласти // include_etrn_src_vo3_CommonModule_ВыполнениеФункцииНаСервере //&НаСервере

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ФормаОбъект // &НаКлиенте
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ФормаОбъект  //&НаКлиенте

#Область include_etrn_src_vo3_CommonModule_ЗначениеМетаданных
#КонецОбласти // include_etrn_src_vo3_CommonModule_ЗначениеМетаданных

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиент_ОткрытиеФорм
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиент_ОткрытиеФорм

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиент_ЮрЛицаИнтерфейс
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиент_ЮрЛицаИнтерфейс

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСоСвязаннымиТаблицами
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСоСвязаннымиТаблицами

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_Общие //&НаКлиенте
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_Общие //&НаКлиенте

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ОбщиеСтруктуры
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_ОбщиеСтруктуры

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РолиКонтрагентов
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РолиКонтрагентов

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСЮрЛицами
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСЮрЛицами

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_КонтактныеДанные
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиентСервер_КонтактныеДанные

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиент_РаботаСЮрЛицами
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиент_РаботаСЮрЛицами

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиент_Общие
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиент_Общие

#Область include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиент_КлючиСтрок
#КонецОбласти // include_etrn_base_CommonModule_ТНОбщегоНазначенияКлиент_КлючиСтрок

#Область include_etrn_src_vo3_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСоСтроками //&НаКлиенте
#КонецОбласти // include_etrn_src_vo3_CommonModule_ТНОбщегоНазначенияКлиентСервер_РаботаСоСтроками //&НаКлиенте

#КонецОбласти // СлужебныеПроцедурыИФункции
